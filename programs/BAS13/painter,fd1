   10 REM > PAINTER
   20 
   30 DIM MD% 64
   40 MD%!00=1:MD%!04=800:MD%!08=600:MD%!12=5:MD%!16=-1:MD%!20=-1
   50 MODE MD%:OFF
   60 
   70 D=12:A=40:D2=D/2
   80 X1%=RND(1088)+256:Y1%=RND(688)+256
   90 DX1%=RND(12)*SGN(RND(1)-.5):DY1%=RND(12)*SGN(RND(1)-.5)
  100 X2%=RND(1088)+256:Y2%=RND(688)+256
  110 DX2%=RND(12)*SGN(RND(1)-.5):DY2%=RND(12)*SGN(RND(1)-.5)
  120 R1%=RND(255):G1%=RND(255):B1%=RND(255)
  130 R2%=RND(255):G2%=RND(255):B2%=RND(255)
  140 DIM X(A),Y(A):X()=800:Y()=600
  150 
  160 REPEAT
  170 H%=X1%+DX1%:IF H%<128 OR H%>1472 DX1%=RND(10)*SGN(-DX1%)
  180 H%=Y1%+DY1%:IF H%<128 OR H%>1072 DY1%=RND(10)*SGN(-DY1%)
  190 X1%+=DX1%:Y1%+=DY1%
  200 IF X2%<X1% AND DX2%<24 DX2%+=1
  210 IF X2%>X1% AND DX2%>-24 DX2%-=1
  220 IF Y2%<Y1% AND DY2%<24 DY2%+=1
  230 IF Y2%>Y1% AND DY2%>-24 DY2%-=1
  240 X2%+=DX2%:Y2%+=DY2%:X(A)=X2%:Y(A)=Y2%
  250 R1%=FNA(R1%):G1%=FNA(G1%):B1%=FNA(B1%)
  260 R2%=FNA(R2%):G2%=FNA(G2%):B2%=FNA(B2%)
  270 WR1%=FNB(R1%):WG1%=FNB(G1%):WB1%=FNB(B1%)
  280 WR2%=FNB(R2%):WG2%=FNB(G2%):WB2%=FNB(B2%)
  290 GCOL WR1%,WG1%,WB1%
  300 CIRCLE FILL X(A),Y(A),D2
  310 FOR N=A-1TO1STEP-1:M=N+1
  320 R%=(WR1%*N+WR2%*(A-N))/A:G%=(WG1%*N+WG2%*(A-N))/A:B%=(WB1%*N+WB2%*(A-N))/A
  330 GCOL R%,G%,B%
  340 Z=SQR((X(N)-X(M))^2+(Y(N)-Y(M))^2)
  350 DZ=Z-D:X(N)=(X(N)*D+X(M)*DZ)/Z:Y(N)=(Y(N)*D+Y(M)*DZ)/Z
  360 CIRCLE FILL X(N),Y(N),D2
  370 NEXT
  380 UNTIL0
  390 
  400 DEFFNA(C%)
  410 =(C%+RND(10))MOD511
  420 
  430 DEFFNB(C%)
  440 C%=ABS(C%-256)
  450 IF C%>255 C%=255
  460 IF C%<0 C%=0
  470 =C%
