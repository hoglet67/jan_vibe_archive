   10 REM > STEREO5       **** JAN VIBE JANUAR 99 ****
   20 
   30 DIM MD% 64:MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=3:MD%!16=-1:MD%!20=-1:MODE MD%:OFF
   40 K%=100000:PC%=0:W%=280:W1%=W%+2:PF=2000:SF=1.7:ZMIN%=0:ZMAX%=0
   50 
   60 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   70 DIM P%(K%),X%(K%),Y%(K%)
   80 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   90 
  100 
  110 REPEAT:CLS
  120 PC%=0:C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  130 PROCROT(10,30,10)
  140 A1%=RND(4):A2%=RND(4)
  150 B1%=RND(4):B2%=RND(4)
  160 C1%=RND(4):C2%=RND(4)
  170 X1%=500:Y1%=500:Z1%=500
  180 FORN=0TO360STEP2
  190 X2%=550+250*(SINRAD(N*A1%)+SINRAD(N*A2%))
  200 Y2%=550+250*(SINRAD(N*B1%)+SINRAD(N*B2%))
  210 Z2%=500+250*(SINRAD(N*C1%)+SINRAD(N*C2%))
  220 PROCL(X1%,Y1%,Z1%,50,X2%,Y2%,Z2%,50)
  230 X1%=X2%:Y1%=Y2%:Z1%=Z2%
  240 NEXT
  250 IF ZMAX%-ZMIN%<2000 THEN ZMIN%=0:ZMAX%=2000
  260 ZS%=(ZMIN%-ZMAX%)/59:C%=4
  270 FORZ%=ZMAX%TOZMIN%STEPZS%:C%+=1:GCOLC%
  280 FORN%=1TOPC%
  290 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  300 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%)+1200,PY%(N%)+700,P%*SF
  310 ENDIF
  320 NEXT,
  330 PRINT'"Press space"'"for another"'"figure,"''"any other"'"key for"'"stereogram."
  340 *FX21,0
  350 Q$=GET$:UNTILQ$<>" "
  360 
  370 W1%=W%+2
  380 VDU26,24,0;0;W%;1535;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1535
  390 FORN%=1TO3000:X%=RND(W%)+4:Y%=RND(1535):R%=RND(16)+8:F%=RND(26)
  400 FORM%=7TO0STEP-1:PROCF(F%,7-M%)
  410 CIRCLE FILL X%-W%,Y%,R%*M%/7:CIRCLE FILL X%,Y%,R%*M%/7:CIRCLE FILL X%+W%,Y%,R%*M%/7
  420 NEXT,:VDU26
  430 
  440 ORIGIN 0,0:FORY%=0TO1535STEP2
  450 FORX%=0TO2047+W1%STEP2
  460 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  470 IF POINT(X%,Y%)=0THEN
  480 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  490 S%=X1%-X%:IF S%<=6 THEN
  500 IF S%>=0 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%,Y%
  510 IF S%>=2 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%,Y%
  520 IF S%>=4 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%+2,Y%
  530 IF S%>=6 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%-2,Y%
  540 ELSE
  550 GCOLPOINT(X%MODW%,(Y%+768)MOD1536) TINT TINT(X%MODW%,(Y%+768)MOD1536):POINT X%,Y%
  560 ENDIF
  570 ENDIF
  580 GCOLPOINT(X%,Y%) TINT TINT(X%,Y%):POINT X%+W1%-P%,Y%
  590 NEXT,
  600 *FX21,0
  610 Q$=GET$:IF Q$="S" OSCLI"SCREENSAVE RDSPIC"
  620 END
  630 
  640 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  650 LOCAL N%,D%,B%,P
  660 K(1)=(X1%-500)*SF:K(2)=(Y1%-500)*SF:K(3)=(Z1%-500)*SF:K(4)=1:R()=K().T()
  670 X1%=R(1):Y1%=R(2):Z1%=R(3)+1200
  680 IF Z1%<ZMIN% ZMIN%=Z1%
  690 IF Z1%>ZMAX% ZMAX%=Z1%
  700 P=1+Z1%/PF:X1%=X1%/P:Y1%=Y1%/P:R1%=R1%/P
  710 K(1)=(X2%-500)*SF:K(2)=(Y2%-500)*SF:K(3)=(Z2%-500)*SF:K(4)=1:R()=K().T()
  720 X2%=R(1):Y2%=R(2):Z2%=R(3)+1200
  730 IF Z2%<ZMIN% ZMIN%=Z2%
  740 IF Z2%>ZMAX% ZMAX%=Z2%
  750 P=1+Z2%/PF:X2%=X2%/P:Y2%=Y2%/P:R2%=R2%/P
  760 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  770 IF R1%<R2% B%=4*D%/R1% ELSE B%=4*D%/R2%
  780 IF B%<>0 THEN
  790 FOR N%=0TOB%:PC%+=1
  800 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%
  810 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%
  820 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%
  830 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  840 NEXT
  850 ELSE
  860 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  870 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  880 ENDIF
  890 ENDPROC
  900 
  910 DEFPROCROT(DX,DY,DZ)
  920 LOCAL T,SI,CO
  930 T()=C()
  940 A()=C():T=RADDX:CO=COST:SI=SINT
  950 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  960 T()=A()
  970 A()=C():T=RADDY:CO=COST:SI=SINT
  980 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  990 T()=A().T()
 1000 A()=C():T=RADDZ:CO=COST:SI=SINT
 1010 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
 1020 T()=A().T()
 1030 ENDPROC
 1040 
 1050 DEFPROCF(F%,I%)
 1060 LOCAL R%,G%,B%
 1070 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
 1080 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
 1090 GCOL F% TINT (64*I%)MOD256
 1100 ENDPROC
