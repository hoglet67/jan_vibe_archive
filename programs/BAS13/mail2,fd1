   10 REM > MAIL2
   20 
   30 
   40 DIM SPR &F0000
   50 SPR!0=&F0000:SPR!4=0:SPR!8=16:SPR!12=16
   60 SYS"OS_SpriteOp",256+10,SPR,"EARTH"
   70 
   80 DIM MD% 64
   90 MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=4:MD%!16=-1:MD%!20=-1
  100 MODE MD%
  110 
  120 FOR N%=0TO3000
  130 X%=RND(2048):Y%=RND(1536):R=RND(1)
  140 POINT X%,Y%
  150 IF R>.9 RECTANGLE FILL X%,Y%,2,2
  160 IF R>.99 RECTANGLE FILL X%-2,Y%-2,4,4
  170 IF R>.999 CIRCLE FILL X%,Y%,4
  180 NEXT
  190 
  200 REM skalering,perspektivfaktor,antal punkter
  210 S=600:PF=2:AP%=14
  220 
  230 REM OpsGCOLtning.
  240 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
  250 DIM PX(AP%),PY(AP%),PZ(AP%),SX(AP%),SY(AP%)
  260 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  270 PROCROT(70,-20,0)
  280 
  290 REM Definition af objekt.
  300 FORI%=1TO10000
  310 IF I%=5000 THEN SYS"OS_SpriteOp",256+34,SPR,"EARTH",400,121,8
  320 RESTORE:IF I%<5000 THEN A=RAD(RND(180)-90) ELSE A=RAD(RND(180)+90)
  330 D=RND(70)+690:Z=RND(1)*2+1
  340 FORN%=1TOAP%:READ PX(N%),PY(N%),PZ(N%)
  350 
  360 PX(N%)=PX(N%)*Z:PY(N%)=PY(N%)*Z+D
  370 X1=PX(N%)*COSA+PY(N%)*SINA
  380 Y1=PY(N%)*COSA-PX(N%)*SINA
  390 PX(N%)=X1:PY(N%)=Y1
  400 NEXT
  410 
  420 DATA -8,-5,0, 8,-5,0, 8,5,0, -8,5,0
  430 DATA 4,1,0, 7,1,0, 7,4,0, 4,4,0
  440 DATA -6,-3,0, 2,-3,0
  450 DATA -6,-1,0, 2,-1,0
  460 DATA -6,1,0, 2,1,0
  470 
  480 REM drej objektet, tegn det og vis det.
  490 
  500 FORN%=1TOAP%
  510 K(1)=PX(N%):K(2)=PY(N%):K(3)=PZ(N%):K(4)=1
  520 R()=K().T()
  530 PX(N%)=R(1):PY(N%)=R(2):PZ(N%)=R(3)
  540 P=1+PZ(N%)/(S*PF)
  550 SX(N%)=R(1)/P+1100:SY(N%)=R(2)/P+916
  560 NEXT
  570 
  580 GCOL255,255,255:MOVE SX(2),SY(2):MOVE SX(1),SY(1):PLOT85,SX(3),SY(3):PLOT85,SX(4),SY(4)
  590 GCOLRND(255),RND(255),RND(255)
  600 MOVE SX(6),SY(6):MOVE SX(5),SY(5):PLOT85,SX(7),SY(7):PLOT85,SX(8),SY(8)
  610 GCOL0,0,0:LINE SX(9),SY(9),SX(10),SY(10)
  620 LINE SX(11),SY(11),SX(12),SY(12)
  630 LINE SX(13),SY(13),SX(14),SY(14)
  640 MOVE SX(1),SY(1):DRAW SX(2),SY(2):DRAW SX(3),SY(3):DRAW SX(4),SY(4):DRAW SX(1),SY(1)
  650 
  660 NEXT
  670 END
  680 
  690 REM generer roteringsmatricen
  700 DEFPROCROT(DX,DY,DZ)
  710 LOCAL T,SI,CO
  720 T()=C()
  730 
  740 REM drejning om X.
  750 A()=C():T=RADDX:CO=COST:SI=SINT
  760 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  770 T()=A()
  780 
  790 REM drejning om Y.
  800 A()=C():T=RADDY:CO=COST:SI=SINT
  810 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  820 T()=A().T()
  830 
  840 REM drejning om Z.
  850 A()=C():T=RADDZ:CO=COST:SI=SINT
  860 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  870 T()=A().T()
  880 ENDPROC
