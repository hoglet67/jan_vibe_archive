   10 REM > STEREO4       **** JAN VIBE JANUAR 99 ****
   20 
   30 DIM MD% 64:MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=3:MD%!16=-1:MD%!20=-1:MODE MD%:OFF
   40 K%=100000:PC%=0:W%=280:W1%=W%+2:PF=2000:SF=2.1:ZMIN%=0:ZMAX%=0
   50 
   60 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   70 DIM P%(K%),X%(K%),Y%(K%)
   80 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 PROCROT(-40,40,0)
  120 FORN%=0TO359STEP8:T=RADN%
  130 A%=500+150*SINT:B%=500+150*COST
  140 PROCL(A%,B%,0,25,A%,B%,1000,25)
  150 PROCL(A%,0,B%,25,A%,1000,B%,25)
  160 PROCL(0,A%,B%,25,1000,A%,B%,25)
  170 NEXT
  180 
  190 IF ZMAX%-ZMIN%<2000 THEN ZMIN%=0:ZMAX%=2000
  200 ZS%=(ZMIN%-ZMAX%)/59:C%=4
  210 FORZ%=ZMAX%TOZMIN%STEPZS%:C%+=1:GCOLC%
  220 FORN%=1TOPC%
  230 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  240 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%)+1200,PY%(N%)+700,P%*SF
  250 ENDIF
  260 NEXT,
  270 
  280 W1%=W%+2
  290 VDU26,24,0;0;W%;1535;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1535
  300 FORN%=1TO3000:X%=RND(W%)+4:Y%=RND(1535):R%=RND(16)+8:F%=RND(26)
  310 FORM%=7TO0STEP-1:PROCF(F%,7-M%)
  320 CIRCLE FILL X%-W%,Y%,R%*M%/7:CIRCLE FILL X%,Y%,R%*M%/7:CIRCLE FILL X%+W%,Y%,R%*M%/7
  330 NEXT,:VDU26:ORIGIN W1%,0
  340 
  350 ORIGIN 0,0:FORY%=0TO1535STEP2
  360 FORX%=0TO2047+W1%STEP2
  370 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  380 IF POINT(X%,Y%)=0THEN
  390 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  400 S%=X1%-X%:IF S%<=6 THEN
  410 IF S%>=0 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%,Y%
  420 IF S%>=2 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%,Y%
  430 IF S%>=4 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%+2,Y%
  440 IF S%>=6 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%-2,Y%
  450 ELSE
  460 GCOLPOINT(X%MODW%,(Y%+768)MOD1536) TINT TINT(X%MODW%,(Y%+768)MOD1536):POINT X%,Y%
  470 ENDIF
  480 ENDIF
  490 GCOLPOINT(X%,Y%) TINT TINT(X%,Y%):POINT X%+W1%-P%,Y%
  500 NEXT,
  510 *FX21,0
  520 Q$=GET$:IF Q$="S" OSCLI"SCREENSAVE RDSPIC"
  530 END
  540 
  550 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  560 LOCAL N%,D%,B%,P
  570 K(1)=(X1%-500)*SF:K(2)=(Y1%-500)*SF:K(3)=(Z1%-500)*SF:K(4)=1:R()=K().T()
  580 X1%=R(1):Y1%=R(2):Z1%=R(3)+1200
  590 IF Z1%<ZMIN% ZMIN%=Z1%
  600 IF Z1%>ZMAX% ZMAX%=Z1%
  610 P=1+Z1%/PF:X1%=X1%/P:Y1%=Y1%/P:R1%=R1%/P
  620 K(1)=(X2%-500)*SF:K(2)=(Y2%-500)*SF:K(3)=(Z2%-500)*SF:K(4)=1:R()=K().T()
  630 X2%=R(1):Y2%=R(2):Z2%=R(3)+1200
  640 IF Z2%<ZMIN% ZMIN%=Z2%
  650 IF Z2%>ZMAX% ZMAX%=Z2%
  660 P=1+Z2%/PF:X2%=X2%/P:Y2%=Y2%/P:R2%=R2%/P
  670 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  680 IF R1%<R2% B%=4*D%/R1% ELSE B%=4*D%/R2%
  690 IF B%<>0 THEN
  700 FOR N%=0TOB%:PC%+=1
  710 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%
  720 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%
  730 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%
  740 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  750 NEXT
  760 ELSE
  770 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  780 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  790 ENDIF
  800 ENDPROC
  810 
  820 DEFPROCROT(DX,DY,DZ)
  830 LOCAL T,SI,CO
  840 T()=C()
  850 A()=C():T=RADDX:CO=COST:SI=SINT
  860 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  870 T()=A()
  880 A()=C():T=RADDY:CO=COST:SI=SINT
  890 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  900 T()=A().T()
  910 A()=C():T=RADDZ:CO=COST:SI=SINT
  920 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  930 T()=A().T()
  940 ENDPROC
  950 
  960 DEFPROCF(F%,I%)
  970 LOCAL R%,G%,B%
  980 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  990 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
 1000 GCOL F% TINT (64*I%)MOD256
 1010 ENDPROC
