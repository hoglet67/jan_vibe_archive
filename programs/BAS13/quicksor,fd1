   10 REM > QUICKSORT
   20 
   30 MODE31:OFF
   40 
   50 DIM ARR%(800)
   60 FORN%=1TO800:ARR%(N%)=RND(1200):NEXT
   70 TIME=0:PROCSORT(ARR%(),1,800):T%=TIME
   80 FORN%=1TO800:LINE 2*N%,0,2*N%,ARR%(N%):NEXT
   90 PRINTT%:END
  100 
  110 DEFPROCSORT(RETURN A%(),L%,U%)
  120 LOCAL L1%,U1%,P%
  130 L1%=L%:U1%=U%:P%=A%((L%+U%)DIV2)
  140 WHILE L1%<=U1%
  150    WHILE A%(L1%)<P% AND L1%<U%
  160       L1%+=1
  170    ENDWHILE
  180    WHILE P%<A%(U1%) AND U1%>L%
  190       U1%-=1
  200    ENDWHILE
  210    IF L1%<=U1% THEN
  220       SWAP A%(L1%),A%(U1%)
  230       L1%+=1:U1%-=1
  240    ENDIF
  250 ENDWHILE
  260 IF L%<U1% THEN PROCSORT(A%(),L%,U1%)
  270 IF L1%<U% THEN PROCSORT(A%(),L1%,U%)
  280 ENDPROC
