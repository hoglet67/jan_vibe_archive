   10 REM > MAGIC6
   20 
   30 MODE31:OFF:PRINT''
   40 DIM A%(7,6),B%(2,2):Q%=0
   50 REPEAT
   60 RESTORE
   70 A%()=0:Q%+=1
   80 FORJ%=1TO6:FORI%=1TO6:READA%(I%,J%):NEXT,
   90 FORJ%=0TO4STEP2:FORI%=0TO4STEP2
  100 B%()=0
  110 FORN%=1TO3:REPEAT:X%=RND(2):Y%=RND(2):UNTILB%(X%,Y%)=0:B%(X%,Y%)=N%:NEXT
  120 FORM%=1TO2:FORN%=1TO2:A%(I%+N%,J%+M%)+=B%(N%,M%):NEXT,
  130 NEXT,
  140 FORM%=1TO6:FORN%=1TO6:A%(0,M%)+=A%(N%,M%):A%(M%,0)+=A%(N%,M%):NEXT,
  150 FORN%=1TO6:A%(0,0)+=A%(N%,N%):A%(7,0)+=A%(N%,7-N%):NEXT
  160 F%=0:FORM%=0TO7:IF A%(M%,0)<>111 THEN F%=1
  170 NEXT
  180 UNTILF%=0
  190 PRINT"A 6*6 MAGIC SQUARE:"''
  200 @%=4:FORM%=1TO6:FORN%=1TO6:PRINTA%(N%,M%);:NEXT:PRINT':NEXT:PRINT
  210 PRINT"NUMBER OF TRIALS: "Q%''
  220 PRINT"THIS PROGRAM CONSTRUCTS A 6*6 MAGIC SQUARE USING A VARIATION OF"
  230 PRINT"PROFESSOR ALAN W. GROGONOS METHOD."'
  240 PRINT"SEE HTTP://WWW.PSE.CHE.TOHOKU.AC.JP/~MSUZUKI/GROGONO.HTML"''
  250 END
  260 
  270 DATA 29,29,9,9,13,13
  280 DATA 29,29,9,9,13,13
  290 DATA 1,1,17,17,33,33
  300 DATA 1,1,17,17,33,33
  310 DATA 21,21,25,25,5,5
  320 DATA 21,21,25,25,5,5
