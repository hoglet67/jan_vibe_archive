   10 REM > STEREO        **** JAN VIBE JANUAR 99 ****
   20 
   30 DIM MD% 64:MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=3:MD%!16=-1:MD%!20=-1:MODE MD%:OFF
   40 K%=10000:PC%=0:W%=256:W1%=W%+2:PF=2000:SF=1.3:ZMIN%=0:ZMAX%=0
   50 
   60 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   70 DIM P%(K%),X%(K%),Y%(K%)
   80 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 FORN%=0TO270STEP90
  120 PROCROT(15,28,10+N%)
  130 PROCL(0,0,0,60,1000,0,0,60)
  140 PROCL(1000,0,0,60,1000,0,1000,60)
  150 PROCL(1000,0,1000,60,0,0,1000,60)
  160 REMPROCL(1000,0,0,200,1000,0,0,200)
  170 REMPROCL(1000,0,1000,200,1000,0,1000,200)
  180 NEXT
  190 
  200 IF ZMAX%-ZMIN%<2000 THEN ZMIN%=0:ZMAX%=2000
  210 ZS%=(ZMIN%-ZMAX%)/60:C%=4
  220 FORZ%=ZMAX%TOZMIN%STEPZS%:C%+=1:GCOLC%
  230 FORN%=1TOPC%
  240 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  250 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%)+1200,PY%(N%)+700,P%*SF
  260 ENDIF
  270 NEXT,
  280 
  290 W1%=W%+2
  300 VDU26,24,0;0;W%;1535;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1535
  310 FORN%=1TO9000:GCOLRND(63):X%=RND(W%)+4:Y%=RND(1535):R%=RND(10)+5
  320 CIRCLE FILL X%-W%,Y%,R%:CIRCLE FILL X%,Y%,R%:CIRCLE FILL X%+W%,Y%,R%
  330 NEXT:VDU26:ORIGIN W1%,0
  340 
  350 ORIGIN 0,0:FORY%=0TO1535STEP2
  360 FORX%=0TO2047+W1%STEP2
  370 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  380 IF POINT(X%,Y%)=0THEN
  390 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  400 CASE X1%-X% OF
  410 WHEN 0,1,2:GCOL(POINT(X%-2,Y%)):POINTX%,Y%
  420 GCOL(POINT(X1%+2,Y%)):POINTX1%,Y%
  430 OTHERWISE
  440 GCOLPOINT(X%MODW%,(Y%+600)MOD1200):POINT X%,Y%
  450 ENDCASE
  460 ENDIF
  470 GCOLPOINT(X%,Y%):POINT X%+W1%-P%,Y%
  480 NEXT,
  490 *FX21,0
  500 Q$=GET$:IF Q$="S" OSCLI"SCREENSAVE RDSPIC"
  510 END
  520 
  530 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  540 LOCAL N%,D%,B%,P
  550 K(1)=(X1%-500)*SF:K(2)=(Y1%-500)*SF:K(3)=(Z1%-500)*SF:K(4)=1:R()=K().T()
  560 X1%=R(1):Y1%=R(2):Z1%=R(3)+1200
  570 IF Z1%<ZMIN% ZMIN%=Z1%
  580 IF Z1%>ZMAX% ZMAX%=Z1%
  590 P=1+Z1%/PF:X1%=X1%/P:Y1%=Y1%/P:R1%=R1%/P
  600 K(1)=(X2%-500)*SF:K(2)=(Y2%-500)*SF:K(3)=(Z2%-500)*SF:K(4)=1:R()=K().T()
  610 X2%=R(1):Y2%=R(2):Z2%=R(3)+1200
  620 IF Z2%<ZMIN% ZMIN%=Z2%
  630 IF Z2%>ZMAX% ZMAX%=Z2%
  640 P=1+Z2%/PF:X2%=X2%/P:Y2%=Y2%/P:R2%=R2%/P
  650 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  660 IF R1%<R2% B%=4*D%/R1% ELSE B%=4*D%/R2%
  670 IF B%<>0 THEN
  680 FOR N%=0TOB%:PC%+=1
  690 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%
  700 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%
  710 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%
  720 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  730 NEXT
  740 ELSE
  750 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  760 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  770 ENDIF
  780 ENDPROC
  790 
  800 DEFPROCROT(DX,DY,DZ)
  810 LOCAL T,SI,CO
  820 T()=C()
  830 A()=C():T=RADDX:CO=COST:SI=SINT
  840 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  850 T()=A()
  860 A()=C():T=RADDY:CO=COST:SI=SINT
  870 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  880 T()=A().T()
  890 A()=C():T=RADDZ:CO=COST:SI=SINT
  900 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  910 T()=A().T()
  920 ENDPROC
