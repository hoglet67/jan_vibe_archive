   10 REM > TRAVEL3      --- THIS ONE WORKS ON RISC OS 3.10 ---
   20 
   30 MODE140:MODE12:OFF
   40 S1%=1:S2%=2:K%=30:F=1.05:G=0.5
   50 FORN%=0TO15:READ R%,G%,B%:COLOURN%,R%,G%,B%:NEXT
   60 DIM X(K%),Y(K%),R(K%),S$(K%)
   70 
   80 SPF$="Resources:$.Resources.Wimp.Sprites"
   90 
  100 SYS"OS_FSControl",28,SPF$ TO ,,LG%:LG%+=32
  110 DIM SPR LG%,BUF 16
  120 SPR!0=LG%:SPR!4=0:SPR!8=16:SPR!12=16
  130 SYS"OS_SpriteOp",256+10,SPR,SPF$
  140 SYS"OS_SpriteOp",256+8,SPR TO ,,,A%
  150 DIM L$(A%):D%=12:F%=100/D%
  160 FOR N%=1TOA%
  170 SYS"OS_SpriteOp",256+13,SPR,BUF,16,N%
  180 N$="":P%=0:REPEAT
  190 T%=BUF?P%:IF T%<>0 L$(N%)+=CHR$T%
  200 P%+=1:UNTIL T%=0
  210 NEXT:AS%=N%-1
  220 
  230 BUF!00=10:BUF!08=10
  240 BUF!04=10:BUF!12=10
  250 
  260 FORN%=1TOK%
  270 REPEAT:X(N%)=RND(1280)-640:UNTILX(N%)<>0
  280 REPEAT:Y(N%)=RND(1024)-512:UNTILY(N%)<>0
  290 R(N%)=RND(1)
  300 REPEAT
  310 S$(N%)=L$(RND(AS%))
  320 SYS"OS_SpriteOp",256+40,SPR,S$(N%) TO ,,,W%,H%,,M%
  330 UNTIL W%<=34 AND H%<=17 AND M%=12
  340 NEXT
  350 
  360 COLOUR135
  370 ONERRORGOTO800
  380 REPEAT
  390 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  400 FORN%=1TOK%
  410 SYS"OS_SpriteOp",256+40,SPR,S$(N%) TO ,,,W%,H%
  420 X(N%)=F*X(N%):Y(N%)=F*Y(N%):R(N%)=G+R(N%)
  430 BUF!00=R(N%):BUF!04=R(N%):S=R(N%)/10
  440 DX%=X(N%)-W%*S+640:DY%=Y(N%)-H%*S+512
  450 SYS"OS_SpriteOp",256+52,SPR,S$(N%),DX%,DY%,8,BUF,0
  460 NEXT
  470 
  480 FORN%=1TOK%
  490 IF ABS(X(N%))>640 OR ABS(Y(N%))>512 THEN
  500 FORI%=N%TO2STEP-1:J%=I%-1:X(I%)=X(J%):Y(I%)=Y(J%):R(I%)=R(J%):S$(I%)=S$(J%):NEXT
  510 REPEAT:X(1)=RND(1280)-640:UNTILX(1)<>0
  520 REPEAT:Y(1)=RND(1024)-512:UNTILY(1)<>0
  530 R(1)=RND(1)
  540 REPEAT
  550 S$(1)=L$(RND(AS%))
  560 SYS"OS_SpriteOp",256+40,SPR,S$(1) TO ,,,W%,H%,,M%
  570 UNTIL W%<=34 AND H%<=34 AND M%=12
  580 ENDIF
  590 NEXT
  600 WAIT
  610 UNTIL0
  620 
  630 DATA 255,255,255
  640 DATA 221,221,221
  650 DATA 187,187,187
  660 DATA 153,153,153
  670 DATA 119,119,119
  680 DATA 85,85,85
  690 DATA 51,51,51
  700 DATA 0,0,0
  710 DATA 0,68,153
  720 DATA 238,238,0
  730 DATA 0,204,0
  740 DATA 221,0,0
  750 DATA 238,238,187
  760 DATA 85,136,0
  770 DATA 255,187,0
  780 DATA 0,187,255
  790 
  800 COLOUR128:SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
