   10 REM > TXTMAZE2
   20 
   30 MODE31:OFF
   40 SIZE%=71:C%=SIZE%/2+1
   50 DIM A%(SIZE%,SIZE%),DX%(4),DY%(4)
   60 FORI%=1TOSIZE%:FORJ%=1TOSIZE%:A%(I%,J%)=1:NEXT,
   70 FORI%=1TO4:READ DX%(I%),DY%(I%):NEXT
   80 
   90 C%=SIZE%/2+1:A%(C%,C%)=0:PROCMAZE(C%,C%):A%(C%,1)=0:A%(C%,SIZE%)=0
  100 
  110 FORI%=1TOSIZE%:FORJ%=1TOSIZE%
  120 IF A%(I%,J%)=1 PRINT"#"; ELSE PRINT" ";
  130 NEXT:PRINT:NEXT
  140 END
  150 
  160 DATA 0,2,0,-2,2,0,-2,0
  170 
  180 DEFPROCMAZE(X%,Y%)
  190 LOCAL N%,R%
  200 FOR N%=1TO8:R%=RND(4)
  210 IF X%+DX%(R%)>=1 AND X%+DX%(R%)<=SIZE% AND Y%+DY%(R%)>=1 AND Y%+DY%(R%)<=SIZE% THEN
  220 IF A%(X%+DX%(R%),Y%+DY%(R%))=1 THEN
  230 A%(X%+DX%(R%),Y%+DY%(R%))=0:A%(X%+DX%(R%)/2,Y%+DY%(R%)/2)=0
  240 PROCMAZE(X%+DX%(R%),Y%+DY%(R%))
  250 ENDIF
  260 ENDIF
  270 NEXT
  280 ENDPROC
