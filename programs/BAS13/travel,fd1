   10 REM > TRAVEL
   20 
   30 MODE159:MODE31:OFF
   40 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT
   50 S1%=1:S2%=2
   60 K%=100:F=1.1:G=1.5
   70 DIM X(K%),Y(K%),R(K%)
   80 FORN%=1TOK%
   90 REPEAT:X(N%)=RND(1600)-800:UNTILX(N%)<>0
  100 REPEAT:Y(N%)=RND(1200)-600:UNTILY(N%)<>0
  110 R(N%)=RND(1)
  120 NEXT
  130 
  140 ONERRORGOTO350
  150 REPEAT
  160 SYS 6,112,S1%:SYS 6,113,S2%:CLS:SWAP S1%,S2%
  170 FORN%=1TOK%
  180 X(N%)=F*X(N%):Y(N%)=F*Y(N%):R(N%)=G+R(N%)
  190 C%=R(N%):IFC%>15 C%=15
  200 GCOL0:CIRCLE FILL X(N%)+800,Y(N%)+600,R(N%)
  210 GCOLC%:CIRCLE X(N%)+800,Y(N%)+600,R(N%)
  220 NEXT
  230 
  240 FORN%=1TOK%
  250 IF ABS(X(N%))>800 OR ABS(Y(N%))>600 THEN
  260 FORI%=N%TO2STEP-1:J%=I%-1:X(I%)=X(J%):Y(I%)=Y(J%):R(I%)=R(J%):NEXT
  270 REPEAT:X(1)=RND(1600)-800:UNTILX(1)<>0
  280 REPEAT:Y(1)=RND(1200)-600:UNTILY(1)<>0
  290 R(1)=RND(1)
  300 ENDIF
  310 NEXT
  320 WAIT
  330 UNTIL0
  340 
  350 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
