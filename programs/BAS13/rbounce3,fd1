   10 REM > RBOUNCE3
   20 
   30 MODE21:OFF
   40 DIM SPR &5FFFF:SPR!0=&5FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   50 DIM PATH% 100,JC% 15:JC%!0=0:JC%!4=0:JC%!8=0:JC%!12=0
   60 DIM ROT 32
   70 
   80 T=1.01:K%=10:A=1
   90 DIM X%(K%),Y%(K%),DX%(K%),DY%(K%),R%(K%),G%(K%),B%(K%),DR%(K%),DG%(K%),DB%(K%)
  100 FORN%=1TOK%
  110 X%(N%)=RND(1080)+100:Y%(N%)=RND(824)+100
  120 DX%(N%)=SGN(RND(1)-.5)*(RND(11)+3):DY%(N%)=SGN(RND(1)-.5)*(RND(11)+3)
  130 R%(N%)=RND(255):G%(N%)=RND(255):B%(N%)=RND(255)
  140 DR%(N%)=RND(5)+7:DG%(N%)=RND(5)+7:DB%(N%)=RND(5)+7
  150 NEXT
  160 
  170 REPEAT
  180 SYS"OS_SpriteOp",256+16,SPR,"SCR",0,0,0,1279,1023
  190 PROCROT(SPR,"SCR",640,512,640*T,512*T,A)
  200 IF RND(1)>.99 A=-A
  210 
  220 FORN%=1TOK%
  230 GCOL R%(N%),G%(N%),B%(N%):CIRCLE FILL X%(N%),Y%(N%),48
  240 NEXT
  250 FORN%=1TOK%
  260 F%=X%(N%)+DX%(N%):IF F%>1180 OR F%<100 THEN DX%(N%)=SGN(-DX%(N%))*(RND(13)+3)
  270 F%=Y%(N%)+DY%(N%):IF F%>924 OR F%<100 THEN DY%(N%)=SGN(-DY%(N%))*(RND(13)+3)
  280 F%=R%(N%)+DR%(N%):IF F%>255 OR F%<0 THEN DR%(N%)=SGN(-DR%(N%))*(RND(11)+9)
  290 F%=G%(N%)+DG%(N%):IF F%>255 OR F%<0 THEN DG%(N%)=SGN(-DG%(N%))*(RND(11)+9)
  300 F%=B%(N%)+DB%(N%):IF F%>255 OR F%<0 THEN DB%(N%)=SGN(-DB%(N%))*(RND(11)+9)
  310 G%(N%)+=DG%(N%):R%(N%)+=DR%(N%):Y%(N%)+=DY%(N%):X%(N%)+=DX%(N%):B%(N%)+=DB%(N%)
  320 NEXT
  330 UNTIL0
  340 
  350 DEFPROCROT(SPR,N$,X%,Y%,SX%,SY%,A%)
  360 LOCAL XR%,YR%,CO,SI:CO=COSRADA%:SI=SINRADA%
  370 XR%=-SX%*CO+SY%*SI:YR%=SY%*CO+SX%*SI
  380 ROT!00=(XR%+X%)*256:ROT!04=(YR%+Y%)*256
  390 XR%=SX%*CO+SY%*SI:YR%=SY%*CO-SX%*SI
  400 ROT!08=(XR%+X%)*256:ROT!12=(YR%+Y%)*256
  410 XR%=SX%*CO-SY%*SI:YR%=-SY%*CO-SX%*SI
  420 ROT!16=(XR%+X%)*256:ROT!20=(YR%+Y%)*256
  430 XR%=-SX%*CO-SY%*SI:YR%=-SY%*CO+SX%*SI
  440 ROT!24=(XR%+X%)*256:ROT!28=(YR%+Y%)*256
  450 SYS"OS_SpriteOp",256+56,SPR,N$,1,,8,ROT
  460 ENDPROC
