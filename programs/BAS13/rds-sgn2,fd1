   10 REM > RDS-SGN2        **** JAN VIBE DECEMBER 98 ****
   20 
   30 DIM MD% 64:MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=3:MD%!16=-1:MD%!20=-1:MODE MD%:OFF
   40 K%=10000:PC%=0:W%=260:W1%=W%+2
   50 
   60 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   70 DIM P%(K%),X%(K%),Y%(K%)
   80 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 PROCROT(25,20,2)
  120 
  130 *POINTER
  140 MOUSE STEP 2
  150 MOUSE RECTANGLE W%+100,0,2047-W%-200,1535
  160 MOUSE X%,Y%,B%
  170 REPEAT
  180 OX%=X%:OY%=Y%:MOUSE X%,Y%,B%
  190 CASE B% OF
  200 WHEN4:LINE OX%,OY%,X%,Y%
  210 WHEN2:CLS
  220 ENDCASE
  230 UNTIL B%=1
  240 FORY%=0TO1535STEP2
  250 FORX%=0TO2047STEP2
  260 IF POINT(X%,Y%)<>0 THEN
  270 X1%=X%-1024:Y1%=Y%-768:Z1%=640
  280 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  290 PC%+=1:PX%(PC%)=R(1)+800:PY%(PC%)=R(2)+968:PZ%(PC%)=R(3):PR%(PC%)=32
  300 ENDIF
  310 NEXT,
  320 CLS:*POINTER 0
  330 
  340 FORZ%=20TO1280STEP20:GCOLZ%/20
  350 FORN%=1TOPC%
  360 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  370 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  380 ENDIF
  390 NEXT,
  400 
  410 W1%=W%+2
  420 VDU26,24,0;0;W%;1535;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1535
  430 FORN%=1TO9000:GCOLRND(63):X%=RND(W%)+4:Y%=RND(1535):R%=RND(10)+5
  440 CIRCLE FILL X%-W%,Y%,R%:CIRCLE FILL X%,Y%,R%:CIRCLE FILL X%+W%,Y%,R%
  450 NEXT:VDU26:ORIGIN W1%,0
  460 
  470 ORIGIN 0,0:FORY%=0TO1535STEP2
  480 FORX%=0TO2047+W1%STEP2
  490 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  500 IF POINT(X%,Y%)=0THEN
  510 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  520 CASE X1%-X% OF
  530 WHEN 0,1,2:GCOL(POINT(X%-2,Y%)):POINTX%,Y%
  540 GCOL(POINT(X1%+2,Y%)):POINTX1%,Y%
  550 OTHERWISE
  560 GCOLPOINT(X%MODW%,(Y%+600)MOD1200):POINT X%,Y%
  570 ENDCASE
  580 ENDIF
  590 GCOLPOINT(X%,Y%):POINT X%+W1%-P%,Y%
  600 NEXT,
  610 *FX21,0
  620 Q$=GET$:IF Q$="S" OSCLI"SCREENSAVE RDSPIC"
  630 END
  640 
  650 DEFPROCROT(DX,DY,DZ)
  660 LOCAL T,SI,CO
  670 T()=C()
  680 A()=C():T=RADDX:CO=COST:SI=SINT
  690 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  700 T()=A()
  710 A()=C():T=RADDY:CO=COST:SI=SINT
  720 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  730 T()=A().T()
  740 A()=C():T=RADDZ:CO=COST:SI=SINT
  750 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  760 T()=A().T()
  770 ENDPROC
