   10 REM > RBOUNCE2
   20 
   30 MODE21:OFF
   40 DIM SPR &5FFFF:SPR!0=&5FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   50 DIM PATH% 100,JC% 15:JC%!0=0:JC%!4=0:JC%!8=0:JC%!12=0
   60 DIM ROT 32
   70 
   80 T=1.02:K%=4:A=1
   90 DIM X%(K%),Y%(K%),DX%(K%),DY%(K%),R%(K%),G%(K%),B%(K%),DR%(K%),DG%(K%),DB%(K%)
  100 FORN%=1TOK%
  110 X%(N%)=RND(1080)+100:Y%(N%)=RND(824)+100
  120 DX%(N%)=SGN(RND(1)-.5)*(RND(11)+3):DY%(N%)=SGN(RND(1)-.5)*(RND(11)+3)
  130 R%(N%)=RND(255):G%(N%)=RND(255):B%(N%)=RND(255)
  140 DR%(N%)=RND(5)+7:DG%(N%)=RND(5)+7:DB%(N%)=RND(5)+7
  150 NEXT
  160 
  170 REPEAT
  180 SYS"OS_SpriteOp",256+16,SPR,"SCR",0,0,0,1279,1023
  190 PROCROT(SPR,"SCR",640,512,640*T,512*T,A)
  200 IF RND(1)>.99 A=-A
  210 
  220 PROCLINE(X%(K%),Y%(K%),R%(K%),G%(K%),B%(K%),X%(1),Y%(1),R%(1),G%(1),B%(1),32)
  230 FORN%=1TOK%-1:N1%=N%+1
  240 PROCLINE(X%(N%),Y%(N%),R%(N%),G%(N%),B%(N%),X%(N1%),Y%(N1%),R%(N1%),G%(N1%),B%(N1%),32)
  250 NEXT
  260 FORN%=1TOK%
  270 F%=X%(N%)+DX%(N%):IF F%>1180 OR F%<100 THEN DX%(N%)=SGN(-DX%(N%))*(RND(11)+7)
  280 F%=Y%(N%)+DY%(N%):IF F%>924 OR F%<100 THEN DY%(N%)=SGN(-DY%(N%))*(RND(11)+7)
  290 F%=R%(N%)+DR%(N%):IF F%>255 OR F%<0 THEN DR%(N%)=SGN(-DR%(N%))*(RND(11)+9)
  300 F%=G%(N%)+DG%(N%):IF F%>255 OR F%<0 THEN DG%(N%)=SGN(-DG%(N%))*(RND(11)+9)
  310 F%=B%(N%)+DB%(N%):IF F%>255 OR F%<0 THEN DB%(N%)=SGN(-DB%(N%))*(RND(11)+9)
  320 G%(N%)+=DG%(N%):R%(N%)+=DR%(N%):Y%(N%)+=DY%(N%):X%(N%)+=DX%(N%):B%(N%)+=DB%(N%)
  330 NEXT
  340 UNTIL0
  350 
  360 DEFPROCLINE(X1,Y1,R1%,G1%,B1%,X2,Y2,R2%,G2%,B2%,B)
  370 LOCAL L,K%,N%,N1%,XA,YA,XB,YB,R%,G%,B%
  380 L=SQR((X2-X1)^2+(Y2-Y1)^2)
  390 K%=L/16:IF K%=0 K%=1
  400 XA=(X1+X2)/2:YA=(Y1+Y2)/2
  410 JC%!0=&010000:GCOL R1%,G1%,B1%:PROCL(X1,Y1,XA,YA,B)
  420 JC%!0=&000100:GCOL R2%,G2%,B2%:PROCL(XA,YA,X2,Y2,B)
  430 JC%!0=&000000
  440 FORN%=2TOK%:N1%=K%-N%
  450 XA=(X1*(N%-2)+X2*(N1%+2))/K%:YA=(Y1*(N%-2)+Y2*(N1%+2))/K%
  460 XB=(X1*N%+X2*N1%)/K%:YB=(Y1*N%+Y2*N1%)/K%
  470 R%=((N%-2)*R1%+(N1%+2)*R2%)/K%:G%=((N%-2)*G1%+(N1%+2)*G2%)/K%
  480 B%=((N%-2)*B1%+(N1%+2)*B2%)/K%:GCOL R%,G%,B%
  490 PROCL(XA,YA,XB,YB,B)
  500 NEXT
  510 XA=(X1*(K%-1)+X2)/K%:YA=(Y1*(K%-1)+Y2)/K%:XB=X1:YB=Y1
  520 R%=((K%-1)*R1%+R2%)/K%:G%=((K%-1)*G1%+G2%)/K%
  530 B%=((K%-1)*B1%+B2%)/K%:GCOL R%,G%,B%
  540 PROCL(XA,YA,XB,YB,B)
  550 ENDPROC
  560 
  570 DEFPROCL(X1,Y1,X2,Y2,B)
  580 LOCAL PP%,W
  590 X1=256*X1:Y1=256*Y1:X2=256*X2:Y2=256*Y2
  600 PATH%!PP%=2:PP%+=4:PATH%!PP%=X1:PP%+=4:PATH%!PP%=Y1:PP%+=4
  610 PATH%!PP%=8:PP%+=4:PATH%!PP%=X2:PP%+=4:PATH%!PP%=Y2:PP%+=4
  620 PATH%!PP%=0:PP%+=4:PATH%!PP%=PP%
  630 W=256*B
  640 SYS"Draw_Stroke",PATH%,0,0,0,W,JC%,0
  650 ENDPROC
  660 
  670 DEFPROCROT(SPR,N$,X%,Y%,SX%,SY%,A%)
  680 LOCAL XR%,YR%,CO,SI:CO=COSRADA%:SI=SINRADA%
  690 XR%=-SX%*CO+SY%*SI:YR%=SY%*CO+SX%*SI
  700 ROT!00=(XR%+X%)*256:ROT!04=(YR%+Y%)*256
  710 XR%=SX%*CO+SY%*SI:YR%=SY%*CO-SX%*SI
  720 ROT!08=(XR%+X%)*256:ROT!12=(YR%+Y%)*256
  730 XR%=SX%*CO-SY%*SI:YR%=-SY%*CO-SX%*SI
  740 ROT!16=(XR%+X%)*256:ROT!20=(YR%+Y%)*256
  750 XR%=-SX%*CO-SY%*SI:YR%=-SY%*CO+SX%*SI
  760 ROT!24=(XR%+X%)*256:ROT!28=(YR%+Y%)*256
  770 SYS"OS_SpriteOp",256+56,SPR,N$,1,,8,ROT
  780 ENDPROC
