   10 REM > STEREO3       **** JAN VIBE JANUAR 99 ****
   20 
   30 DIM MD% 64:MD%!00=1:MD%!04=1024:MD%!08=768:MD%!12=3:MD%!16=-1:MD%!20=-1:MODE MD%:OFF
   40 K%=80000:PC%=0:W%=280:W1%=W%+2:PF=2000:SF=1.5:ZMIN%=0:ZMAX%=0
   50 
   60 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   70 DIM P%(K%),X%(K%),Y%(K%)
   80 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 PROCROT(20,20,0)
  120 FORN%=0TO359STEP5:T=RADN%
  130 X%=500+500*SINT:Y%=500+500*COST
  140 PROCL(X%,Y%,0,40,X%,Y%,1000,40)
  150 PROCL(X%,Y%,1000,40,500,500,1000,40)
  160 NEXT
  170 
  180 IF ZMAX%-ZMIN%<2000 THEN ZMIN%=0:ZMAX%=2000
  190 ZS%=(ZMIN%-ZMAX%)/59:C%=4
  200 FORZ%=ZMAX%TOZMIN%STEPZS%:C%+=1:GCOLC%
  210 FORN%=1TOPC%
  220 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  230 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%)+1200,PY%(N%)+700,P%*SF
  240 ENDIF
  250 NEXT,
  260 
  270 W1%=W%+2
  280 VDU26,24,0;0;W%;1535;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1535
  290 FORN%=1TO3000:X%=RND(W%)+4:Y%=RND(1535):R%=RND(16)+8:F%=RND(26)
  300 FORM%=7TO0STEP-1:PROCF(F%,7-M%)
  310 CIRCLE FILL X%-W%,Y%,R%*M%/7:CIRCLE FILL X%,Y%,R%*M%/7:CIRCLE FILL X%+W%,Y%,R%*M%/7
  320 NEXT,:VDU26:ORIGIN W1%,0
  330 
  340 ORIGIN 0,0:FORY%=0TO1535STEP2
  350 FORX%=0TO2047+W1%STEP2
  360 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  370 IF POINT(X%,Y%)=0THEN
  380 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  390 S%=X1%-X%:IF S%<=6 THEN
  400 IF S%>=0 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%,Y%
  410 IF S%>=2 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%,Y%
  420 IF S%>=4 GCOLPOINT(X%-2,Y%) TINT TINT(X%-2,Y%):POINTX%+2,Y%
  430 IF S%>=6 GCOLPOINT(X1%+2,Y%) TINT TINT(X1%+2,Y%):POINTX1%-2,Y%
  440 ELSE
  450 GCOLPOINT(X%MODW%,(Y%+768)MOD1536) TINT TINT(X%MODW%,(Y%+768)MOD1536):POINT X%,Y%
  460 ENDIF
  470 ENDIF
  480 GCOLPOINT(X%,Y%) TINT TINT(X%,Y%):POINT X%+W1%-P%,Y%
  490 NEXT,
  500 *FX21,0
  510 Q$=GET$:IF Q$="S" OSCLI"SCREENSAVE RDSPIC"
  520 END
  530 
  540 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  550 LOCAL N%,D%,B%,P
  560 K(1)=(X1%-500)*SF:K(2)=(Y1%-500)*SF:K(3)=(Z1%-500)*SF:K(4)=1:R()=K().T()
  570 X1%=R(1):Y1%=R(2):Z1%=R(3)+1200
  580 IF Z1%<ZMIN% ZMIN%=Z1%
  590 IF Z1%>ZMAX% ZMAX%=Z1%
  600 P=1+Z1%/PF:X1%=X1%/P:Y1%=Y1%/P:R1%=R1%/P
  610 K(1)=(X2%-500)*SF:K(2)=(Y2%-500)*SF:K(3)=(Z2%-500)*SF:K(4)=1:R()=K().T()
  620 X2%=R(1):Y2%=R(2):Z2%=R(3)+1200
  630 IF Z2%<ZMIN% ZMIN%=Z2%
  640 IF Z2%>ZMAX% ZMAX%=Z2%
  650 P=1+Z2%/PF:X2%=X2%/P:Y2%=Y2%/P:R2%=R2%/P
  660 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  670 IF R1%<R2% B%=4*D%/R1% ELSE B%=4*D%/R2%
  680 IF B%<>0 THEN
  690 FOR N%=0TOB%:PC%+=1
  700 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%
  710 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%
  720 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%
  730 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  740 NEXT
  750 ELSE
  760 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  770 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  780 ENDIF
  790 ENDPROC
  800 
  810 DEFPROCROT(DX,DY,DZ)
  820 LOCAL T,SI,CO
  830 T()=C()
  840 A()=C():T=RADDX:CO=COST:SI=SINT
  850 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  860 T()=A()
  870 A()=C():T=RADDY:CO=COST:SI=SINT
  880 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  890 T()=A().T()
  900 A()=C():T=RADDZ:CO=COST:SI=SINT
  910 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  920 T()=A().T()
  930 ENDPROC
  940 
  950 DEFPROCF(F%,I%)
  960 LOCAL R%,G%,B%
  970 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  980 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
  990 GCOL F% TINT (64*I%)MOD256
 1000 ENDPROC
