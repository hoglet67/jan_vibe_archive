   10 REM > TXTMAZE
   20 
   30 MODE31:OFF
   40 SIZE%=71:C%=SIZE%/2+1
   50 DIM A%(SIZE%,SIZE%)
   60 FORI%=1TOSIZE%:FORJ%=1TOSIZE%:A%(I%,J%)=1:NEXT,
   70 
   80 C%=SIZE%/2+1:A%(C%,C%)=0
   90 PROCMAZE(C%,C%)
  100 A%(C%,1)=0:A%(C%,SIZE%)=0
  110 
  120 FORI%=1TOSIZE%:FORJ%=1TOSIZE%
  130 IF A%(I%,J%)=1 PRINT"#"; ELSE PRINT" ";
  140 NEXT:PRINT:NEXT
  150 END
  160 
  170 DEFPROCMAZE(X%,Y%)
  180 LOCAL N%
  190 FOR N%=1TO8
  200 CASE RND(4) OF
  210 WHEN1:
  220 IF X%-2>=1 THEN
  230 IF A%(X%-2,Y%)=1 A%(X%-1,Y%)=0:A%(X%-2,Y%)=0:PROCMAZE(X%-2,Y%)
  240 ENDIF
  250 WHEN2:
  260 IF X%+2<=SIZE% THEN
  270 IF A%(X%+2,Y%)=1 A%(X%+1,Y%)=0:A%(X%+2,Y%)=0:PROCMAZE(X%+2,Y%)
  280 ENDIF
  290 WHEN3:
  300 IF Y%-2>=1 THEN
  310 IF A%(X%,Y%-2)=1 A%(X%,Y%-1)=0:A%(X%,Y%-2)=0:PROCMAZE(X%,Y%-2)
  320 ENDIF
  330 WHEN4:
  340 IF Y%+2<=SIZE% THEN
  350 IF A%(X%,Y%+2)=1 A%(X%,Y%+1)=0:A%(X%,Y%+2)=0:PROCMAZE(X%,Y%+2)
  360 ENDIF
  370 ENDCASE
  380 NEXT
  390 ENDPROC
