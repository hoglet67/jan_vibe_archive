   10 REM > CALC
   20 
   30 MODE31
   40 PRINT'"MULTIPLICATION OF BIG INTEGER NUMBERS"'
   50 FORN%=1TO23:A$=A$+STR$(RND(10)-1):B$=B$+STR$(RND(10)-1)
   60 PRINTA$" * "B$" = ";FNMUL(A$,B$):NEXT
   70 FORN%=1TO22:A$=RIGHT$(A$,LEN(A$)-1):B$=RIGHT$(B$,LEN(B$)-1)
   80 PRINTA$" * "B$" = ";:PRINTFNMUL(A$,B$):NEXT:PRINT
   90 END
  100 
  110 DEFFNMUL(A$,B$)
  120 LOCAL T$(),W$,R$,N%
  130 DIM T$(9):W$="0":R$="0"
  140 FORN%=1TO9:W$=FNADD(W$,A$):T$(N%)=W$:NEXT
  150 FORN%=LEN(B$)TO1STEP-1:W%=VALMID$(B$,N%,1):W$=T$(W%)
  160 W$=W$+STRING$(LEN(B$)-N%,"0")
  170 R$=FNADD(R$,W$):NEXT
  180 =R$
  190 
  200 DEFFNADD(A$,B$)
  210 LOCAL R$,C%,N%,W%,P%
  220 P%=LEN(A$):IF LEN(B$)>P% P%=LEN(B$)
  230 A$=RIGHT$(STRING$(P%,"0")+A$,P%):B$=RIGHT$(STRING$(P%,"0")+B$,P%)
  240 FORN%=P%TO1STEP-1
  250 W%=VALMID$(A$,N%,1)+VALMID$(B$,N%,1)+C%:C%=W%DIV10:W%=W%MOD10:R$=STR$(W%)+R$
  260 NEXT
  270 IF C%<>0 R$=STR$(C%)+R$
  280 =R$
