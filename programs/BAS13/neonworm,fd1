   10 REM > NEONWORM5  *** JAN VIBE JULY 1998 ***
   20 
   30 MODE160:MODE32:OFF
   40 S1%=1:S2%=2:L%=20:S=2:F%=7
   50 R1%=RND(255):G1%=RND(255):B1%=RND(255)
   60 R2%=RND(255):G2%=RND(255):B2%=RND(255)
   70 R3%=RND(255):G3%=RND(255):B3%=RND(255)
   80 R4%=RND(255):G4%=RND(255):B4%=RND(255)
   90 DIMX%(L%),Y%(L%),R%(L%)
  100 FORN%=1TOL%:R%(N%)=S*(L%-N%+2):NEXT
  110 X1%=RND(1088)+256:Y1%=RND(688)+256
  120 DX1%=RND(12)*SGN(RND(1)-.5):DY1%=RND(32)*SGN(RND(1)-.5)
  130 X2%=RND(1088)+256:Y2%=RND(688)+256
  140 DX2%=RND(12)*SGN(RND(1)-.5):DY2%=RND(32)*SGN(RND(1)-.5)
  150 
  160 ONERRORGOTO660
  170 
  180 REPEAT
  190 SYS 6,112,S1%:SYS 6,113,S2%:SWAP S1%,S2%:COLOUR176:CLS
  200 H%=X1%+DX1%:IF H%<256 OR H%>1344 DX1%=RND(32)*SGN(-DX1%)
  210 H%=Y1%+DY1%:IF H%<256 OR H%>944 DY1%=RND(32)*SGN(-DY1%)
  220 X1%+=DX1%:Y1%+=DY1%
  230 IF X2%<X1% AND DX2%<24 DX2%+=1
  240 IF X2%>X1% AND DX2%>-24 DX2%-=1
  250 IF Y2%<Y1% AND DY2%<24 DY2%+=1
  260 IF Y2%>Y1% AND DY2%>-24 DY2%-=1
  270 X2%+=DX2%:Y2%+=DY2%
  280 X%(1)=X2%:Y%(1)=Y2%
  290 FORI%=2TOL%
  300 X%=X%(I%-1)-X%(I%):Y%=Y%(I%-1)-Y%(I%)
  310 D=SQR(X%^2+Y%^2):IF D=0 D=1
  320 P=(D-R%(I%)-R%(I%-1))/D:X%(I%)+=X%*P:Y%(I%)+=Y%*P
  330 NEXT
  340 R1%=FNA(R1%):WR1%=FNB(R1%):G1%=FNA(G1%):WG1%=FNB(G1%):B1%=FNA(B1%):WB1%=FNB(B1%)
  350 R2%=FNA(R2%):WR2%=FNB(R2%):G2%=FNA(G2%):WG2%=FNB(G2%):B2%=FNA(B2%):WB2%=FNB(B2%)
  360 R3%=FNA(R3%):WR3%=FNB(R3%):G3%=FNA(G3%):WG3%=FNB(G3%):B3%=FNA(B3%):WB3%=FNB(B3%)
  370 R4%=FNA(R4%):WR4%=FNB(R4%):G4%=FNA(G4%):WG4%=FNB(G4%):B4%=FNA(B4%):WB4%=FNB(B4%)
  380 FORM%=F%TO1STEP-1
  390 RA%=(M%*WR1%+(F%-M%)*WR2%)/F%:GA%=(M%*WG1%+(F%-M%)*WG2%)/F%:BA%=(M%*WB1%+(F%-M%)*WB2%)/F%
  400 RB%=(M%*WR3%+(F%-M%)*WR4%)/F%:GB%=(M%*WG3%+(F%-M%)*WG4%)/F%:BB%=(M%*WB3%+(F%-M%)*WB4%)/F%
  410 FORN%=1TOL%-1:W%=2*M%*R%(N%)/F%
  420 R%=(N%*RA%+(L%-N%)*RB%)/L%:G%=(N%*GA%+(L%-N%)*GB%)/L%:B%=(N%*BA%+(L%-N%)*BB%)/L%
  430 GCOL R%,G%,B%:PROCL(X%(N%),Y%(N%),X%(N%+1),Y%(N%+1),W%)
  440 NEXT,
  450 UNTIL0
  460 
  470 DEFPROCL(X1,Y1,X2,Y2,W)
  480 LOCAL L,T1,T2
  490 L=SQR((X1-X2)^2+(Y1-Y2)^2):IF X1>X2 L=-L
  500 IF L<>0 THEN
  510 T1=ASN((Y1-Y2)/L):T2=T1+PI:W=W/2
  520 MOVEX1+W*SINT1,Y1+W*COST1:MOVEX1+W*SINT2,Y1+W*COST2
  530 PLOT&75,X2+W*SINT2,Y2+W*COST2
  540 CIRCLE FILL X1,Y1,W:CIRCLE FILL X2,Y2,W
  550 ELSE
  560 CIRCLE FILL X1,Y1,W
  570 ENDIF
  580 ENDPROC
  590 
  600 DEFFNA(C%)
  610 =(C%+RND(15))MOD512
  620 
  630 DEFFNB(C%)
  640 =ABS(C%-256)
  650 
  660 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
