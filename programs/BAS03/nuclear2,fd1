   10 REM > NUCLEAR2
   20 
   30 MODE137:MODE9:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:R%=RND(-TIME):K%=50
   60 
   70 FORN%=1TO6:COLOURN%,0,32*N%+63,0:NEXT
   80 FORN%=1TO4:COLOURN%+6,48*N%+63,0,0:NEXT
   90 FORN%=1TO5:COLOURN%+10,255,50*(6-N%),0:NEXT
  100 
  110 ONERRORGOTO730
  120 *SCH. ATOM
  130 *SCH. NEUTRON
  140 *SCH. BANG
  150 
  160 DIM X%(K%),Y%(K%),X1%(K%*3),Y1%(K%*3),DX%(K%*3),DY%(K%*3)
  170 FORN%=1TOK%:X%(N%)=RND(1024)+128:Y%(N%)=RND(768)+128:NEXT
  180 X1%(1)=0:Y1%(1)=512:DX%(1)=12:DY%(1)=0:P%=1
  190 COLOUR0,0,0,160:VDU19,0,24,0,0,160:GCOL8,0:COLOUR6
  200 *CHANNELVOICE 1 8
  210 VOICES 1
  220 
  230 ONERRORGOTO990
  240 REPEAT
  250 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  260 
  270 *SCH. ATOM
  280 FORN%=1TOK%
  290 IF X%(N%)>0 THEN
  300   X%(N%)=(X%(N%)+RND(11)+1274)MOD1280
  310   Y%(N%)=(Y%(N%)+RND(11)+1018)MOD1024
  320   PLOT&ED,X%(N%)-20,Y%(N%)-20
  330 ENDIF
  340 NEXT
  350 *SCH. NEUTRON
  360 
  370 B%=0
  380 FORN%=1TOP%
  390   IF X1%(N%)>=0 THEN
  400     X1%(N%)+=DX%(N%):Y1%(N%)+=DY%(N%)
  410     IF X1%(N%)>=1280 OR X1%(N%)<0 X1%(N%)=-1
  420     IF Y1%(N%)>=1024 OR Y1%(N%)<0 X1%(N%)=-1
  430     C%=POINT(X1%(N%),Y1%(N%))
  440     IF C%>=1 AND C%<=6 THEN
  450       SOUND 1,-8,10,20
  460       FORM%=1TOK%
  470         IF X1%(N%)>=0 THEN
  480           D%=(X%(M%)-X1%(N%))^2+(Y%(M%)-Y1%(N%))^2
  490           IF D%<=800 E%=M%:M%=K%
  500         ENDIF
  510       NEXT
  520       *SCH. BANG
  530       PLOT&ED,X%(E%)-48,Y%(E%)-48
  540       *SCH. NEUTRON
  550       V=RADRND(360):H%=RND(17)+11
  560       DX%(N%)=H%*SINV:DY%(N%)=H%*COSV
  570       FORM%=1TO2
  580         P%+=1
  590         X1%(P%)=X%(E%):Y1%(P%)=Y%(E%)
  600         V=RADRND(360):H%=RND(17)+11
  610         DX%(P%)=H%*SINV:DY%(P%)=H%*COSV
  620       NEXT
  630       X%(E%)=-1
  640     ENDIF
  650   IF X1%(N%)>=0 PLOT&ED,X1%(N%)-12,Y1%(N%)-12:B%+=1
  660   ENDIF
  670 NEXT
  680 
  690 UNTIL B%=0
  700 
  710 SYS"OS_Byte",113,S2%:ON:END
  720 
  730 IF ERR=134 THEN
  740   *SNEW
  750   FORN%=1TO6:GCOLN%:CIRCLE FILL 20,20,4*(6-N%):NEXT
  760   PROCMASK(40,40,"ATOM"):CLS
  770   FORN%=1TO4:GCOLN%+6:CIRCLE FILL 12,12,4*(4-N%):NEXT
  780   PROCMASK(24,24,"NEUTRON"):CLS
  790   FORN%=1TO5:GCOLN%+10
  800   R%=10*(5-N%):FORM%=1TO40:T=RADRND(360):R1%=R%+RND(8)
  810   LINE 48,48,R1%*SINT+48,R1%*COST+48:NEXT,
  820   PROCMASK(96,96,"BANG")
  830 ELSE
  840   REPORT:PRINT" at line "STR$ERL:ON:END
  850 ENDIF
  860 GOTO 160
  870 
  880 DEFPROCMASK(X1%,Y1%,N$)
  890 LOCAL X%,Y%
  900 MOVE0,0:MOVE X1%,Y1%:OSCLI"SGET "+N$
  910 SYS"OS_SpriteOp",29,0,N$
  920 FORY%=0TOY1%STEP4
  930 FORX%=0TOX1%STEP4:S%=1
  940 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  950 SYS"OS_SpriteOp",44,0,N$,X%/4,Y%/4,S%
  960 NEXT,
  970 ENDPROC
  980 
  990 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON:END
 1000 
 1010 REM*********************
 1020 REM* Jan Vibe april 90 *
 1030 REM*********************
