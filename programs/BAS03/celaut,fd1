   10 REM > CELAUT
   20 
   30 MODE137:MODE9:ORIGIN 128,0:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2
   60 
   70 DIMA%(2,50,50):P%=1
   80 FOR N%=1TO15:COLOUR N%,RND(255),RND(255),RND(255):NEXT
   90 FORN%=1TO8:X%=RND(25):Y%=RND(25):FORM%=1TO2
  100 A%(P%,X%,Y%)=15:A%(P%,50-X%,Y%)=15
  110 A%(P%,X%,50-Y%)=15:A%(P%,50-X%,50-Y%)=15
  120 SWAP X%,Y%:NEXT,
  130 
  140 ONERRORGOTO360
  150 REPEAT
  160 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  170 FORY%=1TO49
  180 FORX%=1TO49
  190 GCOL A%(P%,X%,Y%)
  200 RECTANGLE FILL 20*X%,20*Y%,16
  210 NEXT,
  220 Q%=P%:P%=3-P%
  230 FORY%=1TO49
  240 FORX%=1TO49
  250 K%=0:C%=A%(Q%,X%,Y%)
  260 IFC%>0 C%-=1
  270 A%(P%,X%,Y%)=C%
  280 FORJ%=-1TO1
  290 FORI%=-1TO1
  300 IF A%(Q%,X%+I%,Y%+J%)=15 K%+=1
  310 NEXT,
  320 IF K%=2 AND A%(Q%,X%,Y%)<>15 A%(P%,X%,Y%)=15
  330 NEXT,
  340 UNTIL0
  350 
  360 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
