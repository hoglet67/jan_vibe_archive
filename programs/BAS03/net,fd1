   10 REM > NET
   20 
   30 MODE128:MODE0:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1$="1":S2$="2":K%=32:K2%=K%/2
   60 DIM SPR &79000
   70 DIM XP%(K%,K%),YP%(K%,K%),A%(K%,K%),B%(K%,K%)
   80 ORIGIN 0,256
   90 
  100 SPR!0=&78FFF:SPR!4=0:SPR!8=16:SPR!12=16
  110 
  120 ONERRORGOTO560
  130 
  140 CLS
  150 FORY%=0TOK%:PY%=16*Y%
  160 FORX%=0TOK%:PX%=32*X%+8*Y%
  170 XP%(X%,Y%)=PX%
  180 YP%(X%,Y%)=PY%
  190 A%(X%,Y%)=SQR((X%-K2%)^2+(Y%-K2%)^2)*35
  200 NEXT,
  210 
  220 FORI%=1TO23:S$=STR$I%
  230 
  240 FORY%=0TOK%
  250 FORX%=0TOK%
  260 Z=15/(SQR(1+(X%-K2%)^2+(Y%-K2%)^2)/K%)
  270 A%(X%,Y%)=(A%(X%,Y%)+345)MOD360
  280 B%(X%,Y%)=Z*COSRAD(A%(X%,Y%))
  290 NEXT,
  300 
  310 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  320 FORY%=K%TO1STEP-1
  330 FORX%=0TOK%-1
  340 GCOL0
  350 MOVE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%)
  360 MOVE XP%(X%+1,Y%),YP%(X%+1,Y%)+B%(X%+1,Y%)
  370 PLOT85,XP%(X%,Y%-1),YP%(X%,Y%-1)+B%(X%,Y%-1)
  380 PLOT85,XP%(X%+1,Y%-1),YP%(X%+1,Y%-1)+B%(X%+1,Y%-1)
  390 GCOL1
  400 LINE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%),XP%(X%+1,Y%),YP%(X%+1,Y%)+B%(X%+1,Y%)
  410 LINE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%),XP%(X%,Y%-1),YP%(X%,Y%-1)+B%(X%,Y%-1)
  420 NEXT,
  430 FORN%=0TOK%-1
  440 LINE XP%(N%,0),YP%(N%,0)+B%(N%,0),XP%(N%+1,0),YP%(N%+1,0)+B%(N%+1,0)
  450 LINE XP%(K%,N%),YP%(K%,N%)+B%(K%,N%),XP%(K%,N%+1),YP%(K%,N%+1)+B%(K%,N%+1)
  460 NEXT
  470 SYS "OS_SpriteOp",256+16,SPR,S$,0,0,-256,1279,767
  480 NEXT
  490 
  500 REPEAT
  510 FORI%=1TO23:S$=STR$I%
  520 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  530 SYS "OS_SpriteOp",256+34,SPR,S$,0,-256,0:NEXT
  540 UNTIL0
  550 
  560 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON:END
  570 
  580 REM *--------------------------------------*
  590 REM !                                      !
  600 REM !  BlGCOLrerRETURNvsprogram Jan Vibe januar 90 !
  610 REM !                                      !
  620 REM *--------------------------------------*
