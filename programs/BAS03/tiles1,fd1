   10 REM > TILES1
   20 
   30 MODE15:OFF
   40 PROCA(0,0,1280,1024)
   50 END
   60 
   70 DEFPROCA(X1%,Y1%,X3%,Y3%)
   80 LOCAL A1%,A2%,X2%,Y2%
   90 A1%=X3%-X1%:A2%=Y3%-Y1%
  100 IF A1%<=80 AND A2%<=80 PROCB(X1%+2,Y1%+4,X3%,Y3%):ENDPROC
  110 IF A1%>A2% THEN
  120   X2%=X1%+RND(A1%-32)+16
  130   PROCA(X1%,Y1%,X2%,Y3%)
  140   PROCA(X2%,Y1%,X3%,Y3%)
  150 ELSE
  160   Y2%=Y1%+RND(A2%-32)+16
  170   PROCA(X1%,Y1%,X3%,Y2%)
  180   PROCA(X1%,Y2%,X3%,Y3%)
  190 ENDIF
  200 ENDPROC
  210 
  220 DEFPROCB(X1%,Y1%,X3%,Y3%)
  230 LOCALN%,C%,H,B,XC,YC,F
  240 XC=(X1%+X3%)/2:YC=(Y1%+Y3%)/2
  250 B=(X3%-X1%):H=(Y3%-Y1%)
  260 IF RND(1)>.2 THEN
  270   C%=RND(27)-1
  280   FORN%=8TO1STEP-1:PROCF(C%,8-N%):F=N%/8
  290   RECTANGLE FILL XC-(B/2)*F,YC-(H/2)*F,B*F,H*F
  300   NEXT
  310 ELSE
  320   C%=RND(7)
  330   FORN%=12TO1STEP-1:PROCFARVE(C%,12-N%):F=N%/12
  340   RECTANGLE FILL XC-(B/2)*F,YC-(H/2)*F,B*F,H*F
  350   NEXT
  360 ENDIF
  370 ENDPROC
  380 
  390 REM********************************
  400 REM* 27 FARVER MED 8 INTENSITETER *
  410 REM********************************
  420 DEFPROCF(F%,I%)
  430 LOCAL R%,G%,B%
  440 B%=F%DIV9:F%=F%-9*B%
  450 G%=F%DIV3:F%=F%-3*G%
  460 R%=F%
  470 F%=R%+4*G%+16*B%
  480 IF I%>=4 F%=F%+&15
  490 GCOL F% TINT (64*I%)MOD256
  500 ENDPROC
  510 
  520 REM********************************
  530 REM* 7 FARVER MED 12 INTENSITETER *
  540 REM********************************
  550 DEFPROCFARVE(F%,I%)
  560 LOCALN%,K%
  570 DATA1,4,5,16,17,20,21
  580 RESTORE570:FORN%=1TOF%:READK%:NEXT
  590 I%+=4:GCOL (I%DIV4)*K% TINT (I%MOD4)<<6
  600 ENDPROC
