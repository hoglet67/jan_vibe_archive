   10 REM > STAR
   20 
   30 MODE137:MODE9:ORIGIN 640,512:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:A%=0:C%=0:K%=10:S%=360/K%
   60 DIMX(K%),Y(K%),X1%(K%,15),Y1%(K%,15)
   70 
   80 FORN%=1TOK%:T=RAD(S%*N%):X(N%)=SINT:Y(N%)=COST:NEXT
   90 FORN%=1TO15:COLOUR N%,16*N%,0,16*(16-N%):NEXT
  100 
  110 ONERRORGOTO300
  120 REPEAT
  130 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  140 
  150 A%=(A%+8)MOD360:C%=(C%+1)MOD16
  160 Z=160*SINRAD(5*S%*K%+A%)+340
  170 MOVE Z*X(K%),Z*Y(K%)
  180 FORN%=1TOK%:Z=160*SINRAD(5*S%*N%+A%)+340
  190 X1%(N%,C%)=Z*X(N%):Y1%(N%,C%)=Z*Y(N%)
  200 NEXT
  210 FORN%=0TO15:GCOLN%
  220 C1%=(C%+N%)MOD16
  230 MOVEX1%(K%,C1%),Y1%(K%,C1%)
  240 FORM%=1TOK%
  250 DRAWX1%(M%,C1%),Y1%(M%,C1%)
  260 NEXT,
  270 
  280 UNTIL0
  290 
  300 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
