   10 REM > LYSAVIS
   20 
   30 MODE13
   40 A$="Archimedes"
   50 T$="Kender i den om tændstikken? Nå ikke, jamen så stryger vi den. HA HA"
   60 T$=T$+"          ":TP$="          ":T%=0:TX%=0
   70 DIMA1%(100),A2%(100),T1%(100),T2%(100),C%(100)
   80 L%=LENA$:S1$="1":S2$="2"
   90 FORN%=1TOL%
  100 A1%(N%)=RND(360)-1:A2%(N%)=RND(360)-1
  110 T1%(N%)=RND(13)+7:T2%(N%)=RND(13)+7
  120 C%(N%)=RND(63)
  130 NEXT:VDU5:OFF
  140 ONERRORGOTO360
  150 
  160 REPEAT
  170 X%=320:C%(RND(L%))=RND(63)
  180 FORN%=1TOL%
  190 A1%(N%)=(A1%(N%)+T1%(N%))MOD360
  200 A2%(N%)=(A2%(N%)+T2%(N%))MOD360
  210 Z1%=8*(SINRAD(A1%(N%))+1)+8
  220 Z2%=16*(SINRAD(A2%(N%))+1)+8
  230 Y%=800+2*Z2%:MOVEX%,Y%
  240 VDU23,17,7,6,Z1%;Z2%|
  250 GCOLC%(N%):PRINTMID$(A$,N%,1);
  260 X%+=4*Z1%:NEXT
  270 
  280 TX%=(TX%+32)MOD128:GCOL63
  290 IF TX%=0 T%=T%MODLENT$+1:TP$=RIGHT$(TP$,9)+MID$(T$,T%,1)
  300 VDU23,17,7,6,32;64|:MOVE-TX%,512
  310 VDU24,126;256;1150;512;:PRINTTP$:VDU26
  320 GCOL03:RECTANGLE126,256,1024,260
  330 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  340 UNTIL0
  350 
  360 VDU4,20,26:OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
