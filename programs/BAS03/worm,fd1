   10 REM > WORM
   20 
   30 REM Dette program er inspireret af en artikel i
   40 REM Scientific American november 1973
   50 
   60 MODE15:ORIGIN 128,0:OFF
   70 DIM A%(50),D%(50),C%(50):R%=RND(-TIME)
   80 REPEAT
   90 K%=RND(50)
  100 REPEAT:F%=RND(357)+2:UNTILF%*INT(360/F%)=360:G%=360/F%
  110 FORN%=1TOK%:A%(N%)=RND(F%)*G%:D%(N%)=RND(10):C%(N%)=RND(63):NEXT
  120 X=0:Y=0:X1=0:Y1=0:X2=0:Y2=0
  130 V%=0
  140 REPEAT
  150 VG%=V%
  160 FORN%=1TOK%
  170 V%=(V%+A%(N%))MOD360:T=RADV%
  180 DX=D%(N%)*SINT:DY=D%(N%)*COST
  190 X+=DX:Y+=DY
  200 IF X<X1 X1=X
  210 IF X>X2 X2=X
  220 IF Y<Y1 Y1=Y
  230 IF Y>Y2 Y2=Y
  240 NEXT
  250 UNTIL V%=0 OR V%=VG%
  260 
  270 IF V%<>VG% THEN
  280 CLS
  290 IF X2-X1>Y2-Y1 THEN Z=800/(X2-X1) ELSE Z=800/(Y2-Y1)
  300 XC=(X1+X2)/2:YC=(Y1+Y2)/2
  310 X=512-Z*XC:Y=512-Z*YC
  320 V%=0:MOVE X,Y
  330 REPEAT
  340 VG%=V%
  350 FORN%=1TOK%
  360 V%=(V%+A%(N%))MOD360:T=RADV%
  370 DX=Z*D%(N%)*SINT:DY=Z*D%(N%)*COST
  380 X+=DX:Y+=DY:GCOLC%(N%):DRAW X,Y
  390 NEXT
  400 UNTIL V%=0
  410 Q=INKEY(200)
  420 *FX15,0
  430 ENDIF
  440 
  450 UNTIL0
