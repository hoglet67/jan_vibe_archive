   10 REM > TRAVEL2
   20 
   30 MODE13:OFF:ORIGIN 640,512
   40 SYS"OS_UpdateMEMC",64,64
   50 ONERRORGOTO310
   60 
   70 S1$="1":S2$="2":K%=20:S=1.15
   80 DIM X(K%),Y(K%),Z(K%),C%(K%),S%(K%),T%(7)
   90 FORN%=1TOK%
  100 X(N%)=RND(1280)-640:Y(N%)=RND(1024)-512
  110 Z(N%)=RND(5)+1:C%(N%)=0:S%(N%)=RND(7):NEXT
  120 FORN%=1TO7:READT%(N%):NEXT
  130 DATA1,4,5,16,17,20,21
  140 
  150 REPEAT
  160 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  170 FORN%=1TOK%
  180 X(N%)=X(N%)*S:Y(N%)=Y(N%)*S:Z(N%)=Z(N%)*S
  190 IF ABSX(N%)>768 OR ABSY(N%)>640 THEN
  200 X(N%)=RND(1280)-640:Y(N%)=RND(1024)-512
  210 Z(N%)=RND(5)+1:C%(N%)=0:S%(N%)=RND(7)
  220 ENDIF
  230 PROCFARVE(S%(N%),C%(N%)):IF C%(N%)<11 C%(N%)+=1
  240 CIRCLE FILL X(N%),Y(N%),4*(Z(N%)DIV4)+4:NEXT
  250 UNTIL0
  260 
  270 DEFPROCFARVE(F%,I%)
  280 I%+=4:GCOL (I%DIV4)*T%(F%) TINT (I%MOD4)<<6
  290 ENDPROC
  300 
  310 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
