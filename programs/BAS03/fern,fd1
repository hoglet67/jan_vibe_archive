   10 REM > FERN
   20 
   30 MODE128:MODE0:OFF
   40 S1$="1":S2$="2":K%=32:K2%=K%/2
   50 DIM SPR &79000:SPR!0=&78FFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 COLOUR0,80,80,80:COLOUR1,0,255,0:VDU19,0,24,80,80,80
   70 
   80 ONERRORGOTO270
   90 FORI%=1TO23:S$=STR$I%
  100 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  110 T=20*SINRAD(7.5*I%+90):PROCA(640,0,160,0,1)
  120 SYS "OS_SpriteOp",256+16,SPR,S$,0,0,-0,1279,1023
  130 NEXT
  140 
  150 A%=43:REPEAT:A%=(A%+1)MOD44:B%=ABS(22-A%)+1:S$=STR$B%
  160 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  170 SYS "OS_SpriteOp",256+34,SPR,S$,0,0,0:W%=INKEY(2)
  180 UNTIL0
  190 
  200 DEFPROCA(X,Y,Z,A,M%)
  210 IFZ<=3 ENDPROC
  220 LOCAL X1,Y1:X1=X+Z*SINRADA:Y1=Y+Z*COSRADA:LINEX,Y,X1,Y1
  230 PROCA(X1,Y1,.85*Z,A+T*M%,M%):PROCA(X1,Y1,.35*Z,A-60*M%,M%)
  240 PROCA(X1,Y1,.35*Z,A+60*M%,-M%)
  250 ENDPROC
  260 
  270 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON:END
