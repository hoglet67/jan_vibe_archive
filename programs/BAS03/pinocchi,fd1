   10 REM > PINOCCHIO
   20 
   30 A$="Hvorfor går brandmænd med røde seler ?  Ellers taber de bukserne!!  "
   40 
   50 MODE141:MODE13:OFF
   60 SYS"OS_UpdateMEMC",64,64
   70 
   80 DIMLX%(4,64),LY%(4,64),C%(4),SP$(4),B% 9
   90 S1%=1:S2%=2:L%=0
  100 
  110 ONERRORGOTO630
  120 FORN%=0TO26
  130 OSCLI"SCH. "+STR$(N%)
  140 NEXT
  150 
  160 COLOUR165 TINT0:GCOL8,0
  170 ONERRORGOTO920
  180 REPEAT
  190 
  200 FORN%=2TO4:N1%=N%-1
  210   FORM%=1TOC%(N%)
  220     LX%(N1%,M%)=LX%(N%,M%)
  230     LY%(N1%,M%)=LY%(N%,M%)
  240   NEXT
  250   C%(N1%)=C%(N%)
  260   SP$(N1%)=SP$(N%)
  270 NEXT
  280 L%=L%MODLENA$+1
  290 PROCLET(MID$(A$,L%,1))
  300 
  310 FORN%=0TO-512STEP-24
  320 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  330   FORM%=1TO4:F%=(M%-1)*512
  340     IF C%(M%)<>0 THEN
  350       OSCLI"*SCH."+SP$(M%)
  360       FORI%=1TOC%(M%)
  370         PX%=LX%(M%,I%)+N%+F%
  380         IF PX%<1280 OR PX%<-64 PLOT&ED,PX%,LY%(M%,I%)
  390       NEXT
  400     ENDIF
  410   NEXT
  420 NEXT
  430 
  440 UNTIL0
  450 
  460 DEFPROCLET(T$)
  470 LOCAL C%,I%,M%,L%,X%,Y%
  480 ?B%=ASC(T$)
  490 SYS"OS_Word",10,B%
  500 FORI%=1TO8
  510 L%=128
  520 FORM%=1TO8
  530 IFL%AND(B%?I%) THEN
  540   C%+=1
  550   LX%(4,C%)=64*M%
  560   LY%(4,C%)=64*(8-I%)+256
  570 ENDIF
  580 L%=L%/2:NEXT,
  590 SP$(4)=STR$(RND(27)-1)
  600 C%(4)=C%
  610 ENDPROC
  620 
  630 IF ERR=134 PROCSPR:GOTO160
  640 REPORT:PRINT" at line "STR$ERL:END
  650 
  660 DEFPROCSPR
  670 LOCAL X%,Y%,X1%,Y1%,X2%,Y2%,R1%,R2%,B%,C%,S%,B$
  680 *SNEW
  690 FORC%=0TO26:B$=STR$C%
  700 X1%=40:Y1%=50:R1%=60
  710 X2%=32:Y2%=32:R2%=32
  720 GCOL37 TINT0:RECTANGLEFILL0,0,2*X2%
  730 FORY%=-R2%TOR2%STEP4:B%=4*(SQR(R2%^2-Y%^2)DIV4)
  740 FORX%=-B%TOB%STEP4
  750 PROCF(C%,8-SQR((X2%+X%-X1%)^2+(Y2%+Y%-Y1%)^2)/(R1%/9))
  760 POINT X2%+X%,Y2%+Y%:NEXT,
  770 GCOL37 TINT 0:RECTANGLE 0,0,2*X2%
  780 MOVE0,0:MOVE2*X2%,2*Y2%:OSCLI"SGET "+B$
  790 NEXT
  800 ENDPROC
  810 
  820 DEFPROCF(F%,I%)
  830 LOCAL R%,G%,B%
  840 B%=F%DIV9:F%=F%-9*B%
  850 G%=F%DIV3:F%=F%-3*G%
  860 R%=F%
  870 F%=R%+4*G%+16*B%
  880 IF I%>=4 F%=F%+&15
  890 GCOL F% TINT (64*I%)MOD256
  900 ENDPROC
  910 
  920 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
