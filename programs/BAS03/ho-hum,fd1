   10 REM > HO-HUM
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 
   50 MODE141:MODE13:OFF
   60 S1$="1":S2$="2":A%=7
   70 DIMXS%(A%),YS%(A%),DX%(A%),DY%(A%),S$(A%),PARAM% 100
   80 
   90 *SNEW
  100 FORF%=1TO7:CLS:FORI%=1TO12:PROCFARVE(F%,I%-1)
  110 FORN%=1TO200:T=RADRND(360):Z%=RND(112-8*I%)
  120 LINE 100,100,100+Z%*SINT,100+Z%*COST:NEXT,
  130 MOVE0,0:MOVE200,200:NAVN$=STR$F%:OSCLI"SGET "+NAVN$
  140 $PARAM%=NAVN$
  150 SYS"OS_SpriteOp",29,0,PARAM%
  160 FORY%=0TO200STEP4
  170 FORX%=0TO200STEP4:S%=1
  180 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  190 SYS"OS_SpriteOp",44,0,PARAM%,X%/4,Y%/4,S%
  200 NEXT,,:CLS:GCOL8,15
  210 
  220 FORN%=1TOA%:S$(N%)=STR$((N%MOD7)+1)
  230 XS%(N%)=RND(1076):YS%(N%)=RND(820)
  240 DX%(N%)=(7+RND(9))*SGN(RND(1)-.5)
  250 DY%(N%)=(7+RND(9))*SGN(RND(1)-.5)
  260 NEXT
  270 
  280 ONERRORGOTO450
  290 REPEAT
  300 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  310 FORN%=1TOA%:OSCLI"SCH. "+S$(N%)
  320 X%=XS%(N%)+DX%(N%):IF X%<0ORX%>1076 DX%(N%)=-DX%(N%)
  330 Y%=YS%(N%)+DY%(N%):IF Y%<0ORY%>820 DY%(N%)=-DY%(N%)
  340 XS%(N%)+=DX%(N%):YS%(N%)+=DY%(N%)
  350 PLOT&ED,XS%(N%),YS%(N%):NEXT
  360 UNTIL0
  370 
  380 DEFPROCFARVE(F%,I%)
  390 LOCALN%,K%
  400 DATA1,4,5,16,17,20,21
  410 RESTORE400:FORN%=1TOF%:READK%:NEXT
  420 I%+=4:GCOL (I%DIV4)*K% TINT (I%MOD4)<<6
  430 ENDPROC
  440 
  450 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
