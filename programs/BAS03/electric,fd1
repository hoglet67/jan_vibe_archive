   10 REM > ELECTRIC
   20 
   30 REM Denne cellulGCOLre automat laver noget, der kunne minde om
   40 REM et elektrisk kredslRETURNb.
   50 
   60 MODE137:MODE9:OFF
   70 
   80 SYS"OS_UpdateMEMC",64,64
   90 S1%=1:S2%=2:X%=39:Y%=31
  100 DIM A%(X%+1,Y%+1,2):P%=1
  110 PROCINIT
  120 
  130 ONERRORGOTO390
  140 REPEAT
  150 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  160 
  170 VDU30
  180 FORN%=1TOY%:FORM%=1TOX%
  190 CASE A%(M%,N%,P%) OF
  200 WHEN 0:PRINT" ";
  210 WHEN 1:COLOUR6:PRINT"+";
  220 WHEN 2:COLOUR1:PRINT"*";
  230 WHEN 3:COLOUR4:PRINT"+";
  240 ENDCASE
  250 NEXT:PRINT:NEXT
  260 
  270 Q%=P%:P%=3-P%:C%=0
  280 FORN%=1TOY%:FORM%=1TOX%
  290 CASE A%(M%,N%,Q%) OF
  300 WHEN 0:A%(M%,N%,P%)=0
  310 WHEN 1:PROCWIRE(M%,N%)
  320 WHEN 2:A%(M%,N%,P%)=3:C%+=1
  330 WHEN 3:A%(M%,N%,P%)=1
  340 ENDCASE
  350 NEXT,
  360 
  370 UNTIL C%=0
  380 
  390 SYS"OS_Byte",113,S2%:VDU30:ON:COLOUR7:END
  400 
  410 DEFPROCWIRE(X%,Y%)
  420 LOCAL N%,M%,C%
  430 FORN%=-1TO1:FORM%=-1TO1
  440 IF A%(X%+M%,Y%+N%,Q%)=2 C%+=1
  450 NEXT,
  460 IF C%=1 OR C%=2 THEN A%(X%,Y%,P%)=2 ELSE A%(X%,Y%,P%)=1
  470 ENDPROC
  480 
  490 DEFPROCINIT
  500 LOCAL X%,Y%,B%,PX%,PY%
  510 *POINTER
  520 REPEAT
  530 MOUSE X%,Y%,B%
  540 CASE B% OF
  550 WHEN 1:
  560   PX%=X%/32:PY%=(1024-Y%)/32
  570   A%(PX%,PY%,P%)=1
  580   COLOUR6:PRINTTAB(PX%,PY%)"+";
  590 WHEN 4:
  600   PX%=X%/32:PY%=(1024-Y%)/32
  610   A%(PX%,PY%,P%)=0
  620   PRINTTAB(PX%,PY%)" ";
  630 ENDCASE
  640 UNTIL B%=2
  650 PX%=X%/32:PY%=(1024-Y%)/32
  660 A%(PX%,PY%,P%)=2
  670 MOUSE OFF
  680 *POINTER 0
  690 ENDPROC
  700 
  710 SYS"OS_Byte",112,S1%:VDU30:REPORT:PRINT" at line "STR$ERL:ON
