   10 REM > GUMBALLS
   20 
   30 MODE13:OFF
   40 ONERRORGOTO180
   50 
   60 FORN%=0TO26
   70 OSCLI"SCH. "+STR$(N%)
   80 NEXT
   90 
  100 GCOL8,0:CLS
  110 REPEAT
  120 OSCLI"SCH. "+STR$(RND(27)-1)
  130 FORN%=1TO5
  140 PLOT&ED,RND(1216),RND(960)
  150 NEXT
  160 UNTIL0
  170 
  180 IF ERR=134 PROCSPR:GOTO100
  190 REPORT:PRINT" at line "STR$ERL:END
  200 
  210 DEFPROCSPR
  220 *SNEW
  230 FORC%=0TO26:B$=STR$C%:CLS
  240 X1%=40:Y1%=50:R1%=60
  250 X2%=32:Y2%=32:R2%=32
  260 FORY%=-R2%TOR2%STEP4:B%=4*(SQR(R2%^2-Y%^2)DIV4)
  270 FORX%=-B%TOB%STEP4
  280 PROCF(C%,8-SQR((X2%+X%-X1%)^2+(Y2%+Y%-Y1%)^2)/(R1%/9))
  290 POINT X2%+X%,Y2%+Y%:NEXT,
  300 GCOL 0 TINT 0:RECTANGLE 0,0,2*X2%:GCOL63 TINT255
  310 
  320 MOVE0,0:MOVE2*X2%,2*Y2%:OSCLI"SGET "+B$
  330 IFC%=0 CIRCLE FILL X2%,Y2%,R2%-4
  340 SYS"OS_SpriteOp",29,0,B$
  350 FORY%=0TO2*Y2%STEP4
  360 FORX%=0TO2*X2%STEP4:S%=1
  370 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  380 SYS"OS_SpriteOp",44,0,B$,X%/4,Y%/4,S%
  390 NEXT,
  400 NEXT
  410 ENDPROC
  420 
  430 DEFPROCF(F%,I%)
  440 LOCAL R%,G%,B%
  450 B%=F%DIV9:F%=F%-9*B%
  460 G%=F%DIV3:F%=F%-3*G%
  470 R%=F%
  480 F%=R%+4*G%+16*B%
  490 IF I%>=4 F%=F%+&15
  500 GCOL F% TINT (64*I%)MOD256
  510 ENDPROC
