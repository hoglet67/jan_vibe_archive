   10 REM > SINUS2
   20 
   30 MODE15:OFF
   40 GCOL134 TINT128:CLG
   50 R=512:K=3.18:FORF%=1TO7
   60 FORI%=0TO11:PROCFARVE(F%,I%):CIRCLE FILL 640,512,R:R-=K:NEXT
   70 FORI%=10TO0STEP-1:PROCFARVE(F%,I%):CIRCLE FILL 640,512,R:R-=K:NEXT,
   80 K=RAD(720/1024):B=RAD(360/1024)
   90 FORI%=1TO7:T=RAD((I%-1)*51.42)
  100 FORN%=0TO11
  110 PROCFARVE(8-I%,N%):ORIGIN 128,512
  120 FORX%=0TO1024STEP4
  130 Y%=(128*SIN(B*X%))*SIN(K*X%+T)
  140 W=40*SIN(B*X%)/2
  150 R%=W-N%*(W/12)-W/24
  160 CIRCLE FILL X%,Y%,R%
  170 NEXT
  180 PROCFARVE(I%,N%):ORIGIN 640,0
  190 FORX%=0TO1024STEP4
  200 Y%=(128*SIN(B*X%))*SIN(K*X%+T)
  210 W=40*SIN(B*X%)/2
  220 R%=W-N%*(W/12)-W/24
  230 CIRCLE FILL Y%,X%,R%
  240 NEXT,,:ON
  250 END
  260 
  270 DEFPROCFARVE(F%,I%)
  280 LOCALN%,K%
  290 DATA1,4,5,16,17,20,21
  300 RESTORE290:FORN%=1TOF%:READK%:NEXT
  310 I%+=4:GCOL (I%DIV4)*K% TINT (I%MOD4)<<6
  320 ENDPROC
