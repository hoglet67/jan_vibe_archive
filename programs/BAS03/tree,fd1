   10 REM > TREE
   20 
   30 MODE128:MODE0:OFF
   40 S1$="1":S2$="2":K%=32:K2%=K%/2
   50 DIM SPR &7E000:SPR!0=&7DFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 COLOUR0,80,80,80:COLOUR1,0,255,0:VDU19,0,24,80,80,80
   70 
   80 ONERRORGOTO260
   90 FORI%=1TO25:S$=STR$I%
  100 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  110 PROCA(640,64,280,0,3.75*(I%-1))
  120 SYS "OS_SpriteOp",256+16,SPR,S$,0,0,-0,1279,1023
  130 NEXT
  140 
  150 A%=47:REPEAT:A%=(A%+1)MOD48:B%=ABS(24-A%)+1:S$=STR$B%
  160 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:CLS:SWAP S1$,S2$
  170 SYS "OS_SpriteOp",256+34,SPR,S$,0,0,0:W%=INKEY(6)
  180 UNTIL0
  190 
  200 DEFPROCA(X,Y,Z,A,I)
  210 LOCAL X1,Y1:X1=X+Z*SINRADA:Y1=Y+Z*COSRADA
  220 LINEX,Y,X1,Y1:IFZ<=8ENDPROC
  230 PROCA(X1,Y1,.7*Z,A+I,I):PROCA(X1,Y1,.7*Z,A-I,I)
  240 ENDPROC
  250 
  260 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON:END
