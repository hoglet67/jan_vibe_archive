   10 REM > LEAF
   20 
   30 MODE32:OFF
   40 FORY%=0TO1200STEP2:X%=0:MOVEX%,Y%
   50 REPEAT:PROCRGB(0,0,(Y%*(RND(128)+127))/1200)
   60 X%+=RND(64):DRAWX%,Y%:UNTILX%>1600
   70 NEXT
   80 RESTORE:READ K%
   90 DIMGX1(K%),GY1(K%),GX2(K%),GY2(K%),M%(K%),C%(K%),F%(K%)
  100 FORN%=1TOK%:READGX1(N%),GY1(N%),GX2(N%),GY2(N%),M%(N%),C%(N%),F%(N%)
  110 GX1(N%)=GX1(N%)/16:GY1(N%)=(GY1(N%)-8)/16
  120 GX2(N%)=GX2(N%)/16:GY2(N%)=(GY2(N%)-8)/16
  130 NEXT:PROCREPLACE(0,64,1130,860,50,0,1,1)
  140 END
  150 
  160 DEFPROCREPLACE(X1,Y1,X2,Y2,L%,C%,M%,F%)
  170 LOCAL DX,DY,X3,Y3,X4,Y4,N%
  180 IF L%=0 OR (X2-X1)^2+(Y2-Y1)^2<=16 THEN
  190 GCOL C%:PROCL(X1,Y1,X2,Y2)
  200 ELSE
  210 DX=X2-X1:DY=Y2-Y1
  220 FORN%=1TOK%
  230 IFF%=1 THEN
  240 X3=DX*GX1(N%)-M%*DY*GY1(N%)+X1:Y3=DY*GX1(N%)+M%*DX*GY1(N%)+Y1
  250 X4=DX*GX2(N%)-M%*DY*GY2(N%)+X1:Y4=DY*GX2(N%)+M%*DX*GY2(N%)+Y1
  260 PROCREPLACE(X3,Y3,X4,Y4,L%-1,C%(N%),M%*M%(N%),F%(N%))
  270 ELSE
  280 PROCREPLACE(X1,Y1,X2,Y2,0,C%,M%,F%)
  290 ENDIF
  300 NEXT
  310 ENDIF
  320 ENDPROC
  330 
  340 DEFPROCL(X1,Y1,X2,Y2)
  350 LOCAL L,T1,T2,W
  360 L=SQR((X1-X2)^2+(Y1-Y2)^2):W=L/8:IF X1>X2 L=-L
  370 IF L<>0 THEN
  380 T1=ASN((Y1-Y2)/L):T2=T1+PI:W=W/2
  390 MOVEX1+W*SINT1,Y1+W*COST1:MOVEX1+W*SINT2,Y1+W*COST2
  400 PLOT&75,X2+W*SINT2,Y2+W*COST2
  410 CIRCLE FILL X1,Y1,W:CIRCLE FILL X2,Y2,W
  420 ELSE
  430 CIRCLE X1,Y1,W
  440 ENDIF
  450 ENDPROC
  460 
  470 DEFPROCRGB(R%,G%,B%)
  480 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  490 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  500 ENDPROC
  510 
  520 DATA 5 :REM Number of elements
  530 
  540 REM  x1,y1,x2,y2,mirror,colour,fertility
  550 DATA  0, 8, 6, 8,     1,     6,        0
  560 DATA  6, 8,16, 3,    -1,    12,        1
  570 DATA  6, 8,13,12,    -1,     4,        1
  580 DATA  3, 8, 4,10,     1,    12,        1
  590 DATA  3, 8, 5, 5,    -1,     4,        1
