   10 REM > MELT2
   20 
   30 MODE 32:OFF
   40 INPUT"Spritefile="SPF$:CLS
   50 SYS"OS_FSControl",28,SPF$ TO ,,LG%:LG%+=&2000
   60 DIM SPR LG%,BUF 16
   70 SPR!0=LG%:SPR!4=0:SPR!8=16:SPR!12=16
   80 SYS"OS_SpriteOp",256+10,SPR,SPF$
   90 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  100 ONERRORGOTO390
  110 N$="":P%=0:REPEAT
  120 T%=BUF?P%:IF T%<>0 N$+=CHR$(T%)
  130 P%+=1:UNTIL T%=0
  140 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,,W%,H%,,M%
  150 SYS"OS_ReadModeVariable",-1,4 TO,,SX%:SX%=2^SX%
  160 SYS"OS_ReadModeVariable",-1,5 TO,,SY%:SY%=2^SY%
  170 SYS"OS_ReadModeVariable",-1,11 TO,,XS%
  180 SYS"OS_ReadModeVariable",-1,12 TO,,YS%
  190 PX%=(SX%*(XS%-W%))/2:PY%=(SY%*(YS%-H%))/2
  200 WS%=W%*SX%:HS%=H%*SY%
  210 SYS"OS_SpriteOp",256+34,SPR,N$,PX%,PY%,0
  220 
  230 REPEAT
  240 R%=RND(8)*2:X=RND(WS%)+PX%:Y=RND(HS%)+PY%
  250 SYS"OS_SpriteOp",256+16,SPR,"DROP",0,X-R%,Y-R%,X+R%,Y+R%
  260 SYS"OS_SpriteOp",256+29,SPR,"DROP"
  270 D%=RND(360):SI=2*SINRAD(D%):CO=2*COSRAD(D%):N%=R%
  280 REPEAT
  290 SYS"OS_SpriteOp",256+61,SPR,"DROP"
  300 GCOL0TINT0:RECTANGLE FILL 0,0,2*R%
  310 GCOL63TINT255:CIRCLE FILL R%,R%,N%
  320 SYS"OS_SpriteOp",256+61,SPR,0
  330 X=X+SI:Y=Y+CO
  340 SYS"OS_SpriteOp",256+34,SPR,"DROP",X-R%,Y-R%,8
  350 N%-=2
  360 UNTILN%=0
  370 UNTIL0
  380 
  390 SYS"OS_SpriteOp",256+61,SPR,0
  400 REPORT:PRINT" at line ";:PRINTSTR$(ERL)
