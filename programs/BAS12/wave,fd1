   10 REM > WAVE
   20 
   30 Q%=5:S1%=1:S2%=2
   40 MODE158:MODE30:OFF
   50 DIM X(500),Y(500),A(500)
   60 GCOL1:RECTANGLE FILL 0,0,1600,1200:GCOL0:CIRCLE FILL 800,600,580
   70 COLOUR129
   80 PRINT'" This simulation shows"
   90 PRINT" the shape of a wave when"
  100 PRINT" bouncing on the inner wall"
  110 PRINT" of a circular medium."
  120 PRINT'" Click mouse to start wave."
  130 *POINTER
  140 REPEAT:MOUSE MX%,MY%,B%:UNTILB%<>0 AND POINT(MX%,MY%)=0
  150 *POINTER 0
  160 C%=0:FORN%=0TO359STEPQ%:C%+=1:X(C%)=MX%:Y(C%)=MY%:A(C%)=N%:NEXT:P%=C%
  170 
  180 REPEAT
  190 ONERRORGOTO400
  200 GCOL1:RECTANGLE FILL 0,0,1600,1200:GCOL0:CIRCLE FILL 800,600,580
  210 C%=0:FORN%=0TO359STEPQ%:C%+=1
  220 X1=X(C%)+2*SINRAD(A(C%)):Y1=Y(C%)+2*COSRAD(A(C%))
  230 IF (800-X1)^2+(600-Y1)^2>=336400 THEN
  240 K=FNA(X(C%),Y(C%)):V=K+180-A(C%):A(C%)=K+V
  250 ENDIF
  260 X(C%)=X(C%)+2*SINRAD(A(C%)):Y(C%)=Y(C%)+2*COSRAD(A(C%))
  270 NEXT
  280 GCOL3:MOVEX(P%),Y(P%):FOR N%=1 TO P%:DRAW X(N%),Y(N%):NEXT
  290 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  300 UNTIL0
  310 
  320 DEFFNA(X,Y)
  330 XE=X-800:YE=Y-600
  340 D=SQR(XE^2+YE^2)
  350 SI=DEGASN(XE/D)
  360 IF YE<=0 SI=180-SI ELSE SI=360+SI
  370 IF SI>360 SI=SI-360
  380 =SI
  390 
  400 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
