   10 REM > SIERPINSKI
   20 
   30 MODE31:OFF
   40 K%=32
   50 KX%=K%/2:KX%=2*(KX%DIV2):KY%=K%/4:KY%=2*(KY%DIV2)
   60 DX%=(1600-27*(K%+KX%))/2:DY%=(1200-27*(K%+KY%))/2
   70 DIM T%(2,2,2)
   80 DIM S%(26,26,26)
   90 FORZ%=0TO2:FORY%=0TO2:FORX%=0TO2:READT%(X%,Y%,Z%):NEXT,,
  100 PROCA(0,0,0,0)
  110 FOR Z%=26TO0STEP-1:GCOL(Z%MOD7+1)
  120 FORY%=0TO26:FORX%=0TO26
  130 IFS%(X%,Y%,Z%)=1 THEN
  140 BX%=K%*X%+KX%*Z%+DX%:BY%=K%*Y%+KY%*Z%+DY%
  150 GCOL7:MOVEBX%,BY%+K%:MOVEBX%+K%,BY%+K%:PLOT&75,BX%+K%+KX%-1,BY%+K%+KY%-1
  160 GCOL1:MOVEBX%+K%,BY%:MOVEBX%+K%+KX%-1,BY%+KY%:PLOT&75,BX%+K%+KX%-1,BY%+K%+KY%-1
  170 GCOL4:RECTANGLE FILL BX%,BY%,K%-1
  180 ENDIF
  190 NEXT,,
  200 END
  210 
  220 DEFPROCA(X%,Y%,Z%,C%)
  230 IF C%=3 THEN
  240 S%(X%,Y%,Z%)=1
  250 ELSE
  260 LOCAL CX%,CY%,CZ%
  270 FORCZ%=0TO2:FORCY%=0TO2:FORCX%=0TO2
  280 IF T%(CX%,CY%,CZ%)=1 THEN PROCA(3*X%+CX%,3*Y%+CY%,3*Z%+CZ%,C%+1)
  290 NEXT,,
  300 ENDIF
  310 ENDPROC
  320 
  330 DATA 1,1,1
  340 DATA 1,0,1
  350 DATA 1,1,1
  360 
  370 DATA 1,0,1
  380 DATA 0,0,0
  390 DATA 1,0,1
  400 
  410 DATA 1,1,1
  420 DATA 1,0,1
  430 DATA 1,1,1
