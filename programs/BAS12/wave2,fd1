   10 REM > WAVE2
   20 
   30 Q%=5:S1%=1:S2%=2:PI2=2*PI
   40 MODE147:MODE19:OFF
   50 DIM X(500),Y(500),A(500)
   60 GCOL1:RECTANGLE FILL 0,0,1280,1024:GCOL0:CIRCLE FILL 640,512,500
   70 COLOUR129
   80 PRINT'" This simulation shows"
   90 PRINT" the shape of a wave"
  100 PRINT" when bouncing on the"
  110 PRINT" inner wall of a"
  120 PRINT" circular medium."
  130 PRINT'" Click mouse"
  140 PRINT" to start wave."
  150 *POINTER
  160 REPEAT:MOUSE MX%,MY%,B%:UNTILB%<>0 AND POINT(MX%,MY%)=0
  170 *POINTER 0
  180 C%=0:FORN%=0TO359STEPQ%:C%+=1:X(C%)=MX%:Y(C%)=MY%:A(C%)=RADN%:NEXT:P%=C%
  190 
  200 REPEAT
  210 ONERRORGOTO420
  220 GCOL1:RECTANGLE FILL 0,0,1280,1024:GCOL0:CIRCLE FILL 640,512,500
  230 C%=0:FORN%=0TO359STEPQ%:C%+=1
  240 X1=X(C%)+2*SIN(A(C%)):Y1=Y(C%)+2*COS(A(C%))
  250 IF (640-X1)^2+(512-Y1)^2>=250000 THEN
  260 K=FNA(X(C%),Y(C%)):V=K+PI-A(C%):A(C%)=K+V
  270 ENDIF
  280 X(C%)=X(C%)+2*SIN(A(C%)):Y(C%)=Y(C%)+2*COS(A(C%))
  290 NEXT
  300 GCOL3:MOVEX(P%),Y(P%):FOR N%=1 TO P%:DRAW X(N%),Y(N%):NEXT
  310 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  320 UNTIL0
  330 
  340 DEFFNA(X,Y)
  350 XE=X-640:YE=Y-512
  360 D=SQR(XE^2+YE^2)
  370 SI=ASN(XE/D)
  380 IF YE<=0 SI=PI-SI ELSE SI=PI2+SI
  390 IF SI>PI2 SI=SI-PI2
  400 =SI
  410 
  420 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
