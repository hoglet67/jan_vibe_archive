   10 REM>  ALIENTREE4  *** Jan Vibe march 98 ***
   20 
   40 DIM MD% 64
   50 MD%!00=1:MD%!04=800:MD%!08=600:MD%!12=5:MD%!16=-1:MD%!20=-1
   70 MODE MD%:OFF
   80 
  100 DIM PATH% 100,JC% 15
  110 JC%!0=0:JC%!4=0:JC%!8=0:JC%!12=0
  120 DIM X1%(1000),Y1%(1000),X2%(1000),Y2%(1000),B%(1000)
  130 DIM RX%(1000),RY%(1000),RC%(1000)
  140 DIM R1%(1000),G1%(1000),B1%(1000),R2%(1000),G2%(1000),B2%(1000)
  150 DIM R3%(1000),G3%(1000),B3%(1000),R4%(1000),G4%(1000),B4%(1000)
  160 
  170 SYS"OS_ReadModeVariable",-1,4 TO,,SX%:SX%=2^SX%
  180 SYS"OS_ReadModeVariable",-1,5 TO,,SY%:SY%=2^SY%
  190 SYS"OS_ReadModeVariable",-1,11 TO,,XS%
  200 SYS"OS_ReadModeVariable",-1,12 TO,,YS%
  210 WS%=SX%*XS%:HS%=SY%*YS%
  220 MX%=WS%/2:LG%=HS%/3:HL%=HS%*3/4:HL2%=HS%-HL%:L%=HS%/37.5
  230 
  240 REPEAT
  250 P%=0:PR%=0:CLS
  260 PRINT"Press space if this tree is Ok,"'"any other key if not."
  270 PRINT"When the tree is rendered,"'"press s to save the picture (atpic)."
  280 PROCTREE(MX%,0,LG%,0,P%)
  290 Q$=GET$:UNTILQ$=" "
  300 PROCBACKGROUND:PROCRENDER
  310 *FX 15,0
  320 Q$=GET$:IF INSTR("Ss",Q$)<>0 THEN *SCREENSAVE ATPIC
  330 END
  340 
  350 DEFPROCBACKGROUND
  360 LOCAL N%,M%,X%,Y%,R%,F%,RGB%
  370 FORN%=0TOHL%STEP2:GCOL 0,((HL%-N%)*128)/HL%,255:LINE 0,HL2%+N%,WS%,HL2%+N%:NEXT
  380 FORY%=HL2%TOHS%STEP8:X%=RND(WS%):R%=RND(16)+32:F=1.3+RND(1)*2
  390 FORM%=-R%TOR%STEP2:W%=SQR(R%*R%-M%*M%)*F
  400 RGB%=(200*(M%+R%)+55*(2*R%-(M%+R%)))/(2*R%)
  410 GCOL RGB%,RGB%,RGB%:LINE X%-W%,Y%+M%,X%+W%,Y%+M%
  420 NEXT,
  430 FORN%=0TOHL2%STEP2:GCOL0,((HL2%-N%)*255+N%*80)/HL2%,0:LINE 0,N%,WS%,N%:NEXT
  440 ENDPROC
  450 
  460 DEFPROCRENDER
  470 LOCAL I%,I1%,N%,X1%,Y1%,B%,R1%,G1%,B1%,R2%,G2%,B2%,RD%,R%,G%,B%
  480 FORI%=0TO15:I1%=15-I%
  490 FORN%=1TOP%
  500 X1%=X1%(N%):Y1%=Y1%(N%):X2%=X2%(N%):Y2%=Y2%(N%)
  510 B%=(B%(N%)*I1%)/15:IF B%>0 THEN
  520 R1%=(R1%(N%)*I1%+R2%(N%)*I%)/15
  530 G1%=(G1%(N%)*I1%+G2%(N%)*I%)/15
  540 B1%=(B1%(N%)*I1%+B2%(N%)*I%)/15
  550 R2%=(R3%(N%)*I1%+R4%(N%)*I%)/15
  560 G2%=(G3%(N%)*I1%+G4%(N%)*I%)/15
  570 B2%=(B3%(N%)*I1%+B4%(N%)*I%)/15
  580 PROCLINE(X1%,Y1%,R1%,G1%,B1%,X2%,Y2%,R2%,G2%,B2%,B%)
  590 ENDIF
  600 NEXT,
  610 FORN%=1TOPR%
  620 REPEAT:R1%=RND(255):G1%=RND(255):B1%=RND(255):UNTILR1%+G1%+B1%<127
  630 REPEAT:R2%=RND(255):G2%=RND(255):B2%=RND(255):UNTILR2%+G2%+B2%>400
  640 FORI%=15TO1STEP-1
  650 RD%=RC%(N%)*I%/15
  660 R%=(R1%*I%+R2%*(15-I%))/15:G%=(G1%*I%+G2%*(15-I%))/15:B%=(B1%*I%+B2%*(15-I%))/15
  670 GCOL R%,G%,B%:CIRCLE FILL RX%(N%),RY%(N%),RD%
  680 NEXT,
  690 ENDPROC
  700 
  710 DEFPROCTREE(X,Y,Z,A,OP%)
  720 LOCAL X1,Y1:X1=X+Z*SINRAD(A):Y1=Y+Z*COSRAD(A)
  730 LINE X,Y,X1,Y1
  740 P%+=1
  750 X1%(P%)=X:Y1%(P%)=Y:X2%(P%)=X1:Y2%(P%)=Y1:B%(P%)=SQR((X-X1)^2+(Y-Y1)^2)/2.5
  760 IF P%=1 THEN
  770 REPEAT:R1%(P%)=RND(255):G1%(P%)=RND(255):B1%(P%)=RND(255):UNTILR1%(P%)+G1%(P%)+B1%(P%)<127
  780 REPEAT:R2%(P%)=RND(255):G2%(P%)=RND(255):B2%(P%)=RND(255):UNTILR2%(P%)+G2%(P%)+B2%(P%)>400
  790 ELSE
  800 R1%(P%)=R3%(OP%):G1%(P%)=G3%(OP%):B1%(P%)=B3%(OP%)
  810 R2%(P%)=R4%(OP%):G2%(P%)=G4%(OP%):B2%(P%)=B4%(OP%)
  820 ENDIF
  830 REPEAT:R3%(P%)=RND(255):G3%(P%)=RND(255):B3%(P%)=RND(255):UNTILR3%(P%)+G3%(P%)+B3%(P%)<127
  840 REPEAT:R4%(P%)=RND(255):G4%(P%)=RND(255):B4%(P%)=RND(255):UNTILR4%(P%)+G4%(P%)+B4%(P%)>400
  850 IF Z<L% THEN
  860 PR%+=1:RC%(PR%)=RND(L%/2)+8:RX%(PR%)=X+(Z+RC%(PR%))*SINRADA:RY%(PR%)=Y+(Z+RC%(PR%))*COSRADA
  870 ELSE
  880 OP%=P%
  890 PROCTREE(X1,Y1,Z/(1.2+RND(1)*.6),A+30+RND(30),P%)
  900 PROCTREE(X1,Y1,Z/(1.2+RND(1)*.6),A-30-RND(30),OP%)
  910 ENDIF
  920 ENDPROC
  930 
  940 DEFPROCLINE(X1,Y1,R1%,G1%,B1%,X2,Y2,R2%,G2%,B2%,B)
  950 LOCAL L,K%,N%,N1%,XA,YA,XB,YB,R%,G%,B%
  960 L=SQR((X2-X1)^2+(Y2-Y1)^2)
  970 K%=L/8+RND(8):IFK%<32 K%=24+RND(8)
  980 XA=(X1+X2)/2:YA=(Y1+Y2)/2
  990 JC%!0=&010000:GCOL R1%,G1%,B1%:PROCL(X1,Y1,XA,YA,B)
 1000 JC%!0=&000100:GCOL R2%,G2%,B2%:PROCL(XA,YA,X2,Y2,B)
 1010 JC%!0=&000000
 1020 FORN%=2TOK%:N1%=K%-N%
 1030 XA=(X1%*(N%-2)+X2%*(N1%+2))/K%:YA=(Y1%*(N%-2)+Y2%*(N1%+2))/K%
 1040 XB=(X1*N%+X2*N1%)/K%:YB=(Y1*N%+Y2*N1%)/K%
 1050 R%=((N%-2)*R1%+(N1%+2)*R2%)/K%:G%=((N%-2)*G1%+(N1%+2)*G2%)/K%
 1060 B%=((N%-2)*B1%+(N1%+2)*B2%)/K%:GCOL R%,G%,B%
 1070 PROCL(XA,YA,XB,YB,B)
 1080 NEXT
 1090 XA=(X1%*(K%-1)+X2%)/K%:YA=(Y1%*(K%-1)+Y2%)/K%:XB=X1:YB=Y1
 1100 R%=((K%-1)*R1%+R2%)/K%:G%=((K%-1)*G1%+G2%)/K%
 1110 B%=((K%-1)*B1%+B2%)/K%:GCOL R%,G%,B%
 1120 PROCL(XA,YA,XB,YB,B)
 1130 ENDPROC
 1140 
 1150 DEFPROCL(X1,Y1,X2,Y2,B)
 1160 LOCAL PP%,W
 1170 X1=256*X1:Y1=256*Y1:X2=256*X2:Y2=256*Y2
 1180 PATH%!PP%=2:PP%+=4:PATH%!PP%=X1:PP%+=4:PATH%!PP%=Y1:PP%+=4
 1190 PATH%!PP%=8:PP%+=4:PATH%!PP%=X2:PP%+=4:PATH%!PP%=Y2:PP%+=4
 1200 PATH%!PP%=0:PP%+=4:PATH%!PP%=PP%
 1210 W=256*B
 1220 SYS"Draw_Stroke",PATH%,0,0,0,W,JC%,0
 1230 ENDPROC
