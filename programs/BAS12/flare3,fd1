   10 REM > FLARE3
   20 
   30 MODE32:OFF
   40 SYS"OS_ReadModeVariable",-1,4 TO,,SX%:SX%=2^SX%
   50 SYS"OS_ReadModeVariable",-1,5 TO,,SY%:SY%=2^SY%
   60 SYS"OS_ReadModeVariable",-1,11 TO,,XS%
   70 SYS"OS_ReadModeVariable",-1,12 TO,,YS%
   80 PX%=SX%*(XS%+1)/2:PY%=SY%*(YS%+1)/2:D%=(SQR(PX%^2+PY%^2)*.8)DIV60*60
   90 V%=10
  100 FORI%=D%TO60STEP-60:V%=10-V%
  110 REPEAT:R1%=RND(255):G1%=RND(255):B1%=RND(255):UNTILR1%+G1%+B1%<=63
  120 REPEAT:R2%=RND(255):G2%=RND(255):B2%=RND(255):UNTILR2%+G2%+B2%>=256
  130 REPEAT:R3%=RND(255):G3%=RND(255):B3%=RND(255):UNTILR3%+G3%+B3%<=63
  140 REPEAT:R4%=RND(255):G4%=RND(255):B4%=RND(255):UNTILR4%+G4%+B4%>=256
  150 FORN%=0TO359STEP20:T1=RAD(N%+V%):T2=RAD(N%+20+V%)
  160 X1%=I%*SINT1+PX%:Y1%=I%*COST1+PY%
  170 X2%=I%*SINT2+PX%:Y2%=I%*COST2+PY%
  180 PROCFLARE(X1%,Y1%,X2%,Y2%,2.8,R1%,G1%,B1%,R2%,G2%,B2%)
  190 PROCFLARE(X2%,Y2%,X1%,Y1%,2.8,R3%,G3%,B3%,R4%,G4%,B4%)
  200 SWAP R1%,R3%:SWAP G1%,G3%:SWAP B1%,B3%
  210 SWAP R2%,R4%:SWAP G2%,G4%:SWAP B2%,B4%
  220 NEXT,
  230 END
  240 
  250 DEFPROCFLARE(X1%,Y1%,X2%,Y2%,Z,R1%,G1%,B1%,R2%,B2%,G2%)
  260 LOCAL XE,YE,D,SI,XC%,YC%,XA%,YA%,N%,TX1,TX2,TY1,TY2,R,G,B
  270 XC%=(X1%+X2%)/2:YC%=(Y1%+Y2%)/2
  280 XE=X2%-X1%:YE=Y2%-Y1%:D=SQR(XE^2+YE^2):XE=XE/D:YE=YE/D:SI=DEGASN(XE)
  290 IF YE<=0 SI=180-SI ELSE SI=360+SI
  300 IF SI>360 SI=SI-360
  310 D=D*Z:XA%=XC%+D*SINRAD(SI+270):YA%=YC%+D*COSRAD(SI+270)
  320 TX1=X1%:TY1=Y1%
  330 FORN%=1TO32
  340 R=((32-N%)*R1%+N%*R2%)/32:G=((32-N%)*G1%+N%*G2%)/32
  350 B=((32-N%)*B1%+N%*B2%)/32:PROCRGB(R,G,B)
  360 TX2=TX1:TY2=TY1
  370 TX1=((32-N%)*X1%+N%*XA%)/32
  380 TY1=((32-N%)*Y1%+N%*YA%)/32
  390 MOVETX1,TY1:MOVETX2,TY2:PLOT85,XC%,YC%
  400 NEXT
  410 TX1=X2%:TY1=Y2%
  420 FORN%=1TO32
  430 R=((32-N%)*R1%+N%*R2%)/32:G=((32-N%)*G1%+N%*G2%)/32
  440 B=((32-N%)*B1%+N%*B2%)/32:PROCRGB(R,G,B)
  450 TX2=TX1:TY2=TY1
  460 TX1=((32-N%)*X2%+N%*XA%)/32
  470 TY1=((32-N%)*Y2%+N%*YA%)/32
  480 MOVETX1,TY1:MOVETX2,TY2:PLOT85,XC%,YC%
  490 NEXT
  500 ENDPROC
  510 
  520 DEFPROCRGB(R%,G%,B%)
  530 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  540 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  550 ENDPROC
