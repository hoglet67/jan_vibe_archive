   10 REM > WAVE3
   20 
   30 DIM SPR &100000,BUF 16
   40 SPR!0=&FFFFF:SPR!4=0:SPR!8=16:SPR!12=16
   50 
   60 Q%=5:PI2=2*PI:CT%=0:SC%=1000
   70 MODE18:OFF
   80 DIM X(500),Y(500),A(500)
   90 GCOL1:RECTANGLE FILL 0,0,320,320:GCOL0:CIRCLE FILL 160,160,158
  100 *POINTER
  110 REPEAT:MOUSE MX%,MY%,B%:UNTILB%<>0 AND POINT(MX%,MY%)=0
  120 *POINTER 0
  130 C%=0:FORN%=0TO359STEPQ%:C%+=1:X(C%)=MX%:Y(C%)=MY%:A(C%)=RADN%:NEXT:P%=C%
  140 
  150 REPEAT
  160 CLS
  170 GCOL1:RECTANGLE FILL 0,0,320,320:GCOL0:CIRCLE FILL 160,160,158
  180 C%=0:FORN%=0TO359STEPQ%:C%+=1
  190 X1=X(C%)+2*SIN(A(C%)):Y1=Y(C%)+2*COS(A(C%))
  200 IF (160-X1)^2+(160-Y1)^2>=25600 THEN
  210 K=FNA(X(C%),Y(C%)):V=K+PI-A(C%):A(C%)=K+V
  220 ENDIF
  230 X(C%)=X(C%)+2*SIN(A(C%)):Y(C%)=Y(C%)+2*COS(A(C%))
  240 NEXT
  250 GCOL3:MOVEX(P%),Y(P%):FOR N%=1 TO P%:DRAW X(N%),Y(N%):NEXT
  260 CT%=CT%+1:PRINTCT%:IFCT%MOD4=1 PROCSPR
  270 UNTILCT%>=600
  280 SYS"OS_SpriteOp",256+12,SPR,"TESLA"
  290 END
  300 
  310 DEFFNA(X,Y)
  320 XE=X-160:YE=Y-160
  330 D=SQR(XE^2+YE^2)
  340 SI=ASN(XE/D)
  350 IF YE<=0 SI=PI-SI ELSE SI=PI2+SI
  360 IF SI>PI2 SI=SI-PI2
  370 =SI
  380 
  390 DEFPROCSPR
  400 SC%+=1:N$=RIGHT$(STR$(SC%),3)
  410 SYS"OS_SpriteOp",256+16,SPR,N$,0,0,0,320,320
  420 ENDPROC
