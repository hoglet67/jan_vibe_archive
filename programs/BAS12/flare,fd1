   10 REM > FLARE
   20 
   30 MODE32:OFF
   40 FORN%=600TO8STEP-1
   50 T=RADRND(360):X1%=800+N%*SINT:Y1%=600+N%*COST
   60 REPEAT:R1%=RND(255):G1%=RND(255):B1%=RND(255):UNTILR1%+G1%+B1%<=127
   70 REPEAT:R2%=RND(255):G2%=RND(255):B2%=RND(255):UNTILR2%+G2%+B2%>=511
   80 PROCFLARE(800,600,X1%,Y1%,R1%,G1%,B1%,R2%,G2%,B2%)
   90 NEXT
  100 END
  110 
  120 DEFPROCFLARE(X1%,Y1%,X2%,Y2%,R1%,G1%,B1%,R2%,B2%,G2%)
  130 LOCAL XE,YE,D,SI,XA%,YA%,XB%,YB%,N%,TX1,TX2,TY1,TY2,R,G,B
  140 XE=X2%-X1%:YE=Y2%-Y1%:D=SQR(XE^2+YE^2):XE=XE/D:YE=YE/D:SI=DEGASN(XE)
  150 IF YE<=0 SI=180-SI ELSE SI=360+SI
  160 IF SI>360 SI=SI-360
  170 D=D/10:XA%=X1%+D*SINRAD(SI+90):YA%=Y1%+D*COSRAD(SI+90)
  180 XB%=X1%+D*SINRAD(SI+270):YB%=Y1%+D*COSRAD(SI+270)
  190 TX1=XA%:TY1=YA%
  200 FORN%=1TO15
  210 R=((15-N%)*R1%+N%*R2%)/15:G=((15-N%)*G1%+N%*G2%)/15
  220 B=((15-N%)*B1%+N%*B2%)/15:PROCRGB(R,G,B)
  230 TX2=TX1:TY2=TY1
  240 TX1=((15-N%)*XA%+N%*X2%)/15
  250 TY1=((15-N%)*YA%+N%*Y2%)/15
  260 MOVETX1,TY1:MOVETX2,TY2:PLOT85,X1%,Y1%
  270 NEXT
  280 TX1=XB%:TY1=YB%
  290 FORN%=1TO15
  300 R=((15-N%)*R1%+N%*R2%)/15:G=((15-N%)*G1%+N%*G2%)/15
  310 B=((15-N%)*B1%+N%*B2%)/15:PROCRGB(R,G,B)
  320 TX2=TX1:TY2=TY1
  330 TX1=((15-N%)*XB%+N%*X2%)/15
  340 TY1=((15-N%)*YB%+N%*Y2%)/15
  350 MOVETX1,TY1:MOVETX2,TY2:PLOT85,X1%,Y1%
  360 NEXT
  370 ENDPROC
  380 
  390 DEFPROCRGB(R%,G%,B%)
  400 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  410 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  420 ENDPROC
