   10 REM > WAVEPLOT4
   20 
   30 MODE21:OFF
   40 
   50 DIM SPR &80000
   60 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 Q%=RND(-TIME)
   80 XA%=RND(160):YA%=RND(120):F1=RND(800):A1%=RND(360)
   90 R1%=RND(110)+10:AF1%=SGN(RND(1)-.5):FB1%=(120-R1%)DIV60+1
  100 XB%=RND(160):YB%=RND(120):F2=RND(800):A2%=RND(360)
  110 R2%=RND(110)+10:AF2%=SGN(RND(1)-.5):FB2%=(120-R2%)DIV60+1
  120 XC%=RND(160):YC%=RND(120):F3=RND(800):A3%=RND(360)
  130 R3%=RND(110)+10:AF3%=SGN(RND(1)-.5):FB3%=(120-R3%)DIV60+1
  140 
  150 F%=0:FORYP%=5TO0STEP-1:PY%=146*YP%+82:FORXP%=0TO5:PX%=186*XP%+94
  160 NVN$=RIGHT$(STR$(100+F%),2):F%+=1
  170 A1%+=10*FB1%:X1%=XA%+R1%*SINRAD(A1%*AF1%):Y1%=YA%+R1%*COSRAD(A1%*AF1%)
  180 A2%+=10*FB2%:X2%=XB%+R2%*SINRAD(A2%*AF2%):Y2%=YB%+R2%*COSRAD(A2%*AF1%)
  190 A3%+=10*FB3%:X3%=XC%+R3%*SINRAD(A3%*AF3%):Y3%=YC%+R3%*COSRAD(A3%*AF1%)
  200 RESTORE
  210 FORN%=1TO16:READ I%,J%
  220 FORK%=0TO120STEP8:FORL%=0TO160STEP8
  230 Y%=I%+K%:X%=J%+L%
  240 S1=(SINRAD(SQR((X%-X1%)^2+(Y%-Y1%)^2))+1)*F1
  250 R%=S1:G%=255-S1:B%=255-S1
  260 S2=(SINRAD(SQR((X%-X2%)^2+(Y%-Y2%)^2))+1)*F2
  270 R%+=255-S2:G%+=S2:B%+=255-S2
  280 S3=(SINRAD(SQR((X%-X3%)^2+(Y%-Y3%)^2))+1)*F3
  290 R%+=255-S3:G%+=255-S3:B%+=S3
  300 PROCRGB(R%,G%,B%):POINT X%+PX%,Y%+PY%
  310 NEXT,,
  320 SYS"OS_SpriteOp",256+16,SPR,NVN$,0,PX%,PY%,PX%+159,PY%+119
  330 NEXT,
  340 CLS:*FX15,0
  350 REPEAT
  360 FORN%=0TO35:NVN$=RIGHT$(STR$(100+N%),2)
  370 SYS"OS_SpriteOp",256+34,SPR,NVN$,560,452,0
  380 T%=TIME+8:REPEAT:UNTILTIME>T%
  390 NEXT
  400 UNTILINKEY$(0)<>"":*FX15,0
  410 INPUT"FILENAME="FIN$
  420 IF FIN$<>"" SYS"OS_SpriteOp",256+12,SPR,FIN$
  430 END
  440 
  450 DEFPROCRGB(R%,G%,B%)
  460 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  470 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  480 ENDPROC
  490 
  500 DATA 0,0, 4,4, 0,4, 4,0
  510 DATA 2,2, 6,6, 2,6, 6,2
  520 DATA 0,2, 4,6, 0,6, 4,2
  530 DATA 2,0, 6,4, 2,4, 6,0
