   10 REM > COLDEV2
   20 
   30 MODE21:OFF
   40 Q%=20000:T%=0
   50 DIM X%(Q%),Y%(Q%),R%(Q%),G%(Q%),B%(Q%)
   51 R%()=127:G%()=127:B%()=127
   60 
   61 FORN%=0TO350STEP10:N1%=N%+10
   62 PROCL(640+300*SINRADN%,512+300*COSRADN%,640+300*SINRADN1%,512+300*COSRADN1%)
   63 NEXT
   80 
   90 CLS
  100 REPEAT
  110 R%=RND(T%):IF R%=0 R%=1
  120 SWAP X%(R%),X%(T%):SWAP Y%(R%),Y%(T%)
  130 SWAP R%(R%),R%(T%):SWAP G%(R%),G%(T%):SWAP B%(R%),B%(T%)
  140 X1%=X%(T%):Y1%=Y%(T%):R1%=R%(T%):G1%=G%(T%):B1%=B%(T%):T%-=1
  150 PROCP(X1%+2,Y1%,R1%,G1%,B1%)
  160 PROCP(X1%-2,Y1%,R1%,G1%,B1%)
  170 PROCP(X1%,Y1%+2,R1%,G1%,B1%)
  180 PROCP(X1%,Y1%-2,R1%,G1%,B1%)
  190 UNTIL T%=0
  200 *SCREENSAVE CRYSTAL
  210 END
  220 
  230 DEFPROCP(X%,Y%,R%,G%,B%)
  240 IF POINT(X%,Y%)=0 THEN
  250 R%=R%+RND(25)-13:IFR%>255 R%=255
  260 IFR%<0 R%=0
  270 G%=G%+RND(25)-13:IFG%>255 G%=255
  280 IFG%<0 G%=0
  290 B%=B%+RND(25)-13:IFB%>255 B%=255
  300 IFB%<0 B%=0
  310 T%+=1:X%(T%)=X%:Y%(T%)=Y%:R%(T%)=R%:G%(T%)=G%:B%(T%)=B%
  320 PROCRGB(R%,G%,B%):POINT X%,Y%
  330 ENDIF
  340 ENDPROC
  350 
  360 DEFPROCRGB(R%,G%,B%)
  370 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  380 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  390 ENDPROC
  400 
  410 DEFPROCL(X1%,Y1%,X2%,Y2%)
  420 LOCAL N%,D1%,D2%,DX%,DY%
  430 IF ABS(X1%-X2%)>ABS(Y1%-Y2%) THEN
  440 IF X1%>X2% THEN SWAP X1%,X2%:SWAP Y1%,Y2%
  450 D%=X2%-X1%
  460 FORN%=0TOD%STEP2
  470 DX%=X1%+N%:DY%=(Y1%*(D%-N%)+Y2%*N%)/D%
  480 POINTDX%,DY%:T%+=1:X%(T%)=DX%:Y%(T%)=DY%
  500 NEXT
  510 ELSE
  520 IF Y1%>Y2% THEN SWAP X1%,X2%:SWAP Y1%,Y2%
  530 D%=Y2%-Y1%
  540 FORN%=0TOD%STEP2
  550 DY%=Y1%+N%:DX%=(X1%*(D%-N%)+X2%*N%)/D%
  560 POINTDX%,DY%:T%+=1:X%(T%)=DX%:Y%(T%)=DY%
  580 NEXT
  590 ENDIF
  600 ENDPROC
