   10 REM > PATTERN2
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO250
   50 
   60 MODE13:MODE141:OFF:K%=4
   70 DIMA%(K%,K%),C%(K%,8),P%(K%)
   80 R%=RND(-TIME):S1$="1":S2$="2"
   90 
  100 REPEAT
  110 T%=TIME+500
  120 FORN%=0TOK%
  130 FORM%=0TOK%
  140 A%(M%,N%)=RND(255)
  150 NEXT
  160 P%(N%)=RND(7)
  170 FORM%=1TO8
  180 C%(N%,M%)=RND(256)-1
  190 NEXT,
  200 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:SWAP S1$,S2$:CLS
  210 PROCA(640,512,256,0,0,511)
  220 REPEAT:UNTILTIME>T%
  230 UNTIL0
  240 
  250 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON:END
  260 
  270 DEFPROCA(X%,Y%,Z%,T%,V%,F%)
  280 IF (A%(T%,V%)AND001)<>0 PROCB
  290 IF (A%(T%,V%)AND002)<>0 PROCD
  300 IF (A%(T%,V%)AND004)<>0 PROCX
  310 IF (A%(T%,V%)AND008)<>0 PROCK
  320 IF (A%(T%,V%)AND016)<>0 PROCR
  330 IF (A%(T%,V%)AND032)<>0 PROCC
  340 IF (A%(T%,V%)AND064)<>0 PROCS
  350 IF (A%(T%,V%)AND128)<>0 PROCP
  360 IF T%<K% THEN
  370 IF (P%(T%)AND1)<>0 THEN
  380 IF (F%AND001)<>0 PROCA(X%,Y%,Z%/2,T%+1,(V%+1)MODK%,511)
  390 ENDIF
  400 IF (P%(T%)AND2)<>0 THEN
  410 IF (F%AND002)<>0 PROCA(X%-Z%,Y%-Z%,Z%/2,T%+1,(V%+2)MODK%,311)
  420 IF (F%AND004)<>0 PROCA(X%-Z%,Y%+Z%,Z%/2,T%+1,(V%+2)MODK%,111)
  430 IF (F%AND008)<>0 PROCA(X%+Z%,Y%+Z%,Z%/2,T%+1,(V%+2)MODK%,221)
  440 IF (F%AND016)<>0 PROCA(X%+Z%,Y%-Z%,Z%/2,T%+1,(V%+2)MODK%,411)
  450 ENDIF
  460 IF (P%(T%)AND4)<>0 THEN
  470 IF (F%AND032)<>0 PROCA(X%-Z%,Y%,Z%/2,T%+1,(V%+3)MODK%,39)
  480 IF (F%AND064)<>0 PROCA(X%,Y%+Z%,Z%/2,T%+1,(V%+3)MODK%,77)
  490 IF (F%AND128)<>0 PROCA(X%+Z%,Y%,Z%/2,T%+1,(V%+3)MODK%,153)
  500 IF (F%AND256)<>0 PROCA(X%,Y%-Z%,Z%/2,T%+1,(V%+3)MODK%,275)
  510 ENDIF
  520 ENDIF
  530 ENDPROC
  540 
  550 DEFPROCB
  560 GCOL3,C%(T%,1)MOD64 TINT C%(T%,1)
  570 RECTANGLE X%-Z%,Y%-Z%,2*Z%
  580 ENDPROC
  590 
  600 DEFPROCD
  610 GCOL3,C%(T%,2)MOD64 TINT C%(T%,2)
  620 MOVEX%-Z%,Y%
  630 DRAWX%,Y%+Z%:DRAWX%+Z%,Y%
  640 DRAWX%,Y%-Z%:DRAWX%-Z%,Y%
  650 ENDPROC
  660 
  670 DEFPROCX
  680 GCOL3,C%(T%,3)MOD64 TINT C%(T%,3)
  690 LINE X%-Z%,Y%-Z%,X%+Z%,Y%+Z%
  700 LINE X%+Z%,Y%-Z%,X%-Z%,Y%+Z%
  710 ENDPROC
  720 
  730 DEFPROCK
  740 GCOL3,C%(T%,4)MOD64 TINT C%(T%,4)
  750 LINE X%-Z%,Y%,X%+Z%,Y%
  760 LINE X%,Y%-Z%,X%,Y%+Z%
  770 ENDPROC
  780 
  790 DEFPROCR
  800 GCOL3,C%(T%,5)MOD64 TINT C%(T%,5)
  810 CIRCLE X%,Y%,Z%
  820 ENDPROC
  830 
  840 DEFPROCC
  850 GCOL3,C%(T%,6)MOD64 TINT C%(T%,6)
  860 CIRCLE FILL X%,Y%,Z%
  870 ENDPROC
  880 
  890 DEFPROCS
  900 GCOL3,C%(T%,7)MOD64 TINT C%(T%,7)
  910 RECTANGLE FILL X%-Z%,Y%-Z%,2*Z%
  920 ENDPROC
  930 
  940 DEFPROCP
  950 GCOL3,C%(T%,8)MOD64 TINT C%(T%,8)
  960 MOVEX%,Y%-Z%:MOVEX%+Z%,Y%
  970 PLOT&75,X%,Y%+Z%
  980 ENDPROC
  990 
 1000 REM Jan Vibe april 89.
