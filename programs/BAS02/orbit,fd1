   10 REM > ORBIT
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO410
   50 
   60 MODE9:OFF
   70 DIM PARAM% 100
   80 FORN%=1TO15:COLOURN%,16*N%,16*N%,16*N%:NEXT:COLOUR0,0,0,128
   90 
  100 F%=0
  110 *SCH. PLANET
  120 *SCH. MAANE
  130 F%=1
  140 
  150 S1$="1":S2$="2"
  160 XP%=448:YP%=320
  170 A%=0:R%=-TIME:Z%=0
  180 REPEAT
  190 OSCLI"FX 112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  200 PRINTTAB(17,1)"Orbit."
  210 Q%=RND(R%):Z%+=4:GCOL15
  220 FORN%=1TO100:POINT (RND(1276)+Z%)MOD1280,RND(1020):NEXT
  230 GCOL8,0
  240 A%=(A%+2)MOD360:T=RADA%
  250 XM%=550*SINT
  260 YM%=100*COST+XM%/4
  270 XM%=XM%+592:YM%=YM%+464
  280 IF A%>=90 AND A%<=270 THEN
  290 *SCH. PLANET
  300 PLOT&ED,XP%,YP%
  310 *SCH. MAANE
  320 PLOT&ED,XM%,YM%
  330 ELSE
  340 *SCH. MAANE
  350 PLOT&ED,XM%,YM%
  360 *SCH. PLANET
  370 PLOT&ED,XP%,YP%
  380 ENDIF
  390 UNTIL0
  400 
  410 CASE ERR OF
  420 WHEN 134:
  430 *SNEW
  440 X0=10:Y0=20:Z0=20:R=48
  450 M1=0.0061:M2=324:PROCB
  460 PROCSPRITE(8*R,"PLANET")
  470 X0=10:Y0=20:Z0=20:R=12
  480 M1=0.0265:M2=324:PROCB
  490 PROCSPRITE(8*R,"MAANE")
  500 GOTO ERL
  510 
  520 WHEN 133:
  530 CLS
  540 PRINTTAB(6,8)"Der er ikke plads nok til de"
  550 PRINTTAB(6)"sprites, dette program skal"
  560 PRINTTAB(6)"anvende.":PRINT
  570 PRINTTAB(6)"Du skal afsatte mindst 11k"
  580 PRINTTAB(6)"(to sider) til sprites."
  590 PRINT:ON:END
  600 
  610 OTHERWISE
  620 IF F%=1 OSCLI"FX112,"+S1$
  630 REPORT:PRINT" at line "STR$ERL:ON:END
  640 
  650 ENDCASE
  660 
  670 DEFPROCB
  680 CLS:P%=-1
  690 PRINTTAB(8,3)"Jeg skal lige have"
  700 PRINTTAB(8)"lavet et par sprites."
  710 PRINTTAB(8)"Det tager kun et øjeblik."
  720 FORI=1TO2*R:Y=I-R
  730 FORJ=1TO2*R:X=J-R
  740 Z2=R^2-X^2-Y^2
  750 IF Z2<0 THEN 810
  760 Z=SQR(Z2)
  770 D2=((X-X0)^2+(Y-Y0)^2+(Z-Z0)^2)
  780 GP%=P%:P%=31-M1*(D2-M2)
  790 IF P%<>GP% PROCC(P%)
  800 POINT 4*J,4*I
  810 NEXT,
  820 ENDPROC
  830 PRINTM1:PRINTM2
  840 END
  850 
  860 DEFPROCSPRITE(Z%,N$)
  870 MOVE0,0:MOVEZ%,Z%
  880 OSCLI"SGET "+N$
  890 $PARAM%=N$
  900 SYS"OS_SpriteOp",29,0,PARAM%
  910 FORY%=0TOZ%STEP4
  920 FORX%=0TOZ%STEP4:S%=1
  930 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  940 SYS"OS_SpriteOp",44,0,PARAM%,X%/4,Y%/4,S%
  950 NEXT,
  960 ENDPROC
  970 
  980 DEFPROCC(A%)
  990 LOCAL P1%,P2%
 1000 IFA%<0 A%=0
 1010 IFA%>30 A%=30
 1020 IF A%MOD2=0 THEN
 1030   GCOL A%DIV2
 1040 ELSE
 1050   A%=A%DIV2
 1060   P1%=FNA(A%,A%+1)
 1070   P2%=FNA(A%+1,A%)
 1080   VDU23,2,P1%,P2%,P1%,P2%,P1%,P2%,P1%,P2%
 1090   GCOL16,0
 1100 ENDIF
 1110 ENDPROC
 1120 
 1130 DEFFNA(A%,B%)
 1140 LOCAL N%,C%,AB%
 1150 C%=8
 1160 FORN%=1TO4
 1170 AB%=AB%<<1:IF C%ANDA% AB%+=1
 1180 AB%=AB%<<1:IF C%ANDB% AB%+=1
 1190 C%=C%/2:NEXT
 1200 =AB%
 1210 
 1220 REM Jan Vibe august 89.
