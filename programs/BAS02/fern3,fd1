   10 REM > FERN3
   20 
   30 MODE0
   40 DIM IB% 20,OB% 20
   50 !IB%=148:IB%!4=150:IB%!8=-1
   60 SYS "OS_ReadVduVariables",IB%,OB%
   70 IF OB%!4<&78000 THEN
   80 CLS
   90 PRINT TAB(15,12)"Dette program skal danne 24 mode 0 billeder,"
  100 PRINT TAB(15)"og derfor skal der afsættes 480 k til skærmen."
  110 PRINT TAB(15)"Skriv derfor:"
  120 PRINT TAB(15)"*CONFIGURE SCREENSIZE 480K"
  130 PRINT TAB(15)"Tryk så <RETURN> og derefter <CONTROL> og <BREAK>"
  140 PRINT TAB(15)"og kør programmet igen."
  150 
  160 END
  170 ENDIF
  180 
  190 SYS"OS_UpdateMEMC",64,64
  200 ONERRORGOTO680
  210 COLOUR0,80,80,80
  220 COLOUR1,0,255,0
  230 VDU19,0,24,80,80,80
  240 OFF
  250 
  260 *FX112,1
  270 *FX113,1
  280 CLS
  290 FORN%=1TO24:S$=STR$(N%)
  300 OSCLI"FX112,"+S$
  310 CLS
  320 T=20*SINRAD(7.5*N%+90):PROCA(640,0,200,0,1)
  330 OSCLI"FX113,"+S$
  340 NEXT
  350 
  360 T%=4000
  370 REPEAT
  380 FORI%=23TO2STEP-1:S$=STR$I%
  390 OSCLI"FX113,"+S$
  400 IF T%<>0 THEN
  410 FORN%=1TOT%:NEXT
  420 ENDIF
  430 *FX15,0
  440 IFINKEY(-68) T%=T%-100:IFT%<0 T%=0
  450 IFINKEY(-82) T%=T%+100
  460 NEXT
  470 FORI%=1TO24:S$=STR$I%
  480 OSCLI"FX113,"+S$
  490 IF T%<>0 THEN
  500 FORN%=1TOT%:NEXT
  510 ENDIF
  520 *FX15,0
  530 IFINKEY(-68) T%=T%-100:IFT%<0 T%=0
  540 IFINKEY(-82) T%=T%+100
  550 NEXT
  560 UNTIL0
  570 
  580 DEFPROCA(X,Y,Z,A,M%)
  590 IFZ<=2 ENDPROC
  600 LOCAL X1,Y1
  610 X1=X+Z*SINRADA:Y1=Y+Z*COSRADA
  620 LINEX,Y,X1,Y1
  630 PROCA(X1,Y1,.80*Z,A+T*M%,M%)
  640 PROCA(X1,Y1,.37*Z,A-60*M%,M%)
  650 PROCA(X1,Y1,.37*Z,A+60*M%,-M%)
  660 ENDPROC
  670 
  680 OSCLI"FX112,"+S$
  690 OSCLI"FX113,"+S$
  700 REPORT:PRINT" at line "STR$ERL:ON
  710 
  720 REM Jan Vibe juli 89.
