   10 REM > CARDS
   20 
   30 MODE9
   40 SYS"OS_UpdateMEMC",64,64
   50 ONERRORGOTO480
   60 
   70 *SNEW
   80 FOR N%=33TO255:VDUN%,32:NEXT:PRINT:PRINT
   90 INPUT"TEKST: "T$
  100 T$=STRING$(6," ")+T$
  110 FOR N%=1TO14
  120 COLOUR N%,18.28*N%,0,18.28*(15-N%)
  130 NEXT:COLOUR0,0,128,0:COLOUR15,255,255,0
  140 FORN%=1TOLENT$
  150 CLS:PRINTMID$(T$,N%,1)
  160 C%=10:CC%=1:GCOLC%:RECTANGLE FILL 0,0,128
  170 FORM%=1TO16
  180 IF CC%+C%>14 OR CC%+C%<1 CC%=-CC%
  190 C%+=CC%:GCOLC%
  200 RECTANGLE 64-4*M%,64-4*M%,8*M%:NEXT
  210 GCOL15
  220 FORY%=0TO7
  230 FORX%=0TO7
  240 IF POINT(4*X%,4*Y%+992)<>0 THEN
  250 RECTANGLE FILL 12*X%+16,12*Y%+16,08
  260 ENDIF
  270 NEXT,
  280 N$=STR$ASCMID$(T$,N%,1)
  290 MOVE 0,0:MOVE 128,128:OSCLI"SGET "+N$
  300 NEXT:CLS:OFF
  310 
  320 S1$="1":S2$="2":Z%=0:L%=LENT$-1
  330 REPEAT
  340 FORI%=0TOL%
  350 FORN%=24TO4STEP-4
  360 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  370 Z%=(Z%+2)MOD360:Z1%=300*SINRADZ%
  380 FORM%=0TO14
  390 Q%=(I%+M%)MOD(L%+1)
  400 C%=ASCMID$(T$,Q%+1,1)
  410 OSCLI"SCH. "+STR$C%
  420 X%=24*M%+N%
  430 Y%=Z1%*SINRAD(X%)+448
  440 PLOT&ED,4*X%-128,Y%
  450 NEXT,,
  460 UNTIL0
  470 
  480 OSCLI"FX112,"+S1$
  490 REPORT:PRINT" at line "STR$ERL
  500 ON
