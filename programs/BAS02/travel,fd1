   10 REM > TRAVEL
   20 
   30 MODE9:OFF:ORIGIN 640,512
   40 SYS"OS_UpdateMEMC",64,64
   50 ONERRORGOTO280
   60 
   70 S1$="1":S2$="2":K%=40:S=1.15
   80 FORN%=1TO15:COLOUR N%,12*N%+64,12*N%+64,12*N%+64:NEXT
   90 DIM X(K%),Y(K%),Z(K%),C%(K%)
  100 FORN%=1TOK%
  110 X(N%)=RND(640)*SGN(RND(1)-.5)
  120 Y(N%)=RND(512)*SGN(RND(1)-.5)
  130 Z(N%)=RND(4):C%(N%)=0:NEXT
  140 
  150 REPEAT
  160 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  170 FORN%=1TOK%
  180 X(N%)=X(N%)*S:Y(N%)=Y(N%)*S:Z(N%)=Z(N%)*S
  190 IF ABSX(N%)>768 OR ABSY(N%)>640 THEN
  200 X(N%)=RND(640)*SGN(RND(1)-.5)
  210 Y(N%)=RND(512)*SGN(RND(1)-.5)
  220 Z(N%)=RND(4):C%(N%)=0
  230 ENDIF
  240 GCOL C%(N%):IF C%(N%)<15 C%(N%)+=1
  250 CIRCLE FILL X(N%),Y(N%),4*(Z(N%)DIV4)+4:NEXT
  260 UNTIL0
  270 
  280 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
