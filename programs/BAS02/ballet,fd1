   10 REM > BALLET
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO 520
   50 
   60 MODE9:MODE137:OFF
   70 FORN%=1TO15:COLOUR N%,16*N%,08*N%,04*N%:NEXT
   80 COLOUR0,0,128,255:VDU19,0,24,0,128,255
   90 DIMA%(4),X%(45,2),Y%(45,2)
  100 A%=RND(-TIME):A$="1":B$="2":C%=2:D%=1:K%=40
  110 
  120 X1%=60:Y1%=70:R1%=84:X2%=48:Y2%=48:R2%=48
  130 FORY%=-R2%TOR2%STEP4:B%=4*(SQR(R2%^2-Y%^2)DIV4)
  140 FORX%=-B%TOB%STEP4
  150 GCOL 16-SQR((X2%+X%-X1%)^2+(Y2%+Y%-Y1%)^2)/(R1%/17)
  160 POINT X2%+X%,Y2%+Y%:NEXT,
  170 GCOL0:RECTANGLE 0,0,2*R2%:GCOL15
  180 
  190 *SNEW
  200 MOVE0,0:MOVE96,96:*SGET KUGLE
  220 SYS"OS_SpriteOp",29,0,"KUGLE"
  230 FORY%=0TO96STEP4
  240 FORX%=0TO96STEP4:S%=1
  250 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  260 SYS"OS_SpriteOp",44,0,"KUGLE",X%/4,Y%/4,S%
  270 NEXT,
  280 
  290 FORN%=1TO4:A%(N%)=2*RND(4):NEXT
  300 FORN%=45TO135STEP2:N1%=(N%-45)/2
  310 X%(N1%,1)=450*SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)+592
  320 Y%(N1%,1)=450*SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)+464
  330 NEXT:GCOL8,15
  340 
  350 REPEAT
  360 T1%=RND(8):T2%=RND(8):T3%=RND(8)
  370 SWAP C%,D%:FORN%=1TO4:A%(N%)=2*RND(4):NEXT
  380 FORN%=45TO135STEP2:N1%=(N%-45)/2
  390 X%(N1%,D%)=450*SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)+592
  400 Y%(N1%,D%)=450*SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)+464
  410 NEXT
  420 FORI%=0TOK%
  430 FORN%=0TO45
  440 X%(N%,0)=(I%*X%(N%,D%)+(K%-I%)*X%(N%,C%))/K%
  450 Y%(N%,0)=(I%*Y%(N%,D%)+(K%-I%)*Y%(N%,C%))/K%
  460 NEXT
  470 FORN%=0TO45:PLOT&ED,X%(N%,0),Y%(N%,0):NEXT
  480 OSCLI"FX 112,"+A$:OSCLI"FX 113,"+B$:WAIT:SWAP A$,B$:CLS
  490 NEXT
  500 UNTIL0
  510 
  520 OSCLI"FX 112,"+A$
  530 ON:REPORT:PRINT" at line "STR$ERL
  540 
  550 REM ********************
  560 REM * Jan Vibe juli 89 *
  570 REM ********************
