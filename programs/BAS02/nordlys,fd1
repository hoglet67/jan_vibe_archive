   10 REM > NORDLYS
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 MODE12:ORIGIN 640,512:OFF
   50 DIM C1%(15),C2%(15),C3%(15),A%(4,2):N%=RND(-TIME):A%=0
   60 FOR N%=1TO15
   70 F1%=128*(SINRAD(24*N%)+1)
   80 F2%=128*(SINRAD(24*N%+120)+1)
   90 F3%=128*(SINRAD(24*N%+240)+1)
  100 C1%(N%)=F1%:C2%(N%)=F2%:C3%(N%)=F3%
  110 COLOUR N%,F1%,F2%,F3%
  120 NEXT
  130 
  140 REPEAT
  150 FORN%=1TO2:FORM%=1TO4
  160 A%(M%,N%)=RND(5)+1:NEXT,
  170 FORN%=0TO270:T=RAD(N%/3)
  180 X1=600*SIN(T*A%(1,1))*COS(T*A%(2,1))
  190 Y1=500*SIN(T*A%(3,1))*COS(T*A%(4,1))
  200 X2=600*SIN(T*A%(1,2))*COS(T*A%(2,2))
  210 Y2=500*SIN(T*A%(3,2))*COS(T*A%(4,2))
  220 ZX=(X2-X1)/28:ZY=(Y2-Y1)/28
  230 
  240 FORI%=1TO15:C1%(I%-1)=C1%(I%):C2%(I%-1)=C2%(I%):C3%(I%-1)=C3%(I%)
  250 NEXT:C1%(15)=C1%(0):C2%(15)=C2%(0):C3%(15)=C3%(0)
  260 FORI%=1TO15:COLOURI%,C1%(I%),C2%(I%),C3%(I%):NEXT
  270 
  280 A%=(A%+1)MOD15
  290 FORM%=1TO15:GCOL 15-(M%+A%)MOD15
  300 CIRCLE FILL X1,Y1,12:CIRCLE FILL X2,Y2,12
  310 CIRCLE FILL -X1,Y1,12:CIRCLE FILL -X2,Y2,12
  320 CIRCLE FILL -X1,-Y1,12:CIRCLE FILL -X2,-Y2,12
  330 CIRCLE FILL X1,-Y1,12:CIRCLE FILL X2,-Y2,12
  340 X1=X1+ZX:Y1=Y1+ZY:X2=X2-ZX:Y2=Y2-ZY
  350 NEXT,
  360 UNTIL0
