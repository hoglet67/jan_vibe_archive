   10 REM > NET4
   20 
   30 MODE0:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 ONERRORGOTO690
   60 
   70 DIM IB% 20,OB% 20
   80 !IB%=148:IB%!4=150:IB%!8=-1
   90 SYS "OS_ReadVduVariables",IB%,OB%
  100 IF OB%!4<&78000 THEN
  110 CLS
  120 PRINT TAB(20,15)"Dette program kraever en screensize paa"
  130 PRINT TAB(20)"60 sider, altsaa 480 K ram."
  140 END
  150 ENDIF
  160 
  170 *FX112,1
  180 *FX113,1
  190 CLS
  200 ORIGIN 0,256
  210 K%=32
  220 K2%=K%/2
  230 DIM XP%(K%,K%),YP%(K%,K%),A%(K%,K%),B%(K%,K%)
  240 FORY%=0TOK%:PY%=16*Y%
  250 FORX%=0TOK%:PX%=32*X%+8*Y%
  260 XP%(X%,Y%)=PX%
  270 YP%(X%,Y%)=PY%
  280 A%(X%,Y%)=SQR((X%-K2%)^2+(Y%-K2%)^2)*35
  290 NEXT,
  300 
  310 FORI%=1TO23:S$=STR$I%
  320 FORY%=0TOK%
  330 FORX%=0TOK%
  340 Z=15/(SQR(1+(X%-K2%)^2+(Y%-K2%)^2)/K%)
  350 A%(X%,Y%)=(A%(X%,Y%)+345)MOD360
  360 B%(X%,Y%)=Z*COSRAD(A%(X%,Y%))
  370 NEXT,
  380 
  390 OSCLI"FX112,"+S$:CLS
  400 FORY%=K%TO1STEP-1
  410 FORX%=0TOK%-1
  420 GCOL0
  430 MOVE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%)
  440 MOVE XP%(X%+1,Y%),YP%(X%+1,Y%)+B%(X%+1,Y%)
  450 PLOT85,XP%(X%,Y%-1),YP%(X%,Y%-1)+B%(X%,Y%-1)
  460 PLOT85,XP%(X%+1,Y%-1),YP%(X%+1,Y%-1)+B%(X%+1,Y%-1)
  470 GCOL1
  480 LINE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%),XP%(X%+1,Y%),YP%(X%+1,Y%)+B%(X%+1,Y%)
  490 LINE XP%(X%,Y%),YP%(X%,Y%)+B%(X%,Y%),XP%(X%,Y%-1),YP%(X%,Y%-1)+B%(X%,Y%-1)
  500 NEXT,
  510 FORN%=0TOK%-1
  520 LINE XP%(N%,0),YP%(N%,0)+B%(N%,0),XP%(N%+1,0),YP%(N%+1,0)+B%(N%+1,0)
  530 LINE XP%(K%,N%),YP%(K%,N%)+B%(K%,N%),XP%(K%,N%+1),YP%(K%,N%+1)+B%(K%,N%+1)
  540 NEXT,
  550 
  560 T%=4000
  570 REPEAT
  580 FORI%=1TO23:S$=STR$I%
  590 OSCLI"FX113,"+S$
  600 IF T%<>0 THEN
  610 FORN%=1TOT%:NEXT
  620 ENDIF
  630 *FX15,0
  640 IFINKEY(-68) T%=T%-100:IFT%<0 T%=0
  650 IFINKEY(-82) T%=T%+100
  660 NEXT
  670 UNTIL0
  680 
  690 *FX112,1
  700 *FX113,1
  710 REPORT:PRINT" at line "ERL:ON
  720 
  730 REM *--------------------------------------*
  740 REM !                                      !
  750 REM !  BlGCOLrerRETURNvsprogram Jan Vibe april 89  !
  760 REM !                                      !
  770 REM *--------------------------------------*
