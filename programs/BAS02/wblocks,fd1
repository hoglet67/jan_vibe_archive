   10 REM > WBLOCKS
   20 MODE12:ORIGIN 0,256:OFF
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO290
   50 DIM A%(18,15):S1$="1":S2$="2"
   60 REPEAT
   70 FORN%=14TO0STEP-1:FORM%=0TO18:A%(M%,N%+1)=A%(M%,N%):NEXT,
   80 K%=(K%+20)MOD360:Z%=128*SINRAD(K%+10)
   90 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAPS1$,S2$:CLS
  100 FORN%=0TO18
  110 A%(N%,0)=Z%*SINRAD(30*N%+15)
  120 NEXT
  130 FORN%=15TO0STEP-1
  140 FORM%=0TO18
  150 X1%=56*M%+12*N%:Y1%=A%(M%,N%)+32*N%
  160 X6%=X1%:Y6%=32*N%-128
  170 X5%=X6%+56:Y5%=Y6%
  180 X7%=X5%:Y7%=Y1%
  190 X2%=X1%+12:Y2%=Y1%+32
  200 X3%=X2%+56:Y3%=Y2%
  210 X4%=X5%+12:Y4%=Y5%+32
  220 GCOL1:MOVEX5%,Y5%:MOVEX4%,Y4%:PLOT&75,X3%,Y3%
  230 GCOL2:MOVEX1%,Y1%:MOVEX7%,Y7%:PLOT&75,X3%,Y3%
  240 GCOL4:MOVEX6%,Y6%:PLOT&65,X7%,Y7%
  250 GCOL0:MOVEX1%,Y1%:DRAWX2%,Y2%:DRAWX3%,Y3%:DRAWX4%,Y4%:DRAWX5%,Y5%
  260 DRAWX6%,Y6%:DRAWX1%,Y1%:DRAWX7%,Y7%:DRAWX5%,Y5%:LINEX7%,Y7%,X3%,Y3%
  270 NEXT,
  280 UNTIL0
  290 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
