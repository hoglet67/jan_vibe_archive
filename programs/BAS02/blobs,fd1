   10 REM > BLOBS
   20 
   30 MODE12:MODE140:K%=20:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 ONERRORGOTO280
   60 S1$="1":S2$="2"
   70 DIMX%(K%),Y%(K%),R(K%),D(K%),DX%(K%),DY%(K%)
   80 FORN%=1TOK%
   90 R(N%)=64+RND(64):D(N%)=R(N%)/15
  100 X%(N%)=R(N%)+RND(1280-2*R(N%))
  110 Y%(N%)=R(N%)+RND(1024-2*R(N%))
  120 DX%(N%)=4*RND(4)*SGN(RND(1)-.5)
  130 DY%(N%)=4*RND(4)*SGN(RND(1)-.5)
  140 NEXT
  150 
  160 REPEAT
  170 OSCLI"FX 112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  180 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT
  190 FORN%=1TO15:GCOL N%
  200 FORM%=1TOK%:CIRCLE FILL X%(M%),Y%(M%),R(M%)-(N%-1)*D(M%)
  210 NEXT,
  220 FORN%=1TOK%
  230 X1%=X%(N%)+DX%(N%):IFX1%-R(N%)<0ORX1%+R(N%)>1278 DX%(N%)=-DX%(N%):GOTO230
  240 Y1%=Y%(N%)+DY%(N%):IFY1%-R(N%)<0ORY1%+R(N%)>1020 DY%(N%)=-DY%(N%):GOTO240
  250 X%(N%)=X1%:Y%(N%)=Y1%:NEXT
  260 UNTIL0
  270 
  280 OSCLI"FX112,"+S1$:REPORT:PRINT" at line "STR$ERL:ON
