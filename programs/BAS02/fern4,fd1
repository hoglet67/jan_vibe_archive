   10 REM > FERN4
   20 
   30 MODE0
   40 DIM IB% 20,OB% 20
   50 !IB%=148:IB%!4=150:IB%!8=-1
   60 SYS "OS_ReadVduVariables",IB%,OB%
   70 IF OB%!4<&78000 THEN
   80 CLS
   90 PRINT TAB(15,12)"Dette program skal danne 24 mode 0 billeder,"
  100 PRINT TAB(15)"og derfor skal der afsættes 480 k til skærmen."
  110 PRINT TAB(15)"Skriv derfor:"
  120 PRINT TAB(15)"*CONFIGURE SCREENSIZE 480K"
  130 PRINT TAB(15)"Tryk så <RETURN> og derefter <CONTROL> og <BREAK>"
  140 PRINT TAB(15)"og kør programmet igen."
  150 
  160 END
  170 ENDIF
  180 
  190 SYS"OS_UpdateMEMC",64,64
  200 ONERRORGOTO730
  210 COLOUR0,80,80,80
  220 COLOUR1,0,255,0
  230 VDU19,0,24,80,80,80
  240 OFF
  250 
  260 *FX112,1
  270 *FX113,1
  280 CLS
  290 FORN%=1TO24:S$=STR$(N%)
  300 OSCLI"FX112,"+S$
  310 CLS
  320 M%=-1
  330 T=20*SINRAD(7.5*N%+90)
  340 T1=60-30*SINRAD(7.5*N%+90)
  350 FORI%=0TO300STEP60:M%=-M%
  360 PROCA(640,512,100,M%*(I%+T1),M%)
  370 NEXT
  380 OSCLI"FX113,"+S$
  390 NEXT
  400 
  410 T%=4000
  420 REPEAT
  430 FORI%=23TO2STEP-1:S$=STR$I%
  440 OSCLI"FX113,"+S$
  450 IF T%<>0 THEN
  460 FORN%=1TOT%:NEXT
  470 ENDIF
  480 *FX15,0
  490 IFINKEY(-68) T%=T%-100:IFT%<0 T%=0
  500 IFINKEY(-82) T%=T%+100
  510 NEXT
  520 FORI%=1TO24:S$=STR$I%
  530 OSCLI"FX113,"+S$
  540 IF T%<>0 THEN
  550 FORN%=1TOT%:NEXT
  560 ENDIF
  570 *FX15,0
  580 IFINKEY(-68) T%=T%-100:IFT%<0 T%=0
  590 IFINKEY(-82) T%=T%+100
  600 NEXT
  610 UNTIL0
  620 
  630 DEFPROCA(X,Y,Z,A,M%)
  640 IFZ<=2 ENDPROC
  650 LOCAL X1,Y1
  660 X1=X+Z*SINRADA:Y1=Y+Z*COSRADA
  670 LINEX,Y,X1,Y1
  680 PROCA(X1,Y1,.80*Z,A+T*M%,M%)
  690 PROCA(X1,Y1,.37*Z,A-60*M%,M%)
  700 PROCA(X1,Y1,.37*Z,A+60*M%,-M%)
  710 ENDPROC
  720 
  730 OSCLI"FX112,"+S$
  740 OSCLI"FX113,"+S$
  750 REPORT:PRINT" at line "STR$ERL:ON
  760 
  770 REM Jan Vibe juli 89.
