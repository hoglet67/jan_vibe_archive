   10 REM > URBIT
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO630
   50 
   60 MODE9:OFF
   70 DIM PARAM% 100
   80 FORN%=1TO15:COLOURN%,16*N%,16*N%,16*N%:NEXT:COLOUR0,0,0,128
   90 
  100 F%=0
  110 *SCH. PLANET
  120 *SCH. MAANE
  130 F%=1
  140 
  150 S1$="1":S2$="2"
  160 XP%=448:YP%=320
  170 A%=0:R%=-TIME:Z%=0
  180 REPEAT
  190 OSCLI"FX 112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$:CLS
  200 PRINTTAB(17,1)"Urbit."
  210 Q%=RND(R%):Z%+=4:GCOL15
  220 FORN%=1TO100:POINT (RND(1276)+Z%)MOD1280,RND(1020):NEXT
  230 GCOL8,0
  240 A%=(A%+2)MOD360:T=RADA%
  250 XM%=550*SINT
  260 YM%=100*COST+XM%/4
  270 XM%=XM%+592:YM%=YM%+464
  280 IF A%>=90 AND A%<=270 THEN
  290 *SCH. PLANET
  300 PLOT&ED,XP%,YP%
  310 PROCUR
  320 *SCH. MAANE
  330 PLOT&ED,XM%,YM%
  340 ELSE
  350 *SCH. MAANE
  360 PLOT&ED,XM%,YM%
  370 *SCH. PLANET
  380 PLOT&ED,XP%,YP%
  390 PROCUR
  400 ENDIF
  410 UNTIL0
  420 
  430 DEFPROCUR
  440 LOCAL TIM%,MIN%,SEK%
  450 TID$=RIGHT$(TIME$,8)
  460 TIM%=VALMID$(TID$,1,2):TIM%=TIM%MOD12
  470 MIN%=VALMID$(TID$,4,2)
  480 SEK%=VALMID$(TID$,7,2)
  490 GCOL1
  500 PROCH(180,6*SEK%)
  510 PROCH(170,6*MIN%+SEK%/10)
  520 PROCH(130,30*TIM%+MIN%/2+SEK%/120)
  530 GCOL8,0
  540 ENDPROC
  550 
  560 DEFPROCH(Z%,A%)
  570 MOVE640,512
  580 MOVE640+Z%*SINRAD(A%-6)/2,512+Z%*COSRAD(A%-6)/2
  590 PLOT85,640+Z%*SINRAD(A%+6)/2,512+Z%*COSRAD(A%+6)/2
  600 PLOT85,640+Z%*SINRADA%,512+Z%*COSRADA%
  610 ENDPROC
  620 
  630 CASE ERR OF
  640 WHEN 134:
  650 *SNEW
  660 X0=10:Y0=20:Z0=20:R=48
  670 M1=0.0061:M2=324:PROCB
  680 PROCSPRITE(8*R,"PLANET")
  690 X0=10:Y0=20:Z0=20:R=12
  700 M1=0.0265:M2=324:PROCB
  710 PROCSPRITE(8*R,"MAANE")
  720 GOTO ERL
  730 
  740 WHEN 133:
  750 CLS
  760 PRINTTAB(6,8)"Der er ikke plads nok til de"
  770 PRINTTAB(6)"sprites, dette program skal"
  780 PRINTTAB(6)"anvende.":PRINT
  790 PRINTTAB(6)"Du skal afsatte mindst 11k"
  800 PRINTTAB(6)"(to sider) til sprites."
  810 PRINT:ON:END
  820 
  830 OTHERWISE
  840 IF F%=1 OSCLI"FX112,"+S1$
  850 REPORT:PRINT" at line "STR$ERL:ON:END
  860 
  870 ENDCASE
  880 
  890 DEFPROCB
  900 CLS:P%=-1
  910 PRINTTAB(8,3)"Jeg skal lige have"
  920 PRINTTAB(8)"lavet et par sprites."
  930 PRINTTAB(8)"Det tager kun et øjeblik."
  940 FORI=1TO2*R:Y=I-R
  950 FORJ=1TO2*R:X=J-R
  960 Z2=R^2-X^2-Y^2
  970 IF Z2<0 THEN 1030
  980 Z=SQR(Z2)
  990 D2=((X-X0)^2+(Y-Y0)^2+(Z-Z0)^2)
 1000 GP%=P%:P%=31-M1*(D2-M2)
 1010 IF P%<>GP% PROCC(P%)
 1020 POINT 4*J,4*I
 1030 NEXT,
 1040 ENDPROC
 1050 PRINTM1:PRINTM2
 1060 END
 1070 
 1080 DEFPROCSPRITE(Z%,N$)
 1090 MOVE0,0:MOVEZ%,Z%
 1100 OSCLI"SGET "+N$
 1110 $PARAM%=N$
 1120 SYS"OS_SpriteOp",29,0,PARAM%
 1130 FORY%=0TOZ%STEP4
 1140 FORX%=0TOZ%STEP4:S%=1
 1150 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
 1160 SYS"OS_SpriteOp",44,0,PARAM%,X%/4,Y%/4,S%
 1170 NEXT,
 1180 ENDPROC
 1190 
 1200 DEFPROCC(A%)
 1210 LOCAL P1%,P2%
 1220 IFA%<0 A%=0
 1230 IFA%>30 A%=30
 1240 IF A%MOD2=0 THEN
 1250   GCOL A%DIV2
 1260 ELSE
 1270   A%=A%DIV2
 1280   P1%=FNA(A%,A%+1)
 1290   P2%=FNA(A%+1,A%)
 1300   VDU23,2,P1%,P2%,P1%,P2%,P1%,P2%,P1%,P2%
 1310   GCOL16,0
 1320 ENDIF
 1330 ENDPROC
 1340 
 1350 DEFFNA(A%,B%)
 1360 LOCAL N%,C%,AB%
 1370 C%=8
 1380 FORN%=1TO4
 1390 AB%=AB%<<1:IF C%ANDA% AB%+=1
 1400 AB%=AB%<<1:IF C%ANDB% AB%+=1
 1410 C%=C%/2:NEXT
 1420 =AB%
 1430 
 1440 REM Jan Vibe august 89.
