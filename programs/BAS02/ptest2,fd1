   10 REM > PTEST2
   20 
   30 REM 256 farvetoner i mode 12 ved hjGCOLlp af mRETURNnstre.
   40 REM kaldes pGOTO denne mGOTOde:
   50 REM
   60 REM PROCFARVE(F%)    hvor F% ligger mellem 0 og 255.
   70 REM
   80 REM Funktionen FNFLET benyttes af PROCFARVE
   90 REM
  100 REM Proceduren kan anvendes i fRETURNlgende modes : 2,9,12,16,20
  110 
  120 MODE12:OFF
  130 FORN%=1TO15:COLOURN%,16*N%,16*N%,16*N%:NEXT
  140 COLOUR0,80,100,180
  150 
  160 FORN%=1TO15:GCOL16-N%:RECTANGLE FILL 85*N%-72,924,64,64:NEXT
  170 FORN%=16TO240
  180   PROCFARVE(N%)
  190   CIRCLE FILL 320,512,(256-N%)*1.25
  200   T=RAD(1.6*(N%-16)):T2=RAD(1.6*(N%-15))
  210   MOVE960,512
  220   MOVE960+300*SINT,512+300*COST
  230   PLOT85,960+300*SINT2,512+300*COST2
  240   RECTANGLE FILL 5.7*N%-91,32,5.7,80
  250 NEXT
  260 
  270 FORR%=0TO3
  280 FORG%=0TO3
  290 FORB%=0TO3
  300 FORN%=1TO15
  310 COLOUR N%,FNA(R%,N%),FNA(G%,N%),FNA(B%,N%)
  320 NEXT:K=INKEY(200)
  330 NEXT,,
  340 
  350 FOR N%=1TO15
  360 F1%=128*(SINRAD(24*N%)+1)
  370 F2%=128*(SINRAD(24*N%+120)+1)
  380 F3%=128*(SINRAD(24*N%+240)+1)
  390 COLOUR N%,F1%,F2%,F3%
  400 NEXT
  410 
  420 ON:END
  430 
  440 DEFFNA(X%,N%)
  450 LOCAL Y%
  460 CASE X% OF
  470   WHEN 0: Y%=0
  480   WHEN 1: Y%=N%
  490   WHEN 2: Y%=16-N%
  500   WHEN 3: Y%=15
  510 ENDCASE
  520 =16*Y%
  530 
  540 DEFPROCFARVE(C%)
  550 LOCAL CP%,C1%,C2%,N%,B%(),P$
  560 DIMB%(8)
  570 C%=C%MOD256
  580 CP%=C%AND%00001111
  590 C1%=(C%AND%11110000)>>4
  600 C2%=(C1%+1)MOD16
  610 CASE CP% OF
  620   WHEN 00 : P$="00000000"
  630   WHEN 01 : P$="01000000"
  640   WHEN 02 : P$="01000001"
  650   WHEN 03 : P$="01000101"
  660   WHEN 04 : P$="01010101"
  670   WHEN 05 : P$="21010101"
  680   WHEN 06 : P$="21010121"
  690   WHEN 07 : P$="21012121"
  700   WHEN 08 : P$="21212121"
  710   WHEN 09 : P$="21212321"
  720   WHEN 10 : P$="21232321"
  730   WHEN 11 : P$="23232321"
  740   WHEN 12 : P$="23232323"
  750   WHEN 13 : P$="23232333"
  760   WHEN 14 : P$="33232333"
  770   WHEN 15 : P$="33233333"
  780 ENDCASE
  790 FORN%=1TO8
  800   CASE MID$(P$,N%,1) OF
  810     WHEN "0" : B%(N%)=FNFLET(C1%,C1%)
  820     WHEN "1" : B%(N%)=FNFLET(C1%,C2%)
  830     WHEN "2" : B%(N%)=FNFLET(C2%,C1%)
  840     WHEN "3" : B%(N%)=FNFLET(C2%,C2%)
  850   ENDCASE
  860 NEXT
  870 VDU23,2,B%(1),B%(2),B%(3),B%(4),B%(1),B%(2),B%(3),B%(4)
  880 VDU23,3,B%(5),B%(6),B%(7),B%(8),B%(5),B%(6),B%(7),B%(8)
  890 VDU23,4,B%(1),B%(2),B%(3),B%(4),B%(1),B%(2),B%(3),B%(4)
  900 VDU23,5,B%(5),B%(6),B%(7),B%(8),B%(5),B%(6),B%(7),B%(8)
  910 GCOL 80,0
  920 ENDPROC
  930 
  940 DEFFNFLET(A%,B%)
  950 LOCAL N%,C%,AB%
  960 C%=8
  970 FORN%=1TO4
  980   AB%=AB%<<1:IF C%ANDA% AB%+=1
  990   AB%=AB%<<1:IF C%ANDB% AB%+=1
 1000   C%=C%/2
 1010 NEXT
 1020 =AB%
