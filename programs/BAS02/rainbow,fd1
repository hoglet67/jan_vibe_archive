   10 REM > RAINBOW
   20 MODE12:ORIGIN 640,512:OFF
   30 DIMPX%(Z%),PY%(Z%),C1%(15),C2%(15),C3%(15),A%(4)
   40 FOR N%=1TO15
   50 F1%=128*(SINRAD(24*N%)+1)
   60 F2%=128*(SINRAD(24*N%+120)+1)
   70 F3%=128*(SINRAD(24*N%+240)+1)
   80 C1%(N%)=F1%:C2%(N%)=F2%:C3%(N%)=F3%
   90 COLOUR N%,F1%,F2%,F3%
  100 NEXT:R%=RND(-TIME)
  110 
  120 REPEAT
  130 CLS:X=0:Y=0
  140 FORN%=1TO4:A%(N%)=RND(12):NEXT
  150 FORN%=1TO360
  160 GX=X:GY=Y
  170 X=SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)
  180 Y=SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)
  190 B%=B%MOD15+1:C%=B%:FORM%=220TO500STEP20:GCOLC%:C%=C%MOD15+1
  200 LINE M%*GX,M%*GY,M%*X,M%*Y
  210 IF TIME>=4 PROCS:TIME=0
  220 NEXT,
  230 C%=0:REPEAT
  240 IF TIME>=4 PROCS:TIME=0:C%+=1
  250 UNTILC%>=100
  260 UNTIL0
  270 
  280 DEFPROCS
  290 LOCAL I%
  300 FORI%=1TO15:C1%(I%-1)=C1%(I%):C2%(I%-1)=C2%(I%):C3%(I%-1)=C3%(I%)
  310 NEXT:C1%(15)=C1%(0):C2%(15)=C2%(0):C3%(15)=C3%(0)
  320 FORI%=1TO15:COLOUR16-I%,C1%(I%),C2%(I%),C3%(I%):NEXT
  330 ENDPROC
