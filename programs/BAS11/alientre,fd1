   10 REM > ALIENTREE3  *** Jan Vibe april 97 ***
   20 
   30 MODE32:OFF
   40 DIM PATH% 100,JC% 15
   50 JC%!0=0:JC%!4=0:JC%!8=0:JC%!12=0
   60 DIM X1%(1000),Y1%(1000),X2%(1000),Y2%(1000),B%(1000)
   70 DIM RX%(1000),RY%(1000),RC%(1000)
   80 DIM R1%(1000),G1%(1000),B1%(1000),R2%(1000),G2%(1000),B2%(1000)
   90 DIM R3%(1000),G3%(1000),B3%(1000),R4%(1000),G4%(1000),B4%(1000)
  100 
  110 SYS"OS_ReadModeVariable",-1,4 TO,,SX%:SX%=2^SX%
  120 SYS"OS_ReadModeVariable",-1,5 TO,,SY%:SY%=2^SY%
  130 SYS"OS_ReadModeVariable",-1,11 TO,,XS%
  140 SYS"OS_ReadModeVariable",-1,12 TO,,YS%
  150 WS%=SX%*XS%:HS%=SY%*YS%
  160 MX%=WS%/2:LG%=HS%/3:HL%=HS%*3/4:HL2%=HS%-HL%:L%=HS%/37.5
  170 
  180 REPEAT
  190 P%=0:PR%=0:CLS
  200 PRINT"Press space if this tree is Ok,"'"any other key if not."
  210 PRINT"When the tree is rendered,"'"press s to save the picture (atpic)."
  220 PROCTREE(MX%,0,LG%,0,P%)
  230 Q$=GET$:UNTILQ$=" "
  240 PROCBACKGROUND:PROCRENDER
  250 *FX 15,0
  260 Q$=GET$:IF INSTR("Ss",Q$)<>0 THEN *SCREENSAVE ATPIC
  270 END
  280 
  290 DEFPROCBACKGROUND
  300 LOCAL N%,M%,X%,Y%,R%,F%,RGB%
  310 FORN%=0TOHL%STEP2:PROCRGB(0,((HL%-N%)*128)/HL%,255):LINE 0,HL2%+N%,WS%,HL2%+N%:NEXT
  320 FORY%=HL2%TOHS%STEP8:X%=RND(WS%):R%=RND(16)+32:F=1.3+RND(1)*2
  330 FORM%=-R%TOR%STEP2:W%=SQR(R%*R%-M%*M%)*F
  340 RGB%=(200*(M%+R%)+55*(2*R%-(M%+R%)))/(2*R%)
  350 PROCRGB(RGB%,RGB%,RGB%):LINE X%-W%,Y%+M%,X%+W%,Y%+M%
  360 NEXT,
  370 FORN%=0TOHL2%STEP2:PROCRGB(0,((HL2%-N%)*255+N%*80)/HL2%,0):LINE 0,N%,WS%,N%:NEXT
  380 ENDPROC
  390 
  400 DEFPROCRENDER
  410 LOCAL I%,I1%,N%,X1%,Y1%,B%,R1%,G1%,B1%,R2%,G2%,B2%,RD%,R%,G%,B%
  420 FORI%=0TO15:I1%=15-I%
  430 FORN%=1TOP%
  440 X1%=X1%(N%):Y1%=Y1%(N%):X2%=X2%(N%):Y2%=Y2%(N%)
  450 B%=(B%(N%)*I1%)/15:IF B%>0 THEN
  460 R1%=(R1%(N%)*I1%+R2%(N%)*I%)/15
  470 G1%=(G1%(N%)*I1%+G2%(N%)*I%)/15
  480 B1%=(B1%(N%)*I1%+B2%(N%)*I%)/15
  490 R2%=(R3%(N%)*I1%+R4%(N%)*I%)/15
  500 G2%=(G3%(N%)*I1%+G4%(N%)*I%)/15
  510 B2%=(B3%(N%)*I1%+B4%(N%)*I%)/15
  520 PROCLINE(X1%,Y1%,R1%,G1%,B1%,X2%,Y2%,R2%,G2%,B2%,B%)
  530 ENDIF
  540 NEXT,
  550 FORN%=1TOPR%
  560 REPEAT:R1%=RND(255):G1%=RND(255):B1%=RND(255):UNTILR1%+G1%+B1%<127
  570 REPEAT:R2%=RND(255):G2%=RND(255):B2%=RND(255):UNTILR2%+G2%+B2%>400
  580 FORI%=15TO1STEP-1
  590 RD%=RC%(N%)*I%/15
  600 R%=(R1%*I%+R2%*(15-I%))/15:G%=(G1%*I%+G2%*(15-I%))/15:B%=(B1%*I%+B2%*(15-I%))/15
  610 PROCRGB(R%,G%,B%):CIRCLE FILL RX%(N%),RY%(N%),RD%
  620 NEXT,
  630 ENDPROC
  640 
  650 DEFPROCTREE(X,Y,Z,A,OP%)
  660 LOCAL X1,Y1:X1=X+Z*SINRAD(A):Y1=Y+Z*COSRAD(A)
  670 LINE X,Y,X1,Y1
  680 P%+=1
  690 X1%(P%)=X:Y1%(P%)=Y:X2%(P%)=X1:Y2%(P%)=Y1:B%(P%)=SQR((X-X1)^2+(Y-Y1)^2)/2.5
  700 IF P%=1 THEN
  710 REPEAT:R1%(P%)=RND(255):G1%(P%)=RND(255):B1%(P%)=RND(255):UNTILR1%(P%)+G1%(P%)+B1%(P%)<127
  720 REPEAT:R2%(P%)=RND(255):G2%(P%)=RND(255):B2%(P%)=RND(255):UNTILR2%(P%)+G2%(P%)+B2%(P%)>400
  730 ELSE
  740 R1%(P%)=R3%(OP%):G1%(P%)=G3%(OP%):B1%(P%)=B3%(OP%)
  750 R2%(P%)=R4%(OP%):G2%(P%)=G4%(OP%):B2%(P%)=B4%(OP%)
  760 ENDIF
  770 REPEAT:R3%(P%)=RND(255):G3%(P%)=RND(255):B3%(P%)=RND(255):UNTILR3%(P%)+G3%(P%)+B3%(P%)<127
  780 REPEAT:R4%(P%)=RND(255):G4%(P%)=RND(255):B4%(P%)=RND(255):UNTILR4%(P%)+G4%(P%)+B4%(P%)>400
  790 IF Z<L% THEN
  800 PR%+=1:RC%(PR%)=RND(L%/2)+8:RX%(PR%)=X+(Z+RC%(PR%))*SINRADA:RY%(PR%)=Y+(Z+RC%(PR%))*COSRADA
  810 ELSE
  820 OP%=P%
  830 PROCTREE(X1,Y1,Z/(1.2+RND(1)*.6),A+30+RND(30),P%)
  840 PROCTREE(X1,Y1,Z/(1.2+RND(1)*.6),A-30-RND(30),OP%)
  850 ENDIF
  860 ENDPROC
  870 
  880 DEFPROCLINE(X1,Y1,R1%,G1%,B1%,X2,Y2,R2%,G2%,B2%,B)
  890 LOCAL L,K%,N%,N1%,XA,YA,XB,YB,R%,G%,B%
  900 L=SQR((X2-X1)^2+(Y2-Y1)^2)
  910 K%=L/8+RND(8):IFK%<32 K%=24+RND(8)
  920 XA=(X1+X2)/2:YA=(Y1+Y2)/2
  930 JC%!0=&010000:PROCRGB(R1%,G1%,B1%):PROCL(X1,Y1,XA,YA,B)
  940 JC%!0=&000100:PROCRGB(R2%,G2%,B2%):PROCL(XA,YA,X2,Y2,B)
  950 JC%!0=&000000
  960 FORN%=2TOK%:N1%=K%-N%
  970 XA=(X1%*(N%-2)+X2%*(N1%+2))/K%:YA=(Y1%*(N%-2)+Y2%*(N1%+2))/K%
  980 XB=(X1*N%+X2*N1%)/K%:YB=(Y1*N%+Y2*N1%)/K%
  990 R%=((N%-2)*R1%+(N1%+2)*R2%)/K%:G%=((N%-2)*G1%+(N1%+2)*G2%)/K%
 1000 B%=((N%-2)*B1%+(N1%+2)*B2%)/K%:PROCRGB(R%,G%,B%)
 1010 PROCL(XA,YA,XB,YB,B)
 1020 NEXT
 1030 XA=(X1%*(K%-1)+X2%)/K%:YA=(Y1%*(K%-1)+Y2%)/K%:XB=X1:YB=Y1
 1040 R%=((K%-1)*R1%+R2%)/K%:G%=((K%-1)*G1%+G2%)/K%
 1050 B%=((K%-1)*B1%+B2%)/K%:PROCRGB(R%,G%,B%)
 1060 PROCL(XA,YA,XB,YB,B)
 1070 ENDPROC
 1080 
 1090 DEFPROCL(X1,Y1,X2,Y2,B)
 1100 LOCAL PP%,W
 1110 X1=256*X1:Y1=256*Y1:X2=256*X2:Y2=256*Y2
 1120 PATH%!PP%=2:PP%+=4:PATH%!PP%=X1:PP%+=4:PATH%!PP%=Y1:PP%+=4
 1130 PATH%!PP%=8:PP%+=4:PATH%!PP%=X2:PP%+=4:PATH%!PP%=Y2:PP%+=4
 1140 PATH%!PP%=0:PP%+=4:PATH%!PP%=PP%
 1150 W=256*B
 1160 SYS"Draw_Stroke",PATH%,0,0,0,W,JC%,0
 1170 ENDPROC
 1180 
 1190 DEFPROCRGB(R%,G%,B%)
 1200 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
 1210 SYS "ColourTrans_SetGCOL",C%,,,&100,0
 1220 ENDPROC
