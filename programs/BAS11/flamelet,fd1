   10 REM > FLAMELET
   20 
   30 DIM SPR &8000,LF &80000
   40 Q$="ABCDEFGHIJKLMNOPQRSTUWVWXYZÆØÅ"
   50 
   60 FORA%=1TOLENQ$:T$=MID$(Q$,A%,1)
   70 
   80 MODE27:OFF
   90 
  100 SPR!0=&7FFF:SPR!4=0:SPR!8=16:SPR!12=16
  110 LF!0=&7FFFF:LF!4=0:LF!8=16:LF!12=16
  120 
  130 FORN%=1TO15:COLOURN%,255,16*N%,0:NEXT
  140 FORX%=0TO300STEP2:GCOLRND(15):POINTX%,0:NEXT
  150 FORY%=2TO262STEP2
  160 GCOL7:POINT0,Y%:POINT300,Y%
  170 FORX%=2TO298STEP2
  180 P%=POINT(X%+2*(RND(3)-2),Y%-2):IF RND(1)>.4 P%=P%MOD15+1
  190 GCOLP%:POINTX%,Y%
  200 NEXT,
  210 
  220 ORIGIN 24,48
  230 Z%=1200:FONT$="Homerton.bold"
  240 SYS"Font_SetFontColours",0,0,7,0
  250 SYS"Font_FindFont",,FONT$,Z%,Z%,0,0 TO HAN%
  260 SYS"Font_StringBBox",,T$ TO ,X1%,Y1%,X2%,Y2%
  270 SYS"Font_ConverttoOS",,X1%,Y1% TO ,X1%,Y1%
  280 SYS"Font_ConverttoOS",,X2%,Y2% TO ,X2%,Y2%
  290 SYS"Font_Paint",,T$,16,100,400
  300 GCOL0
  310 FORY%=Y1%TOY2%:FORX%=X1%TOX2%
  320 IF POINT(X%+100,Y%+400)=0 THEN POINT X%,Y%
  330 NEXT,
  340 SYS"OS_SpriteOp",256+16,SPR,"FIRE",0,X1%,Y1%,X2%,Y2%
  350 
  360 MODE28
  370 ORIGIN 64,64
  380 FORN%=0TO14:VDU30:PRINTN%:N$=STR$(N%)
  390 SYS"OS_SpriteOp",256+40,SPR,"FIRE" TO,,,W%,H%
  400 FORY%=0TOH%-1:FORX%=0TOW%-1
  410 SYS"OS_SpriteOp",256+41,SPR,"FIRE",X%,Y% TO ,,,,,C%
  420 IF C%<>0 THEN
  430 C%=(C%+16-N%)MOD15+1
  440 PROCRGB(255,16*C%,0):POINT 2*X%+X1%,2*Y%+Y1%
  450 ENDIF
  460 NEXT,
  470 SYS"OS_SpriteOp",256+16,LF,N$,1,X1%,Y1%,X2%+8,Y2%
  480 NEXT
  490 SYS"OS_SpriteOp",256+12,LF,T$
  500 NEXT
  510 END
  520 
  530 DEFPROCRGB(R%,G%,B%)
  540 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  550 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  560 ENDPROC
