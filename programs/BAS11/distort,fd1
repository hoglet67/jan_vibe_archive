   10 REM > DISTORT
   20 
   30 MODE103
   40 INPUT"SPRITEFILE="SPF$
   50 CLS
   60 
   70 SYS"OS_FSControl",28,SPF$ TO ,,LG%
   80 LG%=LG%*1.3
   90 
  100 DIM SPR LG%,BUF 16
  110 DIM XF%(1000),YF%(1000)
  120 SPR!0=LG%-1:SPR!4=0:SPR!8=16:SPR!12=16
  130 
  140 SYS"OS_SpriteOp",256+10,SPR,SPF$
  150 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  160 N$="":P%=0:REPEAT
  170 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  180 P%+=1:UNTIL T%=0
  190 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  200 W%=2*W%:H%=2*H%
  210 X%=(1600-W%)/2:Y%=(1200-H%)/2
  220 CLS
  230 SYS"OS_SpriteOp",256+34,SPR,N$,X%,Y%,0
  240 
  250 REPEAT
  260 X%=RND(1600):Y%=RND(1200)
  270 CASE RND(2)OF
  280 WHEN 1:
  290 CASE RND(4) OF
  300 WHEN1:MOVE0,0:MOVEX%-2,Y%-2:PLOT&BD,2,2
  310 WHEN2:MOVE0,Y%+2:MOVEX%-2,1200:PLOT&BD,2,Y%
  320 WHEN3:MOVEX%+2,Y%+2:MOVE1600,1200:PLOT&BD,X%,Y%
  330 WHEN4:MOVEX%+2,0:MOVE1600,Y%-2:PLOT&BD,X%,2
  340 ENDCASE
  350 WHEN 2:
  360 CASE RND(4) OF
  370 WHEN1:MOVE2,2:MOVEX%,Y%:PLOT&BE,0,0
  380 WHEN2:MOVE2,Y%:MOVEX%,1198:PLOT&BE,0,Y%+2
  390 WHEN3:MOVEX%,Y%:MOVE1598,1198:PLOT&BE,X%+2,Y%+2
  400 WHEN4:MOVEX%,2:MOVE1598,Y%:PLOT&BE,X%+2,0
  410 ENDCASE
  420 ENDCASE
  430 UNTIL0
