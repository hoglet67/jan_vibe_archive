   10 REM > MOSAIC3
   20 
   30 MODE103
   40 
   50 PRINT"This program converts a sprite into"
   60 PRINT"a mosaic with irregular tiles."'
   70 INPUT"SPRITEFILE="SPF$,'"NEW SPRITEFILE="NF$
   80 CLS
   90 
  100 SYS"OS_FSControl",28,SPF$ TO ,,LG%
  110 LG%=LG%*1.3
  120 
  130 DIM SPR LG%,BUF 16
  140 DIM XF%(1000),YF%(1000)
  150 SPR!0=LG%-1:SPR!4=0:SPR!8=16:SPR!12=16
  160 
  170 SYS"OS_SpriteOp",256+10,SPR,SPF$
  180 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  190 N$="":P%=0:REPEAT
  200 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  210 P%+=1:UNTIL T%=0
  220 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  230 W%=2*W%:H%=2*H%
  240 XA=(1600-W%)/2:XB=XA+W%:YA=(1200-H%)/2:YB=YA+H%
  250 W=XB-XA:H=YB-YA:A=W*H/100
  260 
  270 SYS"OS_SpriteOp",256+15,SPR,"MOSA",0,W%/2,H%/2,19
  280 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  290 S%=8:A%=(W%+3*S%)/S%:P%=1:OP%=2
  300 DIM PX%(A%,2),PY%(A%,2)
  310 FORX%=0TOA%:PX%(X%,P%)=RND(S%/2):PY%(X%,P%)=RND(S%/2):NEXT
  320 FORY%=0TOH%+S%STEPS%:SWAP OP%,P%
  330 FORX%=0TOA%:PX%(X%,P%)=RND(S%/2):PY%(X%,P%)=RND(S%/2):NEXT
  340 FORX%=0TOA%-1:X1%=S%*X%:X2%=X1%+S%
  350 LINE X1%+PX%(X%,OP%)-S%,Y%+PY%(X%,OP%)-S%,X1%+PX%(X%,P%)-S%,Y%+S%+PY%(X%,P%)-S%
  360 LINE X1%+PX%(X%,OP%)-S%,Y%+PY%(X%,OP%)-S%,X2%+PX%(X%+1,OP%)-S%,Y%+PY%(X%+1,OP%)-S%
  370 NEXT,
  380 FORM%=0TO H STEP2:FORN%=0TO W STEP2
  390 IF POINT(N%,M%)=0 THEN
  400 GCOL1:PF%=0:R%=0:G%=0:B%=0:L%=0
  410 PROCF(N%,M%)
  420 SYS"OS_SpriteOp",256+60,SPR,0
  430 R%=R%/PF%:G%=G%/PF%:B%=B%/PF%:L%=L%/PF%
  440 C%=R%+G%*4+B%*16
  450 GCOL C% TINT L%
  460 FORP%=1TOPF%:POINT XF%(P%)+XA,YF%(P%)+YA:NEXT
  470 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  480 ENDIF
  490 NEXT,
  500 CLS:SYS"OS_SpriteOp",256+60,SPR,0
  510 IF NF$<>"" THEN
  520 SPR!0=LG%-1:SPR!4=0:SPR!8=16:SPR!12=16
  530 SYS"OS_SpriteOp",256+9,SPR
  540 SYS"OS_SpriteOp",256+16,SPR,"Mosaic",0,XA,YA,XA+W,YA+H
  550 SYS"OS_SpriteOp",256+12,SPR,NF$
  560 ENDIF
  570 END
  580 
  590 DEFPROCF(X%,Y%)
  600 IF POINT(X%,Y%)=0 THEN
  610 PF%+=1:XF%(PF%)=X%:YF%(PF%)=Y%:POINT X%,Y%
  620 SYS"OS_SpriteOp",256+41,SPR,N$,X%/2,Y%/2 TO ,,,,,C%,T%
  630 L%+=T%
  640 R%+=C%AND3
  650 G%+=(C%AND12)DIV4
  660 B%+=(C%AND48)DIV16
  670 ENDIF
  680 IF POINT(X%+2,Y%)=0 THEN PROCF(X%+2,Y%)
  690 IF POINT(X%-2,Y%)=0 THEN PROCF(X%-2,Y%)
  700 IF POINT(X%,Y%+2)=0 THEN PROCF(X%,Y%+2)
  710 IF POINT(X%,Y%-2)=0 THEN PROCF(X%,Y%-2)
  720 ENDPROC
