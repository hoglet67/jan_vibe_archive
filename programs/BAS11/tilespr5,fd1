   10 REM > TILESPR5
   20 
   30 MODE103
   40 
   50 DIM SPR &80000,BUF 16
   60 DIM XF%(1000),YF%(1000)
   70 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 
   90 INPUT"SPRITEFILE="SPF$
  100 INPUT"NEW FILENAME="NF$
  110 SYS"OS_SpriteOp",256+10,SPR,SPF$
  120 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  130 N$="":P%=0:REPEAT
  140 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  150 P%+=1:UNTIL T%=0
  160 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  170 W%=2*W%:H%=2*H%
  180 XA=(1600-W%)/2:XB=XA+W%:YA=(1200-H%)/2:YB=YA+H%
  190 W=XB-XA:H=YB-YA:A=W*H/100
  200 GCOL 63:RECTANGLE XA,YA,W,H
  210 
  220 FORN%=1TOA
  230 REPEAT
  240 X=RND(W)+XA:Y=RND(H)+YA:C%=0
  250 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  260 P%=POINT(X+I%,Y+J%):IF P%<>0 P%=1
  270 C%+=P%:NEXT,
  280 UNTIL C%=0:POINT X,Y
  290 DX=2:DY=RND(1)*2
  300 IF RND(1)>.5 SWAP DX,DY
  310 IFRND(1)>.5 DX=-DX
  320 IFRND(1)>.5 DY=-DY
  330 FORM%=1TO2
  340 DX=-DX:DY=-DY:X1=X:Y1=Y
  350 REPEAT
  360 X1+=DX:Y1+=DY:C%=0
  370 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  380 P%=POINT(X1+I%,Y1+J%):IF P%<>0 P%=1
  390 C%+=P%:NEXT,
  400 IF C%<=2 POINT X1,Y1
  410 UNTIL C%>2:POINT X1,Y1
  420 NEXT,
  430 SYS"OS_SpriteOp",256+16,SPR,"MOSA",0,XA,YA,XA+W,YA+H
  440 CLS
  450 
  460 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  470 FORM%=0TO H STEP2:FORN%=0TO W STEP2
  480 IF POINT(N%,M%)=0 THEN
  490 GCOL63TINT255:PF%=0:R%=0:G%=0:B%=0:L%=0
  500 PROCF(N%,M%)
  510 SYS"OS_SpriteOp",256+60,SPR,0
  520 R%=R%/PF%:G%=G%/PF%:B%=B%/PF%:L%=L%/PF%
  530 C%=R%+G%*4+B%*16
  540 GCOL C% TINT L%
  550 FORP%=1TOPF%:POINT XF%(P%)+XA,YF%(P%)+YA:NEXT
  560 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  570 ENDIF
  580 NEXT,
  590 CLS:SYS"OS_SpriteOp",256+60,SPR,0
  600 IF NF$<>"" THEN
  610 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
  620 SYS"OS_SpriteOp",256+9,SPR
  630 SYS"OS_SpriteOp",256+16,SPR,"Mosaic",0,XA,YA,XA+W,YA+H
  640 SYS"OS_SpriteOp",256+12,SPR,NF$
  650 ENDIF
  660 END
  670 
  680 DEFPROCF(X%,Y%)
  690 IF POINT(X%,Y%)=0 THEN
  700 PF%+=1:XF%(PF%)=X%:YF%(PF%)=Y%:POINT X%,Y%
  710 SYS"OS_SpriteOp",256+41,SPR,N$,X%/2,Y%/2 TO ,,,,,C%,T%
  720 L%+=T%
  730 R%+=C%AND3
  740 G%+=(C%AND12)DIV4
  750 B%+=(C%AND48)DIV16
  760 ENDIF
  770 IF POINT(X%+2,Y%)=0 THEN PROCF(X%+2,Y%)
  780 IF POINT(X%-2,Y%)=0 THEN PROCF(X%-2,Y%)
  790 IF POINT(X%,Y%+2)=0 THEN PROCF(X%,Y%+2)
  800 IF POINT(X%,Y%-2)=0 THEN PROCF(X%,Y%-2)
  810 ENDPROC
