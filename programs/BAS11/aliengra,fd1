   10 REM > ALIENGRASS  *** Jan Vibe march 97 ***
   20 
   30 MODE32:OFF
   40 DIM SPR &3000,SC 16,PX%(50)
   50 SPR!0=&2FFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 SC!00=256:SC!04=256:SC!08=256:SC!12=256
   70 ONERRORGOTO510
   80 
   90 FORY%=0TO1200STEP2:PROCRGB(0,(255*(1200-Y%))/1200,255)
  100 LINE0,Y%,1600,Y%:NEXT
  110 SYS"OS_SpriteOp",256+15,SPR,"COL",0,128,16,28
  120 FORN%=0TO50:PX%(N%)=32*N%+RND(33)-17:NEXT
  130 FORN%=1TO500:SWAP PX%(RND(51)-1),PX%(RND(51)-1):NEXT:P%=-1
  140 FORN%=1200TO200STEP-20:P%+=1:NF=N%/1200
  150 REPEAT
  160 R1%=RND(255):G1%=RND(255):B1%=RND(255)
  170 UNTILR1%+G1%+B1%<=189
  180 REPEAT
  190 R2%=RND(255):G2%=RND(255):B2%=RND(255)
  200 UNTILR2%+G2%+B2%>=510
  210 SYS"OS_SpriteOp",256+60,SPR,"COL"
  220 FORY%=0TO15STEP2:FORX%=0TO255STEP2
  230 I1%=ABS(128-X%):I2%=128-I1%
  240 R%=FNMIX(R1%,R2%,I1%,I2%):G%=FNMIX(G1%,G2%,I1%,I2%)
  250 B%=FNMIX(B1%,B2%,I1%,I2%):PROCRGB(R%,G%,B%)
  260 POINT X%,Y%:NEXT,
  270 SYS"OS_SpriteOp",256+60,SPR,0
  280 S=RND(1)*2*PI:D=RND(1)*.02+0.015:X%=PX%(P%):W=NF*N%/8:E=RND(1)*.75+.25
  290 FORY%=0TON%STEP2
  300 S=S+D:F=(1-Y%/N%):X1%=100*F*E*SINS+X%
  310 SC!00=256*F*NF
  320 SYS"OS_SpriteOp",256+60,SPR,"COL"
  330 C%=RND(16)-1
  340 SYS"OS_SpriteOp",256+16,SPR,"LINE",0,0,C%,255,C%
  350 SYS"OS_SpriteOp",256+60,SPR,0
  360 SYS"OS_SpriteOp",256+52,SPR,"LINE",X1%-W*F,Y%,0,SC
  370 NEXT,
  380 END
  390 
  400 DEFPROCRGB(R%,G%,B%)
  410 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  420 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  430 ENDPROC
  440 
  450 DEFFNMIX(C1%,C2%,I1%,I2%)
  460 LOCAL C%
  470 C%=(C1%*I1%+C2%*I2%)/(I1%+I2%)+RND(33)-17:IFC%>255 C%=255
  480 IFC%<0 C%=0
  490 =C%
  500 
  510 SYS"OS_SpriteOp",256+60,SPR,0:REPORT:PRINT" at line "STR$(ERL)
