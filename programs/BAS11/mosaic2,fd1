   10 REM > MOSAIC2
   20 
   30 MODE99
   40 
   50 PRINT"This program converts a sprite into"
   60 PRINT"a mosaic with irregular tiles."'
   70 INPUT"SPRITEFILE="SPF$
   80 INPUT"NEW SPRITEFILE="NF$
   90 
  100 SYS"OS_FSControl",28,SPF$ TO ,,LG%
  110 LG%=(LG%DIV256+4)*512
  120 
  130 DIM SPR LG%,BUF 16
  140 DIM XF%(1000),YF%(1000)
  150 SPR!0=LG%-1:SPR!4=0:SPR!8=16:SPR!12=16
  160 
  170 SYS"OS_SpriteOp",256+10,SPR,SPF$
  180 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  190 N$="":P%=0:REPEAT
  200 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  210 P%+=1:UNTIL T%=0
  220 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  230 W%=2*W%:H%=2*H%
  240 XA=(1280-W%)/2:XB=XA+W%:YA=(1024-H%)/2:YB=YA+H%
  250 W=XB-XA:H=YB-YA:A=W*H/100
  260 CLS:GCOL 63:RECTANGLE XA,YA,W,H
  270 
  280 FORN%=1TOA
  290 REPEAT
  300 X=RND(W)+XA:Y=RND(H)+YA:C%=0
  310 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  320 P%=POINT(X+I%,Y+J%):IF P%<>0 P%=1
  330 C%+=P%:NEXT,
  340 UNTIL C%=0:POINT X,Y
  350 DX=2:DY=RND(1)*2
  360 IF RND(1)>.5 SWAP DX,DY
  370 IFRND(1)>.5 DX=-DX
  380 IFRND(1)>.5 DY=-DY
  390 FORM%=1TO2
  400 DX=-DX:DY=-DY:X1=X:Y1=Y
  410 REPEAT
  420 X1+=DX:Y1+=DY:C%=0
  430 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  440 P%=POINT(X1+I%,Y1+J%):IF P%<>0 P%=1
  450 C%+=P%:NEXT,
  460 IF C%<=2 POINT X1,Y1
  470 UNTIL C%>2:POINT X1,Y1
  480 NEXT,
  490 SYS"OS_SpriteOp",256+16,SPR,"MOSA",0,XA,YA,XA+W,YA+H
  500 CLS
  510 
  520 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  530 FORM%=0TO H STEP2:FORN%=0TO W STEP2
  540 IF POINT(N%,M%)=0 THEN
  550 GCOL63TINT255:PF%=0:R%=0:G%=0:B%=0:L%=0
  560 PROCF(N%,M%)
  570 SYS"OS_SpriteOp",256+60,SPR,0
  580 R%=R%/PF%:G%=G%/PF%:B%=B%/PF%:L%=L%/PF%
  590 C%=R%+G%*4+B%*16
  600 GCOL C% TINT L%
  610 FORP%=1TOPF%:POINT XF%(P%)+XA,YF%(P%)+YA:NEXT
  620 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  630 ENDIF
  640 NEXT,
  650 CLS:SYS"OS_SpriteOp",256+60,SPR,0
  660 IF NF$<>"" THEN
  670 SPR!0=LG%-1:SPR!4=0:SPR!8=16:SPR!12=16
  680 SYS"OS_SpriteOp",256+9,SPR
  690 SYS"OS_SpriteOp",256+16,SPR,"Mosaic",0,XA,YA,XA+W,YA+H
  700 SYS"OS_SpriteOp",256+12,SPR,NF$
  710 ENDIF
  720 END
  730 
  740 DEFPROCF(X%,Y%)
  750 IF POINT(X%,Y%)=0 THEN
  760 PF%+=1:XF%(PF%)=X%:YF%(PF%)=Y%:POINT X%,Y%
  770 SYS"OS_SpriteOp",256+41,SPR,N$,X%/2,Y%/2 TO ,,,,,C%,T%
  780 L%+=T%
  790 R%+=C%AND3
  800 G%+=(C%AND12)DIV4
  810 B%+=(C%AND48)DIV16
  820 ENDIF
  830 IF POINT(X%+2,Y%)=0 THEN PROCF(X%+2,Y%)
  840 IF POINT(X%-2,Y%)=0 THEN PROCF(X%-2,Y%)
  850 IF POINT(X%,Y%+2)=0 THEN PROCF(X%,Y%+2)
  860 IF POINT(X%,Y%-2)=0 THEN PROCF(X%,Y%-2)
  870 ENDPROC
