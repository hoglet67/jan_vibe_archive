   10 REM > TILESPR3
   20 
   30 MODE103
   40 
   50 DIM SPR &80000,BUF 16
   60 DIM XF%(1000),YF%(1000)
   70 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 
   90 INPUT"SPRITEFILE="SPF$
  100 SYS"OS_SpriteOp",256+10,SPR,SPF$
  110 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  120 N$="":P%=0:REPEAT
  130 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  140 P%+=1:UNTIL T%=0
  150 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  160 SYS"OS_SpriteOp",256+15,SPR,"MOSA",0,W%,H%,21
  170 W%=2*W%:H%=2*H%
  180 XA=(1600-W%)/2:XB=XA+W%:YA=(1200-H%)/2:YB=YA+H%
  190 W=XB-XA:H=YB-YA:A=W*H/100
  200 GCOL 63:RECTANGLE XA,YA,W,H
  210 
  220 FORN%=1TOA
  230 REPEAT
  240 X=RND(W)+XA:Y=RND(H)+YA:C%=0
  250 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  260 P%=POINT(X+I%,Y+J%):IF P%<>0 P%=1
  270 C%+=P%:NEXT,
  280 UNTIL C%=0:POINT X,Y
  290 DX=2:DY=RND(1)*2
  300 IF RND(1)>.5 SWAP DX,DY
  310 IFRND(1)>.5 DX=-DX
  320 IFRND(1)>.5 DY=-DY
  330 FORM%=1TO2
  340 DX=-DX:DY=-DY:X1=X:Y1=Y
  350 REPEAT
  360 X1+=DX:Y1+=DY:C%=0
  370 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  380 P%=POINT(X1+I%,Y1+J%):IF P%<>0 P%=1
  390 C%+=P%:NEXT,
  400 IF C%<=2 POINT X1,Y1
  410 UNTIL C%>2:POINT X1,Y1
  420 NEXT,
  430 
  440 FORM%=0TO H STEP2:FORN%=0TO W STEP2
  450 IF POINT(N%+XA,M%+YA)=0 THEN
  460 GCOL63TINT255:PF%=0:R%=0:G%=0:B%=0:L%=0
  470 PROCF(N%,M%)
  480 R%=R%/PF%:G%=G%/PF%:B%=B%/PF%:L%=L%/PF%
  490 C%=R%+G%*4+B%*16
  500 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  510 GCOL C% TINT L%
  520 FORP%=1TOPF%:POINT XF%(P%),YF%(P%):NEXT
  530 SYS"OS_SpriteOp",256+60,SPR,0
  540 ENDIF
  550 NEXT,
  560 CLS:SYS"OS_SpriteOp",256+34,SPR,"MOSA",XA,YA
  570 END
  580 
  590 DEFPROCF(X%,Y%)
  600 IF POINT(X%+XA,Y%+YA)=0 THEN
  610 PF%+=1:XF%(PF%)=X%:YF%(PF%)=Y%:POINT X%+XA,Y%+YA
  620 SYS"OS_SpriteOp",256+41,SPR,N$,X%/2,Y%/2 TO ,,,,,C%,T%
  630 L%+=T%
  640 R%+=C%AND3
  650 G%+=(C%AND12)DIV4
  660 B%+=(C%AND48)DIV16
  670 ENDIF
  680 IF POINT(X%+2+XA,Y%+YA)=0 THEN PROCF(X%+2,Y%)
  690 IF POINT(X%-2+XA,Y%+YA)=0 THEN PROCF(X%-2,Y%)
  700 IF POINT(X%+XA,Y%+2+YA)=0 THEN PROCF(X%,Y%+2)
  710 IF POINT(X%+XA,Y%-2+YA)=0 THEN PROCF(X%,Y%-2)
  720 ENDPROC
