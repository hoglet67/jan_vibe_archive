   10 REM > TILESPR2
   20 
   30 MODE103
   40 
   50 DIM SPR &80000,BUF 16
   60 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 
   80 INPUT"SPRITEFILE="SPF$
   90 SYS"OS_SpriteOp",256+10,SPR,SPF$
  100 SYS"OS_SpriteOp",256+13,SPR,BUF,16,1
  110 N$="":P%=0:REPEAT
  120 T%=BUF?P%:IF T%<>0 N$+=CHR$T%
  130 P%+=1:UNTIL T%=0
  140 SYS"OS_SpriteOp",256+40,SPR,N$ TO ,,, W%,H%
  150 SYS"OS_SpriteOp",256+15,SPR,"MOSA",0,W%,H%,21
  160 W%=2*W%:H%=2*H%
  170 X1%=(1600-W%)/2:X2%=X1%+W%:Y1%=(1200-H%)/2:Y2%=Y1%+H%
  180 PROCTILE(X1%,Y1%,X2%,Y2%)
  190 END
  200 
  210 DEFPROCTILE(XA,YA,XB,YB)
  220 LOCAL W,H,A,DX,DY,X,Y,X1,Y1,N%,M%,I%,J%,C%,P%,T%,R%,G%,B%,L%,PF%,XF%(),YF%()
  230 DIM XF%(1000),YF%(1000)
  240 W=XB-XA:H=YB-YA:A=W*H/100
  250 GCOL 63:RECTANGLE XA,YA,W,H
  260 FORN%=1TOA
  270 REPEAT
  280 X=RND(W)+XA:Y=RND(H)+YA:C%=0
  290 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  300 P%=POINT(X+I%,Y+J%):IF P%<>0 P%=1
  310 C%+=P%:NEXT,
  320 UNTIL C%=0:POINT X,Y
  330 DX=2:DY=RND(1)*2
  340 IF RND(1)>.5 SWAP DX,DY
  350 IFRND(1)>.5 DX=-DX
  360 IFRND(1)>.5 DY=-DY
  370 FORM%=1TO2
  380 DX=-DX:DY=-DY:X1=X:Y1=Y
  390 REPEAT
  400 X1+=DX:Y1+=DY:C%=0
  410 FORJ%=-2TO2STEP2:FORI%=-2TO2STEP2
  420 P%=POINT(X1+I%,Y1+J%):IF P%<>0 P%=1
  430 C%+=P%:NEXT,
  440 IF C%<=2 POINT X1,Y1
  450 UNTIL C%>2:POINT X1,Y1
  460 NEXT,
  470 FORM%=0TO H STEP2:FORN%=0TO W STEP2
  480 IF POINT(N%+XA,M%+YA)=0 THEN
  490 GCOL63TINT255:PF%=0:R%=0:G%=0:B%=0:L%=0
  500 PROCF(N%,M%)
  510 R%=R%/PF%:G%=G%/PF%:B%=B%/PF%:L%=L%/PF%
  520 C%=R%+G%*4+B%*16
  530 SYS"OS_SpriteOp",256+60,SPR,"MOSA"
  540 GCOL C% TINT L%
  550 FORP%=1TOPF%:POINT XF%(P%),YF%(P%):NEXT
  560 SYS"OS_SpriteOp",256+60,SPR,0
  570 ENDIF
  580 NEXT,
  590 CLS
  600 SYS"OS_SpriteOp",256+34,SPR,"MOSA",XA,YA
  610 ENDPROC
  620 
  630 DEFPROCF(X%,Y%)
  640 IF POINT(X%+XA,Y%+YA)=0 THEN
  650 PF%+=1:XF%(PF%)=X%:YF%(PF%)=Y%:POINT X%+XA,Y%+YA
  660 SYS"OS_SpriteOp",256+41,SPR,N$,X%/2,Y%/2 TO ,,,,,C%,T%
  670 L%+=T%
  680 R%+=C%AND3
  690 G%+=(C%AND12)DIV4
  700 B%+=(C%AND48)DIV16
  710 ENDIF
  720 IF POINT(X%+2+XA,Y%+YA)=0 THEN PROCF(X%+2,Y%)
  730 IF POINT(X%-2+XA,Y%+YA)=0 THEN PROCF(X%-2,Y%)
  740 IF POINT(X%+XA,Y%+2+YA)=0 THEN PROCF(X%,Y%+2)
  750 IF POINT(X%+XA,Y%-2+YA)=0 THEN PROCF(X%,Y%-2)
  760 ENDPROC
