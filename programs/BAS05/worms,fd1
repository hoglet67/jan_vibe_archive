   10 REM > WORMS
   20 
   30 REM Jan Vibe december 91
   40 
   50 MODE15:OFF
   60 Z%=12:K%=2*Z%+12:ST%=Z%/4:IF ST%<4 ST%=4
   70 DIMPX(12),PY(12),LX%(2000),LY%(2000)
   80 FORN%=1TO12:T=RAD(30*N%):PX(N%)=(Z%+8)*COST:PY(N%)=(Z%+8)*SINT:NEXT
   90 PROCA(640,512,RND(360),RND(26),1)
  100 GCOL RND(64)-1 TINT RND(256)-1:FILL0,0
  110 ON:END
  120 
  130 DEFPROCA(X%,Y%,A%,F%,TP%)
  140 LOCAL T,X1%,Y1%,N%,C%,V%,W%
  150 GCOLF%:CIRCLE FILL X%,Y%,Z%
  160 LX%(TP%)=X%:LY%(TP%)=Y%
  170 V%=A%-15+RND(30):T=RADV%
  180 IF POINT(X%+K%*SINT,Y%+K%*COST)=0 THEN
  190   FORN%=1TO12
  200     W%=POINT(X%+PX(N%),Y%+PY(N%))
  210     IFW%=0 OR W%=F% C%+=1
  220   NEXT
  230   IF C%=12 PROCA(X%+ST%*SINT,Y%+ST%*COST,V%,F%,TP%+1)
  240 ENDIF
  250 
  260 IF POINT(X%,Y%)=F% THEN
  270   FORW%=0TO7:T=(7-W%)*Z%/7:PROCF(F%,W%)
  280   FORN%=1TOTP%:CIRCLE FILL LX%(N%),LY%(N%),T:NEXT,
  290 ENDIF
  300 
  310 V%=A%+90:C%=0:T=RADV%:X1%=X%+K%*SINT:Y1%=Y%+K%*COST
  320 IF POINT(X1%,Y1%)=0 THEN
  330   FORN%=1TO12:C%+=POINT(X1%+PX(N%),Y1%+PY(N%)):NEXT
  340   IF C%=0 PROCA(X%+K%*SINT,Y%+K%*COST,V%,RND(26),1)
  350 ENDIF
  360 V%=A%-90:C%=0:T=RADV%:X1%=X%+K%*SINT:Y1%=Y%+K%*COST
  370 IF POINT(X1%,Y1%)=0 THEN
  380   FORN%=1TO12:C%+=POINT(X1%+PX(N%),Y1%+PY(N%)):NEXT
  390   IF C%=0 PROCA(X%+K%*SINT,Y%+K%*COST,V%,RND(26),1)
  400 ENDIF
  410 ENDPROC
  420 
  430 DEFPROCF(F%,I%)
  440 LOCAL R%,G%,B%
  450 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  460 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
  470 GCOL F% TINT (64*I%)MOD256
  480 ENDPROC
