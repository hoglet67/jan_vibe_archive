   10 REM > RAINBOW
   20 MODE9:ORIGIN 640,512:OFF
   30 DIMC1%(15),C2%(15),C3%(15),A%(4)
   40 FOR N%=1TO15
   50 F1%=128*(SINRAD(24*N%)+1)
   60 F2%=128*(SINRAD(24*N%+120)+1)
   70 F3%=128*(SINRAD(24*N%+240)+1)
   80 C1%(N%)=F1%:C2%(N%)=F2%:C3%(N%)=F3%
   90 COLOUR N%,F1%,F2%,F3%
  100 NEXT:R%=RND(-TIME)
  110 
  120 REPEAT
  130 CLS:X=0:Y=0
  140 FORN%=1TO4:A%(N%)=RND(8):NEXT
  150 FORN%=1TO360
  160 GX=X:GY=Y
  170 X=SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)
  180 Y=SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)
  190 B%=B%MOD15+1:C%=B%
  200 FORM%=228TO484STEP16:M1%=M%+16
  210 GCOLC%:C%=C%MOD15+1
  220 MOVE M%*GX,M%*GY:MOVEM%*X,M%*Y
  230 PLOT85,M1%*GX,M1%*GY
  240 PLOT85,M1%*X,M1%*Y
  250 IF TIME>=3 PROCS:TIME=0
  260 NEXT,
  270 C%=0:REPEAT
  280 IF TIME>=3 PROCS:TIME=0:C%+=1
  290 UNTILC%>=100
  300 UNTIL0
  310 
  320 DEFPROCS
  330 LOCAL I%
  340 FORI%=1TO15:C1%(I%-1)=C1%(I%):C2%(I%-1)=C2%(I%):C3%(I%-1)=C3%(I%)
  350 NEXT:C1%(15)=C1%(0):C2%(15)=C2%(0):C3%(15)=C3%(0)
  360 FORI%=1TO15:COLOUR16-I%,C1%(I%),C2%(I%),C3%(I%):NEXT
  370 ENDPROC
