   10 REM > FERN12
   20 
   30 REM Jan Vibe november 91
   40 
   50 MODE140:MODE12:OFF
   60 S1%=1:S2%=2:K%=32:K2%=K%/2:T1%=0:T2%=120:T3%=240
   70 DIMA%(4),C1%(15),C2%(15),C3%(15),PX%(24)
   80 FORN%=1TO15
   90 F1%=24*N%:F2%=F1%+120:F3%=F1%+240
  100 C1%(N%)=127*(SINRADF1%+1)
  110 C2%(N%)=127*(SINRADF2%+1)
  120 C3%(N%)=127*(SINRADF3%+1)
  130 COLOUR N%,C1%(N%),C2%(N%),C3%(N%)
  140 NEXT
  150 
  160 DIM SPR &90000:SPR!0=&90000:SPR!4=0:SPR!8=16:SPR!12=16
  170 
  180 ONERRORGOTO520
  190 FORI%=1TO23:S$=STR$I%:XA%=1280:XB%=0:YB%=0
  200 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  210 T=20*SINRAD(7.5*I%+90):PROCA(640,0,144,0,1,1)
  220 SYS 46,256+16,SPR,S$,0,XA%,-0,XB%,YB%:PX%(I%)=XA%
  230 NEXT
  240 
  250 A%=43:REPEAT:A%=(A%+1)MOD44:B%=ABS(22-A%)+1:S$=STR$B%
  260 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  270 SYS 46,256+34,SPR,S$,PX%(B%),0,0:W%=INKEY(2)
  280 IF TIME>=6 PROCC:TIME=0
  290 UNTIL0
  300 
  310 DEFPROCA(X,Y,Z,A%,M%,F%)
  320 IFZ<=3 ENDPROC
  330 LOCAL X1,Y1
  340 IF TIME>=6 PROCC:TIME=0
  350 X1=X+Z*SINRADA%:Y1=Y+Z*COSRADA%
  360 GCOL F%:F%=F%MOD15+1:LINEX,Y,X1,Y1
  370 IF X1<XA% XA%=X1
  380 IF X1>XB% XB%=X1
  390 IF Y1>YB% YB%=Y1
  400 PROCA(X1,Y1,.85*Z,A%+T*M%,M%,F%)
  410 PROCA(X1,Y1,.35*Z,A%-60*M%,M%,F%)
  420 PROCA(X1,Y1,.35*Z,A%+60*M%,M%,F%)
  430 ENDPROC
  440 
  450 DEFPROCC
  460 LOCAL I%
  470 FORI%=1TO15:J%=I%-1:C1%(J%)=C1%(I%):C2%(J%)=C2%(I%):C3%(J%)=C3%(I%)
  480 NEXT:C1%(15)=C1%(0):C2%(15)=C2%(0):C3%(15)=C3%(0)
  490 FORI%=1TO15:COLOUR16-I%,C1%(I%),C2%(I%),C3%(I%):NEXT
  500 ENDPROC
  510 
  520 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON:END
