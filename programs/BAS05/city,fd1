   10 REM > CITY
   20 
   30 REM Jan Vibe april 92
   40 
   50 MODE15:OFF
   60 COLOUR176TINT255:CLS
   70 FORN%=1TO1000:POINTRND(1280),RND(1024):NEXT
   80 GCOL15:CIRCLE FILL 200,900,60
   90 GCOL48:CIRCLE FILL 230,920,60:GCOL63
  100 FORN%=768TO80STEP-8
  110   PROCHUS(N%)
  120 NEXT
  130 END
  140 
  150 DEFPROCHUS(H%)
  160 LOCALB%,N%,M%,X%,Y%,WX%,WY%,BX%,BY%,PX%,PY%
  170 B%=RND(H%/4)+80:X%=RND(1280-B%)
  180 GCOLRND(63)TINT0:RECTANGLE FILL X%,0,B%,H%
  190 WX%=2*RND(6)+2:BX%=WX%+2*RND(6)+2:PX%=(B%-(B%DIVBX%-1)*BX%-WX%)/2
  200 WY%=4*RND(6)+4:BY%=WY%+4*RND(6)+4:PY%=(H%-(H%DIVBY%-1)*BY%-WY%)/2
  210 GCOLRND(63)TINT255
  220 FORM%=0TOH%-BY%STEPBY%
  230   FORN%=0TOB%-BX%STEPBX%
  240     RECTANGLE FILL X%+N%+PX%,M%+PY%,WX%,WY%
  250 NEXT,
  260 GCOL0TINT0:RECTANGLE X%,0,B%,H%
  270 IF RND(1)>.7 THEN
  280   Y%=H%+4:GCOLRND(64)
  290   REPEAT
  300     BX%=RND(B%/4)+B%/8:BY%=RND(32)+8:GCOLRND(63)
  310     MOVEX%,Y%:MOVEX%+B%,Y%:PLOT85,X%+BX%,Y%+BY%:PLOT85,X%+B%-BX%,Y%+BY%
  320     IF RND(1)>.5 THEN
  330       GCOLRND(63)
  340       FORN%=0TOB%STEPRND(8)*2:M%=N%*(B%-2*BX%)/B%+BX%
  350       LINE N%+X%,Y%,M%+X%,Y%+BY%:NEXT
  360     ENDIF
  370     GCOL0TINT0
  380     MOVEX%,Y%:DRAWX%+BX%,Y%+BY%:DRAWX%+B%-BX%,Y%+BY%:DRAWX%+B%,Y%
  390     X%+=BX%:Y%+=BY%+4:B%-=2*BX%
  400   UNTIL B%<=16
  410   IF RND(1)>.5 LINE X%+B%/2,Y%,X%+B%/2,Y%+RND(8)*4+16
  420 ENDIF
  430 ENDPROC
