   10 REM > BTEST
   20 
   30 MODE15:OFF:ORIGIN 640,512
   40 COLOUR176:CLS
   50 FORZ%=120TO-120STEP-48
   60 FORY%=-120TO120STEP48
   70 FORX%=-120TO120STEP48
   80 PROCA(X%-10,Y%-10,Z%-10,X%+10,Y%+10,Z%+10,RND(63))
   90 NEXT,,
  100 END
  110 
  120 DEFPROCA(XA,YA,ZA,XB,YB,ZB,C%)
  130 IF ABS(XA)<ABS(XB) THEN
  140 PROCB(XA,YA,ZA,XA,YA,ZB,XA,YB,ZB,XA,YB,ZA,C%)
  150 ELSE
  160 PROCB(XB,YA,ZA,XB,YA,ZB,XB,YB,ZB,XB,YB,ZA,C%)
  170 ENDIF
  180 IF ABS(YA)<ABS(YB) THEN
  190 PROCB(XA,YA,ZA,XA,YA,ZB,XB,YA,ZB,XB,YA,ZA,C%)
  200 ELSE
  210 PROCB(XA,YB,ZA,XA,YB,ZB,XB,YB,ZB,XB,YB,ZA,C%)
  220 ENDIF
  230 PROCB(XA,YA,ZA,XB,YA,ZA,XB,YB,ZA,XA,YB,ZA,C%)
  240 ENDPROC
  250 
  260 DEFPROCB(X1,Y1,Z1,X2,Y2,Z2,X3,Y3,Z3,X4,Y4,Z4,C%)
  270 LOCAL S,D:S=2.5:D=400
  280 Z1=S/(1+Z1/D):X1=Z1*X1:Y1=Z1*Y1
  290 Z2=S/(1+Z2/D):X2=Z2*X2:Y2=Z2*Y2
  300 Z3=S/(1+Z3/D):X3=Z3*X3:Y3=Z3*Y3
  310 Z4=S/(1+Z4/D):X4=Z4*X4:Y4=Z4*Y4
  320 GCOLC% TINT 255:MOVEX1,Y1:MOVEX2,Y2:PLOT85,X4,Y4:PLOT85,X3,Y3
  330 GCOLC% TINT 0:MOVEX1,Y1:DRAWX2,Y2:DRAWX3,Y3:DRAWX4,Y4:DRAWX1,Y1
  340 ENDPROC
