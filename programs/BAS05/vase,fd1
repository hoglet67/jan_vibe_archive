   10 REM > VASE
   20 
   30 MODE140:MODE12:ORIGIN 640,0:OFF
   40 DIM VX%(36,2),VY%(36,2),PX%(36),PY%(36)
   50 S1%=1:S2%=2:P1%=1:P2%=2:K%=10
   60 FORN%=1TO12:T%=16*N%+63:COLOUR N%,T%,T%,T%:NEXT:COLOUR 0,0,0,255
   70 FORI%=1TO2:PROCV(I%):NEXT
   80 
   90 ONERRORGOTO520
  100 REPEAT
  110 FORN%=0TOK%
  120 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  130 FORI%=0TO36
  140 PX%(I%)=(VX%(I%,P1%)*(K%-N%)+VX%(I%,P2%)*N%)/K%
  150 PY%(I%)=(VY%(I%,P1%)*(K%-N%)+VY%(I%,P2%)*N%)/K%
  160 NEXT
  170 FORI%=1TO35:PROCL(PX%(I%),PY%(I%),PX%(I%+1),PY%(I%+1))
  180 NEXT,:SWAP P1%,P2%:PROCV(P2%)
  190 UNTIL0
  200 
  210 DEFPROCL(X1%,Y1%,X2%,Y2%)
  220 LOCAL XA%,XB%,N%
  230 FORN%=0TO11:XA%=((11.5-N%)*X1%)/11.5:XB%=((11.5-N%)*X2%)/11.5
  240 GCOLN%+1:MOVE XA%,Y1%:MOVE-XA%,Y1%:PLOT85,XB%,Y2%:PLOT85,-XB%,Y2%:NEXT
  250 ENDPROC
  260 
  270 DEFPROCV(Q%)
  280 LOCAL H%,K%,N%,R%,T%,X%(),Y%(),BX(),BY()
  290 DIM X%(15),Y%(15),BX(3),BY(3)
  300 K%=SGN(RND(1)-.5):H%=0:R%=RND(4)
  310 FORN%=1TO 3*R%+1 STEP3
  320 X%(N%-1)=300-RND(300)*K%:Y%(N%-1)=H%:H%+=RND(250/R%)+200/R%
  330 X%(N%+1)=300+RND(300)*K%:Y%(N%+1)=H%:H%+=RND(250/R%)+200/R%
  340 X%(N%)=(X%(N%-1)+X%(N%+1))/2:Y%(N%)=(Y%(N%-1)+Y%(N%+1))/2
  350 K%=-K%:H%+=RND(30)+32
  360 NEXT:H%=64-Y%(1)
  370 FORN%=1TO 3*R% STEP3
  380 FORM%=0TO3:BX(M%)=X%(N%+M%):BY(M%)=Y%(N%+M%)+H%:NEXT
  390 PROCBEZIER(BX(),BY(),36/R%)
  400 NEXT
  410 ENDPROC
  420 
  430 DEFPROCBEZIER(X(),Y(),P%)
  440 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  450 DIM F(3):RESTORE:FOR I%=0TO3:READ F(I%):NEXT
  460 FORJ%=1TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  470 FORI%=0TO3:K=F(I%)*U1^I%*U2^(3-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  480 NEXT:T%+=1:VX%(T%,Q%)=PX:VY%(T%,Q%)=PY:NEXT
  490 DATA 1,3,3,1
  500 ENDPROC
  510 
  520 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
