   10 REM > BLOBHUNT
   20 
   30 MODE12
   40 ORIGIN 200,384:GCOL7:RECTANGLE -4,-4,264
   50 FORN%=1TO100:GCOLRND(7)
   60 CIRCLE FILL RND(192)+32,RND(192)+32,RND(29)+3:NEXT
   70 
   80 C%=RND(7):GCOLC%
   90 FORY%=0TO256STEP4
  100 FORX%=0TO256STEP2
  110 IF POINT(X%,Y%)=C% POINT X%+300,Y%
  120 NEXT,
  130 
  140 ORIGIN 500,384:GCOL7:RECTANGLE -4,-4,264
  150 FORY%=0TO256STEP4
  160 FORX%=0TO256STEP2
  170 IF POINT(X%,Y%)<>0 THEN
  180   K%=0
  190   IF POINT(X%-2,Y%-4) K%+=1
  200   IF POINT(X%-2,Y%+4) K%+=1
  210   IF POINT(X%+2,Y%-4) K%+=1
  220   IF POINT(X%+2,Y%+4) K%+=1
  230   IF POINT(X%,Y%-4) K%+=1
  240   IF POINT(X%,Y%+4) K%+=1
  250   IF POINT(X%-2,Y%) K%+=1
  260   IF POINT(X%+2,Y%) K%+=1
  270   CASE K% OF
  280     WHEN 6,7,8: GCOL 4
  290     OTHERWISE: GCOL 7
  300   ENDCASE
  310   POINT X%+300,Y%
  320 ENDIF
  330 NEXT,
  340 
  350 ORIGIN 800,384:GCOL7:RECTANGLE -4,-4,264
  360 GCOL7
  370 FORY%=0TO256STEP4
  380 FORX%=0TO256STEP2
  390 IF POINT(X%,Y%)=0 THEN
  400   K%=0
  410   IF POINT(X%,Y%-4)=4 K%+=1
  420   IF POINT(X%,Y%+4)=4 K%+=1
  430   IF POINT(X%-2,Y%)=4 K%+=1
  440   IF POINT(X%+2,Y%)=4 K%+=1
  450   IF K%<>0 POINT X%,Y%
  460 ENDIF
  470 NEXT,
