   10 REM > SPRPEN
   20 
   30 MODE140:MODE12:OFF
   40 S1%=1:S2%=2
   50 DIM GX%(2),GY%(2)
   60 COLOUR 8,255,128,128
   70 
   80 DIM SPR &3000,SZ &10
   90 SPR!0=&2FFF:SPR!4=0:SPR!8=16:SPR!12=16
  100 
  110 SYS "OS_SpriteOp",256+10,SPR,"PENCIL"
  120 SYS "OS_SpriteOp",256+40,SPR,"PENCIL" TO,,,WT%,HT%
  130 WT%=2*WT%:HT%=4*HT%
  140 
  150 ONERRORGOTO490
  160 COLOUR135:COLOUR0:GCOL0
  170 SYS 6,112,S1%:SYS 6,113,S2%:SWAP S1%,S2%:CLS
  180 SYS 6,112,S1%:SYS 6,113,S2%:SWAP S1%,S2%:CLS
  190 
  200 X%=64:Y%=64:DX%=16:DY%=16:L%=1
  210 GX%(1)=X%:GY%(1)=Y%:GX%(2)=X%:GY%(2)=Y%
  220 SYS"OS_SpriteOp",256+16,SPR,"1",0,X%,Y%,WT%+X%,HT%+Y%
  230 SYS"OS_SpriteOp",256+16,SPR,"2",0,X%,Y%,WT%+X%,HT%+Y%
  240 
  250 REPEAT
  260 H%=X%+DX%:IF H%>1216ORH%<64 THEN
  270 DX%=-SGN(DX%)*(RND(29)+3):PROCPEN(X%,Y%,X%,Y%,L%):L%=RND(2)-1
  280 ENDIF
  290 H%=Y%+DY%:IF H%>956ORH%<64 THEN
  300 DY%=-SGN(DY%)*(RND(29)+3):PROCPEN(X%,Y%,X%,Y%,L%):L%=RND(2)-1
  310 ENDIF
  320 PROCPEN(X%,Y%,X%+DX%,Y%+DY%,L%)
  330 X%+=DX%:Y%+=DY%
  340 UNTIL0
  350 
  360 DEFPROCPEN(X1%,Y1%,X2%,Y2%,L%)
  370 SYS 6,112,S1%:SYS 6,113,S2%:SWAP S1%,S2%:WAIT
  380 N$=STR$(S1%)
  390 SYS"OS_SpriteOp",256+34,SPR,N$,GX%(S1%),GY%(S1%),0
  400 IF L%=1 THEN
  410 LINE GX%(S1%),GY%(S1%),X1%,Y1%:SYS 6,113,S1%
  420 LINE GX%(S2%),GY%(S2%),X2%,Y2%:SYS 6,113,S2%
  430 ENDIF
  440 SYS"OS_SpriteOp",256+16,SPR,N$,0,X2%,Y2%,WT%+X2%,HT%+Y2%
  450 SYS "OS_SpriteOp",256+34,SPR,"PENCIL",X2%,Y2%,8
  460 GX%(S1%)=X2%:GY%(S1%)=Y2%
  470 ENDPROC
  480 
  490 SYS 6,112,S1%:SYS6,113,S1%:REPORT:PRINT" at line "STR$ERL:ON:END
