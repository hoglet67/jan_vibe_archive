   10 REM > ROTDRAW
   20 
   30 REM *--------------------*
   40 REM ! Jan Vibe august 91 !
   50 REM *--------------------*
   60 
   70 MODE140:MODE12:OFF:ORIGIN 640,512
   80 
   90 REM OpsGCOLtning af matricer.
  100 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
  110 
  120 REM OpsGCOLtning af koordinattabeller
  130 DIM PX(2000),PY(2000),PZ(2000),SX(2000),SY(2000),L%(5000,4)
  140 
  150 REM skalering,perspektivfaktor,antal punkter
  160 S=600:PF=3:AP%=0:AL%=0
  170 
  180 REM OpsGCOLtning af diverse variabler.
  190 S1%=1:S2%=2
  200 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  210 
  220 REM *---------------------------------------------------------------*
  230 REM ! Herunder kan man definere et 3-dimensionalt objekt, og        !
  240 REM ! dreje det rundt i et 3D koordinatsystem.                      !
  250 REM !                                                               !
  260 REM ! PROCM(X,Y,Z)   SGCOLtter punktet X,Y,Z og flytter til det.       !
  270 REM ! PROCD(X,Y,Z,F) SGCOLtter punktet X,Y,Z og tegner en linie med    !
  280 REM !                farven F til dette punkt.                      !
  290 REM ! PROCL(A,B,F)   TrGCOLkker en linie med farve F mellem punkterne  !
  300 REM !                A og B.                                        !
  310 REM ! PROCBOX(X1,Y1,Z1,X2,Y2,Z2,F) Tegner en kasse fra hjRETURNrnepunkt  !
  320 REM !                X1,X1,X1 til hjRETURNrnepunkt X2,Y2,Z2 med farven f.!
  330 REM !                Hvis et af 1-2 talparrene er identiske, vil    !
  340 REM !                der blive tegnet en firkant med hjRETURNrnepunkter  !
  350 REM !                i de to andre 1-2 talpar.                      !
  360 REM ! PROCTXT(X1,Y1,Z1,X2,Y2,Z2,TXT,F) Skriver teksten TXT med      !
  370 REM !                farve F i den firkant, der udgRETURNres af de to    !
  380 REM !                1-2 talpar, der indeholder forskellige vGCOLrdier.!
  390 REM !                det tredie 1-2 talpar skal indeholde to ens    !
  400 REM !                vGCOLrdier.                                       !
  410 REM ! PROCROT(X,Y,Z) sGCOLtter omdrejningsmatricen op.                 !
  420 REM ! PROCSCN        Skalerer og normaliserer objektet.             !
  430 REM ! PROCVIS        Roterer objektet, og viser det pGOTO skGCOLrmen.     !
  440 REM !                                                               !
  450 REM ! Der ikke er nogen begrGCOLnsning pGOTO stRETURNrrelsen af objektet, men  !
  460 REM ! husk at jo flere linier der skal tegnes, jo langsommere gGOTOr   !
  470 REM ! optegningen. Variablen AP% angiver antallet af punkter, AL%   !
  480 REM ! angiver antallet at linier, der skal tegnes.                  !
  490 REM !                                                               !
  500 REM *------------------- Definer objekt herunder -------------------*
  510 
  520 PROCBOX(1,1,0,9,9,0,1):PROCBOX(1,1,10,9,9,10,2)
  530 PROCBOX(0,1,1,0,9,9,3):PROCBOX(10,1,1,10,9,9,4)
  540 PROCBOX(1,0,1,9,0,9,5):PROCBOX(1,10,1,9,10,9,6)
  550 PROCTXT(4,4,5,6,6,5,"JV",5)
  560 PROCM(5,8,5):K%=AP%:PROCM(2,5,5):PROCM(5,5,2)
  570 PROCM(8,5,5):PROCM(5,5,8):PROCM(5,2,5)
  580 FORN%=1TO4:PROCL(K%,K%+N%,7):PROCL(K%+N%,K%+5,7):NEXT
  590 FORN%=1TO3:PROCL(K%+N%,K%+N%+1,7):NEXT:PROCL(K%+1,K%+4,7)
  600 
  610 REM ------------ Slut pGOTO definition, begynd at rotere --------------
  620 
  630 PROCSCN
  640 ONERRORGOTO3210
  650 PROCROT(3-RND(6),3-RND(6),3-RND(6))
  660 REPEAT
  670 PROCVIS
  680 IF RND(1)>.99 PROCROT(3-RND(6),3-RND(6),3-RND(6))
  690 UNTIL0
  700 
  710 REM ---------- Procedurer ------------
  720 
  730 REM drej objektet, tegn det og vis det.
  740 DEFPROCVIS
  750 LOCAL N%
  760 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  770 FORN%=1TOAP%
  780 K(1)=PX(N%):K(2)=PY(N%):K(3)=PZ(N%):K(4)=1
  790 R()=K().T()
  800 PX(N%)=R(1):PY(N%)=R(2):PZ(N%)=R(3)
  810 P=1+PZ(N%)/(S*PF)
  820 SX(N%)=R(1)/P:SY(N%)=R(2)/P
  830 NEXT
  840 FORN%=1TOAL%:GCOL L%(N%,4)
  850 CASE L%(N%,1) OF
  860 WHEN 0:MOVE SX(L%(N%,2)),SY(L%(N%,2))
  870 WHEN 1:DRAW SX(L%(N%,2)),SY(L%(N%,2))
  880 WHEN 2:LINE SX(L%(N%,2)),SY(L%(N%,2)),SX(L%(N%,3)),SY(L%(N%,3))
  890 ENDCASE
  900 NEXT
  910 ENDPROC
  920 
  930 REM Flyt til X,Y,Z.
  940 DEFPROCM(X,Y,Z)
  950 AP%+=1:PX(AP%)=X:PY(AP%)=Y:PZ(AP%)=Z
  960 AL%+=1:L%(AL%,1)=0:L%(AL%,2)=AP%:L%(AL%,4)=0
  970 ENDPROC
  980 
  990 REM Tegn en linie med farve C% til X,Y,Z.
 1000 DEFPROCD(X,Y,Z,C%)
 1010 AP%+=1:PX(AP%)=X:PY(AP%)=Y:PZ(AP%)=Z
 1020 AL%+=1:L%(AL%,1)=1:L%(AL%,2)=AP%:L%(AL%,4)=C%
 1030 ENDPROC
 1040 
 1050 REM trGCOLk en linie med farve C% mellem punkt A% og B%
 1060 DEFPROCL(A%,B%,C%)
 1070 AL%+=1:L%(AL%,1)=2:L%(AL%,2)=A%:L%(AL%,3)=B%:L%(AL%,4)=C%
 1080 ENDPROC
 1090 
 1100 REM Tegn en kasse eller en firkant med hjRETURNrnepunkter i
 1110 REM (X1,Y1,Z1),(X2,Y2,Z2) med farve C%
 1120 DEFPROCBOX(X1,Y1,Z1,X2,Y2,Z2,C%)
 1130 LOCAL K1%,K2%,K3%,K4%,N%,F%
 1140 IF X1<>X2 F%+=1
 1150 IF Y1<>Y2 F%+=2
 1160 IF Z1<>Z2 F%+=4
 1170 CASE F% OF
 1180 WHEN 7
 1190 PROCM(X1,Y1,Z1):K1%=AP%:PROCD(X2,Y1,Z1,C%):PROCD(X2,Y2,Z1,C%)
 1200 PROCD(X1,Y2,Z1,C%):K2%=AP%:PROCL(K2%,K1%,C%)
 1210 PROCM(X1,Y1,Z2):K3%=AP%:PROCD(X2,Y1,Z2,C%):PROCD(X2,Y2,Z2,C%)
 1220 PROCD(X1,Y2,Z2,C%):K4%=AP%:PROCL(K4%,K3%,C%)
 1230 FORN%=0TO3:PROCL(K1%+N%,K3%+N%,C%):NEXT
 1240 WHEN 6
 1250 PROCM(X1,Y1,Z1):K1%=AP%:PROCD(X1,Y2,Z1,C%):PROCD(X1,Y2,Z2,C%)
 1260 PROCD(X1,Y1,Z2,C%):K2%=AP%:PROCL(K2%,K1%,C%)
 1270 WHEN 5
 1280 PROCM(X1,Y1,Z1):K1%=AP%:PROCD(X2,Y1,Z1,C%):PROCD(X2,Y1,Z2,C%)
 1290 PROCD(X1,Y1,Z2,C%):K2%=AP%:PROCL(K2%,K1%,C%)
 1300 WHEN 3
 1310 PROCM(X1,Y1,Z1):K1%=AP%:PROCD(X2,Y1,Z1,C%):PROCD(X2,Y2,Z1,C%)
 1320 PROCD(X1,Y2,Z1,C%):K2%=AP%:PROCL(K2%,K1%,C%)
 1330 ENDCASE
 1340 ENDPROC
 1350 
 1360 REM Beregn roteringsmatricen.
 1370 DEFPROCROT(DX,DY,DZ)
 1380 LOCAL T,SI,CO
 1390 T()=C()
 1400 
 1410 REM drejning om X.
 1420 A()=C():T=RADDX:CO=COST:SI=SINT
 1430 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
 1440 T()=A()
 1450 
 1460 REM drejning om Y.
 1470 A()=C():T=RADDY:CO=COST:SI=SINT
 1480 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
 1490 T()=A().T()
 1500 
 1510 REM drejning om Z.
 1520 A()=C():T=RADDZ:CO=COST:SI=SINT
 1530 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
 1540 T()=A().T()
 1550 ENDPROC
 1560 
 1570 REM Normaliser og skaler objekt.
 1580 DEFPROCSCN
 1590 LOCAL N%,K,SC,MAXX,MINX,MAXY,MINY,MAXZ,MINZ
 1600 MAXX=PX(1):MINX=PX(1)
 1610 MAXY=PY(1):MINY=PY(1)
 1620 MAXZ=PZ(1):MINZ=PZ(1)
 1630 FORN%=1TOAP%
 1640 IF PX(N%)>MAXX MAXX=PX(N%)
 1650 IF PX(N%)<MINX MINX=PX(N%)
 1660 IF PY(N%)>MAXY MAXY=PY(N%)
 1670 IF PY(N%)<MINY MINY=PY(N%)
 1680 IF PZ(N%)>MAXZ MAXZ=PZ(N%)
 1690 IF PZ(N%)<MINZ MINZ=PZ(N%)
 1700 NEXT
 1710 MAXX=(MINX+MAXX)/2:MAXY=(MINY+MAXY)/2:MAXZ=(MINZ+MAXZ)/2
 1720 FORN%=1TOAP%:PX(N%)-=MAXX:PY(N%)-=MAXY:PZ(N%)-=MAXZ:NEXT
 1730 FORN%=1TOAP%
 1740 IF ABS(PX(N%))>SC SC=ABS(PX(N%))
 1750 IF ABS(PY(N%))>SC SC=ABS(PY(N%))
 1760 IF ABS(PZ(N%))>SC SC=ABS(PZ(N%))
 1770 NEXT:SC=2*SC
 1780 FORN%=1TOAP%
 1790 PX(N%)=S*PX(N%)/SC:PY(N%)=S*PY(N%)/SC:PZ(N%)=S*PZ(N%)/SC:NEXT
 1800 ENDPROC
 1810 
 1820 REM Skriv teksten TXT$ i den ramme der udgRETURNres af to af 1-2 talparrene.
 1830 DEFPROCTXT(X1,Y1,Z1,X2,Y2,Z2,TXT$,C%)
 1840 LOCAL O%(),X(),Y(),F%,I%,N%,K%,L%,A$,B$,ZX,ZY,H
 1850 DIM O%(20),X(20),Y(20)
 1860 IF Z1=Z2 F%=1
 1870 IF Y1=Y2 F%=2
 1880 IF X1=X2 F%=3
 1890 L%=LENTXT$
 1900 CASE F% OF
 1910 WHEN 1:ZY=Y2-Y1:H=(X2-X1)/(5*L%):ZX=(X2-X1+H)/L%
 1920 WHEN 2:ZY=Z2-Z1:H=(X2-X1)/(5*L%):ZX=(X2-X1+H)/L%
 1930 WHEN 3:ZY=Y2-Y1:H=(Z2-Z1)/(5*L%):ZX=(Z2-Z1+H)/L%
 1940 ENDCASE
 1950 FORI%=1TOL%
 1960 A$=MID$(TXT$,I%,1)
 1970 IF INSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ{|}0123456789",A$) THEN
 1980 IF INSTR("0123456789",A$) A$="'"+A$+"'"
 1990 RESTORE 2310
 2000 REPEAT:READ B$:UNTIL A$=B$:READ K%
 2010 FORN%=1TOK%:READ O%(N%),X(N%),Y(N%):X(N%)=X(N%)/5:Y(N%)=Y(N%)/6:NEXT
 2020 FORN%=1TOK%
 2030 CASE F% OF
 2040 WHEN 1:
 2050 CASE O%(N%) OF
 2060 WHEN 0: PROCM(X1+ZX*X(N%),Y1+ZY*Y(N%),Z1)
 2070 WHEN 1: PROCD(X1+ZX*X(N%),Y1+ZY*Y(N%),Z1,C%)
 2080 ENDCASE
 2090 WHEN 2:
 2100 CASE O%(N%) OF
 2110 WHEN 0: PROCM(X1+ZX*X(N%),Y1,Z1+ZY*Y(N%))
 2120 WHEN 1: PROCD(X1+ZX*X(N%),Y1,Z1+ZY*Y(N%),C%)
 2130 ENDCASE
 2140 WHEN 3:
 2150 CASE O%(N%) OF
 2160 WHEN 0: PROCM(X1,Y1+ZY*Y(N%),Z1+ZX*X(N%))
 2170 WHEN 1: PROCD(X1,Y1+ZY*Y(N%),Z1+ZX*X(N%),C%)
 2180 ENDCASE
 2190 ENDCASE
 2200 NEXT
 2210 ENDIF
 2220 CASE F% OF
 2230 WHEN 1:X1+=ZX
 2240 WHEN 2:X1+=ZX
 2250 WHEN 3:Z1+=ZX
 2260 ENDCASE
 2270 NEXT
 2280 ENDPROC
 2290 
 2300 REM Data for bogstaverne.
 2310 DATA A,5
 2320 DATA 0,0,0, 1,2,6, 1,4,0, 0,1,3, 1,3,3
 2330 DATA B,12
 2340 DATA 0,0,0, 1,0,6, 1,3,6, 1,4,5, 1,4,4, 1,3,3, 1,4,2, 1,4,1, 1,3,0
 2350 DATA 1,0,0, 0,0,3, 1,3,3
 2360 DATA C,8
 2370 DATA 0,4,1, 1,3,0, 1,1,0, 1,0,1, 1,0,5, 1,1,6, 1,3,6, 1,4,5
 2380 DATA D,7
 2390 DATA 0,0,0, 1,0,6, 1,3,6, 1,4,5, 1,4,1, 1,3,0, 1,0,0
 2400 DATA E, 6
 2410 DATA 0,4,0, 1,0,0, 1,0,6, 1,4,6, 0,0,3, 1,2,3
 2420 DATA F,5
 2430 DATA 0,0,0, 1,0,6, 1,4,6, 0,0,3, 1,2,3
 2440 DATA G,10
 2450 DATA 0,2,3, 1,4,3, 1,4,1, 1,3,0, 1,1,0, 1,0,1, 1,0,5, 1,1,6, 1,3,6
 2460 DATA 1,4,5
 2470 DATA H,6
 2480 DATA 0,0,0, 1,0,6, 0,0,3, 1,4,3, 0,4,0, 1,4,6
 2490 DATA I,6
 2500 DATA 0,1,0, 1,3,0, 0,2,0, 1,2,6, 0,1,6, 1,3,6
 2510 DATA J,8
 2520 DATA 0,1,6, 1,4,6, 0,3,6, 1,3,1, 1,2,0, 1,1,0, 1,0,1, 1,0,2
 2530 DATA K,5
 2540 DATA 0,0,0, 1,0,6, 0,4,0, 1,0,3, 1,4,6
 2550 DATA L,3
 2560 DATA 0,4,0, 1,0,0, 1,0,6
 2570 DATA M,5
 2580 DATA 0,0,0, 1,0,6, 1,2,3, 1,4,6, 1,4,0
 2590 DATA N,4
 2600 DATA 0,0,0, 1,0,6, 1,4,0, 1,4,6
 2610 DATA O,9
 2620 DATA 0,0,1, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,1, 1,3,0, 1,1,0, 1,0,1
 2630 DATA P,7
 2640 DATA 0,0,0, 1,0,6, 1,3,6, 1,4,5, 1,4,4, 1,3,3, 1,0,3
 2650 DATA Q,11
 2660 DATA 0,0,1, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,1, 1,3,0, 1,1,0, 1,0,1
 2670 DATA 0,3,1, 1,4,0
 2680 DATA R,9
 2690 DATA 0,0,0, 1,0,6, 1,3,6, 1,4,5, 1,4,4, 1,3,3, 1,0,3, 0,2,3, 1,4,0
 2700 DATA S,12
 2710 DATA 0,0,1, 1,1,0, 1,3,0, 1,4,1, 1,4,2, 1,3,3, 1,1,3, 1,0,4, 1,0,5
 2720 DATA 1,1,6, 1,3,6, 1,4,5
 2730 DATA T,4
 2740 DATA 0,2,0, 1,2,6, 0,0,6, 1,4,6
 2750 DATA U,6
 2760 DATA 0,0,6, 1,0,1, 1,1,0, 1,3,0, 1,4,1, 1,4,6
 2770 DATA V,3
 2780 DATA 0,0,6, 1,2,0, 1,4,6
 2790 DATA W,5
 2800 DATA 0,0,6, 1,0,0, 1,2,3, 1,4,0, 1,4,6
 2810 DATA X,4
 2820 DATA 0,0,0, 1,4,6, 0,0,6, 1,4,0
 2830 DATA Y,5
 2840 DATA 0,0,6, 1,2,3, 1,4,6, 0,2,0, 1,2,3
 2850 DATA Z,4
 2860 DATA 0,4,0, 1,0,0, 1,4,6, 1,0,6
 2870 DATA {,9
 2880 DATA 0,0,0, 1,0,3, 1,2,6, 1,2,0, 1,4,0, 0,0,3, 1,3,3, 0,2,6, 1,4,6
 2890 DATA |,11
 2900 DATA 0,0,1, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,1, 1,3,0, 1,1,0, 1,0,1
 2910 DATA 0,0,0, 1,4,6
 2920 DATA },13
 2930 DATA 0,0,0, 1,0,3, 1,1,4, 1,3,4, 1,4,3, 1,4,0, 0,0,2, 1,4,2, 0,2,4
 2940 DATA 1,1,5, 1,2,6, 1,3,5, 1,2,4
 2950 DATA '0',9
 2960 DATA 0,0,1, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,1, 1,3,0, 1,1,0, 1,0,1
 2970 DATA '1',5
 2980 DATA 0,1,5, 1,2,6, 1,2,0, 0,1,0, 1,3,0
 2990 DATA '2',7
 3000 DATA 0,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,4, 1,0,0, 1,4,0
 3010 DATA '3',13
 3020 DATA 0,0,1, 1,1,0, 1,3,0, 1,4,1, 1,4,2, 1,3,3, 1,2,3, 0,3,3, 1,4,4
 3030 DATA 1,4,5, 1,3,6, 1,1,6, 1,0,5
 3040 DATA '4',4
 3050 DATA 0,3,0, 1,3,6, 1,0,3, 1,4,3
 3060 DATA '5',10
 3070 DATA 0,0,1, 1,1,0, 1,3,0, 1,4,1, 1,4,3, 1,3,4, 1,1,4, 1,0,3, 1,0,6
 3080 DATA 1,4,6
 3090 DATA '6',12
 3100 DATA 0,0,3, 1,1,4, 1,3,4, 1,4,3, 1,4,1, 1,3,0, 1,1,0, 1,0,1, 1,0,5
 3110 DATA 1,1,6, 1,3,6, 1,4,5
 3120 DATA '7',3
 3130 DATA 0,0,6, 1,4,6, 1,0,0
 3140 DATA '8',16
 3150 DATA 0,1,3, 1,0,4, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,4, 1,3,3, 1,1,3
 3160 DATA 1,0,2, 1,0,1, 1,1,0, 1,3,0, 1,4,1, 1,4,2, 1,3,3
 3170 DATA '9',12
 3180 DATA 0,4,3, 1,3,2, 1,1,2, 1,0,3, 1,0,5, 1,1,6, 1,3,6, 1,4,5, 1,4,1
 3190 DATA 1,3,0, 1,1,0, 1,0,1
 3200 
 3210 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
