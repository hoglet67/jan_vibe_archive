   10 REM > JELLY
   20 
   30 MODE157:MODE 29
   40 DIM X(39,1),Y(39,1),DX(39),DY(39),OX(39),OY(39)
   50 P1%=0:P2%=1:S1%=1:S2%=2:*POINTER
   60 FORN%=0TO39:T=RAD(9*N%)
   70 X(N%,0)=800+400*SINT:OX(N%)=X(N%,0)
   80 Y(N%,0)=600+400*COST:OY(N%)=Y(N%,0)
   90 NEXT
  100 ONERRORGOTO330
  110 REPEAT:SWAP P1%,P2%
  120 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  130 MOVEX(39,P2%),Y(39,P2%)
  140 FORN%=0TO39:DRAWX(N%,P2%),Y(N%,P2%):NEXT
  150 FORN%=0TO39:N1%=(N%+1)MOD40:N2%=(N%+39)MOD40
  160 OB%=B%:MOUSE X%,Y%,B%:IF B%<>0 THEN
  170 IF B%<>OB% THEN
  180 D=1E9:M%=0:FORI%=0TO39:Z=(X%-X(I%,P2%))^2+(Y%-Y(I%,P2%))^2
  190 IF Z<D D=Z:M%=I%
  200 NEXT
  210 ENDIF
  220 X(M%,P1%)=X%:Y(M%,P1%)=Y%
  230 ENDIF
  240 X(N%,P1%)=(3*X(N%,P2%)+X(N1%,P2%)+X(N2%,P2%)+OX(N%))/6+DX(N%)
  250 Y(N%,P1%)=(3*Y(N%,P2%)+Y(N1%,P2%)+Y(N2%,P2%)+OY(N%))/6+DY(N%)
  260 IF B%=0 OR N%<>M% THEN
  270 DX(N%)=(X(N%,P1%)-X(N%,P2%))*.98
  280 DY(N%)=(Y(N%,P1%)-Y(N%,P2%))*.98
  290 ENDIF
  300 NEXT
  310 UNTIL0
  320 
  330 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
