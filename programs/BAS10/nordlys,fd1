   10 REM > NORDLYS
   20 
   30 MODE103:OFF
   40 DIM F%(255),RGB%(3,256),A%(4,2)
   50 P%=256:N%=RND(-TIME):A%=0:TI%=TIME
   60 
   70 FORI%=0TO32STEP32
   80 FOR N%=I%TOI%+14STEP2
   90 FOR M%=N%TO255STEP64:P%-=1:F%(P%)=M%:NEXT
  100 FOR M%=N%+1TO255STEP64:P%-=1:F%(P%)=M%:NEXT
  110 FOR M%=N%+16TO255STEP64:P%-=1:F%(P%)=M%:NEXT
  120 FOR M%=N%+17TO255STEP64:P%-=1:F%(P%)=M%:NEXT
  130 NEXT,
  140 
  150 F=360/255
  160 FORN%=1TO255:T=F*N%
  170 RGB%(1,N%)=127*(1+SINRAD(T))
  180 RGB%(2,N%)=127*(1+SINRAD(T+120))
  190 RGB%(3,N%)=127*(1+SINRAD(T+240))
  200 COLOUR 256-N%,RGB%(1,N%),RGB%(2,N%),RGB%(3,N%)
  210 NEXT
  220 
  230 REPEAT
  240 FORN%=1TO2:FORM%=1TO4
  250 A%(M%,N%)=RND(5)+1:NEXT,
  260 FORN%=0TO255:T=RAD(N%/3)
  270 X1=800*SIN(T*A%(1,1))*COS(T*A%(2,1))
  280 Y1=600*SIN(T*A%(3,1))*COS(T*A%(4,1))
  290 X2=800*SIN(T*A%(1,2))*COS(T*A%(2,2))
  300 Y2=600*SIN(T*A%(3,2))*COS(T*A%(4,2))
  310 PROCS:S%=(S%+19)MOD256
  320 FORK%=0TO255:K1%=255-K%:PROCS
  330 C%=F%((K%+S%)MOD255+1)MOD64:T%=F%(N%)-C%
  340 IFC%<>0GCOLC%TINTT%
  350 XB%=(X1*K%+X2*K1%)/255:YB%=(Y1*K%+Y2*K1%)/255
  360 CIRCLE FILL XB%+800,YB%+600,12
  370 CIRCLE FILL 800-XB%,YB%+600,12
  380 CIRCLE FILL XB%+800,600-YB%,12
  390 CIRCLE FILL 800-XB%,600-YB%,12
  400 NEXT,
  410 UNTIL0
  420 
  430 DEFPROCS
  440 IF TIME>TI% THEN
  450 LOCAL N%,P%,RGB1%()
  460 DIM RGB1%(3,256)
  470 FORN%=1TO255:P%=(N%+7)MOD255+1
  480 RGB1%(1,N%)=RGB%(1,P%):RGB1%(2,N%)=RGB%(2,P%):RGB1%(3,N%)=RGB%(3,P%)
  490 NEXT
  500 RGB%()=RGB1%()
  510 FORN%=1TO255:COLOUR N%,RGB%(1,N%),RGB%(2,N%),RGB%(3,N%):NEXT
  520 TI%=TIME+10
  530 ENDIF
  540 ENDPROC
