   10 REM > ROTCOP
   20 
   30 MODE25
   40 
   50 DIM SPR &30000,ROT 32
   60 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 SYS"OS_SpriteOp",256+15,SPR,"SC",0,640,480,25
   80 SYS"OS_SpriteOp",256+29,SPR,"SC"
   90 SYS"OS_SpriteOp",256+60,SPR,"SC"
  100 GCOL1:RECTANGLE FILL 0,0,1280,960
  110 SYS"OS_SpriteOp",256+60,SPR,0
  120 
  130 REPEAT
  140 SC=RND(1)*.4+.8:A=0:T=RND(1)*360:CLS
  150 FORN%=1TO50
  160 A=A MOD360+T
  170 POINT RND(1280),RND(960)
  180 SYS"OS_SpriteOp",256+16,SPR,"SC1",0,0,0,1279,959
  190 SYS"OS_SpriteOp",256+61,SPR,"SC"
  200 SYS"OS_SpriteOp",256+34,SPR,"SC1",0,0
  210 SYS"OS_SpriteOp",256+61,SPR,0:OFF
  220 PROCROT(SPR,"SC",640,480,640*SC,480*SC,A)
  230 K$=INKEY$(0):IF K$<>"" N%=50
  240 NEXT
  250 IF K$="" K$=INKEY$(200)
  260 UNTIL0
  270 
  280 DEFPROCROT(SPR,N$,X%,Y%,SX%,SY%,A%)
  290 LOCAL XR%,YR%
  300 XR%=-SX%*COSRADA%+SY%*SINRADA%:YR%=SY%*COSRADA%+SX%*SINRADA%
  310 ROT!00=(XR%+X%)*256:ROT!04=(YR%+Y%)*256
  320 XR%=SX%*COSRADA%+SY%*SINRADA%:YR%=SY%*COSRADA%-SX%*SINRADA%
  330 ROT!08=(XR%+X%)*256:ROT!12=(YR%+Y%)*256
  340 XR%=SX%*COSRADA%-SY%*SINRADA%:YR%=-SY%*COSRADA%-SX%*SINRADA%
  350 ROT!16=(XR%+X%)*256:ROT!20=(YR%+Y%)*256
  360 XR%=-SX%*COSRADA%-SY%*SINRADA%:YR%=-SY%*COSRADA%+SX%*SINRADA%
  370 ROT!24=(XR%+X%)*256:ROT!28=(YR%+Y%)*256
  380 SYS"OS_SpriteOp",256+56,SPR,N$,1,,8,ROT
  390 ENDPROC
