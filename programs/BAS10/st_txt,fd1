   10 REM > ST_TXT
   20 
   30 MODE31
   40 DIM TXT$(100),CH% 10
   50 TP%=0:L%=0
   60 PROCTXT("Jan Vibe")
   70 END
   80 
   90 DEFPROCTXT(A$)
  100 LOCALB$,C$,D$,N%
  110 A$=A$+" "
  120 FORN%=1TOLENA$:D$=MID$(A$,N%,1):C$=C$+D$
  130 IF D$=" " THEN
  140 IF LEN(B$)+LEN(C$)>6THEN
  150 B$=LEFT$(B$,LEN(B$)-1):PROCT(B$):B$=C$:C$=""
  160 ELSE
  170 B$=B$+C$:C$=""
  180 ENDIF
  190 ENDIF
  200 NEXT
  210 IF B$<>"" B$=LEFT$(B$,LEN(B$)-1):PROCT(B$)
  220 FORN%=1TO3:PROCST(STRING$(L%,"0")):NEXT
  230 FORN%=1TOTP%:TXT$(N%)=LEFT$(TXT$(N%)+STRING$(L%,"0"),L%)
  240 PROCST(TXT$(N%)):NEXT:FORN%=1TO2:PROCST(STRING$(L%,"0")):NEXT
  250 ENDPROC
  260 
  270 DEFPROCT(A$)
  280 LOCAL A%,I%,N%,M%,T$()
  290 DIM T$(8)
  300 FORI%=1TOLENA$
  310 CH%?0=ASCMID$(A$,I%,1)
  320 SYS"OS_Word",10 ,CH% TO ,CH%
  330 FORN%=1TO8:A%=128:B%=CH%?N%
  340 FORM%=1TO8:IF (A%ANDB%)<>0 T$(N%)+="11" ELSE T$(N%)+="00"
  350 A%=A%/2:NEXT,,
  360 FORN%=1TO8:FORM%=1TO2:TP%+=1:TXT$(TP%)="00"+T$(N%)+"00":NEXT
  370 IF LEN(TXT$(TP%))>L% L%=LEN(TXT$(TP%))
  380 NEXT:TP%+=1
  390 ENDPROC
  400 
  410 DEFPROCST(A$)
  420 LOCAL B$,C$,N%,V%,P%
  430 L%=LEN(A$)
  440 B$=STRING$(L%+10,"0"):V%=RND(26)
  450 FORN%=1TOL%+10:C$=MID$(B$,N%,1):P%=N%+10-VALMID$(A$,N%,1)
  460 IF C$="0" V%=(V%+RND(3)+2)MOD26:C$=CHR$(V%+97):MID$(B$,N%,1)=C$
  470 MID$(B$,P%,1)=C$
  480 NEXT:PRINTB$
  490 ENDPROC
