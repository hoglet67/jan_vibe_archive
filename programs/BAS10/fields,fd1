   10 REM > FIELDS
   20 
   30 MODE128+18:MODE18:OFF
   40 S1%=1:S2%=2:K%=3
   50 DIM X%(K%),Y%(K%),RX%(K%),RY%(K%)
   60 FORN%=1TOK%
   70 X%(N%)=RND(1280):Y%(N%)=RND(1024)
   80 RX%(N%)=(RND(16)+4)*2*SGN(RND(1)-.5)
   90 RY%(N%)=(RND(16)+4)*2*SGN(RND(1)-.5)
  100 NEXT
  110 
  120 ONERRORGOTO310
  130 REPEAT
  140 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  150 FORJ%=0TO1024STEP32
  160 FORI%=0TO1280STEP32
  170 DX=0:DY=0
  180 FORN%=1TOK%
  190 D=SQR((X%(N%)-I%)^2+(Y%(N%)-J%)^2):IF D<.1 D=.1
  200 DX=DX+(X%(N%)-I%)/D:DY=DY+(Y%(N%)-J%)/D
  210 NEXT
  220 LINE I%,J%,I%+DX*30,J%+DY*30
  230 NEXT,
  240 FORN%=1TOK%
  250 T%=X%(N%)+RX%(N%):IF T%>1280 OR T%<0 RX%(N%)=(RND(16)+4)*2*SGN(-RX%(N%))
  260 T%=Y%(N%)+RY%(N%):IF T%>1024 OR T%<0 RY%(N%)=(RND(16)+4)*2*SGN(-RY%(N%))
  270 X%(N%)+=RX%(N%):Y%(N%)+=RY%(N%)
  280 NEXT
  290 UNTIL0
  300 
  310 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
