   10 REM > LAND
   20 
   30 MODE21:OFF
   40 DIM PAT%(256,256),R%(64),G%(64),B%(64)
   50 FORN%=1TO20:R%(N%)=0:G%(N%)=0:B%(N%)=127+6.35*N%:NEXT
   60 FORN%=1TO25:T%=N%+20:R%(T%)=0:G%(T%)=255-7.63*N%:B%(T%)=0:NEXT
   70 FORN%=1TO18:T%=N%+45:R%(T%)=127+N%*7:G%(T%)=R%(T%):B%(T%)=255:NEXT
   80 PROCA(0,0,256,256,32)
   90 END
  100 
  110 DEFPROCA(X1%,Y1%,X2%,Y2%,F%)
  120 PROCP(X1%,Y1%,RND(63)):PROCP(X1%,Y2%,RND(63))
  130 PROCP(X2%,Y1%,RND(63)):PROCP(X2%,Y2%,RND(63))
  140 PROCB(X1%,Y1%,X2%,Y2%,F%)
  150 ENDPROC
  160 
  170 DEFPROCB(X1%,Y1%,X3%,Y3%,F%)
  180 LOCAL X2%,Y2%,C%
  190 X2%=(X1%+X3%)/2:Y2%=(Y1%+Y3%)/2
  200 IF PAT%(X2%,Y1%)=0 C%=FNC(X1%,Y1%,X3%,Y1%,F%):PROCP(X2%,Y1%,C%)
  210 IF PAT%(X1%,Y2%)=0 C%=FNC(X1%,Y1%,X1%,Y3%,F%):PROCP(X1%,Y2%,C%)
  220 IF PAT%(X2%,Y3%)=0 C%=FNC(X1%,Y3%,X3%,Y3%,F%):PROCP(X2%,Y3%,C%)
  230 IF PAT%(X3%,Y2%)=0 C%=FNC(X3%,Y1%,X3%,Y3%,F%):PROCP(X3%,Y2%,C%)
  240 IF PAT%(X2%,Y2%)=0 THEN
  250 IF X2%-X1%>F% THEN
  260 C%=RND(63)
  270 ELSE
  280 C%=(PAT%(X2%,Y1%)+PAT%(X2%,Y3%)+PAT%(X1%,Y2%)+PAT%(X3%,Y2%))/4
  290 C%=C%+RND(7)-4
  300 IFC%>63 C%=63
  310 IFC%<1 C%=1
  320 ENDIF
  330 PROCP(X2%,Y2%,C%)
  340 ENDIF
  350 IF X2%-X1%>1 THEN
  360 PROCB(X1%,Y1%,X2%,Y2%,F%):PROCB(X2%,Y1%,X3%,Y2%,F%)
  370 PROCB(X1%,Y2%,X2%,Y3%,F%):PROCB(X2%,Y2%,X3%,Y3%,F%)
  380 ENDIF
  390 ENDPROC
  400 
  410 DEFFNC(X1%,Y1%,X2%,Y2%,F%)
  420 LOCALC%
  430 IF X2%-X1%>F% THEN
  440 C%=RND(63)
  450 ELSE
  460 C%=(PAT%(X1%,Y1%)+PAT%(X2%,Y2%))/2+RND(7)-4
  470 IFC%>63 C%=63
  480 IFC%<1 C%=1
  490 ENDIF
  500 =C%
  510 
  520 DEFPROCP(X%,Y%,C%)
  530 PROCRGB(R%(C%),G%(C%),B%(C%))
  540 PAT%(X%,Y%)=C%
  550 RECTANGLE FILL 4*X%+128,4*Y%,2
  560 ENDPROC
  570 
  580 DEFPROCRGB(R%,G%,B%)
  590 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  600 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  610 ENDPROC
