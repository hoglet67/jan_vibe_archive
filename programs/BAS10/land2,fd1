   10 REM > LAND2
   20 
   30 MODE21:OFF
   40 DIM PAT% &41000,R%(64),G%(64),B%(64)
   50 FORN%=1TO20:R%(N%)=0:G%(N%)=0:B%(N%)=127+6.35*N%:NEXT
   60 FORN%=1TO25:T%=N%+20:R%(T%)=0:G%(T%)=255-7.63*N%:B%(T%)=0:NEXT
   70 FORN%=1TO18:T%=N%+45:R%(T%)=127+N%*7:G%(T%)=R%(T%):B%(T%)=255:NEXT
   80 FORN%=0TO&41000STEP4:PAT%!N%=0:NEXT
   90 PROCA(0,0,512,512,32)
  100 END
  110 
  120 DEFPROCA(X1%,Y1%,X2%,Y2%,F%)
  130 PROCP(X1%,Y1%,RND(63)):PROCP(X1%,Y2%,RND(63))
  140 PROCP(X2%,Y1%,RND(63)):PROCP(X2%,Y2%,RND(63))
  150 PROCB(X1%,Y1%,X2%,Y2%,F%)
  160 ENDPROC
  170 
  180 DEFPROCB(X1%,Y1%,X3%,Y3%,F%)
  190 LOCAL X2%,Y2%,C%
  200 X2%=(X1%+X3%)/2:Y2%=(Y1%+Y3%)/2
  210 IF FNPT(X2%,Y1%)=0 C%=FNC(X1%,Y1%,X3%,Y1%,F%):PROCP(X2%,Y1%,C%)
  220 IF FNPT(X1%,Y2%)=0 C%=FNC(X1%,Y1%,X1%,Y3%,F%):PROCP(X1%,Y2%,C%)
  230 IF FNPT(X2%,Y3%)=0 C%=FNC(X1%,Y3%,X3%,Y3%,F%):PROCP(X2%,Y3%,C%)
  240 IF FNPT(X3%,Y2%)=0 C%=FNC(X3%,Y1%,X3%,Y3%,F%):PROCP(X3%,Y2%,C%)
  250 IF FNPT(X2%,Y2%)=0 THEN
  260 IF X2%-X1%>F% THEN
  270 C%=RND(63)
  280 ELSE
  290 C%=(FNPT(X2%,Y1%)+FNPT(X2%,Y3%)+FNPT(X1%,Y2%)+FNPT(X3%,Y2%))/4
  300 C%=C%+RND(7)-4
  310 IFC%>63 C%=63
  320 IFC%<1 C%=1
  330 ENDIF
  340 PROCP(X2%,Y2%,C%)
  350 ENDIF
  360 IF X2%-X1%>1 THEN
  370 PROCB(X1%,Y1%,X2%,Y2%,F%):PROCB(X2%,Y1%,X3%,Y2%,F%)
  380 PROCB(X1%,Y2%,X2%,Y3%,F%):PROCB(X2%,Y2%,X3%,Y3%,F%)
  390 ENDIF
  400 ENDPROC
  410 
  420 DEFFNC(X1%,Y1%,X2%,Y2%,F%)
  430 LOCALC%
  440 IF X2%-X1%>F% THEN
  450 C%=RND(63)
  460 ELSE
  470 C%=(FNPT(X1%,Y1%)+FNPT(X2%,Y2%))/2+RND(7)-4
  480 IFC%>63 C%=63
  490 IFC%<1 C%=1
  500 ENDIF
  510 =C%
  520 
  530 DEFFNPT(X%,Y%)
  540 LOCAL P%:P%=X%+Y%*512
  550 =PAT%?P%
  560 
  570 DEFPROCP(X%,Y%,C%)
  580 LOCAL P%
  590 PROCRGB(R%(C%),G%(C%),B%(C%))
  600 P%=X%+Y%*512:PAT%?P%=C%
  610 POINT 2*X%+128,2*Y%
  620 ENDPROC
  630 
  640 DEFPROCRGB(R%,G%,B%)
  650 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  660 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  670 ENDPROC
