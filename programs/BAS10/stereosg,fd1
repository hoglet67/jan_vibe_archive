   10 REM > STEREOSGN5
   20 
   30 REM *** Jan Vibe januar 95 ***
   40 
   50 MODE159:MODE31
   60 DIM A$(10),X1%(10),Y1%(10),X2%(10),Y2%(10)
   70 DIM PX%(20000),PY%(20000):P%=0
   80 K%=398:POSX%=350:S1%=1:S2%=2
   90 
  100 INPUT'"Number of lines: "PL%:Q$=""
  110 FOR N%=1 TO PL%:PRINT'"Line no. "STR$(N%);:INPUT" "A$(N%)
  120 Q$=Q$+A$(N%):NEXT:CLS
  130 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT:COLOUR 0,0,0,128
  140 *POINTER
  150 Z%=1000:ZX=1:D=1:FONT$="Trinity.Medium"
  160 SYS"Font_FindFont",,FONT$,Z%,Z%,0,0 TO HAN%
  170 SYS"Font_SetFontColours",0,0,15,0
  180 SYS"Font_StringBBox",,Q$ TO ,X1%,Y1%,X2%,Y2%
  190 FH%=(Y2%-Y1%)/K%:FP%=1200-FH%
  200 XA%=POSX%:YA%=FP%
  210 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  220 
  230 ONERRORGOTO930
  240 REPEAT
  250 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  260 PROCBUTTON(32,1100,"+ Y POS. -")
  270 PROCBUTTON(32,1036,"+ DIST.  -")
  280 PROCBUTTON(32,972,"+ SIZE   -")
  290 PROCBUTTON(32,908,"+ X SIZE -")
  300 PROCBUTTON(32,844,"+ X POS. -")
  310 PROCBUTTON(32,780,"START")
  320 MOUSE X%,Y%,B%
  330 F%=0
  340 IF B%<>0 THEN
  350 IF X%>32ANDX%<224 THEN
  360 F%=1:IF X%>=128 F%=-1
  370 IF Y%>1100ANDY%<1132 FP%=FP%+F%*10
  380 IF Y%>1036ANDY%<1068 D=D+F%*0.05
  390 IF Y%>972ANDY%<1004 Z%=Z%+F%*50
  400 IF Y%>908ANDY%<940 ZX=ZX+F%*0.02
  410 IF Y%>908ANDY%<940 ZX=ZX+F%*0.02
  420 IF Y%>844ANDY%<876 POSX%=POSX%+F%*4:IF POSX%<350 POSX%=350
  430 ENDIF
  440 ENDIF
  450 XA%=POSX%:YA%=FP%
  460 SYS"Font_FindFont",,FONT$,Z%*ZX,Z%,0,0 TO HAN%
  470 SYS"Font_StringBBox",,Q$ TO ,X1%,Y1%,X2%,Y2%
  480 FH%=(Y2%-Y1%)/K%
  490 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  500 SYS"Font_LoseFont",HAN%
  510 UNTIL Y%>780ANDY%<812AND B%<>0
  520 SYS 6,112,S1%:ONERROROFF
  530 GCOL0:RECTANGLE FILL 0,0,256,1200:GCOL15:COLOUR 0,0,0,0:*POINTER 0
  540 
  550 FORY%=0TO1199STEP2:LINE 120,Y%,136,Y%:FORX%=XA%TO1599STEP2
  560 IF POINT(X%,Y%)<>0 THEN
  570 F%=0
  580 IF POINT(X%+2,Y%)=0 F%=1
  590 IF POINT(X%-2,Y%)=0 F%=1
  600 IF POINT(X%,Y%+2)=0 F%=1
  610 IF POINT(X%,Y%-2)=0 F%=1
  620 IFF%=1 P%+=1:PX%(P%)=X%:PY%(P%)=Y%
  630 ENDIF
  640 NEXT,
  650 FORC%=1TO14:R%=6*(15-C%):GCOLC%
  660 FORN%=1TOP%:CIRCLE FILL PX%(N%),PY%(N%),R%
  670 NEXT,
  680 SYS"Font_FindFont",,FONT$,Z%*ZX,Z%,0,0 TO HAN%
  690 XA%=POSX%:YA%=FP%
  700 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  710 SYS"Font_LoseFont",HAN%
  720 
  730 VDU24,0;0;255;1199;:GCOL128+15:GCOL0
  740 REPEAT
  750 CLG:FORN%=1TO4000:X%=RND(256):Y%=RND(1200):R%=RND(3)*2+8:R1%=R%-4
  760 GCOL0:CIRCLEFILL X%-256,Y%,R%:CIRCLEFILL X%,Y%,R%:CIRCLEFILLX%+256,Y%,R%
  770 GCOL15:CIRCLEFILLX%-256,Y%,R1%:CIRCLEFILLX%,Y%,R1%:CIRCLEFILLX%+256,Y%,R1%
  780 NEXT:K$=GET$
  790 UNTILINSTR("Pp",K$)<>0
  800 
  810 VDU26:FORY%=0TO1199STEP2:FORX%=256TO1600STEP2
  820 F%=POINT(X%,Y%)*2:C%=POINT(X%+F%-256,Y%):GCOLC%:POINTX%,Y%
  830 NEXT,
  840 *SCREENSAVE STSIGN
  850 END
  860 
  870 DEFPROCBUTTON(X1%,Y1%,TXT$)
  880 LOCAL L%:L%=LEN(TXT$)*16
  890 GCOL 15:RECTANGLE FILL X1%,Y1%,192,32:GCOL 0
  900 VDU5:MOVEX1%+(192-L%)/2,Y1%+(32)/2+8:PRINTTXT$:VDU4:GCOL7
  910 ENDPROC
  920 
  930 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
