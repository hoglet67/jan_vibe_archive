   10 REM > STEREO13
   20 
   30 MODE21:OFF:K%=1000:PC%=0:W%=200:W1%=W%+2
   40 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   50 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   60 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
   70 
   80 PROCROT(-30,20,10)
   90 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
  100 FORI%=-256TO256STEP512:FORJ%=-256TO256STEP512
  110 PROCL(I%+190*SINT1,J%+190*COST1,0,40,I%+190*SINT2,J%+190*COST2,0,40)
  120 NEXT
  130 PROCL(190*COST1,I%,190*SINT1+0,40,190*COST2,I%,190*SINT2+0,40)
  140 PROCL(I%,190*COST1,190*SINT1+0,40,I%,190*COST2,190*SINT2+0,40)
  150 NEXT,
  160 
  170 ORIGIN W%,0
  180 FORZ%=24TO1024STEP24:GCOLZ%/24
  190 FORN%=1TOPC%
  200 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  210 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  220 ENDIF
  230 NEXT,
  240 
  250 ORIGIN0,0:VDU24,0;0;W%;1023;:GCOLRND(63):RECTANGLEFILL 0,0,W%,1023
  260 FORN%=1TO2000:GCOLRND(63):X%=RND(W%)+4:Y%=RND(1023):R%=RND(7)+3
  270 CIRCLE FILL X%-W%,Y%,R%:CIRCLE FILL X%,Y%,R%:CIRCLE FILL X%+W%,Y%,R%
  280 NEXT:VDU26
  290 
  300 FORY%=0TO1023STEP2
  310 FORX%=0TO1279+W1%STEP2
  320 P%=2*POINT(X%+W1%,Y%):GCOL0:POINT X%+W1%,Y%
  330 IF POINT(X%,Y%)=0THEN
  340 X1%=X%:REPEAT:X1%+=2:UNTIL POINT(X1%,Y%)<>0:X1%-=2
  350 CASE X1%-X% OF
  360 WHEN 0:GCOL(POINT(X%-2,Y%)):POINTX%,Y%
  370 WHEN 2:GCOL(POINT(X%-2,Y%)):POINTX%,Y%
  380 GCOL(POINT(X1%+2,Y%)):POINTX1%,Y%
  390 OTHERWISE
  400 GCOLPOINT(X%MODW%,(Y%+512)MOD1024):POINT X%,Y%
  410 ENDCASE
  420 ENDIF
  430 GCOLPOINT(X%,Y%):POINT X%+W1%-P%,Y%
  440 NEXT,
  450 END
  460 
  470 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  480 LOCAL N%,D%,B%
  490 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  500 X1%=R(1):Y1%=R(2):Z1%=R(3)
  510 K(1)=X2%:K(2)=Y2%:K(3)=Z2%:K(4)=1:R()=K().T()
  520 X2%=R(1):Y2%=R(2):Z2%=R(3)
  530 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  540 IF R1%<R2% B%=2*D%/R1% ELSE B%=2*D%/R2%
  550 IF B%<>0 THEN
  560 FOR N%=0TOB%:PC%+=1
  570 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%+562
  580 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%+512
  590 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%+512
  600 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  610 NEXT
  620 ELSE
  630 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  640 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  650 ENDIF
  660 ENDPROC
  670 
  680 DEFPROCROT(DX,DY,DZ)
  690 LOCAL T,SI,CO
  700 T()=C()
  710 A()=C():T=RADDX:CO=COST:SI=SINT
  720 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  730 T()=A()
  740 A()=C():T=RADDY:CO=COST:SI=SINT
  750 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  760 T()=A().T()
  770 A()=C():T=RADDZ:CO=COST:SI=SINT
  780 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  790 T()=A().T()
  800 ENDPROC
