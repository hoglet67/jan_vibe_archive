   10 REM > HUNT
   20 
   30 MODE155:MODE27:OFF
   40 S1%=1:S2%=2
   50 DIM SX1%(10),SX2%(10),SY1%(10),SY2%(10),SX%(10),SY%(10),S%(10),K%(10)
   60 REPEAT
   70 FORN%=1TO10
   80 SX1%(N%)=RND(1280):SY1%(N%)=RND(960)
   90 SX2%(N%)=RND(1280):SY2%(N%)=RND(960):S%(N%)=RND(21)+29:K%(N%)=0
  100 NEXT
  110 WX%=RND(1280):WY%=RND(960)
  120 
  130 ONERRORGOTO400
  140 REPEAT
  150 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  160 GCOL2:FORN%=1TO10
  170 K%(N%)+=1
  180 SX%(N%)=(SX1%(N%)*(S%(N%)-K%(N%))+SX2%(N%)*K%(N%))/S%(N%)
  190 SY%(N%)=(SY1%(N%)*(S%(N%)-K%(N%))+SY2%(N%)*K%(N%))/S%(N%)
  200 CIRCLE FILL SX%(N%),SY%(N%),16
  210 IF K%(N%)=S%(N%) THEN
  220 SX1%(N%)=SX2%(N%):SY1%(N%)=SY2%(N%)
  230 SX2%(N%)=RND(1280):SY2%(N%)=RND(960):S%(N%)=RND(21)+29:K%(N%)=0
  240 ENDIF
  250 NEXT
  260 
  270 D%=(WX%-SX%(1))^2+(WY%-SY%(1))^2:P%=1
  280 FORN%=2TO10
  290 D1%=(WX%-SX%(N%))^2+(WY%-SY%(N%))^2
  300 IF D1%<D% D%=D1%:P%=N%
  310 NEXT
  320 WX%+=2*SGN(SX%(P%)-WX%):WY%+=2*SGN(SY%(P%)-WY%)
  330 GCOL1:CIRCLE FILL WX%,WY%,12
  340 
  350 UNTIL D%<784
  360 SYS 6,112,S1%:SYS 6,113,S2%
  370 K=INKEY(100)
  380 UNTIL0
  390 
  400 SYS 6,112,S1%:SYS 6,113,S1%
  410 REPORT:PRINT" at line "STR$ERL:ON
