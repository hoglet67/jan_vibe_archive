   10 REM > MLINES
   20 
   30 MODE28:OFF
   40 DIM SPR &80000
   50 SPR!0=&7FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 
   70 ONERRORGOTO480
   80 REPEAT
   90 C%=RND(26):FORM%=1TO14:FORN%=1TO14:I%=7-ABS(8-N%)
  100 PROCF(C%,I%):CIRCLE FILL 640,480,40
  110 A%=(A%+2)MOD360:B%=(B%+10)MOD360:D%=6*(SINRAD(B%)+2)
  120 X1=640+400*SINRADA%:Y1=480+400*COSRADA%
  130 PROCL(640,480,X1,Y1,80,D%)
  140 NEXT,
  150 UNTIL0
  160 
  170 DEFPROCF(F%,I%)
  180 LOCAL R%,G%,B%
  190 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  200 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
  210 GCOL F% TINT (64*I%)MOD256
  220 ENDPROC
  230 
  240 DEFPROCL(X1,Y1,X2,Y2,W,D)
  250 LOCAL L,T1,T2,F,HT,WT,XA,YA,WA
  260 WT=ABS(X2-X1)+W:HT=ABS(Y2-Y1)+W:WA=W/2
  270 XA=X1:IF X2<X1 XA=X2
  280 YA=Y1:IF Y2<Y1 YA=Y2
  290 SYS"OS_SpriteOp",256+16,SPR,"LS",0,XA-WA,YA-WA,XA-WA+WT,YA-WA+HT
  300 SYS"OS_SpriteOp",256+29,SPR,"LS"
  310 SYS"OS_SpriteOp",256+61,SPR,"LS"
  320 GCOL0TINT0:RECTANGLE FILL 0,0,WT+4,HT+4:GCOL63TINT255
  330 X1-=XA-WA:Y1-=YA-WA:X2-=XA-WA:Y2-=YA-WA
  340 L=SQR((X1-X2)^2+(Y1-Y2)^2):IF X1>X2 L=-L
  350 IF L<>0 THEN
  360 T1=ASN((Y1-Y2)/L):T2=T1+PI:W=W/2
  370 MOVEX1+W*SINT1,Y1+W*COST1:MOVEX1+W*SINT2,Y1+W*COST2
  380 PLOT&75,X2+W*SINT2,Y2+W*COST2
  390 CIRCLE FILL X1,Y1,W:CIRCLE FILL X2,Y2,W
  400 ELSE
  410 CIRCLE FILL X1,Y1,W
  420 ENDIF
  430 F=ABS(L):XA=XA-D*(X1-X2)/F:YA=YA+D*(Y2-Y1)/F
  440 SYS"OS_SpriteOp",256+61,SPR,0:OFF
  450 SYS"OS_SpriteOp",256+34,SPR,"LS",XA-W,YA-W,8
  460 ENDPROC
  470 
  480 SYS"OS_SpriteOp",256+61,SPR,0
  490 REPORT:PRINT" at line "+STR$(ERL)
