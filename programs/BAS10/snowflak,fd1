   10 REM > SNOWFLAKE3
   20 
   30 MODE107:OFF
   40 K%=10000:S%=28
   50 DIM X(K%),Y(K%),A%(K%),P%(K%),SI(5),CO(5)
   60 FORN%=0TO5:T=RAD(60*N%+30):SI(N%)=SINT:CO(N%)=COST:NEXT
   70 
   80 REPEAT
   90 FORN%=424TO8STEP-8:N1%=N%*.6:PROCRGB(255,255-N1%,0)
  100 CIRCLE FILL 576,424,N%:NEXT
  110 A$="00":FORN%=1TO28:IF RND(1)>.85 A$+="1" ELSE A$+="0"
  120 NEXT:T%=0
  130 FORN%=1TO6:T%+=1:X(N%)=576:Y(N%)=424:A%(N%)=N%-1:P%(N%)=0:NEXT
  140 FORI%=1TO15:PROCRGB(16*I%,16*I%,255):T1%=T%:R%=RND(15)
  150 FORN%=1TOT%
  160 X1=X(N%)+S%*SI(A%(N%)):Y1=Y(N%)+S%*CO(A%(N%))
  170 PROCL(X(N%),Y(N%),X1,Y1,3*(16-I%)):X(N%)=X1:Y(N%)=Y1
  180 P%(N%)=P%(N%)+1:F%=VALMID$(A$,P%(N%),1)
  190 IFF%=1 THEN
  200 T1%+=1:X(T1%)=X1:Y(T1%)=Y1:A%(T1%)=(A%(N%)+1)MOD6:P%(T1%)=R%
  210 T1%+=1:X(T1%)=X1:Y(T1%)=Y1:A%(T1%)=(A%(N%)+5)MOD6:P%(T1%)=R%
  220 ENDIF
  230 NEXT
  240 T%=T1%
  250 NEXT
  260 K$=INKEY$(500)
  270 UNTIL0
  280 
  290 DEFPROCL(X1,Y1,X2,Y2,W)
  300 LOCAL L,T1,T2
  310 L=SQR((X1-X2)^2+(Y1-Y2)^2):IF X1>X2 L=-L
  320 IF L<>0 THEN
  330 T1=ASN((Y1-Y2)/L):T2=T1+PI:W=W/2
  340 MOVEX1+W*SINT1,Y1+W*COST1:MOVEX1+W*SINT2,Y1+W*COST2
  350 PLOT&75,X2+W*SINT2,Y2+W*COST2
  360 CIRCLE FILL X1,Y1,W:CIRCLE FILL X2,Y2,W
  370 ELSE
  380 CIRCLE FILL X1,Y1,W
  390 ENDIF
  400 ENDPROC
  410 
  420 DEFPROCRGB(R%,G%,B%)
  430 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  440 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  450 ENDPROC
