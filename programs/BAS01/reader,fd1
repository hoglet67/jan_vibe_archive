   10 REM > READER
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ON ERROR GOTO 410
   50 MODE9
   60 MODE137
   70 DIM BIT$(7),B% 9
   80 BIT$()=STRING$(20,"0")
   90 P%=1:ANG%=0:COLOUR 15:OFF
  100 
  110 FIL$="BREV"
  120 
  130 FOR N%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT
  140 COLOUR 0,0,0,240
  150 FORN%=15TO1STEP-1:GCOL0,16-N%
  160 RECTANGLEFILL 31-2*N%,31-2*N%,4*N%,4*N%:NEXT
  170 MOVE0,0:MOVE60,60:*SGET BOX
  180 CLS:*SCH.BOX
  190 
  200 CH%=OPENIN FIL$:F%=0
  210 REPEAT
  220 C%=BGET#CH%
  230 IFC%<32 C%=32
  240 IFC%=32 AND F%=1 THEN 360
  250 IFC%=32 F%=1
  260 IFC%>32 F%=0
  270 ?B%=C%
  280 A%=10:X%=B%MOD256:Y%=B%DIV256:CALL &FFF1
  290 L%=128:FORM%=1TO8
  300 FORI%=8TO1STEP-1
  310 K$="0":IFL%AND(B%?I%) K$="1"
  320 BIT$(I%-1)=RIGHT$(BIT$(I%-1),19)+K$
  330 NEXT:L%=L%/2
  340 PROCSPR
  350 NEXT
  360 UNTIL EOF# CH%
  370 FORN%=1TO20
  380 FORM%=0TO7:BIT$(M%)=RIGHT$(BIT$(M%),19)+"0":NEXT
  390 PROCSPR:NEXT
  400 
  410 *FX112,0
  420 *FX113,0
  430 CLOSE#CH%
  440 ON:END
  450 
  460 DEFPROCSPR
  470 LOCALX%,Y%,N%
  480 P%=3-P%:ANG%=(ANG%+1)MOD360:WAIT
  490 OSCLI"FX 113,"+STR$(3-P%)
  500 OSCLI"FX 112,"+STR$P%
  510 GCOL0,7:CLS
  520 MOVE640+400*SINRADANG%,512+400*COSRADANG%
  530 FORN%=1TO5:T=RAD(144*N%+ANG%)
  540 DRAW640+400*SINT,512+400*COST:NEXT
  550 FORY%=0TO7:FORX%=1TO20
  560 IF MID$(BIT$(7-Y%),X%,1)="1" PLOT&ED,64*X%-64,64*Y%+256+100*SINRAD(8*(X%+4*ANG%))
  570 NEXT,
  580 ENDPROC
