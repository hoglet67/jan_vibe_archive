   10 REM > HEKSEDANS
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ON ERROR GOTO 400
   50 MODE12
   60 MODE140
   70 VDU23,128,255,255,255,255,255,255,255,255
   80 A%=20:OFF:COLOUR 0,64,0,240
   90 ORIGIN 576,448
  100 DIMXS(A%),YS(A%),DX(A%),DY(A%),PARAM% 100
  110 $PARAM%="BALL"
  120 FORN%=1TO15:M%=16*N%:COLOUR N%,M%,M%,64
  130 GCOL0,N%:CIRCLE FILL 64,64,(16-N%)*4:NEXT
  140 GCOL0,0:MOVE0,0:MOVE128,128
  150 *SGET BALL
  160 SYS"OS_SpriteOp",29,0,PARAM%
  170 FORY%=0TO128STEP4
  180 FORX%=0TO128STEP2
  190 IFPOINT(X%,Y%)>0 SOLID=1 ELSE SOLID=0
  200 SYS"OS_SpriteOp",44,0,PARAM%,X%/2,Y%/4,SOLID
  210 NEXT,:ORIGIN 0,0
  220 *SCH. BALL
  230 
  240 GCOL8,15:FORN%=1TOA%:V=RAD(N%*360/A%)
  250 XS(N%)=576:YS(N%)=448
  260 DX(N%)=12*SINV:DY(N%)=12*COSV
  270 NEXT:P%=1
  280 REPEAT
  290 FORN%=1TOA%
  300 X1=XS(N%)+DX(N%):IF X1<=0 OR X1>=1152 DX(N%)=-DX(N%)
  310 Y1=YS(N%)+DY(N%):IF Y1<=0 OR Y1>=896  DY(N%)=-DY(N%)
  320 XS(N%)=X1:YS(N%)=Y1:NEXT
  330 P%=3-P%:WAIT
  340 OSCLI"FX 113,"+STR$(3-P%)
  350 OSCLI"FX 112,"+STR$P%
  360 CLS
  370 FORN%=1TOA%:PLOT&ED,XS(N%),YS(N%):NEXT
  380 UNTIL0
  390 
  400 *FX112,0
  410 *FX113,0
  420 REPORT:PRINT"     at line "STR$ERL:ON
  430 
  440 REM LEN Jan Vibe januar 89
