   10 REM > DUST2
   20 
   30 ONERRORGOTO440
   40 SYS"OS_UpdateMEMC",64,64
   50 
   60 MODE12:MODE140:OFF:K%=64
   70 FORN%=0TO15:COLOUR 16-N%,16*N%,16*N%,16*N%:NEXT
   80 DIMX%(K%),Y%(K%),S%(K%)
   90 DIM PARAM% 100
  100 COLOUR 0,0,0,255
  110 
  120 *SNEW
  130 FORN%=1TO8:F=N%/2:C%=4*((15*F)DIV4):ORIGIN 128*N%,0
  140 NAME$="BALL"+STR$(N%)
  150 FORM%=15TO1STEP-1:GCOL M%
  160 CIRCLE FILL C%,C%,F*M%:NEXT
  170 MOVE0,0:MOVE2*C%,2*C%:OSCLI"SGET "+NAME$
  180 $PARAM%=NAME$
  190 SYS"OS_SpriteOp",29,0,PARAM%
  200 FORY%=0TO2*C%STEP4
  210 FORX%=0TO2*C%STEP2:S%=1
  220 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  230 SYS"OS_SpriteOp",44,0,PARAM%,X%/2,Y%/4,S%
  240 NEXT,,
  250 
  260 ORIGIN 0,0:CLS:GCOL8,15
  270 S1$="1":S2$="2"
  280 FORN%=0TOK%
  290 X%(N%)=RND(1276):Y%(N%)=RND(940)
  300 NEXT:RESTORE:S%(1)=1
  310 B%=1:FORN%=1TO7:READA%:B%=B%+A%:S%(B%)=1:NEXT
  320 DATA20,15,10,6,4,4,3
  330 
  340 REPEAT
  350 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:SWAP S1$,S2$
  360 WAIT:CLS:B%=0
  370 FORN%=1TOK%
  380 IF S%(N%)=1 B%=B%+1:OSCLI"SCH. BALL"+STR$B%
  390 X%(N%)=X%(N%)+3*B%+3
  400 IFX%(N%)>=1408 X%(N%)=0:Y%(N%)=RND(940)
  410 PLOT&ED,X%(N%)-128,Y%(N%)
  420 NEXT:UNTIL0
  430 
  440 *FX112,1
  450 *FX113,1
  460 REPORT:PRINT" at line "ERL
