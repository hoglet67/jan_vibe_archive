   10 REM > TRIANGLE
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 MODE12:MODE140
   50 DIM X%(3),Y%(3),DX%(3),DY%(3)
   60 FORN%=1TO3
   70 X%(N%)=RND(1278):Y%(N%)=RND(1020)
   80 DX%(N%)=FNA(8):DY%(N%)=FNA(8)
   90 NEXT:COLOUR 0,0,0,240
  100 FORN%=1TO15:COLOUR 16-N%,16*N%,16*N%,16*N%:NEXT
  110 P%=1:F%=0:OFF:ONERRORGOTO400
  120 
  130 REPEAT
  140 P%=3-P%:ANG%=(ANG%+1)MOD360:WAIT
  150 OSCLI"FX 113,"+STR$(3-P%)
  160 OSCLI"FX 112,"+STR$P%
  170 CLS
  180 F%=(F%+8)MOD64
  190 FORN%=F%TO1278STEP64:LINEN%,0,N%,1020:NEXT
  200 FORN%=32TO1020STEP64:LINE0,N%,1278,N%:NEXT
  210 CX%=(X%(1)+X%(2)+X%(3))/3:CY%=(Y%(1)+Y%(2)+Y%(3))/3
  220 FORI%=15TO1STEP-1:GCOL0,I%
  230 MOVE I%*(X%(1)-CX%)/15+CX%,I%*(Y%(1)-CY%)/15+CY%
  240 MOVE I%*(X%(2)-CX%)/15+CX%,I%*(Y%(2)-CY%)/15+CY%
  250 PLOT85,I%*(X%(3)-CX%)/15+CX%,I%*(Y%(3)-CY%)/15+CY%
  260 NEXT
  270 FORN%=1TO3
  280 REPEAT
  290 X2%=X%(N%)+DX%(N%)
  300 IFX2%>1278ORX2%<0 DX%(N%)=FNA(8)
  310 UNTILX2%<=1278ANDX2%>=0
  320 REPEAT
  330 Y2%=Y%(N%)+DY%(N%)
  340 IFY2%>1020ORY2%<0 DY%(N%)=FNA(8)
  350 UNTILY2%<=1020ANDY2%>=0
  360 X%(N%)=X2%:Y%(N%)=Y2%
  370 NEXT
  380 UNTIL0
  390 
  400 *FX112,0
  410 *FX113,0
  420 ON
  430 REPORT:PRINT" at line "ERL
  440 END
  450 
  460 DEFFNA(X%)
  470 =RND(X%)*SGN(RND(1)-.5)*4
