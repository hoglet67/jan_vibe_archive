   10 REM > 3DBOX
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ON ERROR GOTO 760
   50 DIM XW(20),YW(20),ZW(20)
   60 DIM XR(20),YR(20),ZR(20)
   70 DIM XS(20),YS(20)
   80 DIM plot(20),point(20)
   90 MODE0:MODE128:SP%=1
  100 ORIGIN 640,512:OFF
  110 COLOUR 0,4
  120 XROT=0:YROT=0:ZROT=0
  130 C%=0:D%=0
  140 XPOS=50*SIN RAD C%
  150 YPOS=30*SIN RAD D%
  160 ZPOS=120+50*COS RAD C%
  170 PROCINIT
  180 REPEAT
  190 PROCROTATE
  200 SP%=3-SP%:WAIT
  210 OSCLI"FX113,"+STR$ SP%
  220 OSCLI"FX112,"+STR$(3-SP%)
  230 CLS
  240 PROCPLOT
  250 XROT+=3:YROT+=2
  260 C%=(C%+3)MOD360
  270 D%=(D%+1)MOD360
  280 XPOS=50*SIN RAD C%
  290 YPOS=30*SIN RAD D%
  300 ZPOS=120+50*COS RAD C%
  310 UNTIL FALSE
  320 END
  330 :
  340 DEF PROCINIT
  350 coords=8:lines=16
  360 RESTORE 440
  370 FOR X=0 TO coords-1
  380 READ XW(X),YW(X),ZW(X):NEXT
  390 FOR X=1 TO lines
  400 READ plot(X),point(X):NEXT
  410 ENDPROC
  420 :
  430 REM COORDINATE DATA
  440 DATA 10,-10,-10,10,10,-10
  450 DATA 10,10,10,10,-10,10
  460 DATA -10,-10,-10,-10,10,-10
  470 DATA -10,10,10,-10,-10,10
  480 :
  490 REM LINK DATA
  500 DATA 4,0,5,1,5,2,5,3,5,0,5,4
  510 DATA 5,7,5,3,4,2,5,6,5,5,5,1
  520 DATA 4,4,5,5,4,6,5,7
  530 :
  540 DEF PROCROTATE
  550 FOR I%=0 TO coords-1
  560 XA=XW(I%):YA=YW(I%):ZA=ZW(I%)
  570 XR=XA*COS(RAD(ZROT))-YA*SIN(RAD(ZROT))
  580 YA=YA*COS(RAD(ZROT))+XA*SIN(RAD(ZROT))
  590 XA=XR
  600 XR=XA*COS(RAD(YROT))-ZA*SIN(RAD(YROT))
  610 ZA=ZA*COS(RAD(YROT))+XA*SIN(RAD(YROT))
  620 XA=XR
  630 YR=YA*COS(RAD(XROT))-ZA*SIN(RAD(XROT))
  640 ZA=ZA*COS(RAD(XROT))+YA*SIN(RAD(XROT))
  650 YA=YR
  660 XR(I%)=XA+XPOS:YR(I%)=YA+YPOS:ZR(I%)=ZA+ZPOS
  670 XS(I%)=800*XR(I%)/ZR(I%)
  680 YS(I%)=800*YR(I%)/ZR(I%)
  690 NEXT:ENDPROC
  700 :
  710 DEF PROCPLOT
  720 FOR P%=1 TO lines
  730 PLOT plot(P%),XS(point(P%)),YS(point(P%))
  740 NEXT:ENDPROC
  750 
  760 *FX112,1
  770 *FX113,1
  780 ON:REPORT:PRINT" at line "ERL
