   10 REM > BBCfg
   20 
   30 MODE0:DIMQ%(50,1):GC%=0:IC%=0:APG%=0:API%=0
   40 REPEAT:VDU23;8202;0;0;0;:CLS
   50 PRINTTAB(25,2)"F R A K T A L   G E N E R A T O R"
   60 RESTORE:FORN%=1TO5:READA$:PRINTTAB(30,2*N%+4)"F"+STR$(N%)": "A$:NEXT
   70 DATADEFINER GENERATOR,DEFINER INITIATOR
   80 DATAEDITER GENERATOR,EDITER INITIATOR,DAN FRAKTAL
   90 FL%=0:REPEAT:*FX15,0
  100 IFINKEY(-114)FL%=1:PROCGEN
  110 IFINKEY(-115)FL%=1:PROCINI
  120 IFINKEY(-116)FL%=1:PROCEGEN
  130 IFINKEY(-21)FL%=1:PROCEINI
  140 IFINKEY(-117)FL%=1:PROCDAN
  150 UNTILFL%=1
  160 UNTIL0
  170 
  180 DEFPROCGEN:GC%=0:APG%=FNED("GENERATOR",0):ENDPROC
  190 DEFPROCINI:IC%=0:API%=FNED("INITIATOR",1):ENDPROC
  200 
  210 DEFPROCEGEN
  220 IFAPG%=0CLS:PRINTTAB(30,8)"INGEN GENERATOR":K=GET:ENDPROC
  230 LOCALAP%,ST%,QP%,X1%,Y1%,X2%,Y2%,N%,K$
  240 CLS:AP%=APG%:QP%=0:PROCDOT(AP%,"GENERATOR")
  250 FORN%=1TOGC%:PROCK(Q%(N%,0)):PROCD:NEXT
  260 X1%=X2%:Y1%=Y2%:PROCED
  270 ENDPROC
  280 
  290 DEFPROCEINI
  300 IFAPG%=0CLS:PRINTTAB(30,8)"INGEN INITIATOR":K=GET:ENDPROC
  310 LOCALAP%,ST%,QP%,X1%,Y1%,X2%,Y2%,N%,K$
  320 CLS:AP%=API%:QP%=1:PROCDOT(AP%,"INITIATOR")
  330 FORN%=1TOIC%:PROCK(Q%(N%,1)):PROCD:NEXT
  340 X1%=X2%:Y1%=Y2%:PROCED
  350 ENDPROC
  360 
  370 DEFPROCDAN
  380 LOCALL%,N%,X1%,Y1%,X2%,Y2%,K,P$
  390 *FX15,0
  400 IFGC%=0CLS:PRINTTAB(30,8)"INGEN GENERATOR":K=GET:ENDPROC
  410 IFIC%=0CLS:PRINTTAB(30,8)"INGEN INITIATOR":K=GET:ENDPROC
  420 CLS:GCOL0,1:VDU29,128;0;:K=1020/API%
  430 INPUT"ANTAL UDSKIFTNINGER "L%:CLS
  440 FORN%=1TOIC%:PROCK(Q%(N%,1))
  450 PROCFRAC(X1%*K,Y1%*K,X2%*K,Y2%*K,L%):NEXT:*FX15,0
  460 P$=GET$:IFP$="P"THEN *GD.
  470 ENDPROC
  480  
  490 DEFFNED(TXT$,QP%)
  500 LOCALAP%,ST%,X1%,Y1%,X2%,Y2%,K$
  510 CLS:INPUT"ANTAL PUNKTER "AP%:CLS:PROCDOT(AP%,TXT$)
  520 X1%=0:Y1%=AP%/2:X2%=1:Y2%=Y1%:PROCD
  530 PROCED:=AP%
  540 
  550 DEFPROCED
  560 REPEAT:K$=INKEY$(0)
  570 IFK$="'"PROCD:Y2%=Y2%+1:PROCD
  580 IFK$="/"PROCD:Y2%=Y2%-1:PROCD
  590 IFK$="Z"PROCD:X2%=X2%-1:PROCD
  600 IFK$="X"PROCD:X2%=X2%+1:PROCD
  610 IFK$=" "PROCBYT
  620 IFINKEY(-74)PROCGEM(QP%):*FX15,0
  630 IFINKEY(-90)PROCDEL(QP%):*FX15,0
  640 UNTILINKEY(-97)
  650 ENDPROC
  660  
  670 DEFPROCDOT(P%,TXT$)
  680 LOCALN%,M%,Y%
  690 PRINTTAB(35)TXT$:PRINT
  700 PRINTTAB(18)"'  OP            <SPACE>  BYT FLYTTEPUNKT"
  710 PRINTTAB(18)"/  NED           RETURN   GEM LINIE"
  720 PRINTTAB(18)"X  H0JRE         <DEL>    SLET SIDSTE LINIE"
  730 PRINTTAB(18)"Z  VENSTRE       <TAB>    TILBAGE TIL HOVEDMENU":PRINT
  740 VDU29,384;128;:GCOL3,1:ST%=512/P%
  750 FORM%=0TOP%:Y%=ST%*M%:FORN%=0TOP%:PLOT69,ST%*N%,Y%:NEXT,
  760 FORN%=1TO20000:NEXT:*FX15,0
  770 ENDPROC
  780  
  790 DEFPROCD
  800 MOVEX1%*ST%,Y1%*ST%:DRAWX2%*ST%,Y2%*ST%
  810 ENDPROC
  820  
  830 DEFPROCBYT
  840 LOCALK%:K%=X1%:X1%=X2%:X2%=K%:K%=Y1%:Y1%=Y2%:Y2%=K%
  850 ENDPROC
  860 
  870 DEFPROCGEM(QP%)
  880 LOCALA%,B:FORA%=1TO20000:NEXT:*FX15,0
  890 A%=X1%:A%=A%*256+Y1%:A%=A%*256+X2%:A%=A%*256+Y2%
  900 IFQP%=0GC%=GC%+1:B%=GC%:ELSEIC%=IC%+1:B%=IC%
  910 Q%(B%,QP%)=A%:PROCC(B%)
  920 X1%=X2%:Y1%=Y2%:PROCD
  930 ENDPROC
  940 
  950 DEFPROCDEL(QP%)
  960 LOCALA%,B%
  970 IFQP%=0 B%=GC% ELSE B%=IC%
  980 FORA%=1TO20000:NEXT:*FX15,0
  990 PROCD
 1000 IFB%=0X1%=0:Y1%=AP%/2:X2%=1:Y2%=Y1%:PROCD:B=0:GOTO1020
 1010 A%=Q%(B%,QP%):PROCK(A%):B%=B%-1:IFQP%=0GC%=B%ELSEIC%=B%
 1020 PROCC(B%):ENDPROC
 1030 
 1040 DEFPROCK(A%)
 1050 X1%=(A%AND&FF000000)DIV&1000000
 1060 Y1%=(A%AND&FF0000)DIV&10000
 1070 X2%=(A%AND&FF00)DIV&100
 1080 Y2%=A%AND&FF
 1090 ENDPROC
 1100 
 1110 DEFPROCC(C%)
 1120 VDU30,32,32,30:PRINTSTR$(C%):VDU31,0,7
 1130 ENDPROC
 1140 
 1150 DEFPROCFRAC(XS,YS,XE,YE,L%)
 1160 IFL%=0MOVEXS,YS:DRAWXE,YE:ENDPROC
 1170 LOCALDX,DY,N%
 1180 DX=XE-XS:DY=YE-YS:L%=L%-1:N%=1
 1190 PROCK(Q%(N%,0))
 1200 GX1=X1%/APG%:GY1=(Y1%-APG%/2)/APG%:GX2=X2%/APG%:GY2=(Y2%-APG%/2)/APG%
 1210 X3=DX*GX1-DY*GY1+XS:Y3=DY*GX1+DX*GY1+YS
 1220 X4=DX*GX2-DY*GY2+XS:Y4=DY*GX2+DX*GY2+YS
 1230 PROCFRAC(X3,Y3,X4,Y4,L%)
 1240 N%=N%+1:IFN%<=GC%THEN1190
 1250 ENDPROC
 1260  
 1270 REM************************
 1280 REM* Fraktal generator    *
 1290 REM* Jan Vibe november 86 *
 1300 REM************************
