   10 REM > UR
   20 
   30 MODE12:MODE140:OFF
   40 ORIGIN 640,512
   50 S1$="1":S2$="2"
   60 ONERRORGOTO410
   70 
   80 COLOUR0,0,0,255:VDU19,0,24,0,0,255,5
   90 FORI%=1TO2
  100 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:CLS:SWAP S1$,S2$
  110 T%=0:FORN%=6TO360STEP6
  120 Z%=8:GCOL2:IF(N%MOD30)=0 Z%=26:GCOL1:T%+=1
  130 CIRCLE FILL 480*SINRADN%,480*COSRADN%,Z%
  140 IF Z%>8 THEN
  150 B%=8:IF T%>9 B%=16
  160 MOVE 480*SINRADN%-B%,480*COSRADN%+12:GCOL3:PRINTSTR$T%
  170 ENDIF
  180 NEXT,:GTID$=RIGHT$(TIME$,8):VDU4
  190 
  200 REPEAT
  210 REPEAT:TID$=RIGHT$(TIME$,8):UNTILTID$<>GTID$
  220 GTID$=TID$
  230 TIM%=VALMID$(TID$,1,2):TIM%=TIM%MOD12
  240 MIN%=VALMID$(TID$,4,2)
  250 SEK%=VALMID$(TID$,7,2)
  260 GCOL0:CIRCLE FILL 0,0,420
  270 GCOL6:PROCH(300,30*TIM%+MIN%/2+SEK%/120)
  280 GCOL5:PROCH(350,6*MIN%+SEK%/10)
  290 GCOL3:PROCH(400,6*SEK%)
  300 GCOL0:CIRCLE FILL 0,0,32:GCOL7:CIRCLE 0,0,32:CIRCLE FILL 0,0,12
  310 OSCLI"FX112,"+S1$:OSCLI"FX113,"+S2$:WAIT:SWAP S1$,S2$
  320 UNTIL0
  330 
  340 DEFPROCH(Z%,A%)
  350 MOVE0,0
  360 MOVEZ%*SINRAD(A%-6)/2,Z%*COSRAD(A%-6)/2
  370 PLOT85,Z%*SINRAD(A%+6)/2,Z%*COSRAD(A%+6)/2
  380 PLOT85,Z%*SINRADA%,Z%*COSRADA%
  390 ENDPROC
  400 
  410 OSCLI"FX112,"+S1$:ON:REPORT:PRINT" at line "STR$ERL
