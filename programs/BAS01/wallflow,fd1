   10 REM > WALLFLOWER
   20 SYS"OS_UpdateMEMC",64,64
   30 MODE12:OFF
   40 DIMC%(15)
   50 FORN%=1TO15:C%(N%)=256-16*N%
   60 COLOUR N%,C%(N%),C%(N%),C%(N%):NEXT
   70 
   80 F%=0:S%=16
   90 FORY%=0TO1020STEP128
  100 FORX%=0TO1280STEP128:S%=16-S%
  110 X1%=X%:Y1%=Y%:XF%=128:YF%=128
  120 FORN%=0TO15:GCOLABS(S%-N%):RECTANGLE FILL X1%,Y1%,XF%,YF%
  130 X1%=X1%+4:Y1%=Y1%+4:XF%=XF%-8:YF%=YF%-8
  140 NEXT,,
  150 
  160 ORIGIN 640,512
  170 FORN%=15TO1STEP-1
  180 A=0:B=0:Z=0:R=4.2*N%:GCOL N%
  190 REPEAT
  200 CIRCLE FILL Z*SINRADA,Z*COSRADA,R
  210 CIRCLE FILL Z*SINRADB,Z*COSRADB,R
  220 A=A+40.2:B=B-40.2:Z=Z+1:R=R-.1
  230 UNTILZ>=500 OR R<=0
  240 NEXT
  250 
  260 T%=1000
  270 REPEAT
  280 FORN%=0TO15:C%(N%)=(C%(N%)+16)MOD256:NEXT
  290 WAIT:FORN%=0TO15:COLOUR N%,C%(N%),C%(N%),C%(N%):NEXT
  300 K$=INKEY$(0)
  310 CASE K$ OF
  320 WHEN "F" : T%=T%-1000:IFT%<0 T%=0
  330 WHEN "S" : T%=T%+1000
  340 ENDCASE
  350 FORN%=1TOT%:NEXT
  360 UNTIL0
