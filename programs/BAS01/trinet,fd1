   10 REM > TRINET
   20 
   30 MODE0:OFF
   40 ONERRORGOTO290
   50 SYS"OS_UpdateMEMC",64,64
   60 S1$="1":S2$="2"
   70 DIM X%(2),Y%(2),DX%(2),DY%(2)
   80 FOR N%=0TO2
   90 X%(N%)=RND(1278):Y%(N%)=RND(1024)
  100 DX%(N%)=3+RND(5):DY%(N%)=3+RND(5)
  110 NEXT
  120 
  130 REPEAT
  140 OSCLI"FX 112,"+S1$:OSCLI"FX 113,"+S2$:SWAP S1$,S2$:WAIT:CLS
  150 RECTANGLE 0,0,1278,1020
  160 MOVE X%(2),Y%(2):FORN%=0TO2:DRAWX%(N%),Y%(N%):NEXT
  170 FORN%=0TO2:N1%=(N%+1)MOD3:N2%=(N%+2)MOD3
  180 FORM%=1TO3
  190 X1%=(M%*X%(N1%)+(4-M%)*X%(N2%))/4
  200 Y1%=(M%*Y%(N1%)+(4-M%)*Y%(N2%))/4
  210 LINE X%(N%),Y%(N%),X1%,Y1%:NEXT,
  220 FORN%=0TO2
  230 X1%=X%(N%)+DX%(N%):IF X1%>1278 OR X1%<0 DX%(N%)=-DX%(N%):GOTO230
  240 Y1%=Y%(N%)+DY%(N%):IF Y1%>1020 OR Y1%<0 DY%(N%)=-DY%(N%):GOTO240
  250 X%(N%)=X1%:Y%(N%)=Y1%
  260 NEXT
  270 UNTIL0
  280 
  290 *FX 112,1
  300 *FX 113,1
  310 ON:REPORT:PRINT" at line "ERL
