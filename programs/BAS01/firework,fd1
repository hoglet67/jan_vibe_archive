   10 REM > FIREWORKS
   20 SYS "OS_UpdateMEMC",64,64
   30 MODE12:OFF
   40 G=9.81:K%=30
   50 DIMVX(K%),VY(K%),X%(K%,15),Y%(K%,15),C%(15)
   60 
   70 REPEAT
   80 X%()=(0):Y%()=(0)
   90 
  100 FORN%=1TOK%
  110 A=RAD(RND(360)):V%=RND(30)+30
  120 VX(N%)=V%*COSA:VY(N%)=V%*SINA:NEXT
  130 FORN%=1TO15:C%(N%)=N%:NEXT
  140 
  150 A=RAD(RND(1)*20+80):V%=RND(20)+110
  160 VX(0)=V%*COSA:VY(0)=V%*SINA
  170 P%=440+RND(400)
  180 
  190 T=0:T1%=15:T2%=1:Y%=0
  200 REPEAT
  210 T=T+.2:GY%=Y%
  220 T1%=T1%MOD15+1:T2%=T2%MOD15+1
  230 FORN%=1TO15:C%(N%)=C%(N%)MOD15+1
  240 F%=256-16*C%(N%):COLOUR N%,F%,F%/2,0:NEXT
  250 X%=VX(0)*T+P%
  260 Y%=VY(0)*T-T*T*G/2
  270 X%(0,T1%)=X%:Y%(0,T1%)=Y%
  280 GCOL0,0:CIRCLEFILL X%(0,T2%),Y%(0,T2%),4
  290 GCOL0,16-T1%:CIRCLEFILL X%,Y%,4
  300 UNTILY%<=GY%
  310 PX%=X%:PY%=Y%
  320 A=RAD(RND(360)):V%=RND(30)+30
  330 VX(0)=V%*COSA:VY(0)=V%*SINA
  340 
  350 T=0
  360 REPEAT
  370 T=T+.2:T2=G*T*T/2:C%=0
  380 T1%=T1%MOD15+1:T2%=T2%MOD15+1
  390 FORN%=1TO15:C%(N%)=C%(N%)MOD15+1
  400 F%=256-16*C%(N%):COLOUR N%,F%,F%/2,0:NEXT
  410 FORN%=0TOK%
  420 X%=PX%+VX(N%)*T
  430 Y%=PY%+VY(N%)*T-T2
  440 X%(N%,T1%)=X%:Y%(N%,T1%)=Y%
  450 GCOL0,0:CIRCLEFILL X%(N%,T2%),Y%(N%,T2%),4
  460 GCOL0,16-T1%:CIRCLEFILL X%,Y%,4
  470 IF Y%(N%,T2%)<0 C%=C%+1
  480 NEXT
  490 UNTIL C%=K%+1
  500 
  510 UNTIL0
