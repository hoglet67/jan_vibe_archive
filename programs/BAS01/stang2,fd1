   10 REM > STANG2
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 ONERRORGOTO 590
   50 MODE0:OFF:ORIGIN 48,48
   60 VDU23,2,17,0,68,0,17,0,68,0
   70 DIMA%(4),X%(45,2),Y%(45,2)
   80 DIM PARAM% 100
   90 A%=RND(-TIME)
  100 
  110 FORX%=-48TO48STEP2
  120 B%=SQR(2304-X%*X%):Y%=-B%:B%=2*B%
  130 FORN%=0TOB%STEP4:IFRND(B%)<N% POINT X%,Y%+N%
  140 NEXT,
  150 
  160 *SNEW
  170 ORIGIN 0,0
  180 MOVE0,0:MOVE96,96:*SGET KUGLE
  190 CIRCLE FILL 48,48,46
  200 $PARAM%="KUGLE"
  210 SYS"OS_SpriteOp",29,0,PARAM%
  220 FORY%=0TO96STEP4
  230 FORX%=0TO96STEP2:S%=1
  240 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  250 SYS"OS_SpriteOp",44,0,PARAM%,X%/2,Y%/4,S%
  260 NEXT,
  270 
  280 FORN%=1TO4:A%(N%)=2*RND(4):NEXT
  290 FORN%=45TO135STEP2:N1%=(N%-45)/2
  300 X%(N1%,1)=450*SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)+592
  310 Y%(N1%,1)=450*SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)+464
  320 NEXT
  330 A$="1":B$="2":C%=2:D%=1:K%=30
  340 FORN%=1TO2
  350 OSCLI"FX 112,"+B$:OSCLI"FX 113,"+A$:SWAP A$,B$
  360 GCOL 16,15:RECTANGLE FILL 0,0,1278,1020:GCOL8,15
  370 NEXT
  380 
  390 REPEAT
  400 SWAP C%,D%
  410 FORN%=1TO4:A%(N%)=2*RND(4):NEXT
  420 FORN%=45TO135STEP2:N1%=(N%-45)/2
  430 X%(N1%,D%)=450*SINRAD(A%(1)*N%)*COSRAD(A%(2)*N%)+592
  440 Y%(N1%,D%)=450*SINRAD(A%(3)*N%)*COSRAD(A%(4)*N%)+464
  450 NEXT
  460 FORI%=0TOK%
  470 FORN%=0TO45
  480 X%(N%,0)=(I%*X%(N%,D%)+(K%-I%)*X%(N%,C%))/K%
  490 Y%(N%,0)=(I%*Y%(N%,D%)+(K%-I%)*Y%(N%,C%))/K%
  500 NEXT
  510 FORN%=0TO45:PLOT&ED,X%(N%,0),Y%(N%,0):NEXT
  520 OSCLI"FX 112,"+A$
  530 OSCLI"FX 113,"+B$
  540 SWAP A$,B$
  550 WAIT:GCOL 16,15:RECTANGLE FILL 0,0,1278,1020:GCOL8,15
  560 NEXT
  570 UNTIL0
  580 
  590 *FX112,1
  600 *FX113,1
  610 ON:REPORT:PRINT" at line "ERL
