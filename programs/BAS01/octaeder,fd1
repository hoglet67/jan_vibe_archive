   10 REM >OCTAEDER1
   20 SYS "OS_UpdateMEMC",64,64
   30 MODE 128+12
   40 MODE 12
   50 COLOUR0,0,32,64
   60 FOR C=1TO15:COLOURC,C<<4,C*8,C*2:NEXTC
   70 VDU23,17,4,1|
   80 DIM PNTS(5,2),TRI(7,2), ROT(2,2), ROTX(2,2),ROTY(2,2),ROTZ(2,2)
   90 ROTX()=0:ROTX(0,0)=1:ROTY()=0:ROTY(1,1)=1:ROTZ()=0:ROTZ(2,2)=1
  100 DATA 0,0,1, 0,1,0, 1,0,0, 0,0,-1, 0,-1,0, -1,0,0
  110 DATA 0,1,2, 0,1,5, 0,2,4, 0,4,5, 1,2,3, 1,3,5, 2,3,4, 3,4,5
  120 FORI=0TO5:FORJ=0TO2:READ PNTS(I,J):NEXT,
  130 FORI=0TO7:FORJ=0TO2:READ TRI(I,J):NEXT,
  140 TX=RAD3:TY=RAD2:TZ=RAD4
  150 PROCSETROT
  160 W=0:SHW$="1":DRW$="2"
  170 ONERROR PROCTRAP
  180 REPEAT
  190 WAIT
  200 OSCLI "FX 113,"+SHW$
  210 OSCLI "FX 112,"+DRW$
  220 SWAP SHW$,DRW$
  230 CLS
  240 SCALE=300*COS(W)^2+100:CX=150*SIN(.7*W)*SIN(1.7*W):CY=150*SIN(1.1*W)*SIN(1.3*W)
  250 IFRND(200)=1 THEN
  260 TX-=TX*RND(1)/3-(RND(1)-.5)/20:TY-=TY*RND(1)/3-(RND(1)-.5)/20:TZ-=TZ*RND(1)/3-(RND(1)-.5)/20
  270 PROCSETROT
  280 ENDIF
  290 FOR T=0TO7:PROCTRIA(TRI(T,0),TRI(T,1),TRI(T,2)):NEXT
  300 W+=.01
  310 PNTS()=PNTS().ROT()
  320 UNTIL FALSE
  330 END
  340 DEFPROCSETROT
  350 ROTX(1,1)=COSTX:ROTX(1,2)=-SINTX:ROTX(2,1)=SINTX:ROTX(2,2)=COSTX
  360 ROTY(0,0)=COSTY:ROTY(0,2)=SINTY:ROTY(2,0)=-SINTY:ROTY(2,2)=COSTY
  370 ROTZ(0,0)=COSTZ:ROTZ(0,1)=-SINTZ:ROTZ(1,0)=SINTZ:ROTZ(1,1)=COSTZ
  380 ROT()=ROTX().ROTY()
  390 ROT()=ROT().ROTZ()
  400 ENDPROC
  410 DEFPROCTRIA(A,B,C)
  420 LOCAL F
  430 IF PNTS(A,2)+PNTS(B,2)+PNTS(C,2)<=0 THEN
  440 F=PNTS(A,1)+PNTS(B,1)+PNTS(C,1)
  450 IF F>0 THEN F%=INT(20+F*22) ELSE F%=INT(20+F*8)
  460 IF F%MOD4=0 THEN
  470 GCOL F%DIV4
  480 ELSE
  490 CASE F%MOD4 OF
  500 WHEN 1: F1%=F%DIV4*17:F2%=F%DIV4+1 +(F%DIV4<<4)
  510 WHEN 2: F1%=F%DIV4 +(F%DIV4+1<<4):F2%=F%DIV4+1 +(F%DIV4<<4)
  520 WHEN 3: F1%=F%DIV4 +(F%DIV4+1<<4):F2%=(F%DIV4+1)*17
  530 ENDCASE
  540 VDU23,2,F1%,F2%,F1%,F2%,F1%,F2%,F1%,F2%
  550 GCOL 16,0
  560 ENDIF
  570 ORIGIN 640+CX,512+CY
  580 MOVE SCALE*PNTS(A,0),SCALE*PNTS(A,1)
  590 MOVE SCALE*PNTS(B,0),SCALE*PNTS(B,1)
  600 PLOT 85,SCALE*PNTS(C,0),SCALE*PNTS(C,1)
  610 ENDIF
  620 ENDPROC
  630 DEFPROCTRAP
  640 *FX 112,0
  650 *FX 113,0
  660 PRINT REPORT$;" at ";ERL
  670 END
