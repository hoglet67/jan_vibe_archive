   10 REM > MAGIC_EYE   *** Jan Vibe april 94 ***
   20 
   30 MODE15:OFF:K%=10000:PC%=0
   40 DIM SPR &30000
   50 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   60 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   70 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 
   90 COLOUR 176:CLS:VDU 28,5,31,79,0
  100 PRINT'TAB(30)"Magic eye"''
  110 PRINT"This program can generate pictures like the ones in the book 'Magic"
  120 PRINT"eye'. It does this by making a normal 3D model of the object on the"
  130 PRINT"screen. This model is colour coded in such a way that the points"
  140 PRINT"farthest away has the highest colour number, and the points nearest"
  150 PRINT"to you has the lowest number. This is used for depth information."
  160 PRINT"If the color model looks OK to you, just tap the keyboard, and the"
  170 PRINT"picture will be converted to a real 'Magic eye' picture."
  180 PRINT"If you wish to save the picture, just tap the 'S' key and it will"
  190 PRINT"be saved as a MODE 8 sprite named 'MagicPic'."'
  200 PRINT"You can choose from the following objects:"'
  210 PRINTTAB(20)"A Chain"
  220 PRINTTAB(20)"B Rattle"
  230 PRINTTAB(20)"C Star"
  240 PRINTTAB(20)"D Box"
  250 PRINTTAB(20)"E Squiggle (different each time)"
  260 PRINTTAB(20)"F Maze cube (different each time)"
  270 PRINTTAB(20)"G X-mas tree"
  280 PRINTTAB(20)"H Spiral"
  290 PRINTTAB(20)"I Random balls (different each time)"
  300 PRINT'TAB(20)"Input letter...";:VDU26
  310 REPEAT:Q$=GET$:UNTIL INSTR("AaBbCcDdEeFfGgHhIi",Q$)<>0
  320 GCOL 63 TINT 0:RECTANGLE FILL 0,0,1280,1024
  330 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT:PROCROT(0,0,0)
  340 CASE Q$ OF
  350 WHEN "A","a":PROCPIC1
  360 WHEN "B","b":PROCPIC2
  370 WHEN "C","c":PROCPIC3
  380 WHEN "D","d":PROCPIC4
  390 WHEN "E","e":PROCPIC5
  400 WHEN "F","f":PROCPIC6
  410 WHEN "G","g":PROCPIC7
  420 WHEN "H","h":PROCPIC8
  430 WHEN "I","i":PROCPIC9
  440 ENDCASE
  450 
  460 FORZ%=1024TO16STEP-16:GCOLZ%/16TINT0
  470 FORN%=1TOPC%
  480 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  490 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  500 ENDIF
  510 NEXT,:Q=GET
  520 SYS"OS_SpriteOp",256+16,SPR,"SCR",1,0,0,1279,1023
  530 MODE8:OFF
  540 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  550 FORX%=0TO639:FORY%=0TO255
  560 SYS"OS_SpriteOp",256+41,SPR,"SCR",X%,Y% TO ,,,,,C%,T%:OFF
  570 P%=POINT(2*X%,4*Y%):S%=(C%+T%)+100
  580 IF P%=1 THEN
  590 GCOL3:IFRND(1)>.6 GCOL0:P%=0
  600 POINT 2*X%,4*Y%
  610 ENDIF
  620 GCOL3:IF P%=0 GCOL0
  630 POINT 2*X%+S%,4*Y%
  640 NEXT,
  650 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE MagicPic"
  660 ON:END
  670 
  680 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  690 LOCAL N%,D%,B%
  700 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  710 X1%=R(1):Y1%=R(2):Z1%=R(3)
  720 K(1)=X2%:K(2)=Y2%:K(3)=Z2%:K(4)=1:R()=K().T()
  730 X2%=R(1):Y2%=R(2):Z2%=R(3)
  740 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  750 IF R1%<R2% B%=2*D%/R1% ELSE B%=2*D%/R2%
  760 IF B%<>0 THEN
  770 FOR N%=0TOB%:PC%+=1
  780 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%+562
  790 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%+512
  800 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%+512
  810 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  820 NEXT
  830 ELSE
  840 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  850 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  860 ENDIF
  870 ENDPROC
  880 
  890 DEFPROCB(X1%,Y1%,Z1%,R%)
  900 LOCAL N%,D%,B%
  910 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  920 X1%=R(1):Y1%=R(2):Z1%=R(3):PC%+=1
  930 PX%(PC%)=X1%+562:PY%(PC%)=Y1%+512:PZ%(PC%)=Z1%+512:PR%(PC%)=R%
  940 ENDPROC
  950 
  960 DEFPROCR(X%,Y%,Z%)
  970 LOCAL N%
  980 PROCROT(X%,Y%,Z%)
  990 FORN%=1TOPC%
 1000 K(1)=PX%(N%)-562:K(2)=PY%(N%)-512:K(3)=PZ%(N%)-512:K(4)=1:R()=K().T()
 1010 PX%(N%)=R(1)+562:PY%(N%)=R(2)+512:PZ%(N%)=R(3)+512
 1020 NEXT
 1030 ENDPROC
 1040 
 1050 DEFPROCROT(DX,DY,DZ)
 1060 LOCAL T,SI,CO
 1070 T()=C()
 1080 A()=C():T=RADDX:CO=COST:SI=SINT
 1090 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
 1100 T()=A()
 1110 A()=C():T=RADDY:CO=COST:SI=SINT
 1120 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
 1130 T()=A().T()
 1140 A()=C():T=RADDZ:CO=COST:SI=SINT
 1150 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
 1160 T()=A().T()
 1170 ENDPROC
 1180 
 1190 DEFPROCPIC1
 1200 LOCAL N%,I%,J%,T1,T2
 1210 PROCROT(30,20,10)
 1220 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
 1230 FORI%=-256TO256STEP512:FORJ%=-256TO256STEP512
 1240 PROCL(I%+180*SINT1,J%+180*COST1,0,40,I%+180*SINT2,J%+180*COST2,0,40)
 1250 NEXT
 1260 PROCL(180*COST1,I%,180*SINT1+0,40,180*COST2,I%,180*SINT2+0,40)
 1270 PROCL(I%,180*COST1,180*SINT1+0,40,I%,180*COST2,180*SINT2+0,40)
 1280 NEXT,
 1290 ENDPROC
 1300 
 1310 DEFPROCPIC2
 1320 LOCAL N%,A1%,A2%,B1%,B2%,T1,T2
 1330 PROCROT(30,40,0)
 1340 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
 1350 A1%=450*SINT1:B1%=450*COST1
 1360 A2%=450*SINT2:B2%=450*COST2
 1370 PROCL(A1%,B1%,0,50,A2%,B2%,0,50)
 1380 PROCL(A1%,0,B1%,50,A2%,0,B2%,50)
 1390 PROCL(0,A1%,B1%,50,0,A2%,B2%,50)
 1400 NEXT
 1410 PROCB(0,0,0,150)
 1420 ENDPROC
 1430 
 1440 DEFPROCPIC3
 1450 LOCAL A%,N%,X%,Y%,T
 1460 FORA%=0TO150STEP30
 1470 PROCROT(8,8+A%,0)
 1480 FORN%=0TO330STEP30:T=RADN%
 1490 X%=480*SINT:Y%=480*COST
 1500 PROCL(0,0,0,60,X%,Y%,0,8)
 1510 NEXT,
 1520 ENDPROC
 1530 
 1540 DEFPROCPIC4
 1550 LOCAL N%,W%,X%
 1560 W%=40:Z%=300
 1570 FORN%=0TO270STEP90
 1580 PROCROT(10,20,N%)
 1590 PROCL(-Z%,-Z%,-Z%,W%, Z%,-Z%,-Z%,W%)
 1600 PROCL( Z%,-Z%,-Z%,W%, Z%,-Z%, Z%,W%)
 1610 PROCL( Z%,-Z%, Z%,W%,-Z%,-Z%, Z%,W%)
 1620 NEXT
 1630 W%=24:Z%=100
 1640 FORN%=0TO270STEP90
 1650 PROCROT(60,55,N%)
 1660 PROCL(-Z%,-Z%,-Z%,W%, Z%,-Z%,-Z%,W%)
 1670 PROCL( Z%,-Z%,-Z%,W%, Z%,-Z%, Z%,W%)
 1680 PROCL( Z%,-Z%, Z%,W%,-Z%,-Z%, Z%,W%)
 1690 NEXT
 1700 ENDPROC
 1710 
 1720 DEFPROCPIC5
 1730 LOCAL N%,TB%(),A,T
 1740 DIM TB%(6)
 1750 FORN%=1TO6:TB%(N%)=RND(6):NEXT
 1760 PROCROT(20,20,0)
 1770 FORA=0TO360STEP.25:T=RAD(A)
 1780 X%=400*SIN(TB%(1)*A)*COS(TB%(2)*A)
 1790 Y%=400*SIN(TB%(3)*A)*COS(TB%(4)*A)
 1800 Z%=400*SIN(TB%(5)*A)*COS(TB%(6)*A)
 1810 PROCB(X%,Y%,Z%,32)
 1820 NEXT
 1830 ENDPROC
 1840 
 1850 DEFPROCPIC6
 1860 LOCAL MX%(),MY%(),MZ%(),MP%
 1870 DIM MX%(200),MY%(200),MZ%(200)
 1880 PROCROT(20,20,10)
 1890 PROCMAZE(0,0,0)
 1900 ENDPROC
 1910 DEFPROCMAZE(X%,Y%,Z%)
 1920 LOCAL N%,M%,F%,X1%,Y1%,Z1%
 1930 FORN%=1TO8:X1%=X%:Y1%=Y%:Z1%=Z%
 1940 CASE RND(3) OF
 1950 WHEN 1:REPEAT:X1%=X%+160*SGN(RND(1)-.5):UNTILABS(X1%)<=320
 1960 WHEN 2:REPEAT:Y1%=Y%+160*SGN(RND(1)-.5):UNTILABS(Y1%)<=320
 1970 WHEN 3:REPEAT:Z1%=Z%+160*SGN(RND(1)-.5):UNTILABS(Z1%)<=320
 1980 ENDCASE
 1990 F%=0:IF PC%>0 THEN
 2000 FORM%=1TOMP%
 2010 IF X1%=MX%(M%) AND Y1%=MY%(M%) AND Z1%=MZ%(M%) F%=1:M%=MP%
 2020 NEXT
 2030 ENDIF
 2040 IF F%=0 THEN
 2050 MP%=MP%+1:MX%(MP%)=X1%:MY%(MP%)=Y1%:MZ%(MP%)=Z1%
 2060 PROCL(X%,Y%,Z%,20,X1%,Y1%,Z1%,20):PROCMAZE(X1%,Y1%,Z1%)
 2070 ENDIF
 2080 NEXT
 2090 ENDPROC
 2100 
 2110 DEFPROCPIC7
 2120 LOCAL N%,A%,Z%
 2130 PROCL(0,-550,0,50,0,450,0,10)
 2140 Z%=500:A%=30
 2150 FORN%=-400TO350STEP50:Z%=Z%-28
 2160 A%+=20
 2170 FORM%=0TO288STEP72
 2180 PROCROT(0,A%+M%,0)
 2190 PROCL(0,N%,0,20,Z%,N%+30,0,10)
 2200 NEXT,
 2210 PROCR(20,0,0)
 2220 ENDPROC
 2230 
 2240 DEFPROCPIC8
 2250 LOCAL X%,Y%,Z%,R%,N,T
 2260 PROCROT(-70,20,20)
 2270 FORN=-450TO450STEP.5:Y%=N:T=T+.03
 2280 R%=SQR(450^2-Y%^2)
 2290 X%=R%*SINT:Z%=R%*COST
 2300 PROCB(X%,Y%,Z%,25)
 2310 NEXT
 2320 ENDPROC
 2330 
 2340 DEFPROCPIC9
 2350 LOCAL X%(),Y%(),Z%(),R%(),N%,C%,D%,F%,T%
 2360 DIM X%(100),Y%(100),Z%(100),R%(100)
 2370 C%=1:R%(1)=RND(40)+80
 2380 X%(1)=RND(1024-2*R%(1))+R%(1)
 2390 Y%(1)=RND(1024-2*R%(1))+R%(1)
 2400 Z%(1)=RND(1024-2*R%(1))+R%(1)
 2410 REPEAT
 2420 R%(C%+1)=RND(40)+80
 2430 X%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2440 Y%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2450 Z%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2460 T%+=1:F%=0
 2470 FOR N%=1 TO C%
 2480 D%=SQR((X%(C%+1)-X%(N%))^2+(Y%(C%+1)-Y%(N%))^2+(Z%(C%+1)-Z%(N%))^2)
 2490 IF D%<(R%(C%+1)+R%(N%)+80) N%=C%:F%=1
 2500 NEXT
 2510 IFF%=0 C%+=1:T%=0
 2520 UNTIL T%=200 OR C%=100
 2530 FORN%=1TOC%:PROCB(X%(N%)-512,Y%(N%)-512,Z%(N%)-512,R%(N%)):NEXT
 2540 ENDPROC
