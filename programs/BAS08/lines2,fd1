   10 REM > LINES2
   20 
   30 MODE15:OFF:K%=10000:PC%=0
   40 DIM SPR &30000
   50 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   60 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   70 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 GCOL 63 TINT 0:RECTANGLE FILL 0,0,1280,1024
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 FORA%=0TO150STEP30
  120 PROCROT(8,8+A%,0)
  130 FORN%=0TO330STEP30:T=RADN%
  140 X%=480*SINT:Y%=480*COST
  150 PROCL(0,0,0,60,X%,Y%,0,8)
  160 NEXT,
  170 
  180 FORZ%=1024TO16STEP-16:GCOLZ%/16TINT0
  190 FORN%=1TOPC%
  200 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  210 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  220 ENDIF
  230 NEXT,:Q=GET
  240 SYS"OS_SpriteOp",256+16,SPR,"SCR",1,0,0,1279,1023
  250 MODE8:OFF
  260 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  270 FORX%=0TO639:FORY%=0TO255
  280 SYS"OS_SpriteOp",256+41,SPR,"SCR",X%,Y% TO ,,,,,C%,T%:OFF
  290 P%=POINT(2*X%,4*Y%):S%=(C%+T%)+100
  300 IF P%=1 THEN
  310 GCOL3:IFRND(1)>.6 GCOL0:P%=0
  320 POINT 2*X%,4*Y%
  330 ENDIF
  340 GCOL3:IF P%=0 GCOL0
  350 POINT 2*X%+S%,4*Y%
  360 NEXT,
  370 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE Pic"
  380 ON:END
  390 
  400 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  410 LOCAL N%,D%,B%
  420 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  430 X1%=R(1):Y1%=R(2):Z1%=R(3)
  440 K(1)=X2%:K(2)=Y2%:K(3)=Z2%:K(4)=1:R()=K().T()
  450 X2%=R(1):Y2%=R(2):Z2%=R(3)
  460 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  470 IF R1%<R2% B%=2*D%/R1% ELSE B%=2*D%/R2%
  480 FOR N%=0TOB%:PC%+=1
  490 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%+562
  500 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%+512
  510 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%+512
  520 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  530 NEXT
  540 ENDPROC
  550 
  560 DEFPROCB(X1%,Y1%,Z1%,R%)
  570 LOCAL N%,D%,B%
  580 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  590 X1%=R(1):Y1%=R(2):Z1%=R(3):PC%+=1
  600 PX%(PC%)=X1%+562:PY%(PC%)=Y1%+512:PZ%(PC%)=Z1%+512:PR%(PC%)=R%
  610 ENDPROC
  620 
  630 DEFPROCROT(DX,DY,DZ)
  640 LOCAL T,SI,CO
  650 T()=C()
  660 A()=C():T=RADDX:CO=COST:SI=SINT
  670 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  680 T()=A()
  690 A()=C():T=RADDY:CO=COST:SI=SINT
  700 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  710 T()=A().T()
  720 A()=C():T=RADDZ:CO=COST:SI=SINT
  730 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  740 T()=A().T()
  750 ENDPROC
