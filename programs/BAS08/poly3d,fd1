   10 REM > POLY3D
   20 
   30 MODE15:OFF
   40 DIM SPR &30000:SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   50 GCOL 64:RECTANGLE FILL 0,0,1280,1024
   60 K%=RND(10)+2:PROCC
   70 PROCA(640,512,500,K%)
   80 K$=GET$
   90 SYS"OS_SpriteOp",256+16,SPR,"SCR",1,0,0,1279,1023
  100 MODE8:OFF
  110 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  120 FORX%=0TO639:FORY%=0TO255
  130 SYS"OS_SpriteOp",256+41,SPR,"SCR",X%,Y% TO ,,,,,C%,T%:OFF
  140 P%=POINT(2*X%,4*Y%):S%=(C%+T%)+100
  150 IF P%=1 THEN
  160 GCOL3:IFRND(1)>.6 GCOL0:P%=0
  170 POINT 2*X%,4*Y%
  180 ENDIF
  190 GCOL3:IF P%=0 GCOL0
  200 POINT 2*X%+S%,4*Y%
  210 NEXT,
  220 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE Pic"
  230 ON:END
  240 
  250 DEFPROCA(X,Y,Z,A%)
  260 LOCAL T,N%,PX(),PY()
  270 DIM PX(20),PY(20)
  280 T=RAD(360/A%)
  290 FORN%=1TOA%:PX(N%)=X+Z*SIN(N%*T):PY(N%)=Y+Z*COS(N%*T):NEXT
  300 PROCB(PX(),PY(),A%)
  310 ENDPROC
  320 
  330 DEFPROCB(PX(),PY(),A%)
  340 LOCAL N%,N1%,CX,CY,X1,Y1,PX1(),PY1(),XP(),YP()
  350 DIM PX1(20),PY1(20),XP(20),YP(20)
  360 FORN%=1TOA%:CX+=PX(N%):CY+=PY(N%):NEXT:CX=CX/A%:CY=CY/A%
  370 FORN%=1TOA%:N1%=N%MODA%+1
  380 X1=(PX(N%)+PX(N1%))/2:Y1=(PY(N%)+PY(N1%))/2
  390 PX1(N%)=(2*X1+CX)/3:PY1(N%)=(2*Y1+CY)/3
  400 NEXT
  410 FORN%=1TOA%:N1%=N%MODA%+1
  420 XP(1)=PX(N%):YP(1)=PY(N%):XP(2)=PX(N1%):YP(2)=PY(N1%)
  430 XP(3)=PX1(N%):YP(3)=PY1(N%):PROCP(3,1,15,XP(),YP())
  440 XP(1)=PX1(N%):YP(1)=PY1(N%):XP(2)=PX1(N1%):YP(2)=PY1(N1%)
  450 XP(3)=PX(N1%):YP(3)=PY(N1%):PROCP(3,1,15,XP(),YP())
  460 NEXT
  470 IF (CX-PX1(1))^2+(CY-PY1(1))^2>30000 THEN
  480 PROCB(PX1(),PY1(),A%)
  490 ELSE
  500 PROCP(A%,1,15-A%,PX1(),PY1())
  510 ENDIF
  520 ENDPROC
  530 
  540 DEFPROCP(K%,D1%,D2%,X(),Y())
  550 LOCAL C%,N%,M%,I%,L%,L1%,H1%,H2%,X1(),Y1()
  560 DIM X1(20),Y1(20)
  570 L%=1000
  580 REPEAT:C%=C%+1:GCOLABS(C%MOD128-64)
  590 MOVEX(1),Y(1):MOVEX(K%),Y(K%):H1%=1:H2%=K%:I%=0
  600 REPEAT
  610 IF I%=0 H1%+=1:PLOT85,X(H1%),Y(H1%):IF H1%=H2% I%=1
  620 IF I%=0 H2%-=1:PLOT85,X(H2%),Y(H2%):IF H1%=H2% I%=1
  630 UNTILI%=1
  640 FORN%=1TOK%:M%=N%MODK%+1
  650 X1(N%)=(D1%*X(N%)+D2%*X(M%))/(D1%+D2%)
  660 Y1(N%)=(D1%*Y(N%)+D2%*Y(M%))/(D1%+D2%)
  670 NEXT:X()=X1():Y()=Y1()
  680 L1%=(X1(1)-X1(2))^2+(Y1(1)-Y1(2))^2:IF L1%<L% L%=L1%
  690 UNTIL L%<=64
  700 ENDPROC
  710 
  720 DEFPROCC
  730 LOCAL N%,R%,G%,B%,F1%,F2%,F3%,FA%,FB%,FC%,V1%,V2%
  740 REPEAT
  750 F1%=240*(RND(2)-1):F2%=240*(RND(2)-1):F3%=240*(RND(2)-1)
  760 FA%=240*(RND(2)-1):FB%=240*(RND(2)-1):FC%=240*(RND(2)-1)
  770 UNTIL ABS(F1%-FA%)+ABS(F2%-FB%)+ABS(F3%-FC%)>=300
  780 FORN%=1TO15:V1%=N%-1:V2%=14-V1%
  790 R%=(V1%*F1%+V2%*FA%)/14:G%=(V1%*F2%+V2%*FB%)/14:B%=(V1%*F3%+V2%*FC%)/14
  800 COLOURN%,R%,G%,B%
  810 NEXT
  820 ENDPROC
