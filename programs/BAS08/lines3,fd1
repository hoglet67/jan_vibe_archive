   10 REM > LINES3
   20 
   30 MODE15:OFF:K%=10000:PC%=0
   40 DIM SPR &30000
   50 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   60 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   70 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 GCOL 63 TINT 0:RECTANGLE FILL 0,0,1280,1024
   90 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
  100 
  110 PROCROT(30,20,10)
  120 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
  130 FORI%=-256TO256STEP512:FORJ%=-256TO256STEP512
  140 PROCL(I%+180*SINT1,J%+180*COST1,0,40,I%+180*SINT2,J%+180*COST2,0,40)
  150 NEXT
  160 PROCL(180*COST1,I%,180*SINT1+0,40,180*COST2,I%,180*SINT2+0,40)
  170 PROCL(I%,180*COST1,180*SINT1+0,40,I%,180*COST2,180*SINT2+0,40)
  180 NEXT,
  190 
  200 FORZ%=1024TO16STEP-16:GCOLZ%/16TINT0
  210 FORN%=1TOPC%
  220 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  230 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  240 ENDIF
  250 NEXT,:Q=GET
  260 SYS"OS_SpriteOp",256+16,SPR,"SCR",1,0,0,1279,1023
  270 MODE8:OFF
  280 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  290 FORX%=0TO639:FORY%=0TO255
  300 SYS"OS_SpriteOp",256+41,SPR,"SCR",X%,Y% TO ,,,,,C%,T%:OFF
  310 P%=POINT(2*X%,4*Y%):S%=(C%+T%)+100
  320 IF P%=1 THEN
  330 GCOL3:IFRND(1)>.6 GCOL0:P%=0
  340 POINT 2*X%,4*Y%
  350 ENDIF
  360 GCOL3:IF P%=0 GCOL0
  370 POINT 2*X%+S%,4*Y%
  380 NEXT,
  390 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE Pic"
  400 ON:END
  410 
  420 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  430 LOCAL N%,D%,B%
  440 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  450 X1%=R(1):Y1%=R(2):Z1%=R(3)
  460 K(1)=X2%:K(2)=Y2%:K(3)=Z2%:K(4)=1:R()=K().T()
  470 X2%=R(1):Y2%=R(2):Z2%=R(3)
  480 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  490 IF R1%<R2% B%=2*D%/R1% ELSE B%=2*D%/R2%
  500 IF B%<>0 THEN
  510 FOR N%=0TOB%:PC%+=1
  520 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%+562
  530 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%+512
  540 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%+512
  550 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  560 NEXT
  570 ELSE
  580 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  590 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  600 ENDIF
  610 ENDPROC
  620 
  630 DEFPROCB(X1%,Y1%,Z1%,R%)
  640 LOCAL N%,D%,B%
  650 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  660 X1%=R(1):Y1%=R(2):Z1%=R(3):PC%+=1
  670 PX%(PC%)=X1%+562:PY%(PC%)=Y1%+512:PZ%(PC%)=Z1%+512:PR%(PC%)=R%
  680 ENDPROC
  690 
  700 DEFPROCROT(DX,DY,DZ)
  710 LOCAL T,SI,CO
  720 T()=C()
  730 A()=C():T=RADDX:CO=COST:SI=SINT
  740 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
  750 T()=A()
  760 A()=C():T=RADDY:CO=COST:SI=SINT
  770 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
  780 T()=A().T()
  790 A()=C():T=RADDZ:CO=COST:SI=SINT
  800 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
  810 T()=A().T()
  820 ENDPROC
