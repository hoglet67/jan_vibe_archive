   10 REM > MARBLE2
   20 
   30 MODE9:OFF:A%=59:B%=512
   40 A1%=360/(A%+1):B1%=B%/2:B2%=B1%^2
   50 DIM SPR &110000,H% 16
   60 SPR!0=&10FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 H%!00=B%:H%!04=B%:H%!08=B%:H%!12=B%
   80 S1%=1:S2%=2
   90 FORN%=8TO15:COLOUR N%,RND(255),RND(255),RND(255):NEXT
  100 CLS:FORN%=1TO1000:GCOLRND(15)
  110 CIRCLE FILL RND(1280),RND(1024),7+RND(57):NEXT
  120 SYS"OS_SpriteOp",256+16,SPR,"PIC",0,0,0,1279,1023
  130 GCOL0
  140 FORI%=0TOA%:T=RAD(A1%*I%):N$=STR$(I%)
  150 X%=640-B1%+(640-B1%)*SINT:Y%=512-B1%+(512-B1%)*COST
  160 SYS"OS_SpriteOp",256+34,SPR,"PIC",0,0
  170 FORN%=0TOB%STEP4:X1%=X%:Y1%=Y%+N%:X2%=X%+B%:Y2%=Y%+N%
  180 W%=SQR(B2%-(B1%-N%)^2):H%!00=2*W%
  190 SYS"OS_SpriteOp",256+16,SPR,"LINE",0,X1%,Y1%,X2%,Y2%
  200 LINE X1%,Y1%,X2%,Y2%
  210 SYS"OS_SpriteOp",256+52,SPR,"LINE",X1%+B1%-W%,Y1%,0,H%,0
  220 NEXT
  230 SYS"OS_SpriteOp",256+16,SPR,N$,0,X%,Y%,X%+B%,Y%+B%
  240 SYS"OS_SpriteOp",256+29,SPR,N$
  250 SYS"OS_SpriteOp",256+61,SPR,N$,0
  260 GCOL0:RECTANGLE FILL 0,0,B%:GCOL15:CIRCLE FILL B1%,B1%,B1%-8
  270 SYS"OS_SpriteOp",256+61,SPR,0,0:GCOL0
  280 NEXT:CLS
  290 
  300 ONERRORGOTO410
  310 REPEAT
  320 FORN%=0TOA%:TI%=TIME+4:T=RAD(A1%*N%):N$=STR$(N%)
  330 X%=640-B1%+(640-B1%)*SINT:Y%=512-B1%+(512-B1%)*COST
  340 SYS"OS_SpriteOp",256+34,SPR,"PIC",0,0
  350 SYS"OS_SpriteOp",256+34,SPR,N$,X%,Y%,8
  360 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  370 REPEAT:UNTILTIME>=TI%
  380 NEXT
  390 UNTIL0
  400 
  410 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
