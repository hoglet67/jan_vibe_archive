   10 REM > PLANETS
   20 
   30 MODE15:OFF:K%=100
   40 DIM SPR &30000
   50 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   60 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 GCOL 63 TINT 0:RECTANGLE FILL 0,0,1280,1024
   80 
   90 C%=0:N%=0
  100 REPEAT:N%=N%+1:F%=0
  110 REPEAT
  120 PR%(N%)=RND(4)+6:C%=C%+1
  130 PX%(N%)=RND(1080-20*PR%(N%))+10*PR%(N%)
  140 PY%(N%)=RND(1024-20*PR%(N%))+10*PR%(N%)
  150 PZ%(N%)=20*(RND(63-2*PR%(N%))+PR%(N%))
  160 IF N%>=3 THEN
  170 F%=0:FORM%=1TON%-1
  180 D%=SQR((PX%(N%)-PX%(M%))^2+(PY%(N%)-PY%(M%))^2+(PZ%(N%)-PZ%(M%))^2)
  190 IF D%<22*(PR%(N%)+PR%(M%)) F%=1
  200 NEXT
  210 ENDIF
  220 UNTIL F%=0 OR C%=2000 OR N%=K%
  230 UNTIL C%=2000 OR N%=K%:K%=N%
  240 FORN%=1TOK%:PZ%(N%)=PZ%(N%)/20:NEXT
  250 FORZ%=63TO1STEP-1:GCOLZ%TINT0
  260 FORN%=1TOK%
  270 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  280 P%=10*SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%+16
  290 ENDIF
  300 NEXT,:Q=INKEY(200)
  310 
  320 SYS"OS_SpriteOp",256+16,SPR,"SCR",1,0,0,1279,1023
  330 MODE8:OFF
  340 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  350 FORX%=0TO639:FORY%=0TO255
  360 SYS"OS_SpriteOp",256+41,SPR,"SCR",X%,Y% TO ,,,,,C%,T%:OFF
  370 P%=POINT(2*X%,4*Y%):S%=(C%+T%)+100
  380 IF P%=1 THEN
  390 GCOL3:IFRND(1)>.6 GCOL0:P%=0
  400 POINT 2*X%,4*Y%
  410 ENDIF
  420 GCOL3:IF P%=0 GCOL0
  430 POINT 2*X%+S%,4*Y%
  440 NEXT,
  450 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE Pic"
  460 ON:END
