   10 REM > SPACEBALLS            *** Jan Vibe september 93 ***
   20 
   30 MODE140:MODE12:OFF:ORIGIN 640,512
   40 S1%=1:S2%=2:AP%=30
   50 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   60 DIM PX(AP%),PY(AP%),PZ(AP%)
   70 DIM X1(AP%),Y1(AP%),Z1(AP%),X2(AP%),Y2(AP%),Z2(AP%)
   80 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT
   90 PROCOBJ:PROCROT(FNR(0),FNR(0),FNR(0)):TIME=0
  100 FORN%=1TO15:COLOUR 16-N%,12*N%+63,12*N%+63,12*N%+63:NEXT
  110 COLOUR0,0,0,240:VDU19,0,24,0,0,240
  120 ONERRORGOTO810
  130 
  140 REPEAT
  150 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  160 FORN%=1TOAP%
  170 K(1)=PX(N%):K(2)=PY(N%):K(3)=PZ(N%):K(4)=1
  180 R()=K().T():PX(N%)=R(1):PY(N%)=R(2):PZ(N%)=R(3)
  190 NEXT:PROCSORT(AP%)
  200 FORN%=1TOAP%:P=1+PZ(N%)/1.5
  210 SX=600*PX(N%)/P:SY=600*PY(N%)/P
  220 GCOL 1+9*(PZ(N%)-PZ(AP%)):CIRCLE FILL SX,SY,32/P
  230 GCOL 15:CIRCLE SX,SY,32/P:NEXT
  240 IF RND(1)>.99 PROCROT(FNR(0),FNR(0),FNR(0))
  250 IF INKEY$(0)<>"" OR TIME>2000 THEN
  260 X1()=PX():Y1()=PY():Z1()=PZ():PROCOBJ
  270 X2()=PX():Y2()=PY():Z2()=PZ()
  280 PROCROT(FNR(0),FNR(0),FNR(0))
  290 FORI%=0TO20:J%=20-I%
  300 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  310 FORN%=1TOAP%
  320 K(1)=(J%*X1(N%)+I%*X2(N%))/20:K(2)=(J%*Y1(N%)+I%*Y2(N%))/20
  330 K(3)=(J%*Z1(N%)+I%*Z2(N%))/20:K(4)=1
  340 R()=K().T():PX(N%)=R(1):PY(N%)=R(2):PZ(N%)=R(3)
  350 NEXT:PROCSORT(AP%)
  360 FORN%=1TOAP%:P=1+PZ(N%)/1.5
  370 SX=600*PX(N%)/P:SY=600*PY(N%)/P
  380 GCOL 1+9*(PZ(N%)-PZ(AP%)):CIRCLE FILL SX,SY,32/P
  390 GCOL 15:CIRCLE SX,SY,32/P:NEXT,:TIME=0
  400 ENDIF
  410 UNTIL0
  420 
  430 DEFFNR(X)=(2+RND(1))*SGN(RND(1)-.5)
  440 
  450 DEFPROCROT(DX,DY,DZ)
  460 LOCAL T,SI,CO
  470 A()=C():T=RADDX:CO=COST:SI=SINT
  480 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO:T()=A()
  490 A()=C():T=RADDY:CO=COST:SI=SINT
  500 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO:T()=A().T()
  510 A()=C():T=RADDZ:CO=COST:SI=SINT
  520 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO:T()=A().T()
  530 ENDPROC
  540 
  550 DEFPROCSORT(N%)
  560 LOCAL G%,J%,K%,M%
  570 M%=1
  580 REPEAT:M%=M%+M%:UNTIL M%>N%:M%=M%-1
  590 REPEAT:M%=M%DIV2
  600 FORK%=1TON%-M%:J%=K%
  610 REPEAT:G%=J%+M%
  620 IF PZ(J%)<PZ(G%) THEN
  630 SWAP PX(J%),PX(G%):SWAP PY(J%),PY(G%):SWAP PZ(J%),PZ(G%)
  640 ELSE J%=1
  650 ENDIF
  660 J%=J%-1:UNTILJ%=0
  670 NEXT:UNTILM%=1
  680 ENDPROC
  690 
  700 DEFPROCOBJ
  710 LOCALN%,K,SQ%()
  720 DIMSQ%(6)
  730 FORN%=1TO6:SQ%(N%)=RND(5):NEXT
  740 FORN%=1TOAP%:K=N%*180/(AP%+1)+90
  750 PX(N%)=(SINRAD(SQ%(1)*K)+SINRAD(SQ%(2)*K))/4
  760 PY(N%)=(SINRAD(SQ%(3)*K)+SINRAD(SQ%(4)*K))/4
  770 PZ(N%)=(SINRAD(SQ%(5)*K)+SINRAD(SQ%(6)*K))/4
  780 NEXT
  790 ENDPROC
  800 
  810 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
