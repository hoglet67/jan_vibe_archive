   10 REM > ART3
   20 
   30 MODE15:OFF
   40 DIM SPR &1000:SPR!0=&FFF:SPR!4=0:SPR!8=16:SPR!12=16:C%=RND(4)
   50 REPEAT
   60 CLS:FORN%=1TO1000:GCOLRND(63)
   70 CIRCLE FILL RND(1280),RND(1024),24+RND(76):NEXT
   80 FORI%=1TO4+RND(10):D%=0:IFC%<=2 D%=2
   90 C%=RND(2)+D%
  100 CASE C% OF
  110 WHEN 1,2:S%=SGN(C%MOD2-.5):S1%=RND(2)
  120 CA%=FNR(1024):CB%=2*CA%:CC%=CA%^2
  130 FORN%=0TO1023STEP4:X1%=0:Y1%=N%:X2%=1279:Y2%=N%
  140 IFN%MODCB%=0ANDS1%=1 S%=-S%
  150 SYS"OS_SpriteOp",256+16,SPR,"LINE",0,X1%,Y1%,X2%,Y2%
  160 F%=SQR(CC%-(N%MODCB%-CA%)^2)*S%
  170 SYS"OS_SpriteOp",256+34,SPR,"LINE",X1%+F%,Y1%,0
  180 SYS"OS_SpriteOp",256+34,SPR,"LINE",X1%+F%-1280*S%,Y1%,0
  190 NEXT
  200 WHEN 3,4:S%=SGN(C%MOD2-.5):S1%=RND(2)
  210 CA%=FNR(1280):CB%=2*CA%:CC%=CA%^2
  220 FORN%=0TO1280STEP2:X1%=N%:Y1%=0:X2%=N%:Y2%=1023
  230 IFN%MODCB%=0ANDS1%=1 S%=-S%
  240 SYS"OS_SpriteOp",256+16,SPR,"LINE",0,X1%,Y1%,X2%,Y2%
  250 F%=SQR(CC%-(N%MODCB%-CA%)^2)*S%
  260 SYS"OS_SpriteOp",256+34,SPR,"LINE",X1%,Y1%+F%,0
  270 SYS"OS_SpriteOp",256+34,SPR,"LINE",X1%,Y1%+F%-1024*S%,0
  280 NEXT
  290 ENDCASE
  300 NEXT
  310 K%=INKEY(1000)
  320 UNTIL0
  330 
  340 DEFFNR(R%)
  350 CASE RND(3) OF
  360 WHEN 1:R%=R%/2
  370 WHEN 2:R%=R%/4
  380 WHEN 3:R%=R%/8
  390 ENDCASE
  400 =R%
