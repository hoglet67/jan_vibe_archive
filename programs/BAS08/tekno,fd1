   10 REM > TEKNO
   20 
   30 MODE137:MODE9
   40 DIM SPR &20000
   50 K%=4:DIM BX%(K%),BY%(K%),BR%(K%)
   60 SPR!0=&1FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 S1%=1:S2%=2
   80 SYS"OS_SpriteOp",256+15,SPR,"BGR",0,320,256,9
   90 SYS"OS_SpriteOp",256+15,SPR,"BAL",0,320,256,9
  100 SYS"OS_SpriteOp",256+29,SPR,"BAL"
  110 SYS"OS_SpriteOp",256+61,SPR,"BAL",0
  120 GCOL0:RECTANGLE FILL 0,0,1279,1023
  130 SYS"OS_SpriteOp",256+61,SPR,0,0
  140 FORN%=8TO15:COLOURN%,RND(255),RND(255),RND(255):NEXT
  150 ONERRORGOTO390
  160 REPEAT
  170 SYS"OS_SpriteOp",256+60,SPR,"BGR",0
  180 MOVE0,0:MOVE 1279,1023:PLOT&BD,4,0
  190 GCOLRND(15):W%=RND(49)+15:H%=RND(49)+15
  200 RECTANGLE FILL RND(128)-W%,RND(1024)-H%,2*W%,2*H%
  210 SYS"OS_SpriteOp",256+60,SPR,0,0
  220 SYS"OS_SpriteOp",256+60,SPR,"BAL",0
  230 MOVE0,0:MOVE 1279,1023:PLOT&BD,-4,0
  240 GCOLRND(15):W%=RND(49)+15:H%=RND(49)+15
  250 RECTANGLE FILL 1152+RND(128)-W%,RND(1024)-H%,2*W%,2*H%
  260 SYS"OS_SpriteOp",256+61,SPR,"BAL",0
  270 MOVE0,0:MOVE 1279,1023:PLOT&BD,0,4:GCOL3,15
  280 FORN%=1TOK%
  290 BY%(N%)+=4:IF BY%(N%)>BR%(N%) THEN
  300 BX%(N%)=RND(1280):BR%(N%)=RND(109)+51:BY%(N%)=-BR%(N%)
  310 ENDIF
  320 W%=SQR(BR%(N%)^2-BY%(N%)^2):LINE BX%(N%)-W%,0,BX%(N%)+W%,0
  330 NEXT
  340 SYS"OS_SpriteOp",256+61,SPR,0,0
  350 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  360 SYS"OS_SpriteOp",256+34,SPR,"BGR",0,0,0
  370 SYS"OS_SpriteOp",256+34,SPR,"BAL",0,0,8
  380 UNTIL0
  390 MODE12:REPORT:PRINT" at line "STR$ERL:ON
