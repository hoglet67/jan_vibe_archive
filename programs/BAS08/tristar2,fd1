   10 REM > TRISTAR2
   20 
   30 MODE15:OFF
   40 DIM SPR &30000
   50 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 
   70 SYS"OS_SpriteOp",256+15,SPR,"PAT",0,640,256,15
   80 SYS"OS_SpriteOp",256+60,SPR,"PAT",0
   90 K%=52:L%=1000/K%
  100 GCOL K% TINT 0:RECTANGLE FILL 0,0,1280,1024
  110 FOR I%=K%TO1STEP-1:GCOL I%+10 TINT 0
  120 Z1%=L%*I%:Z2%=(1000*I%+400*(K%-I%))/K%
  130 FOR N%=0TO30:SWAP Z1%,Z2%
  140 X%=640-K%+1.2*Z1%*SINRAD(12*N%):Y%=512+Z1%*COSRAD(12*N%)
  150 IF N%=0 MOVE X%,Y%:ELSE MOVE 640-K%,512:PLOT85,X%,Y%
  160 NEXT,
  170 
  180 FONT$="Clauch"
  190 SYS"Font_SetPalette",FONT$,0,0,0,0,0
  200 SYS"Font_SetFontColours",FONT$,0,0,0
  210 SYS"Font_FindFont",,FONT$,1300,1300,0,0 TO HAN%
  220 SYS"Font_Paint",,"mind",16,410,280
  230 SYS"Font_Paint",,"GALLERY",16,216,100
  240 SYS"Font_LoseFont",HAN%
  250 
  260 SYS"OS_SpriteOp",256+60,SPR,0,0
  270 SYS"OS_SpriteOp",256+34,SPR,"PAT",0,0,0
  280 K$=GET$
  290 
  300 MODE8:OFF
  310 COLOUR1,0,0,0:GCOL1:RECTANGLEFILL0,0,1280,1024
  320 FORX%=0TO639:FORY%=0TO255
  330 SYS"OS_SpriteOp",256+41,SPR,"PAT",X%,Y% TO ,,,,,F%,T%:OFF
  340 P%=POINT(2*X%,4*Y%):S%=(F%+T%)+100
  350 IF P%=1 THEN
  360 GCOL3:P%=3:IFRND(1)>.6 GCOL0:P%=0
  370 POINT 2*X%,4*Y%
  380 ENDIF
  390 GCOL3:IF P%=0 GCOL0
  400 POINT 2*X%+S%,4*Y%
  410 NEXT,
  420 *FX15,0
  430 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE Pic"
  440 ON:END
