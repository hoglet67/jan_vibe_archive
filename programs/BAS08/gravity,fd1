   10 REM > GRAVITY
   20 
   30 MODE128:MODE0:OFF
   40 S1%=1:S2%=2:P1%=0:P2%=1:G=.1:K%=10
   50 DIM X(K%,1),Y(K%,1),DX(K%),DY(K%),M(K%)
   60 FORN%=1TOK%
   70 X(N%,P1%)=RND(640)+320:Y(N%,P1%)=RND(512)+256
   80 DX(N%)=(RND(1)-.5)*10:DY(N%)=(RND(1)-.5)*10
   90 M(N%)=(RND(15)+2)*2:NEXT
  100 
  110 ONERRORGOTO300
  120 REPEAT
  130 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  140 FORN%=1TOK%
  150 X1=X(N%,P1%):Y1=Y(N%,P1%):DX1=DX(N%):DY1=DY(N%)
  160 FORM%=1TOK%
  170 IF N%<>M% THEN
  180 X2=X(M%,P1%):Y2=Y(M%,P1%)
  190 D=G*M(N%)*M(M%)/((X1-X2)^2+(Y1-Y2)^2)
  200 DX1+=(X2-X1)*D:DY1+=(Y2-Y1)*D
  210 ENDIF
  220 NEXT
  230 X(N%,P2%)=X(N%,P1%)+DX1:Y(N%,P2%)=Y(N%,P1%)+DY1
  240 DX(N%)=DX1:DY(N%)=DY1:CIRCLE FILL X(N%,P2%),Y(N%,P2%),M(N%)
  250 NEXT
  260 SWAP P1%,P2%
  270 
  280 UNTIL0
  290 
  300 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
