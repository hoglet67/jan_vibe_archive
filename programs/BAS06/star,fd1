   10 REM > STAR     *** Jan Vibe august 92 ***
   20 
   30 MODE12:OFF
   40 DIM P%(4,17),PT%(15,15),C%(15,3),LX%(50,2),LY%(50,2),PATH% 500
   50 PROCINIT
   60 
   70 REPEAT
   80 P%=RND(12)+4:CLS
   90 P2%=2*P%:D=360/P2%
  100 Z1%=RND(300)+200:D1%=RND(200)+100:Z2%=RND(32)+16:D2%=RND(400)+100
  110 FORN%=1TOP2%:PROCF:T=RAD(D*N%)
  120 LX%(N%,1)=Z1%*SINT+640:LY%(N%,1)=Z1%*COST+512:SWAP Z1%,D1%
  130 LX%(N%,2)=Z2%*SINT+640:LY%(N%,2)=Z2%*COST+512:SWAP Z2%,D2%
  140 NEXT
  150 PROCFS(P2%):T%=TIME+3000
  160 REPEAT:PROCF:UNTIL TIME>T%
  170 UNTIL0
  180 
  190 DEFPROCFS(AP%)
  200 LOCAL N%,N1%,PP%,X%,Y%,I%
  210 FORN%=0TO224:N1%=224-N%:PROCT(N%+16)
  220 PP%=0
  230 X%=(N1%*LX%(1,1)+N%*LX%(1,2))/224
  240 Y%=(N1%*LY%(1,1)+N%*LY%(1,2))/224
  250 PROCM(X%,Y%)
  260 FORI%=2TOAP%:PROCF
  270 X%=(N1%*LX%(I%,1)+N%*LX%(I%,2))/224
  280 Y%=(N1%*LY%(I%,1)+N%*LY%(I%,2))/224
  290 PROCD(X%,Y%):NEXT:PROCCL:PROCE
  300 SYS"Draw_Fill",PATH%,0,0,0
  310 NEXT
  320 SYS"Draw_Stroke",PATH%,0,0,0,0,0,0
  330 ENDPROC
  340 
  350 DEFPROCT(C%)
  360 LOCAL C1%,C2%,L%
  370 C1%=C%DIV16:L%=C%MOD16
  380 C2%=C1%+1:IF C2%>15 C2%=15
  390 PROCC(C1%,C2%,L%)
  400 ENDPROC
  410 
  420 DEFPROCC(C1%,C2%,L%)
  430 LOCALPC$(),C1$,C2$,CP$,HTAB$,B%,M%,N%,S%
  440 DIM PC$(8):HTAB$="0123456789ABCDEF"
  450 C1$=MID$(HTAB$,C1%+1,1):C2$=MID$(HTAB$,C2%+1,1)
  460 FORN%=1TO4:S%=P%(N%,L%):CP$="":B%=1
  470 FORM%=1TO8:IF(B% AND S%)=0 CP$=CP$+C1$ ELSE CP$=CP$+C2$
  480 B%=2*B%:NEXT:PC$(N%)=CP$:PC$(N%+4)=CP$:NEXT
  490 PROCPAT(PC$())
  500 ENDPROC
  510 
  520 DEFPROCPAT(PAT$())
  530 LOCAL N%,M%,V%():DIMV%(8,8)
  540 FORM%=1TO8:FORN%=1TO8:V%(M%,N%)=EVAL("&"+MID$(PAT$(M%),N%,1)):NEXT,
  550 FORN%=1TO4:VDU23,N%+1:FORM%=1TO8
  560 VDU PT%(V%(M%,2*N%-1),V%(M%,2*N%)):NEXT,
  570 ENDPROC
  580 
  590 DEFPROCM(X%,Y%)
  600 X%=X%*K%:Y%=Y%*K%
  610 PATH%!PP%=2:PP%+=4:PATH%!PP%=X%:PP%+=4:PATH%!PP%=Y%:PP%+=4
  620 ENDPROC
  630 
  640 DEFPROCD(X%,Y%)
  650 X%=X%*K%:Y%=Y%*K%
  660 PATH%!PP%=8:PP%+=4:PATH%!PP%=X%:PP%+=4:PATH%!PP%=Y%:PP%+=4
  670 ENDPROC
  680 
  690 DEFPROCCL
  700 PATH%!PP%=5:PP%+=4
  710 ENDPROC
  720 
  730 DEFPROCE
  740 PATH%!PP%=0:PP%+=4:PATH%!PP%=PP%
  750 ENDPROC
  760 
  770 DEFPROCF
  780 LOCAL N%,T%,F%
  790 IF TIME-TI%>=6 THEN
  800 FORF%=1TO3:FORN%=15TO2STEP-1:C%(N%,F%)=C%(N%-1,F%):NEXT
  810 C%(0,F%)=(C%(0,F%)+RND(16)+12)MOD360
  820 C%(1,F%)=127*(SINRADC%(0,F%)+1):NEXT
  830 FORN%=1TO15:COLOUR 16-N%,C%(N%,1),C%(N%,2),C%(N%,3):NEXT
  840 TI%=TIME
  850 ENDIF
  860 ENDPROC
  870 
  880 DEFPROCINIT
  890 LOCAL X%,Y%,N%,C1%,C2%
  900 FORY%=1TO17:FORX%=1TO4:READP%(X%,Y%):NEXT,
  910 FORY%=0TO15:FORX%=0TO15
  920 C1%=1:C2%=1
  930 FORN%=1TO4
  940 IF (C1%ANDX%)<>0 PT%(X%,Y%)+=C2%
  950 C2%=C2%*2
  960 IF (C1%ANDY%)<>0 PT%(X%,Y%)+=C2%
  970 C2%=C2%*2:C1%=C1%*2
  980 NEXT,,
  990 GCOL80,0:TI%=TIME:K%=256
 1000 ENDPROC
 1010 
 1020 DATA 0,0,0,0,128,0,8,0,136,0,136,0,136,32,136,2,136,34,136,34
 1030 DATA 168,34,138,34,170,34,170,34,170,42,170,162,170,170,170,170
 1040 DATA 234,170,174,170,238,170,238,170,238,186,238,171,238,187
 1050 DATA 238,187,238,251,238,191,238,255,238,255,254,255,239,255
 1060 DATA 255,255,255,255
