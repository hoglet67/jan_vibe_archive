   10 REM > NETSHADE4     *** Jan Vibe october 92 ***
   20 
   30 MODE15:OFF
   40 REPEAT:CLS:K%=RND(15)+5:PROCA(640,512,500,K%):K=INKEY(1000):UNTIL0
   50 
   60 DEFPROCA(X,Y,Z,A%)
   70 LOCAL T,N%,PX(),PY(),PC()
   80 DIM PX(20),PY(20),PC(20)
   90 T=RAD(360/A%)
  100 FORN%=1TOA%
  110 PX(N%)=X+Z*SIN(N%*T):PY(N%)=Y+Z*COS(N%*T):PC(N%)=RND(256)-1
  120 NEXT
  130 PROCB(PX(),PY(),PC(),A%)
  140 ENDPROC
  150 
  160 DEFPROCB(PX(),PY(),PC(),A%)
  170 LOCAL N%,M%,CX,CY,X1,Y1,XP(),YP(),CP()
  180 DIM XP(20),YP(20),CP(20)
  190 FORN%=1TOA%:CX+=PX(N%):CY+=PY(N%):NEXT:CX=CX/A%:CY=CY/A%
  200 FORN%=1TOA%:M%=N%MODA%+1
  210 X1=(PX(N%)+PX(M%))/2:Y1=(PY(N%)+PY(M%))/2
  220 XP(N%)=(3*X1+CX)/4:YP(N%)=(3*Y1+CY)/4:CP(N%)=RND(256)-1:NEXT
  230 FORN%=1TOA%:M%=N%MODA%+1
  240 PROCCT(PX(N%),PY(N%),PC(N%),XP(N%),YP(N%),CP(N%),PX(M%),PY(M%),PC(M%))
  250 PROCCT(XP(N%),YP(N%),CP(N%),XP(M%),YP(M%),CP(M%),PX(M%),PY(M%),PC(M%))
  260 NEXT
  270 IF (CX-XP(1))^2+(CY-YP(1))^2>500 PROCB(XP(),YP(),CP(),A%)
  280 ENDPROC
  290 
  300 DEFPROCCT(X1,Y1,C1%,X2,Y2,C2%,X3,Y3,C3%)
  310 LOCAL P1(),P2(),P3()
  320 DIM P1(5),P2(5),P3(5)
  330 P1(1)=X1:P1(2)=Y1:P1(3)=1
  340 P2(1)=X2:P2(2)=Y2:P2(4)=1
  350 P3(1)=X3:P3(2)=Y3:P3(5)=1
  360 GCOL80,0:PROCT(P1(),P2(),P3())
  370 ENDPROC
  380 
  390 DEFPROCT(P1(),P2(),P3())
  400 LOCAL PA(),PB(),PC(),C(),L,R,N%,M%
  410 DIM PA(5),PB(5),PC(5),C(3)
  420 L+=(P1(1)-P2(1))^2+(P1(2)-P2(2))^2
  430 L+=(P2(1)-P3(1))^2+(P2(2)-P3(2))^2
  440 L+=(P3(1)-P1(1))^2+(P3(2)-P1(2))^2
  450 IF L<=2000 THEN
  460 FORN%=1TO3:C(N%)=P1(N%+2)+P2(N%+2)+P3(N%+2):NEXT
  470 FORN%=2TO5:VDU23,N%
  480 FORM%=1TO8:R=RND(1)*(C(1)+C(2)+C(3))
  490 CASE TRUE OF
  500 WHEN R<=C(1):IF RND(1)<=C(1) VDU C1% ELSE VDUC2%
  510 WHEN R<=C(1)+C(2):IF RND(1)<=C(2) VDU C2% ELSE VDUC3%
  520 OTHERWISE:IF RND(1)<=C(3) VDU C3% ELSE VDUC1%
  530 ENDCASE
  540 NEXT,
  550 MOVE P1(1),P1(2):MOVEP2(1),P2(2):PLOT85,P3(1),P3(2):ENDPROC
  560 ELSE
  570 FORN%=1TO5
  580 PA(N%)=(P1(N%)+P2(N%))/2
  590 PB(N%)=(P2(N%)+P3(N%))/2
  600 PC(N%)=(P3(N%)+P1(N%))/2
  610 NEXT
  620 PROCT(P1(),PA(),PC())
  630 PROCT(PA(),PB(),PC())
  640 PROCT(PC(),PB(),P3())
  650 PROCT(PA(),P2(),PB())
  660 ENDIF
  670 ENDPROC
