   10 REM > WAVES2        *** Jan Vibe september 92 ***
   20 
   30 MODE12:OFF
   40 DIM SPR &10000,A%(4),C1%(15,2),C2%(15,2),C3%(15,2)
   50 SPR!0=&FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 Z%=150:Z2%=Z%/2:R%=50:D%=Z2%-R%-4:A%=0:C%=0:T%=TIME:L%=0
   70 PROCINIT:COLOUR0,0,0,128:VDU19,0,24,0,0,128
   80 ONERRORGOTO820
   90 
  100 REPEAT
  110 PROCC(1):PROCC(1)
  120 CLS:FORN%=1TO4:A%(N%)=RND(7):NEXT
  130 FORN=-90TO90STEP.1
  140 X%=(640-R%)*SINRAD(A%(1)*N)*COSRAD(A%(2)*N)+640
  150 Y%=(512-R%)*SINRAD(A%(3)*N)*COSRAD(A%(4)*N)+512
  160 PROCP(X%,Y%):PROCS(1)
  170 NEXT
  180 T1%=TIME+500:REPEAT:PROCS(1):UNTILTIME>T1%
  190 T1%=TIME+700:REPEAT:PROCS(0):UNTILTIME>T1%
  200 UNTIL0
  210 
  220 DEFPROCP(X%,Y%)
  230 LOCAL T,XM%,YM%
  240 A%=(A%+18)MOD360:T=RADA%
  250 XM%=Z2%+D%*SINT:XM%=2*(XM%DIV2)
  260 YM%=Z2%+D%*COST:YM%=4*(YM%DIV4)
  270 SYS"OS_SpriteOp",256+61,SPR,"BOX",0
  280 GCOL0:RECTANGLE FILL 0,0,Z%
  290 GCOL15:CIRCLE FILL XM%,YM%,R%
  300 GCOL0:CIRCLE FILL XM%,YM%,R%-12
  310 SYS"OS_SpriteOp",256+61,SPR,0,0:OFF
  320 SYS "OS_SpriteOp",256+34,SPR,"BOX",X%-XM%,Y%-YM%,8
  330 ENDPROC
  340 
  350 DEFPROCS(P%)
  360 LOCAL N%,CS%
  370 IF TIME>T% THEN
  380 L%+=1:IF L%=50 PROCC(P%)
  390 FORN%=1TO15
  400 C1%(N%,0)=((50-L%)*C1%(N%,2)+L%*C1%(N%,1))/50
  410 C2%(N%,0)=((50-L%)*C2%(N%,2)+L%*C2%(N%,1))/50
  420 C3%(N%,0)=((50-L%)*C3%(N%,2)+L%*C3%(N%,1))/50
  430 NEXT
  440 C%=C%MOD15+1
  450 FORN%=1TO15
  460 CS%=(N%+C%)MOD15+1:COLOUR N%,C1%(CS%,0),C2%(CS%,0),C3%(CS%,0)
  470 NEXT:T%=TIME+4
  480 ENDIF
  490 ENDPROC
  500 
  510 DEFPROCC(P%)
  520 LOCAL F1%,F2%,F3%,FA%,FB%,FC%,V1%,V2%,C%,N%
  530 FORN%=1TO15
  540 C1%(N%,2)=C1%(N%,1):C2%(N%,2)=C2%(N%,1):C3%(N%,2)=C3%(N%,1)
  550 NEXT:L%=0
  560 REPEAT
  570 F1%=RND(256)-1:F2%=RND(256)-1:F3%=RND(256)-1
  580 FA%=RND(256)-1:FB%=RND(256)-1:FC%=RND(256)-1
  590 UNTIL ABS(F1%-FA%)+ABS(F2%-FB%)+ABS(F3%-FC%)>=400
  600 IF P%=0 F1%=0:F2%=0:F3%=128:FA%=0:FB%=0:FC%=128
  610 FORN%=1TO15:V1%=N%-1:V2%=14-V1%
  620 C%=(V1%*F1%+V2%*FA%)/14:C1%(N%,1)=C%
  630 C%=(V1%*F2%+V2%*FB%)/14:C2%(N%,1)=C%
  640 C%=(V1%*F3%+V2%*FC%)/14:C3%(N%,1)=C%
  650 NEXT
  660 ENDPROC
  670 
  680 DEFPROCINIT
  690 LOCAL N%,T
  700 SYS"OS_SpriteOp",256+15,SPR,"BOX",0,Z%/2,Z%/4,12
  710 SYS"OS_SpriteOp",256+60,SPR,"BOX",0
  720 MOVE Z2%,Z2%:FORN%=0TO45:T=RAD(8*N%):GCOLN%MOD15+1
  730 MOVE Z2%,Z2%:PLOT85,200*SINT+Z2%,200*COST+Z2%:NEXT
  740 SYS"OS_SpriteOp",256+60,SPR,0,0
  750 SYS"OS_SpriteOp",256+29,SPR,"BOX"
  760 SYS"OS_SpriteOp",256+61,SPR,"BOX",0
  770 GCOL0:RECTANGLE FILL 0,0,Z%
  780 SYS"OS_SpriteOp",256+61,SPR,0,0
  790 CLS
  800 ENDPROC
  810 
  820 SYS"OS_SpriteOp",256+61,SPR,0,0:REPORT:PRINT" at line "STR$ERL:ON
