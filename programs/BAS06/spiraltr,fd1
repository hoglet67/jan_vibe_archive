   10 REM > SPIRALTREE   *** Jan Vibe august 92 ***
   20 
   30 MODE12:OFF
   40 DIM P%(4,17),PT%(15,15),C%(15,3)
   50 PROCINIT
   60 PROCTREE(870,216,235,0,64,RND(15))
   70 REPEAT:PROCS:UNTIL0
   80 
   90 DEFPROCTREE(X%,Y%,Z%,A%,R%,C%)
  100 LOCAL X1%,Y1%,C1%
  110 X1%=X%+Z%*SINRADA%:Y1%=Y%+Z%*COSRADA%
  120 PROCL(X%,Y%,X1%,Y1%,C%,C%MOD15+1,R%)
  130 IF Z%>8 THEN
  140 PROCTREE(X1%,Y1%,Z%/1.1,A%-36,R%/1.1,C%MOD15+1)
  150 X1%=X1%+.9*R%*SINRAD(A%+90):Y1%=Y1%+.9*R%*COSRAD(A%+90)
  160 PROCTREE(X1%,Y1%,Z%/2.30,A%+90,R%/2.30,C%MOD15+1)
  170 ENDIF
  180 ENDPROC
  190 
  200 DEFPROCL(X1%,Y1%,X2%,Y2%,C1%,C2%,R%)
  210 LOCAL D,D2,K,F,P%,PG%,NP%,N%,PX%,PY%
  220 P%=1:PROCC(C1%,C2%,P%)
  230 IF R%<=1 LINE X1%-1,Y1%,X2%-1,Y2%:LINE X1%+1,Y1%,X2%+1,Y2%:ENDPROC
  240 D=SQR((X1%-X2%)^2+(Y1%-Y2%)^2):NP%=D/(R%/4)
  250 IF D>=68 K=17:F=1 ELSE K=D/4:F=17/K
  260 D2=D/K
  270 FORN%=0TONP%:PROCS
  280 PX%=(X2%*N%+X1%*(NP%-N%))/NP%:PY%=(Y2%*N%+Y1%*(NP%-N%))/NP%
  290 PG%=P%:P%=SQR((X1%-PX%)^2+(Y1%-PY%)^2)/D2+1
  300 IF P%<>PG% AND P%<=K PROCC(C1%,C2%,P%*F)
  310 CIRCLE FILL PX%,PY%,R%:NEXT
  320 ENDPROC
  330 
  340 DEFPROCC(C1%,C2%,L%)
  350 LOCALPC$(),C1$,C2$,CP$,HTAB$,B%,M%,N%,S%
  360 DIM PC$(8):HTAB$="0123456789ABCDEF"
  370 C1$=MID$(HTAB$,C1%+1,1):C2$=MID$(HTAB$,C2%+1,1)
  380 FORN%=1TO4:S%=P%(N%,L%):CP$="":B%=1
  390 FORM%=1TO8:PROCS:IF(B% AND S%)=0 CP$=CP$+C1$ ELSE CP$=CP$+C2$
  400 B%=2*B%:NEXT:PC$(N%)=CP$:PC$(N%+4)=CP$:NEXT
  410 PROCPAT(PC$())
  420 ENDPROC
  430 
  440 DEFPROCPAT(PAT$())
  450 LOCAL N%,M%,V%():DIMV%(8,8)
  460 FORM%=1TO8:FORN%=1TO8:V%(M%,N%)=EVAL("&"+MID$(PAT$(M%),N%,1)):NEXT,
  470 FORN%=1TO4:PROCS:VDU23,N%+1:FORM%=1TO8
  480 VDU PT%(V%(M%,2*N%-1),V%(M%,2*N%)):NEXT,
  490 ENDPROC
  500 
  510 DEFPROCS
  520 LOCAL N%,CS%
  530 IF TIME>TI% THEN
  540 IF RND(1)>.9 THEN
  550 CS%=RND(3):FORN%=1TO15:C%(N%-1,CS%)=C%(N%,CS%):NEXT:C%(15,CS%)=C%(0,CS%)
  560 ENDIF
  570 SH%=SH%MOD15+1
  580 FORN%=1TO15:CS%=(N%+SH%)MOD15+1
  590 COLOUR 16-N%,C%(CS%,1),C%(CS%,2),C%(CS%,3):NEXT
  600 TI%=TIME+6
  610 ENDIF
  620 ENDPROC
  630 
  640 DEFPROCINIT
  650 LOCAL N%,V%,X%,Y%,C1%,C2%
  660 FORN%=1TO15:V%=127*(SINRAD(24*N%)+1)
  670 C%(N%,1)=V%:C%(N%,2)=V%:C%(N%,3)=V%:NEXT
  680 COLOUR0,128,128,128:VDU19,0,24,128,128,128
  690 FORY%=1TO17:FORX%=1TO4:READP%(X%,Y%):NEXT,
  700 FORY%=0TO15:FORX%=0TO15
  710 C1%=1:C2%=1
  720 FORN%=1TO4
  730 IF (C1%ANDX%)<>0 PT%(X%,Y%)+=C2%
  740 C2%=C2%*2
  750 IF (C1%ANDY%)<>0 PT%(X%,Y%)+=C2%
  760 C2%=C2%*2:C1%=C1%*2
  770 NEXT,,
  780 GCOL80,0:TI%=TIME:SH%=RND(15)
  790 ENDPROC
  800 
  810 DATA 0,0,0,0,128,0,8,0,136,0,136,0,136,32,136,2,136,34,136,34
  820 DATA 168,34,138,34,170,34,170,34,170,42,170,162,170,170,170,170
  830 DATA 234,170,174,170,238,170,238,170,238,186,238,171,238,187
  840 DATA 238,187,238,251,238,191,238,255,238,255,254,255,239,255
  850 DATA 255,255,255,255
