   10 REM > SPRTXT2
   20 
   30 MODE13
   40 DIMF%(7),I%(7),SPR &20000
   50 SPR!0=&FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 R1%=48:R2%=32
   70 B1%=2*R1%:B2%=2*R2%:D=SQR(R1%^2+R2%^2)/7
   80 
   90 FORN%=1TO26:CLS:H$=STR$N%
  100 SYS"OS_SpriteOp",256+15,SPR,H$,0,B2%/4,B1%/4,13
  110 SYS"OS_SpriteOp",256+60,SPR,H$,0
  120 FORM%=0TO7
  130 F%=N%:I%=M%:B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  140 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
  150 F%(7-M%)=F%:I%(7-M%)=(64*I%)MOD256:NEXT
  160 FORK%=7TO0STEP-1:GCOL F%(K%) TINT I%(K%)
  170 CIRCLE FILL R2%,R1%,K%*D+D/2:NEXT
  180 SYS"OS_SpriteOp",256+60,SPR,0,0
  190 SYS"OS_SpriteOp",256+29,SPR,H$
  200 NEXT
  210 COLOUR176 TINT 255:VDU19,0,24,0,0,255:CLS:OFF
  220 
  230 ONERRORGOTO540
  240 
  250 REPEAT
  260 PROCTXT(0,1,"This is a completely")
  270 PROCTXT(0,2,"new way of using the")
  280 PROCTXT(0,3,"ARC's spritesystem. ")
  290 PROCTXT(0,4,"Letters is made into")
  300 PROCTXT(0,5,"spritemasks, and    ")
  310 PROCTXT(0,6,"plotted with random-")
  320 PROCTXT(0,7,"ly selected sprites ")
  330 PROCTXT(0,8,"as background.      ")
  340 PROCTXT(0,10,"Jan Vibe august 92")
  350 UNTIL0
  360 
  370 DEFPROCTXT(X%,Y%,TXT$)
  380 LOCALN%,C$
  390 FORN%=1TOLENTXT$:C$=MID$(TXT$,N%,1)
  400 PROCL(X%,1020-Y%*B1%,C$):X%=X%+B2%:NEXT
  410 ENDPROC
  420 
  430 DEFPROCL(X%,Y%,C$)
  440 LOCAL H$
  450 H$=STR$(RND(26))
  460 SYS"OS_SpriteOp",256+61,SPR,H$,0
  470 GCOL0TINT0:RECTANGLE FILL 0,0,B2%,B1%
  480 GCOL63TINT192:MOVE0,2*R1%
  490 VDU23,17,7,6,R2%/2;R1%/2|5:PRINTC$;:VDU4
  500 SYS"OS_SpriteOp",256+61,SPR,0,0:OFF
  510 SYS"OS_SpriteOp",256+34,SPR,H$,X%,Y%,8
  520 ENDPROC
  530 
  540 SYS"OS_SpriteOp",256+61,SPR,0,0:COLOUR176 TINT 255:VDU4
  550 REPORT:PRINT" at line "STR$ERL:ON
