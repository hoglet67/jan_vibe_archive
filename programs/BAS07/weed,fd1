   10 REM > WEED
   20 
   30 MODE9:OFF
   40 DIM X(500,2),Y(500,2),DX(500,2),A(500,2)
   50 COLOUR 6,200,140,0:COLOUR 2,0,128,0
   60 REPEAT:CLS:T1%=1:P1%=1:P2%=2:Q=.2
   70 X(T1%,P1%)=640:Y(T1%,P1%)=512
   80 A(T1%,P1%)=RND(360):T=RADA(T1%,P1%)
   90 REPEAT:T2%=0
  100 FORN%=1TOT1%
  110 NX=X(N%,P1%)+5*SINRAD(A(N%,P1%)):NY=Y(N%,P1%)+5*COSRAD(A(N%,P1%))
  120 C%=POINT(NX,NY):IF C%>=0 AND C%<>6 THEN
  130 T2%+=1:X(T2%,P2%)=NX:Y(T2%,P2%)=NY
  140 A(T2%,P2%)=A(N%,P1%)+RND(41)-21
  150 GCOL6:LINE X(N%,P1%),Y(N%,P1%),NX,NY
  160 IFRND(1)>Q THEN
  170 T2%+=1:X(T2%,P2%)=NX:Y(T2%,P2%)=NY:M%=0
  180 REPEAT:M%+=1:A(T2%,P2%)=(RND(120)+A(N%,P1%)+300)MOD360:T=RADA(T2%,P2%)
  190 VX=NX+5*SINRADT:VY=NY+5*COSRADT
  200 UNTIL POINT(VX,VY)=0 OR M%=5
  210 IF M%=5 T2%-=1
  220 ENDIF
  230 IF RND(1)>.9 THEN
  240 T=A(N%,P1%)-45*SGN(RND(1)-.5):T1=T-20:T2=T+20
  250 GCOL 2:MOVE X(N%,P1%),Y(N%,P1%)
  260 MOVE X(N%,P1%)+20*SINRADT1,Y(N%,P1%)+20*COSRADT1
  270 PLOT85,X(N%,P1%)+20*SINRADT2,Y(N%,P1%)+20*COSRADT2
  280 PLOT85,X(N%,P1%)+40*SINRADT,Y(N%,P1%)+40*COSRADT
  290 ENDIF
  300 IF RND(1)>.99 THEN
  310 T=RAD(A(N%,P1%)-60*SGN(RND(1)-.5))
  320 LX=X(N%,P1%)+32*SINT:LY=Y(N%,P1%)+32*COST
  330 GCOL 6:LINE X(N%,P1%),Y(N%,P1%),LX,LY
  340 GCOL 5:FORI%=40TO360STEP40:T=RADI%:LINE LX,LY,LX+16*SINT,LY+16*COST:NEXT
  350 GCOL 4:CIRCLE FILL LX,LY,4
  360 ENDIF
  370 ENDIF
  380 NEXT
  390 SWAP T1%,T2%:SWAP P1%,P2%:Q=.9:IF T1%<10 Q=.8
  400 UNTIL T1%=0
  410 K=INKEY(300)
  420 UNTIL0
