   10 REM > PAT2
   20 
   30 MODE15:@%=0
   40 DIM SI(5),CO(5),F%(6,6),CF%(6,6),C%(6,6,5)
   50 FORN%=0TO5:T=RAD(60*N%+30):SI(N%)=SINT:CO(N%)=COST:NEXT
   60 REPEAT:CLS
   70 FORN%=1TO6
   80 FORM%=1TO6
   90 B1%=0:B2%=0:C%=1:FORI%=1TO5:IF RND(1)>.5 B%+=C%
  100 C%=2*C%:NEXT:F%(N%,M%)=B%
  110 CF%(N%,M%)=INT(RND(1)+.5)
  120 FORI%=1TO5:C%(N%,M%,I%)=RND(64)-1:NEXT
  130 NEXT,
  140 PROCH(640,512,300,127,RND(6),1)
  150 Q%=GET
  160 UNTIL0
  170 END
  180 
  190 DEFPROCH(X,Y,Z,S%,C%,L%)
  200 IF F%(C%,L%)AND01 PROCA(X,Y,Z,C%(C%,L%,1))
  210 IF F%(C%,L%)AND02 PROCB(X,Y,Z,C%(C%,L%,2))
  220 IF F%(C%,L%)AND04 PROCC(X,Y,Z,C%(C%,L%,3))
  230 IF F%(C%,L%)AND08 PROCD(X,Y,Z,C%(C%,L%,4))
  240 IF F%(C%,L%)AND16 PROCE(X,Y,Z,C%(C%,L%,5))
  250 IF C%>=1 THEN
  260 IF S%AND01 PROCH(X+Z*SI(0),Y+Z*CO(0),Z/2,35,C%-1,L%)
  270 IF S%AND02 PROCH(X+Z*SI(1),Y+Z*CO(1),Z/2,07,C%-1,L%)
  280 IF S%AND04 PROCH(X+Z*SI(2),Y+Z*CO(2),Z/2,14,C%-1,L%)
  290 IF S%AND08 PROCH(X+Z*SI(3),Y+Z*CO(3),Z/2,28,C%-1,L%)
  300 IF S%AND16 PROCH(X+Z*SI(4),Y+Z*CO(4),Z/2,56,C%-1,L%)
  310 IF S%AND32 PROCH(X+Z*SI(5),Y+Z*CO(5),Z/2,49,C%-1,L%)
  320 IF CF%(C%,L%)=1 PROCH(X,Y,Z/2,127,C%-1,L%+1)
  330 ENDIF
  340 ENDPROC
  350 
  360 DEFPROCA(X,Y,Z,C%)
  370 GCOL C%:CIRCLE FILL X,Y,Z
  380 ENDPROC
  390 
  400 DEFPROCB(X,Y,Z,C%)
  410 GCOL C%:CIRCLE X,Y,Z
  420 ENDPROC
  430 
  440 DEFPROCC(X,Y,Z,C%)
  450 GCOL C%:MOVE X+Z*SI(5),Y+Z*CO(5):MOVE X,Y
  460 FORN%=0TO5:PLOT85,X+Z*SI(N%),Y+Z*CO(N%):MOVE X,Y:NEXT
  470 ENDPROC
  480 
  490 DEFPROCD(X,Y,Z,C%)
  500 GCOL C%:MOVE X+Z*SI(5),Y+Z*CO(5)
  510 FORN%=0TO5:DRAWX+Z*SI(N%),Y+Z*CO(N%):NEXT
  520 ENDPROC
  530 
  540 DEFPROCE(X,Y,Z,C%)
  550 GCOL C%
  560 LINE X+Z*SI(0),Y+Z*CO(0),X+Z*SI(3),Y+Z*CO(3)
  570 LINE X+Z*SI(1),Y+Z*CO(1),X+Z*SI(4),Y+Z*CO(4)
  580 LINE X+Z*SI(2),Y+Z*CO(2),X+Z*SI(5),Y+Z*CO(5)
  590 ENDPROC
