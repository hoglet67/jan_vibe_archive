   10 REM > EMBOSS
   20 
   30 MODE12:OFF
   40 DIM SPR &1000
   50 SPR!0=&FFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 PROCINIT
   70 ONERRORGOTO550
   80 
   90 COLOUR135:CLS
  100 FORN%=3TO360STEP3:T1=RAD(N%-3):T2=RAD(N%)
  110 PROCL(640+400*SINT2,512+400*COST2,640+400*SINT1,512+400*COST1)
  120 NEXT
  130 END
  140 
  150 DEFPROCL(X1%,Y1%,X2%,Y2%)
  160 LOCAL L%,N%,T,XA,YA,XB,YB,X1,Y1,X2,Y2,X3,Y3,X4,Y4
  170 T=ACS((Y2%-Y1%)/SQR((X2%-X1%)^2+(Y2%-Y1%)^2)):IF X1%>X2% T=2*PI-T
  180 XA=64+4*SINT:YA=64+4*COST
  190 XB=64-4*SINT:YB=64-4*COST
  200 T+=PI/2
  210 X1=XA+48*SINT:Y1=YA+48*COST
  220 X2=XB+48*SINT:Y2=YB+48*COST
  230 X3=XA-48*SINT:Y3=YA-48*COST
  240 X4=XB-48*SINT:Y4=YB-48*COST
  250 SYS"OS_SpriteOp",256+61,SPR,"BOX",0
  260 GCOL0:RECTANGLE FILL 0,0,128:GCOL15
  270 MOVE X1,Y1:MOVEX2,Y2:PLOT85,X3,Y3:PLOT85,X4,Y4
  280 SYS"OS_SpriteOp",256+61,SPR,0,0:OFF
  290 L%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2)
  300 FORN%=0TOL%
  310 XA=(X1%*N%+X2%*(L%-N%))/L%
  320 YA=(Y1%*N%+Y2%*(L%-N%))/L%
  330 SYS "OS_SpriteOp",256+34,SPR,"BOX",XA-64,YA-64,8
  340 NEXT
  350 ENDPROC
  360 
  370 DEFPROCINIT
  380 LOCAL N%,T,D
  390 SYS"OS_SpriteOp",256+15,SPR,"BOX",0,64,32,12
  400 SYS"OS_SpriteOp",256+60,SPR,"BOX",0
  410 FORN%=1TO15:COLOURN%,16*N%,16*N%,16*N%:NEXT
  420 D=360/28:MOVE64,64
  430 FORN%=1TO29:T=RAD(N%*D+D/2)
  440 GCOL ABS(14-N%MOD28)+1
  450 MOVE 64,64:PLOT85,64+100*SINT,64+100*COST
  460 NEXT
  470 SYS"OS_SpriteOp",256+60,SPR,0,0
  480 SYS"OS_SpriteOp",256+29,SPR,"BOX"
  490 SYS"OS_SpriteOp",256+61,SPR,"BOX",0
  500 GCOL0:RECTANGLE FILL 0,0,128
  510 SYS"OS_SpriteOp",256+61,SPR,0,0
  520 CLS
  530 ENDPROC
  540 
  550 SYS"OS_SpriteOp",256+61,SPR,0,0:REPORT:PRINT" at line "STR$ERL:ON
