   10 REM > KALEJDO2
   20 
   30 MODE137:MODE9:OFF
   40 DIM SPR &20000
   50 SPR!0=&1FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 DIM R%(15),G%(15),B%(15)
   70 FORN%=1TO15
   80 R%(N%)=RND(510):G%(N%)=RND(510):B%(N%)=RND(510)
   90 COLOURN%,ABS(R%(N%)MOD510-255),ABS(G%(N%)MOD510-255),ABS(B%(N%)MOD510-255)
  100 NEXT
  110 S1%=1:S2%=2:X%=512:Y%=384
  120 DX%=4*RND(4)*SGN(RND(1)-.5):DY%=4*RND(4)*SGN(RND(1)-.5)
  130 ONERRORGOTO490
  140 SYS"OS_SpriteOp",256+15,SPR,"SCR",0,320,256,9
  150 SYS"OS_SpriteOp",256+60,SPR,"SCR",0
  160 FORN%=1TO1000:GCOLRND(15)
  170 TX%=RND(1280):TY%=RND(1024)
  180 MOVE TX%+RND(300)-150,TY%+RND(300)-150
  190 MOVE TX%+RND(300)-150,TY%+RND(300)-150
  200 PLOT85,TX%+RND(300)-150,TY%+RND(300)-150
  210 NEXT
  220 SYS"OS_SpriteOp",256+60,SPR,0,0
  230 
  240 REPEAT
  250 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  260 SYS"OS_SpriteOp",256+60,SPR,"SCR",0
  270 H%=X%+DX%:IF H%>980 OR H%<0 DX%=4*(RND(4)+1)*SGN(-DX%)
  280 H%=Y%+DY%:IF H%>724 OR H%<0 DY%=4*(RND(4)+1)*SGN(-DY%)
  290 X%+=DX%:Y%+=DY%
  300 T%=RND(15)
  310 R%(T%)=(R%(T%)+RND(20))MOD510
  320 G%(T%)=(G%(T%)+RND(20))MOD510
  330 B%(T%)=(B%(T%)+RND(20))MOD510
  340 COLOURT%,ABS(R%(T%)MOD510-255),ABS(G%(T%)MOD510-255),ABS(B%(T%)MOD510-255)
  350 SYS"OS_SpriteOp",256+16,SPR,"BOX",0,X%,Y%,X%+256,Y%+256
  360 SYS"OS_SpriteOp",256+60,SPR,0,0:OFF
  370 PROCP(0,0)
  380 SYS"OS_SpriteOp",256+47,SPR,"BOX":PROCP(256,0)
  390 SYS"OS_SpriteOp",256+33,SPR,"BOX":PROCP(256,256)
  400 SYS"OS_SpriteOp",256+47,SPR,"BOX":PROCP(0,256)
  410 UNTIL0
  420 
  430 DEFPROCP(PX%,PY%)
  440 LOCAL X%,Y%
  450 FORX%=0TO1023STEP512:FORY%=0TO1023STEP512
  460 SYS"OS_SpriteOp",256+34,SPR,"BOX",X%+PX%+128,Y%+PY%,0
  470 NEXT,
  480 ENDPROC
  490 
  500 SYS"OS_SpriteOp",256+60,SPR,0,0
  510 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
