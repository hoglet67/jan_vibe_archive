   10 REM > TRAVEL    *** Jan Vibe april 93 ***
   20 
   30 MODE137:MODE9:OFF
   40 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT
   50 DIM X%(1000,2),Y%(1000,2),R%(1000,2),C(1000,2)
   60 S1%=1:S2%=2:F=0.87:A%=0:V=0:DV=0.1:C1%=1:C2%=1
   70 PROCB:X%(C1%,S1%)=BX%:Y%(C1%,S1%)=BY%:R%(C1%,S1%)=BR%:C(C1%,S1%)=15
   80 
   90 ONERRORGOTO370
  100 REPEAT
  110 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  120 V+=DV:IF ABS(V)>10 DV=-DV
  130 T=RAD(V):SI=SINT:CO=COST
  140 C1%=C2%:C2%=0:A%=(A%+8)MOD360:T=RADA%:DX%=640+300*SINT:DY%=512+300*COST
  150 FORN%=1TOC1%:GCOL C(N%,S1%)
  160 X1=X%(N%,S1%):Y1=Y%(N%,S1%):R1=R%(N%,S1%):CIRCLE FILL X1,Y1,R1
  170 IF C(N%,S1%)>1 THEN
  180 XA=F*((X1-DX%)*CO+(Y1-DY%)*SI)+DX%:YA=F*((Y1-DY%)*CO-(X1-DX%)*SI)+DY%
  190 RA=F*R1:C2%+=1
  200 X%(C2%,S2%)=XA:Y%(C2%,S2%)=YA:R%(C2%,S2%)=RA:C(C2%,S2%)=C(N%,S1%)-.6
  210 ENDIF
  220 NEXT
  230 PROCB:C2%+=1
  240 X%(C2%,S2%)=BX%:Y%(C2%,S2%)=BY%:R%(C2%,S2%)=BR%:C(C2%,S2%)=15
  250 UNTIL0
  260 
  270 DEFPROCB
  280 CASE RND(4) OF
  290 WHEN 1: BX%=0:BY%=RND(1024)
  300 WHEN 2: BX%=1280:BY%=RND(1024)
  310 WHEN 3: BX%=RND(1280):BY%=0
  320 WHEN 4: BX%=RND(1280):BY%=1024
  330 ENDCASE
  340 BR%=RND(80)+8
  350 ENDPROC
  360 
  370 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
