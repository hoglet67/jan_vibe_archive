   10 REM > WORM       *** Jan Vibe march 93 ***
   20 
   30 MODE137:MODE9:OFF
   40 S1%=1:S2%=2:L%=20:S=2.2
   50 DIM SPR &10000
   60 DIMX%(L%),Y%(L%),R%(L%)
   70 SPR!0=&FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   80 FORN%=1TOL%:R%(N%)=S*(L%-N%+2):NEXT
   90 X%(1)=800:Y%(1)=800:DX%=20:DY%=20:MX%=1280-R%(1):MY%=1024-R%(1)
  100 FORN%=1TO15:COLOUR 16-N%,240,16*N%,0:NEXT
  110 COLOUR0,0,0,240:VDU19,0,24,0,0,240
  120 FORN%=1TOL%:CLS:R%=S*(L%-N%+2):H$=STR$(N%)
  130 HX%=R%*.3:HY%=R%*.6:Z=(SQR(HX%^2+HY%^2)+R%)/15
  140 FORY%=-R%TOR%STEP4:B%=SQR(R%^2-Y%^2):B%=4*(B%DIV4)
  150 FORX%=-B%TOB%STEP4
  160 C%=(SQR((HX%-X%)^2+(HY%-Y%)^2)+RND(3*Z)-1.5*Z)/Z+.5
  170 IF C%<1 C%=1
  180 IF C%>15 C%=15
  190 GCOLC%:POINT X%+R%,Y%+R%:NEXT,
  200 SYS"OS_SpriteOp",256+16,SPR,H$,0,0,0,2*R%,2*R%
  210 SYS"OS_SpriteOp",256+29,SPR,H$
  220 SYS"OS_SpriteOp",256+61,SPR,H$,0
  230 GCOL0:RECTANGLE FILL 0,0,2*R%
  240 GCOL15:FORY%=-R%TOR%STEP4:B%=SQR(R%^2-Y%^2):B%=4*(B%DIV4)
  250 LINE R%-B%,R%+Y%,R%+B%,R%+Y%:NEXT
  260 SYS"OS_SpriteOp",256+61,SPR,0,0:OFF
  270 NEXT
  280 
  290 ONERRORGOTO430
  300 REPEAT
  310 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  320 H%=X%(1)+DX%:IF H%>MX% OR H%<R%(1) DX%=(RND(29)+19)*SGN(-DX%)
  330 H%=Y%(1)+DY%:IF H%>MY% OR H%<R%(1) DY%=(RND(29)+19)*SGN(-DY%)
  340 X%(1)+=DX%:Y%(1)+=DY%
  350 SYS "OS_SpriteOp",256+34,SPR,"1",X%(1)-R%(1),Y%(1)-R%(1),8
  360 FORI%=2TOL%:H$=STR$(I%):X%=X%(I%-1)-X%(I%):Y%=Y%(I%-1)-Y%(I%)
  370 D=SQR(X%^2+Y%^2):IF D=0 D=1
  380 P=(D-R%(I%)-R%(I%-1))/D:X%(I%)+=X%*P:Y%(I%)+=Y%*P
  390 SYS "OS_SpriteOp",256+34,SPR,H$,X%(I%)-R%(I%),Y%(I%)-R%(I%),8
  400 NEXT
  410 UNTIL0
  420 
  430 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
