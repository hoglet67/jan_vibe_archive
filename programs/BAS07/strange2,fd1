   10 REM > STRANGE2
   20 
   30 MODE0:OFF
   40 DIMO$(4),R$(4)
   50 FORN%=1TO4:READO$(N%):NEXT
   60 FORN%=1TO4:READR$(N%):NEXT
   70 
   80 ONERRORGOTO440
   90 
  100 REPEAT
  110 CLS:X=RND(1):Y=RND(1):IX=X:IY=Y
  120 T1=0:T2=0:T=TIME+6000
  130 Z1=SGN(RND(1)-.5)*RND(1)/RND(1E4)
  140 Z2=SGN(RND(1)-.5)*RND(1)/RND(1E4)
  150 A$=FNF(RND(2)+1):B$=FNF(RND(2)+1)
  160 REPEAT
  170 T1=T1+Z1:T2=T2+Z2
  180 K1=EVAL(A$):K2=EVAL(B$)
  190 Y=ABS(K1-INT(K1))
  200 X=ABS(K2-INT(K2))
  210 POINT 1280*X,1024*Y
  220 UNTILTIME>T OR INKEY$(0)<>""
  230 PRINT"X="A$'"Y="B$'"X_init="IX'"Y_init="IY'"X_incr="Z1'"Y_incr="Z2
  240 K=GET
  250 UNTIL0
  260 
  270 DEFFNF(K%)
  280 LOCAL N%,A$,B$,F1,F2
  290 FORN%=1TOK%
  300 F1=RND(4):REPEAT:F2=RND(4):UNTIL F1<>F2
  310 A$=O$(F1)+R$(RND(4))+O$(F2)
  320 IF RND(1)>.5 THEN
  330 CASE RND(3) OF
  340 WHEN 1:A$="("+A$+")"
  350 WHEN 2:A$="SQR("+A$+")"
  360 WHEN 3:A$="("+A$+")^2"
  370 ENDCASE
  380 ENDIF
  390 IF B$<>"" B$=B$+R$(RND(4))
  400 B$=B$+A$
  410 NEXT
  420 =B$
  430 
  440 IF ERR=18 OR ERR=20 OR ERR=21 RUN
  450 REPORT:PRINT" at line "STR$ERL
  460 
  470 DATA X,Y,T1,T2
  480 DATA +,-,*,/
