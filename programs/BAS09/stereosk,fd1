   10 REM > STEREOSKLT
   20 
   30 MODE31
   40 DIM TXT$(4),YPOS%(4),PX%(20000),PY%(20000):P%=0
   50 INPUT"Number of lines (Max 4)="L%:PRINT
   60 FORN%=1TOL%:PRINT"Line no "+STR$(N%);:INPUT" "TXT$(N%):NEXT
   70 INPUT'"Steepness="ST%:CLS:OFF
   80 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT:COLOUR 0,0,0,64
   90 
  100 MAX%=0:FORN%=1TOL%
  110 IF LEN(TXT$(N%))>MAX% MAX%=LEN(TXT$(N%))
  120 NEXT
  130 CASE MAX% OF
  140 WHEN 1,2,3: XZ%=3500
  150 WHEN 4: XZ%=2500
  160 WHEN 5: XZ%=2000
  170 OTHERWISE :XZ%=1700
  180 ENDCASE
  190 CASE L% OF
  200 WHEN 1:YPOS%(1)=300:YZ%=6000
  210 WHEN 2:YPOS%(2)=150:YPOS%(1)=650:YZ%=3700
  220 WHEN 3:YPOS%(3)=100:YPOS%(2)=450:YPOS%(1)=800:YZ%=2800
  230 WHEN 4:YPOS%(4)=100:YPOS%(3)=350:YPOS%(2)=600:YPOS%(1)=850:YZ%=2000
  240 ENDCASE
  250 
  260 FONT$="Trinity.Medium"
  270 SYS"Font_SetFontColours",0,0,15,0
  280 SYS"Font_FindFont",,FONT$,XZ%,YZ%,0,0 TO HAN%
  290 FOR N%=1 TO L%
  300 SYS"Font_Paint",,TXT$(N%),16,400,YPOS%(N%)
  310 NEXT
  320 
  330 FORY%=0TO1199STEP2:FORX%=399TO1599STEP2
  340 IF POINT(X%,Y%)<>0 THEN
  350 F%=0:FORM%=-2TO2STEP2:FORN%=-2TO2STEP2
  360 IF POINT(X%+N%,Y%+N%)=0 F%=1:N%=2:M%=2
  370 NEXT,:IFF%=1 P%+=1:PX%(P%)=X%:PY%(P%)=Y%
  380 ENDIF
  390 NEXT,
  400 FORC%=1TO14:R%=2*ST%*(15-C%):GCOLC%
  410 FORN%=1TOP%:CIRCLE FILL PX%(N%),PY%(N%),R%
  420 NEXT,
  430 FOR N%=1 TO L%
  440 SYS"Font_Paint",,TXT$(N%),16,400,YPOS%(N%)
  450 NEXT
  460 SYS"Font_LoseFont",HAN%
  470 
  480 VDU24,0;0;255;1199;
  490 FORN%=1TO8000:X%=RND(256):Y%=RND(1200):R%=RND(6)+3:GCOLRND(15)
  500 FORM%=-256TO256STEP256:CIRCLEFILLX%+M%,Y%,R%:NEXT,
  510 
  520 VDU26
  530 FORY%=0TO1199STEP2
  540 FORX%=256TO1600STEP2
  550 F%=POINT(X%,Y%)*2
  560 C%=POINT(X%+F%-256,Y%)
  570 GCOLC%:POINTX%,Y%
  580 NEXT,
  590 *SCREENSAVE STSKILT
