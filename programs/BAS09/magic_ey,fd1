   10 REM > MAGIC_EYE2
   20 
   30 MODE21:OFF:K%=10000:PC%=0:DP%=200
   40 DIM PX%(K%),PY%(K%),PZ%(K%),PR%(K%)
   50 DIM A(4,4),T(4,4),C(4,4),K(4),R(4)
   60 
   70 CLS:VDU 28,5,31,79,0
   80 PRINT'TAB(30)"Magic eye"''
   90 PRINT"This program can generate pictures like the ones in the book 'Magic"
  100 PRINT"eye'. It does this by making a normal 3D model of the object on the"
  110 PRINT"screen. This model is colour coded in such a way that the points"
  120 PRINT"farthest away has the highest colour number, and the points nearest"
  130 PRINT"to you has the lowest number. This is used for depth information."
  140 PRINT"If the color model looks OK to you, just tap the keyboard, and the"
  150 PRINT"picture will be converted to a real 'Magic eye' picture."
  160 PRINT"If you wish to save the picture, just tap the 'S' key and it will"
  170 PRINT"be saved as a MODE 8 sprite named 'MagicPic'."'
  180 PRINT"You can choose from the following objects:"'
  190 PRINTTAB(20)"A Chain"
  200 PRINTTAB(20)"B Rattle"
  210 PRINTTAB(20)"C Star"
  220 PRINTTAB(20)"D Box"
  230 PRINTTAB(20)"E Squiggle (different each time)"
  240 PRINTTAB(20)"F Maze cube (different each time)"
  250 PRINTTAB(20)"G X-mas tree"
  260 PRINTTAB(20)"H Spiral"
  270 PRINTTAB(20)"I Random balls (different each time)"
  280 PRINT'TAB(20)"Input letter...";:VDU26
  290 REPEAT:Q$=GET$:UNTIL INSTR("AaBbCcDdEeFfGgHhIi",Q$)<>0
  300 
  310 CLS:VDU24,0;0;DP%-1;1023;
  320 FORN%=1TO300:X%=RND(DP%):Y%=RND(1200):R%=RND(24)+12:C%=RND(26)
  330 FORI%=0TO7:PROCF(C%,I%):FORM%=-DP%TODP%STEPDP%
  340 CIRCLEFILLX%+M%,Y%,(I%*4+(7-I%)*R%)/7:NEXT,,
  350 VDU26,24,DP%;0;1279;1023;:ORIGIN DP%,0
  360 
  370 C()=0:FORN%=1TO4:C(N%,N%)=1:NEXT:PROCROT(0,0,0)
  380 CASE Q$ OF
  390 WHEN "A","a":PROCPIC1
  400 WHEN "B","b":PROCPIC2
  410 WHEN "C","c":PROCPIC3
  420 WHEN "D","d":PROCPIC4
  430 WHEN "E","e":PROCPIC5
  440 WHEN "F","f":PROCPIC6
  450 WHEN "G","g":PROCPIC7
  460 WHEN "H","h":PROCPIC8
  470 WHEN "I","i":PROCPIC9
  480 ENDCASE
  490 
  500 FORZ%=1024TO16STEP-16:GCOL48-Z%/21
  510 FORN%=1TOPC%
  520 IF ABS(Z%-PZ%(N%))<=PR%(N%) THEN
  530 P%=SQR(PR%(N%)^2-(PZ%(N%)-Z%)^2):CIRCLE FILL PX%(N%),PY%(N%),P%
  540 ENDIF
  550 NEXT,
  560 
  570 VDU26
  580 FORY%=0TO1023STEP2
  590 FORX%=DP%TO1280STEP2
  600 F%=POINT(X%,Y%)
  610 C%=POINT(X%-DP%+F%,Y%):T%=TINT(X%-DP%+F%,Y%)
  620 GCOLC%TINTT%:POINTX%,Y%
  630 NEXT,
  640 
  650 
  660 K$=GET$:IF INSTR("Ss",K$)<>0 OSCLI"SCREENSAVE MagicPic"
  670 ON:END
  680 
  690 DEFPROCL(X1%,Y1%,Z1%,R1%,X2%,Y2%,Z2%,R2%)
  700 LOCAL N%,D%,B%
  710 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  720 X1%=R(1):Y1%=R(2):Z1%=R(3)
  730 K(1)=X2%:K(2)=Y2%:K(3)=Z2%:K(4)=1:R()=K().T()
  740 X2%=R(1):Y2%=R(2):Z2%=R(3)
  750 D%=SQR((X1%-X2%)^2+(Y1%-Y2%)^2+(Z1%-Z2%)^2)
  760 IF R1%<R2% B%=2*D%/R1% ELSE B%=2*D%/R2%
  770 IF B%<>0 THEN
  780 FOR N%=0TOB%:PC%+=1
  790 PX%(PC%)=(X1%*N%+X2%*(B%-N%))/B%+512
  800 PY%(PC%)=(Y1%*N%+Y2%*(B%-N%))/B%+512
  810 PZ%(PC%)=(Z1%*N%+Z2%*(B%-N%))/B%+512
  820 PR%(PC%)=(R1%*N%+R2%*(B%-N%))/B%
  830 NEXT
  840 ELSE
  850 PC%+=1:PX%(PC%)=X1%:PY%(PC%)=Y1%:PZ%(PC%)=Z1%:PR%(PC%)=R1%
  860 PC%+=1:PX%(PC%)=X2%:PY%(PC%)=Y2%:PZ%(PC%)=Z2%:PR%(PC%)=R2%
  870 ENDIF
  880 ENDPROC
  890 
  900 DEFPROCB(X1%,Y1%,Z1%,R%)
  910 LOCAL N%,D%,B%
  920 K(1)=X1%:K(2)=Y1%:K(3)=Z1%:K(4)=1:R()=K().T()
  930 X1%=R(1):Y1%=R(2):Z1%=R(3):PC%+=1
  940 PX%(PC%)=X1%+562:PY%(PC%)=Y1%+512:PZ%(PC%)=Z1%+512:PR%(PC%)=R%
  950 ENDPROC
  960 
  970 DEFPROCR(X%,Y%,Z%)
  980 LOCAL N%
  990 PROCROT(X%,Y%,Z%)
 1000 FORN%=1TOPC%
 1010 K(1)=PX%(N%)-562:K(2)=PY%(N%)-512:K(3)=PZ%(N%)-512:K(4)=1:R()=K().T()
 1020 PX%(N%)=R(1)+562:PY%(N%)=R(2)+512:PZ%(N%)=R(3)+512
 1030 NEXT
 1040 ENDPROC
 1050 
 1060 DEFPROCROT(DX,DY,DZ)
 1070 LOCAL T,SI,CO
 1080 T()=C()
 1090 A()=C():T=RADDX:CO=COST:SI=SINT
 1100 A(2,2)=CO:A(3,2)=-SI:A(2,3)=SI:A(3,3)=CO
 1110 T()=A()
 1120 A()=C():T=RADDY:CO=COST:SI=SINT
 1130 A(1,1)=CO:A(3,1)=SI:A(1,3)=-SI:A(3,3)=CO
 1140 T()=A().T()
 1150 A()=C():T=RADDZ:CO=COST:SI=SINT
 1160 A(1,1)=CO:A(2,1)=-SI:A(1,2)=SI:A(2,2)=CO
 1170 T()=A().T()
 1180 ENDPROC
 1190 
 1200 DEFPROCPIC1
 1210 LOCAL N%,I%,J%,T1,T2
 1220 PROCROT(30,20,10)
 1230 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
 1240 FORI%=-256TO256STEP512:FORJ%=-256TO256STEP512
 1250 PROCL(I%+180*SINT1,J%+180*COST1,0,40,I%+180*SINT2,J%+180*COST2,0,40)
 1260 NEXT
 1270 PROCL(180*COST1,I%,180*SINT1+0,40,180*COST2,I%,180*SINT2+0,40)
 1280 PROCL(I%,180*COST1,180*SINT1+0,40,I%,180*COST2,180*SINT2+0,40)
 1290 NEXT,
 1300 ENDPROC
 1310 
 1320 DEFPROCPIC2
 1330 LOCAL N%,A1%,A2%,B1%,B2%,T1,T2
 1340 PROCROT(30,40,0)
 1350 FOR N%=0TO340STEP20:T1=RAD(N%):T2=RAD(N%+20)
 1360 A1%=450*SINT1:B1%=450*COST1
 1370 A2%=450*SINT2:B2%=450*COST2
 1380 PROCL(A1%,B1%,0,50,A2%,B2%,0,50)
 1390 PROCL(A1%,0,B1%,50,A2%,0,B2%,50)
 1400 PROCL(0,A1%,B1%,50,0,A2%,B2%,50)
 1410 NEXT
 1420 PROCB(0,0,0,150)
 1430 ENDPROC
 1440 
 1450 DEFPROCPIC3
 1460 LOCAL A%,N%,X%,Y%,T
 1470 FORA%=0TO150STEP30
 1480 PROCROT(8,8+A%,0)
 1490 FORN%=0TO330STEP30:T=RADN%
 1500 X%=480*SINT:Y%=480*COST
 1510 PROCL(0,0,0,60,X%,Y%,0,8)
 1520 NEXT,
 1530 ENDPROC
 1540 
 1550 DEFPROCPIC4
 1560 LOCAL N%,W%,X%
 1570 W%=40:Z%=300
 1580 FORN%=0TO270STEP90
 1590 PROCROT(10,20,N%)
 1600 PROCL(-Z%,-Z%,-Z%,W%, Z%,-Z%,-Z%,W%)
 1610 PROCL( Z%,-Z%,-Z%,W%, Z%,-Z%, Z%,W%)
 1620 PROCL( Z%,-Z%, Z%,W%,-Z%,-Z%, Z%,W%)
 1630 NEXT
 1640 W%=24:Z%=100
 1650 FORN%=0TO270STEP90
 1660 PROCROT(60,55,N%)
 1670 PROCL(-Z%,-Z%,-Z%,W%, Z%,-Z%,-Z%,W%)
 1680 PROCL( Z%,-Z%,-Z%,W%, Z%,-Z%, Z%,W%)
 1690 PROCL( Z%,-Z%, Z%,W%,-Z%,-Z%, Z%,W%)
 1700 NEXT
 1710 ENDPROC
 1720 
 1730 DEFPROCPIC5
 1740 LOCAL N%,TB%(),A,T
 1750 DIM TB%(6)
 1760 FORN%=1TO6:TB%(N%)=RND(6):NEXT
 1770 PROCROT(20,20,0)
 1780 FORA=0TO360STEP.25:T=RAD(A)
 1790 X%=400*SIN(TB%(1)*A)*COS(TB%(2)*A)
 1800 Y%=400*SIN(TB%(3)*A)*COS(TB%(4)*A)
 1810 Z%=400*SIN(TB%(5)*A)*COS(TB%(6)*A)
 1820 PROCB(X%,Y%,Z%,32)
 1830 NEXT
 1840 ENDPROC
 1850 
 1860 DEFPROCPIC6
 1870 LOCAL MX%(),MY%(),MZ%(),MP%
 1880 DIM MX%(200),MY%(200),MZ%(200)
 1890 PROCROT(20,20,10)
 1900 PROCMAZE(0,0,0)
 1910 ENDPROC
 1920 DEFPROCMAZE(X%,Y%,Z%)
 1930 LOCAL N%,M%,F%,X1%,Y1%,Z1%
 1940 FORN%=1TO8:X1%=X%:Y1%=Y%:Z1%=Z%
 1950 CASE RND(3) OF
 1960 WHEN 1:REPEAT:X1%=X%+160*SGN(RND(1)-.5):UNTILABS(X1%)<=320
 1970 WHEN 2:REPEAT:Y1%=Y%+160*SGN(RND(1)-.5):UNTILABS(Y1%)<=320
 1980 WHEN 3:REPEAT:Z1%=Z%+160*SGN(RND(1)-.5):UNTILABS(Z1%)<=320
 1990 ENDCASE
 2000 F%=0:IF PC%>0 THEN
 2010 FORM%=1TOMP%
 2020 IF X1%=MX%(M%) AND Y1%=MY%(M%) AND Z1%=MZ%(M%) F%=1:M%=MP%
 2030 NEXT
 2040 ENDIF
 2050 IF F%=0 THEN
 2060 MP%=MP%+1:MX%(MP%)=X1%:MY%(MP%)=Y1%:MZ%(MP%)=Z1%
 2070 PROCL(X%,Y%,Z%,20,X1%,Y1%,Z1%,20):PROCMAZE(X1%,Y1%,Z1%)
 2080 ENDIF
 2090 NEXT
 2100 ENDPROC
 2110 
 2120 DEFPROCPIC7
 2130 LOCAL N%,A%,Z%
 2140 PROCL(0,-550,0,50,0,450,0,10)
 2150 Z%=500:A%=30
 2160 FORN%=-400TO350STEP50:Z%=Z%-28
 2170 A%+=20
 2180 FORM%=0TO288STEP72
 2190 PROCROT(0,A%+M%,0)
 2200 PROCL(0,N%,0,20,Z%,N%+30,0,10)
 2210 NEXT,
 2220 PROCR(20,0,0)
 2230 ENDPROC
 2240 
 2250 DEFPROCPIC8
 2260 LOCAL X%,Y%,Z%,R%,N,T
 2270 PROCROT(-70,20,20)
 2280 FORN=-450TO450STEP.5:Y%=N:T=T+.03
 2290 R%=SQR(450^2-Y%^2)
 2300 X%=R%*SINT:Z%=R%*COST
 2310 PROCB(X%,Y%,Z%,25)
 2320 NEXT
 2330 ENDPROC
 2340 
 2350 DEFPROCPIC9
 2360 LOCAL X%(),Y%(),Z%(),R%(),N%,C%,D%,F%,T%
 2370 DIM X%(100),Y%(100),Z%(100),R%(100)
 2380 C%=1:R%(1)=RND(40)+80
 2390 X%(1)=RND(1024-2*R%(1))+R%(1)
 2400 Y%(1)=RND(1024-2*R%(1))+R%(1)
 2410 Z%(1)=RND(1024-2*R%(1))+R%(1)
 2420 REPEAT
 2430 R%(C%+1)=RND(40)+80
 2440 X%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2450 Y%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2460 Z%(C%+1)=RND(1024-2*R%(C%+1))+R%(C%+1)
 2470 T%+=1:F%=0
 2480 FOR N%=1 TO C%
 2490 D%=SQR((X%(C%+1)-X%(N%))^2+(Y%(C%+1)-Y%(N%))^2+(Z%(C%+1)-Z%(N%))^2)
 2500 IF D%<(R%(C%+1)+R%(N%)+80) N%=C%:F%=1
 2510 NEXT
 2520 IFF%=0 C%+=1:T%=0
 2530 UNTIL T%=200 OR C%=100
 2540 FORN%=1TOC%:PROCB(X%(N%)-512,Y%(N%)-512,Z%(N%)-512,R%(N%)):NEXT
 2550 ENDPROC
 2560 
 2570 DEFPROCF(F%,I%)
 2580 LOCAL R%,G%,B%
 2590 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
 2600 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
 2610 GCOL F% TINT (64*I%)MOD256
 2620 ENDPROC
