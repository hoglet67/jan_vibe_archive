   10 REM > KALEJ
   20 
   30 MODE28:OFF
   40 DIM SPR &100000,MAT 24
   50 SPR!0=&FFFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 
   70 ONERRORGOTO590
   80 SYS"OS_SpriteOp",256+15,SPR,"KS",0,640,480,28
   90 SYS"OS_SpriteOp",256+15,SPR,"BG",0,640,480,28
  100 SYS"OS_SpriteOp",256+60,SPR,"BG"
  110 FORN%=1TO1000:GCOLRND(64)-1TINT(RND(4)-1)*64
  120 X%=RND(1280):Y%=RND(960):R%=RND(48)+16
  130 CASE RND(2) OF
  140 WHEN 1: CIRCLE FILL X%,Y%,R%
  150 WHEN 2:
  160 R%=R%*2
  170 T=RADRND(360):MOVE X%+R%*SINT,Y%+R%*COST
  180 T=RADRND(360):MOVE X%+R%*SINT,Y%+R%*COST
  190 T=RADRND(360):PLOT85,X%+R%*SINT,Y%+R%*COST
  200 ENDCASE
  210 NEXT
  220 
  230 X%=0:Y%=0:DX%=8:DY%=8
  240 REPEAT
  250 H%=X%+DX%:IF H%>800 OR H%<=0 DX%=(RND(7)*2+2)*SGN(-DX%)
  260 H%=Y%+DY%:IF H%>545 OR H%<=0 DY%=(RND(7)*2+2)*SGN(-DY%)
  270 X%+=DX%:Y%+=DY%
  280 SYS"OS_SpriteOp",256+60,SPR,"BG"
  290 SYS"OS_SpriteOp",256+16,SPR,"TG",0,X%,Y%,X%+480,Y%+415
  300 SYS"OS_SpriteOp",256+29,SPR,"TG"
  310 SYS"OS_SpriteOp",256+61,SPR,"TG"
  320 GCOL0TINT0:RECTANGLE FILL 0,0,480,415
  330 GCOL63TINT192:MOVE0,0:MOVE240,415:PLOT85,480,0
  340 SYS"OS_SpriteOp",256+61,SPR,0,0
  350 SYS"OS_SpriteOp",256+60,SPR,"KS"
  360 SYS"OS_SpriteOp",256+34,SPR,"TG",638,480,8
  370 PROCROT(640,480,120,"TG")
  380 PROCROT(640,482,240,"TG")
  390 SYS"OS_SpriteOp",256+33,SPR,"TG"
  400 SYS"OS_SpriteOp",256+34,SPR,"TG",638,65,8
  410 PROCROT(280,690,240,"TG")
  420 PROCROT(1000,686,120,"TG")
  430 SYS"OS_SpriteOp",256+60,SPR,0,0
  440 SYS"OS_SpriteOp",256+34,SPR,"KS",0,0,0
  450 UNTIL0
  460 
  470 DEFPROCROT(X%,Y%,A%,SP$)
  480 LOCAL T,SI%,CO%
  490 T=RAD(A%):SI%=65536*SINT::CO%=65536*COST
  500 MAT!00=CO%
  510 MAT!04=SI%
  520 MAT!08=-SI%
  530 MAT!12=CO%
  540 MAT!16=256*X%
  550 MAT!20=256*Y%
  560 SYS"OS_SpriteOp",256+56,SPR,SP$,0,,8,MAT
  570 ENDPROC
  580 
  590 SYS"OS_SpriteOp",256+60,SPR,0,0
  600 SYS"OS_SpriteOp",256+61,SPR,0,0
  610 REPORT:PRINT" at line "STR$ERL
