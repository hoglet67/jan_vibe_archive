   10 REM > FACETS2
   20 
   30 MODE15:OFF
   40 K%=1000:R%=80:RX%=R%:RY%=R%/2:Z%=2*R%:D%=Z%/8
   50 DIM X%(K%),Y%(K%),C%(K%),SPR &100000
   60 SPR!0=&FFFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 
   80 B%=0:REPEAT:T%=0:B%+=1
   90 REPEAT:X%(B%)=RND(1280):Y%(B%)=RND(1024):C%(B%)=RND(26):T%+=1
  100 UNTIL POINT(X%(B%),Y%(B%))=0 OR T%>=5000
  110 GCOL C%(B%):CIRCLE FILL X%(B%),Y%(B%),R%
  120 UNTILT%>=5000
  130 X%()-=R%:Y%()-=R%
  140 
  150 ONERRORGOTO410
  160 FORN%=1TO26:N$=STR$(N%)
  170 SYS"OS_SpriteOp",256+15,SPR,N$,0,RX%,RY%,15
  180 SYS"OS_SpriteOp",256+29,SPR,N$
  190 SYS"OS_SpriteOp",256+60,SPR,N$
  200 FORI%=0TO7:PROCF(N%,I%):RECTANGLE FILL 0,I%*D%,Z%,D%
  210 NEXT,
  220 
  230 FORI%=R%TO0STEP-2
  240 FORJ%=1TO26:N$=STR$(J%)
  250 SYS"OS_SpriteOp",256+61,SPR,N$
  260 GCOL0TINT0:RECTANGLEFILL0,0,Z%:GCOL63TINT192:CIRCLEFILLR%,R%,I%
  270 NEXT
  280 SYS"OS_SpriteOp",256+61,SPR,0
  290 FORN%=1TOB%:N$=STR$(C%(N%))
  300 SYS"OS_SpriteOp",256+34,SPR,N$,X%(N%),Y%(N%),8
  310 NEXT,
  320 END
  330 
  340 DEFPROCF(F%,I%)
  350 LOCAL R%,G%,B%
  360 B%=F%DIV9:F%=F%MOD9:G%=F%DIV3:F%=F%MOD3:R%=F%
  370 F%=R%+4*G%+16*B%:IF I%>=4 F%=F%+&15
  380 GCOL F% TINT (64*I%)MOD256
  390 ENDPROC
  400 
  410 SYS"OS_SpriteOp",256+60,SPR,0,0
  420 SYS"OS_SpriteOp",256+61,SPR,0,0
  430 REPORT:PRINT" at line "STR$ERL
