   10 REM > STEREOSGN5
   20 
   30 REM *** Jan Vibe januar 95 ***
   40 
   50 MODE159:MODE31
   60 DIM A$(10),X1%(10),Y1%(10),X2%(10),Y2%(10)
   70 DIM PX%(20000),PY%(20000):P%=0
   80 K%=398:S1%=1:S2%=2
   90 
  100 INPUT'"Number of lines: "PL%:Q$=""
  110 FOR N%=1 TO PL%:PRINT'"Line no. "STR$(N%);:INPUT" "A$(N%)
  120 Q$=Q$+A$(N%):NEXT:CLS
  130 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT:COLOUR 0,0,0,128
  140 *POINTER
  150 Z%=1000:ZX=1:D=1:FONT$="Trinity.Medium"
  160 SYS"Font_FindFont",,FONT$,Z%,Z%,0,0 TO HAN%
  170 SYS"Font_SetFontColours",0,0,15,0
  180 SYS"Font_StringBBox",,Q$ TO ,X1%,Y1%,X2%,Y2%
  190 FH%=(Y2%-Y1%)/K%:FP%=1200-FH%
  200 XA%=350:YA%=FP%
  210 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  220 
  230 ONERRORGOTO870
  240 REPEAT
  250 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  260 PROCBUTTON(32,1100,"+ Y POS. -")
  270 PROCBUTTON(32,1036,"+ DIST.  -")
  280 PROCBUTTON(32,972,"+ SIZE   -")
  290 PROCBUTTON(32,908,"+ X SIZE -")
  300 PROCBUTTON(32,844,"START")
  310 MOUSE X%,Y%,B%
  320 F%=0
  330 IF B%<>0 THEN
  340 IF X%>32ANDX%<224 THEN
  350 F%=1:IF X%>=128 F%=-1
  360 IF Y%>1100ANDY%<1132 FP%=FP%+F%*10
  370 IF Y%>1036ANDY%<1068 D=D+F%*0.05
  380 IF Y%>972ANDY%<1004 Z%=Z%+F%*50
  390 IF Y%>908ANDY%<940 ZX=ZX+F%*0.02
  400 ENDIF
  410 ENDIF
  420 XA%=350:YA%=FP%
  430 SYS"Font_FindFont",,FONT$,Z%*ZX,Z%,0,0 TO HAN%
  440 SYS"Font_StringBBox",,Q$ TO ,X1%,Y1%,X2%,Y2%
  450 FH%=(Y2%-Y1%)/K%
  460 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  470 SYS"Font_LoseFont",HAN%
  480 UNTIL Y%>844ANDY%<876AND B%<>0
  490 SYS 6,112,S1%:ONERROROFF
  500 GCOL0:RECTANGLE FILL 0,0,256,1200:GCOL15:COLOUR 0,0,0,0:*POINTER 0
  510 
  520 FORY%=0TO1199STEP2:LINE 120,Y%,136,Y%:FORX%=XA%TO1599STEP2
  530 IF POINT(X%,Y%)<>0 THEN
  540 F%=0
  550 IF POINT(X%+2,Y%)=0 F%=1
  560 IF POINT(X%-2,Y%)=0 F%=1
  570 IF POINT(X%,Y%+2)=0 F%=1
  580 IF POINT(X%,Y%-2)=0 F%=1
  590 IFF%=1 P%+=1:PX%(P%)=X%:PY%(P%)=Y%
  600 ENDIF
  610 NEXT,
  620 FORC%=1TO14:R%=6*(15-C%):GCOLC%
  630 FORN%=1TOP%:CIRCLE FILL PX%(N%),PY%(N%),R%
  640 NEXT,
  650 SYS"Font_FindFont",,FONT$,Z%*ZX,Z%,0,0 TO HAN%
  660 XA%=350:YA%=FP%
  670 FORN%=1TOPL%:YA%=YA%-FH%*D:SYS"Font_Paint",,A$(N%),16,XA%,YA%:NEXT
  680 SYS"Font_LoseFont",HAN%
  690 
  700 VDU24,0;0;255;1199;:CLG
  710 FORN%=1TO8000:X%=RND(256):Y%=RND(1200):R%=RND(6)+3:GCOLRND(15)
  720 FORM%=-256TO256STEP256:CIRCLEFILLX%+M%,Y%,R%:NEXT,
  730 
  740 VDU26:FORY%=0TO1199STEP2:FORX%=256TO1600STEP2
  750 F%=POINT(X%,Y%)*2:C%=POINT(X%+F%-256,Y%):GCOLC%:POINTX%,Y%
  760 NEXT,
  770 *FX21,0
  780 Q$=GET$:IF Q$="S" THEN OSCLI"SCREENSAVE STSIGN"
  790 END
  800 
  810 DEFPROCBUTTON(X1%,Y1%,TXT$)
  820 LOCAL L%:L%=LEN(TXT$)*16
  830 GCOL 15:RECTANGLE FILL X1%,Y1%,192,32:GCOL 0
  840 VDU5:MOVEX1%+(192-L%)/2,Y1%+(32)/2+8:PRINTTXT$:VDU4:GCOL7
  850 ENDPROC
  860 
  870 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
