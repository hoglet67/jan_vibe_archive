   10 REM > STEREOSIGN
   20 
   30 MODE31
   40 DIM TXT$(10),PX%(20000),PY%(20000):P%=0
   50 INPUT"Number of lines="L%:PRINT
   60 FORN%=1TOL%:PRINT"Line no "+STR$(N%);:INPUT" "TXT$(N%):NEXT
   70 CLS:OFF
   80 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT:COLOUR 0,0,0,64
   90 
  100 FONT$="Trinity.Medium"
  110 SYS"Font_FindFont",,FONT$,12,12,0,0 TO HAN%
  120 MAX%=0:PT%=0:FORN%=1TOL%
  130 SYS"Font_StringBBox",,STR$(N%) TO ,X1%,Y1%,X2%,Y2%
  140 IF X2%-X1%>MAX% MAX%=X2%-X1%:PT%=N%
  150 NEXT
  160 A$=TXT$(PT%):Z%=8192:ST%=Z%/2
  170 SYS"Font_SetFontColours",0,0,15,0
  180 REPEAT
  190 SYS"Font_FindFont",,FONT$,Z%,Z%,0,0 TO HAN%
  200 SYS"Font_StringBBox",,A$ TO ,X1%,Y1%,X2%,Y2%
  210 X1%=X1%/398:X2%=X2%/398:Y1%=Y1%/398:Y2%=Y2%/398
  220 IF 400+X1%+X2%>1500 THEN Z%-=ST% ELSE Z%+=ST%
  230 ST%=ST%/2:UNTILST%=1
  240 H%=1000/L%:ZY%=H%*7.5:IF ZY%>5000 ZY%=5000:H%=750
  250 SYS"Font_FindFont",,FONT$,Z%,ZY%,0,0 TO HAN%
  260 FOR N%=1 TO L%
  270 SYS"Font_Paint",,TXT$(N%),16,400,1150-H%*N%
  280 NEXT
  290 
  300 FORY%=0TO1199STEP2:FORX%=399TO1599STEP2
  310 IF POINT(X%,Y%)<>0 THEN
  320 F%=0:FORM%=-2TO2STEP2:FORN%=-2TO2STEP2
  330 IF POINT(X%+N%,Y%+M%)=0 F%=1:N%=2:M%=2
  340 NEXT,:IFF%=1 P%+=1:PX%(P%)=X%:PY%(P%)=Y%
  350 ENDIF
  360 NEXT,
  370 FORC%=1TO14:R%=6*(15-C%):GCOLC%
  380 FORN%=1TOP%:CIRCLE FILL PX%(N%),PY%(N%),R%
  390 NEXT,
  400 FOR N%=1 TO L%
  410 SYS"Font_Paint",,TXT$(N%),16,400,1150-H%*N%
  420 NEXT
  430 SYS"Font_LoseFont",HAN%
  440 
  450 VDU24,0;0;255;1199;
  460 FORN%=1TO8000:X%=RND(256):Y%=RND(1200):R%=RND(6)+3:GCOLRND(15)
  470 FORM%=-256TO256STEP256:CIRCLEFILLX%+M%,Y%,R%:NEXT,
  480 
  490 VDU26
  500 FORY%=0TO1199STEP2
  510 FORX%=256TO1600STEP2
  520 F%=POINT(X%,Y%)*2
  530 C%=POINT(X%+F%-256,Y%)
  540 GCOLC%:POINTX%,Y%
  550 NEXT,
  560 *SCREENSAVE STSIGN
