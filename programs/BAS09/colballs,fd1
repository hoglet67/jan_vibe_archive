   10 REM > COLBALLS
   20 
   30 MODE 107:OFF
   40 SYS "OS_SWINumberFromString",,"ColourTrans_SetGCOL" TO set_gcol%
   50 DIM SPR &20000
   60 SPR!0=&1FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   70 SYS"OS_SpriteOp",256+15,SPR,"BALL",0,128,128,107
   80 SYS"OS_SpriteOp",256+29,SPR,"BALL"
   90 
  100 ONERRORGOTO300
  110 REPEAT
  120 SYS"OS_SpriteOp",256+60,SPR,"BALL",0
  130 CLG:RA%=RND(97)+31
  140 R1%=RND(127):G1%=RND(127):B1%=RND(127)
  150 R2%=R1%+127:G2%=G1%+127:B2%=B1%+127
  160 FORN%=32TO0STEP-1:N1%=32-N%
  170 R%=(N%*R1%+N1%*R2%)/32:G%=(N%*G1%+N1%*G2%)/32:B%=(N%*B1%+N1%*B2%)/32
  180 SYS set_gcol%,(R%<<8)+(G%<<16)+(B%<<24),,,&100,0
  190 CIRCLE FILL RA%,RA%,N%*RA%/32:NEXT
  200 SYS"OS_SpriteOp",256+61,SPR,"BALL",0
  210 GCOL0TINT0:RECTANGLE FILL 0,0,256
  220 GCOL63TINT240
  230 D=SQR(2*RA%^2)-RA%:PD=SQR(D^2/2):R1=RA%*.6:P=SQR(R1^2/2)+PD+4
  240 CIRCLE FILL P,P,R1
  250 SYS"OS_SpriteOp",256+61,SPR,0,0:OFF
  260 X%=RND(1152-2*R1)+R1:Y%=RND(848-2*R1)+R1
  270 SYS "OS_SpriteOp",256+34,SPR,"BALL",X%-P,Y%-P,8
  280 UNTIL0
  290 
  300 SYS"OS_SpriteOp",256+60,SPR,0,0
  310 SYS"OS_SpriteOp",256+61,SPR,0,0
  320 REPORT:PRINT" at line "STR$ERL
