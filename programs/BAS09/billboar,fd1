   10 REM > BILLBOARD
   20 
   30 MODE13:OFF
   40 DIM SPR &20000
   50 SPR!0=&1FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 INPUTLINE"Tekst="A$:B$="           ":CLS
   70 R%=RND(360):G%=RND(360):B%=RND(360):H%=RND(360):A%=RND(360)
   80 
   90 SYS"OS_SpriteOp",256+15,SPR,"LINE",0,384,63,13
  100 SYS"OS_SpriteOp",256+29,SPR,"LINE"
  110 
  120 ONERRORGOTO 400
  130 REPEAT
  140 FORN%=1TOLENA$:B$=RIGHT$(B$,12)+MID$(A$,N%,1):PROCTXT(B$):NEXT
  150 FORN%=1TO5:B$=RIGHT$(B$,12)+" ":PROCTXT(B$):NEXT
  160 UNTIL0
  170 
  180 DEFPROCTXT(B$)
  190 LOCAL X%,Y%,R1,G1,B1
  200 FORX%=0TO-120STEP-8
  210 SYS"OS_SpriteOp",256+60,SPR,"LINE",0
  220 R%=(RND(8)+8+R%)MOD360:G%=(RND(8)+8+G%)MOD360:B%=(RND(8)+8+B%)MOD360
  230 R1=127*(SINRADR%+1):G1=127*(SINRADG%+1):B1=127*(SINRADB%+1)
  240 PROCRGB(R1,G1,B1):RECTANGLE FILL 0,0,1536,256
  250 H%=(H%+2)MOD360:A%=(A%+10)MOD360:Y%=256*(SINRADH%+1)
  260 SYS"OS_SpriteOp",256+61,SPR,"LINE",0
  270 VDU23,17,7,6,32;64|5
  280 GCOL0TINT0:CLG:GCOL63TINT240:MOVEX%,256:PRINT B$;
  290 SYS"OS_SpriteOp",256+61,0,0
  300 MOVE 0,0:MOVE1280,1024:PLOT&BD,4,4
  310 SYS"OS_SpriteOp",256+34,SPR,"LINE",128*SINRADA%-128,Y%+128*COSRADA%,8
  320 NEXT
  330 ENDPROC
  340 
  350 DEFPROCRGB(R%,G%,B%)
  360 LOCAL C%:C%=(R%<<8)+(G%<<16)+(B%<<24)
  370 SYS "ColourTrans_SetGCOL",C%,,,&100,0
  380 ENDPROC
  390 
  400 SYS"OS_SpriteOp",256+61,0,0
  410 SYS"OS_SpriteOp",256+60,0,0
  420 REPORT:PRINT" at line "STR$(ERL)
