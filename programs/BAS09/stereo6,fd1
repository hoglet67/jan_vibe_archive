   10 REM > STEREO6
   20 
   30 MODE103:OFF
   40 DIM SPR &30000
   50 SPR!0=&2FFFF:SPR!4=0:SPR!8=16:SPR!12=16
   60 SYS"OS_SpriteOp",256+15,SPR,"PAT",0,128,600,103
   70 SYS"OS_SpriteOp",256+60,SPR,"PAT"
   80 GCOL128TINT255:CLG
   90 FORY%=0TO1200STEP8:FORX%=1TO12:GCOLRND(63):PX%=RND(256):R%=RND(10)+4
  100 FORN%=-256TO256STEP256:CIRCLE FILL PX%+N%,Y%,R%:NEXT,,
  110 SYS"OS_SpriteOp",256+60,SPR,0:OFF
  120 
  130 FORN%=63TO0STEP-1:GCOL 63-N%:CIRCLEFILL928,600,9.5*N%:NEXT
  140 
  150 FORY%=0TO1199STEP2
  160 SYS"OS_SpriteOp",256+16,SPR,"TMP",0,0,Y%,1799,Y%
  170 FORX%=800TO1600STEP2
  180 F%=2*POINT(X%,Y%)
  190 IF X%+F%<1056 THEN
  200 SYS"OS_SpriteOp",256+41,SPR,"PAT",(X%+F%-800)/2,Y%/2 TO ,,,,,C%:GCOLC%
  210 ELSE
  220 GCOL POINT(X%+F%-256,Y%)
  230 ENDIF
  240 POINTX%,Y%
  250 NEXT
  260 SYS"OS_SpriteOp",256+16,SPR,"PAT1",0,1340,Y%,1599,Y%
  270 SYS"OS_SpriteOp",256+47,SPR,"PAT1"
  280 SYS"OS_SpriteOp",256+34,SPR,"TMP",0,Y%
  290 FORX%=0TO1600STEP2
  300 F%=2*POINT(X%,Y%)
  310 IF X%+F%<256 THEN
  320 SYS"OS_SpriteOp",256+41,SPR,"PAT1",(X%+F%)/2,0 TO ,,,,,C%:GCOLC%
  330 ELSE
  340 GCOL POINT(X%+F%-256,Y%)
  350 ENDIF
  360 POINTX%,Y%
  370 NEXT,
