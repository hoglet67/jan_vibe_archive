   10 REM > TREE
   20 
   30 MODE15:OFF
   40 PROCF(16,0):RECTANGLE FILL 0,0,1280,300
   50 PROCF(21,7):RECTANGLE FILL 0,300,1280,724
   60 FORN%=500TO200STEP-1:MA%=RND(N%/4)+8:MI%=RND(MA%/2)+4:PROCF(26,RND(4)+3)
   70 ELLIPSE FILL RND(1280),RND(400)+500,MA%,MI%:NEXT
   80 PROCTREE(640,0,256,0,48):END
   90 
  100 DEFPROCTREE(X%,Y%,Z%,A%,B)
  110 LOCALX1%,Y1%,N%,R%,F%,Z
  120 R%=Z%:IF Z%<16 THEN R%=1.5*R%
  130 X1%=X%+R%*SINRADA%:Y1%=Y%+R%*COSRADA%:PROCL(X%,Y%,X1%,Y1%,B)
  140 IFZ%>=16 THEN
  150 PROCTREE(X1%,Y1%,Z%/(1.2+RND(1)*.3),A%-30-RND(20),B/1.4)
  160 PROCTREE(X1%,Y1%,Z%/(1.2+RND(1)*.3),A%+30+RND(20),B/1.4)
  170 ELSE
  180 F%=RND(27)-1:Z=1+RND(1)/2:FORN%=7TO0STEP-1:PROCF(F%,7-N%)
  190 CIRCLE FILL X1%,Y1%,Z*N%:NEXT
  200 ENDIF
  210 ENDPROC
  220 
  230 DEFPROCL(X1%,Y1%,X2%,Y2%,B%)
  240 LOCAL D%,N%,M%,F%,X,Y,Z
  250 IF B%<=2:LINE X1%,Y1%,X2%,Y2%:ENDPROC
  260 D%=SQR((X2%-X1%)^2+(Y2%-Y1%)^2)
  270 DX=(X2%-X1%)/D%:DY=(Y2%-Y1%)/D%:F%=RND(27)-1
  280 FORM%=0TO7:PROCF(F%,M%):Z=B%-(M%+1)*B%/8
  290 IF Z<=2 THEN
  300 LINE X1%,Y1%,X2%,Y2%
  310 ELSE
  320 FOR N%=0TOD%STEP Z/4+2
  330 X=(N%*X2%+(D%-N%)*X1%)/D%:Y=(N%*Y2%+(D%-N%)*Y1%)/D%:CIRCLE FILL X,Y,Z
  340 NEXT
  350 ENDIF
  360 NEXT
  370 ENDPROC
  380 
  390 DEFPROCF(F%,I%)
  400 LOCAL R%,G%,B%
  410 B%=F%DIV9:F%=F%-9*B%:G%=F%DIV3:F%=F%-3*G%:R%=F%:F%=R%+4*G%+16*B%
  420 IF I%>=4 F%=F%+&15
  430 GCOL F% TINT (64*I%)MOD256
  440 ENDPROC
