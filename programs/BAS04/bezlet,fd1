   10 REM > BEZLET
   20 
   30 MODE12:@%=0
   40 DIM X(30),Y(30),LX%(30,30),LY%(30,30),LA%(30)
   50 
   60 INPUT"Filnavn="FILE$
   70 INPUT"Ny fil J/N "NF$
   80 IF NF$="J" THEN
   90   OSCLI"SPOOL "+FILE$
  100   PRINT"AUTO 10000"
  110   PRINT"REM Bezier data til bogstaver."
  120   PRINT
  130   *SPOOL
  140 ENDIF
  150 OFF
  160 
  170 *POINTER
  180 LP%=0:PROCGRID
  190 
  200 REPEAT
  210 C%=-1:LP%+=1
  220 REPEAT
  230 MOUSE MX%,MY%,MB%
  240 CASE MB% OF
  250 WHEN 1:
  260   C%+=1:GCOL1:MX%=20*((MX%+10)DIV20):MY%=20*((MY%+10)DIV20)
  270   LX%(C%,LP%)=MX%:LY%(C%,LP%)=MY%:POINT MX%,MY%:CIRCLE MX%,MY%,8
  280   PROCSLOW(50)
  290 WHEN 2:
  300   IF C%>=0 THEN
  310     GCOL0
  320     POINT LX%(C%,LP%),LY%(C%,LP%)
  330     CIRCLE LX%(C%,LP%),LY%(C%,LP%),8:C%-=1
  340     PROCSLOW(50)
  350   ELSE
  360     LP%-=1:PROCGRID:LP%+=1:C%=-1
  370   ENDIF
  380 ENDCASE
  390 IF INKEY$(0)=" " PROCLET
  400 UNTILMB%=4
  410 LA%(LP%)=C%
  420 GCOL7
  430 IF C%>=1
  440   FORN%=0TOC%:X(N%)=LX%(N%,LP%):Y(N%)=LY%(N%,LP%):NEXT
  450   PROCBEZIER(X(),Y(),C%,200):PROCSLOW(50)
  460 ENDIF
  470 UNTIL0
  480 
  490 DEFPROCBEZIER(X(),Y(),D%,P%)
  500 GCOL7
  510 IF D%=0 ENDPROC
  520 IF D%=1 THEN LINE X(0),Y(0),X(1),Y(1):ENDPROC
  530 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  540 DIM F(D%)
  550 FOR I%=0TOD%:F(I%)=1
  560 FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  570 FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  580 FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  590 NEXT
  600 MOVE X(0),Y(0)
  610 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  620 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  630 NEXT:DRAW PX,PY:NEXT
  640 ENDPROC
  650 
  660 DEFPROCLET
  670 LOCAL I%,N%,L$
  680 INPUT"Bogstav="L$
  690 OSCLI"SPOOLON "+FILE$
  700 PRINT"REM"L$
  710 FORI%=1TOLP%-1:PRINT"DATA ";LA%(I%)",";
  720 FORN%=0TOLA%(I%)
  730 PRINTLX%(N%,I%)/20","LY%(N%,I%)/20;
  740 IF N%<>LA%(I%) PRINT","; ELSE PRINT
  750 NEXT,
  760 PRINT"DATA 0"
  770 *SPOOLON
  780 LP%=1
  790 ENDPROC
  800 
  810 DEFPROCGRID
  820 LOCAL X%,Y%,I%,N%,H%,B%
  830 H%=640:B%=320
  840 H%=20*(H%DIV20):B%=20*(B%DIV20)
  850 ORIGIN 640-B%/2,512-H%/2
  860 MOUSE RECTANGLE 0,0,B%,H%
  870 CLS
  880 GCOL6:FORY%=0TOH%STEP20:FORX%=0TOB%STEP20:POINT X%,Y%:NEXT,
  890 GCOL4:FORY%=0TOH%STEP80:FORX%=0TOB%STEP80:CIRCLE X%,Y%,8:NEXT,
  900 IF LP%>=1 LP%-=1
  910 IF LP%>=1 THEN
  920   FORI%=1TOLP%
  930   FORN%=0TOLA%(I%):X(N%)=LX%(N%,I%):Y(N%)=LY%(N%,I%):NEXT
  940   PROCBEZIER(X(),Y(),LA%(I%),200):NEXT
  950 ENDIF
  960 PROCSLOW(50)
  970 ENDPROC
  980 
  990 DEFPROCSLOW(W%)
 1000 LOCAL T%
 1010 T%=TIME+W%:REPEAT UNTILTIME>=T%
 1020 ENDPROC
