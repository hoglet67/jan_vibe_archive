   10 REM > BEZKAL
   20 
   30 MODE140:MODE12:OFF:ORIGIN 640,512
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:P%=5:NP%=20
   60 DIM X(P%),Y(P%),DX(P%),DY(P%),XP%(NP%),YP%(NP%)
   70 FORN%=0TOP%
   80 X(N%)=RND(511):Y(N%)=RND(511)
   90 DX(N%)=(RND(13)+3)*SGN(RND(1)-.5)
  100 DY(N%)=(RND(13)+3)*SGN(RND(1)-.5)
  110 NEXT
  120 
  130 ONERRORGOTO480
  140 REPEAT
  150 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  160 
  170 FORN%=0TOP%
  180 A%=X(N%)+DX(N%):IF ABS(A%)>511 DX(N%)=(RND(13)+3)*SGN(-DX(N%))
  190 A%=Y(N%)+DY(N%):IF ABS(A%)>511 DY(N%)=(RND(13)+3)*SGN(-DY(N%))
  200 X(N%)+=DX(N%):Y(N%)+=DY(N%):NEXT
  210 PROCBEZIER(X(),Y(),P%,NP%)
  220 MOVE XP%(0),YP%(0):FORN%=1TONP%:DRAW XP%(N%),YP%(N%):NEXT
  230 MOVE -XP%(0),YP%(0):FORN%=1TONP%:DRAW -XP%(N%),YP%(N%):NEXT
  240 MOVE -XP%(0),-YP%(0):FORN%=1TONP%:DRAW -XP%(N%),-YP%(N%):NEXT
  250 MOVE XP%(0),-YP%(0):FORN%=1TONP%:DRAW XP%(N%),-YP%(N%):NEXT
  260 MOVE YP%(0),XP%(0):FORN%=1TONP%:DRAW YP%(N%),XP%(N%):NEXT
  270 MOVE -YP%(0),XP%(0):FORN%=1TONP%:DRAW -YP%(N%),XP%(N%):NEXT
  280 MOVE -YP%(0),-XP%(0):FORN%=1TONP%:DRAW -YP%(N%),-XP%(N%):NEXT
  290 MOVE YP%(0),-XP%(0):FORN%=1TONP%:DRAW YP%(N%),-XP%(N%):NEXT
  300 
  310 UNTIL0
  320 
  330 DEFPROCBEZIER(X(),Y(),D%,P%)
  340 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  350 DIM F(D%)
  360 FOR I%=0TOD%:F(I%)=1
  370 FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  380 FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  390 FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  400 NEXT
  410 MOVE X(0),Y(0)
  420 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  430 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%)
  440 PX+=X(I%)*K:PY+=Y(I%)*K
  450 NEXT:XP%(J%)=PX:YP%(J%)=PY:NEXT
  460 ENDPROC
  470 
  480 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
