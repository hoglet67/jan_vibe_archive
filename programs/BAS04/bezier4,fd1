   10 REM > BEZIER4
   20 
   30 MODE12
   40 DIM X(49),Y(50):@%=0:OFF
   50 
   60 *POINTER
   70 PROCGRID
   80 REPEAT
   90 C%=-1
  100 REPEAT
  110 MOUSE MX%,MY%,MB%
  120 CASE MB% OF
  130 WHEN 1:
  140   C%+=1:GCOL1:MX%=32*((MX%+16)DIV32):MY%=32*((MY%+16)DIV32)
  150   X(C%)=MX%:Y(C%)=MY%:POINT MX%,MY%:CIRCLE MX%,MY%,8
  160   T%=TIME+50:REPEAT UNTILTIME>=T%
  170 WHEN 2:
  180   IF C%>=0 THEN
  190     GCOL0:POINT X(C%),Y(C%):CIRCLE X(C%),Y(C%),8:C%-=1
  200     T%=TIME+50:REPEAT UNTILTIME>=T%
  210   ELSE
  220     PROCGRID:C%=-1
  230   ENDIF
  240 ENDCASE
  250 UNTILMB%=4
  260 GCOL7
  270 IF C%>=1PROCBEZIER(X(),Y(),C%,200)
  280 UNTIL0
  290 
  300 DEFPROCBEZIER(X(),Y(),D%,P%)
  310 IF D%=1 THEN LINE X(0),Y(0),X(1),Y(1):ENDPROC
  320 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  330 DIM F(D%)
  340 FOR I%=0TOD%:F(I%)=1
  350 FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  360 FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  370 FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  380 NEXT
  390 MOVE X(0),Y(0)
  400 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  410 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  420 NEXT:DRAW PX,PY:NEXT
  430 ENDPROC
  440 
  450 DEFPROCGRID
  460 LOCAL X%,Y%
  470 CLS
  480 GCOL6:FORY%=0TO1023STEP32:FORX%=0TO1279STEP32:POINT X%,Y%:NEXT,
  490 GCOL4:FORY%=0TO1023STEP128:FORX%=0TO1279STEP128:CIRCLE X%,Y%,8:NEXT,
  500 ENDPROC
