   10 REM > TRIANGLES
   20 
   30 MODE137:MODE9:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:Z%=15:K%=64:LX%=1279-K%:LY%=1023-K%
   60 DIM X%(Z%),Y%(Z%),A%(3,Z%),DX%(Z%),DY%(Z%),DA%(3,Z%)
   70 COLOUR0,64,64,64:VDU19,0,24,64,64,64
   80 FORN%=1TOZ%
   90 REPEAT C1%=RND(255):C2%=RND(255):C3%=RND(255):UNTIL C1%+C2%+C3%>255
  100 COLOUR N%,C1%,C2%,C3%:X%(N%)=640:Y%(N%)=512
  110 FORM%=1TO3:A%(M%,N%)=RND(360):DA%(M%,N%)=(RND(7)+3)*SGN(RND(1)-.5):NEXT
  120 DX%(N%)=(RND(13)+3)*SGN(RND(1)-.5):DY%(N%)=(RND(13)+3)*SGN(RND(1)-.5)
  130 NEXT
  140 
  150 ONERRORGOTO290
  160 REPEAT
  170 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  180 FORN%=1TOZ%:GCOLN%
  190 FORM%=1TO3:A%(M%,N%)=(A%(M%,N%)+DA%(M%,N%))MOD360:NEXT
  200 H%=X%(N%)+DX%(N%):IF H%<K%ORH%>LX% DX%(N%)=(RND(17)+3)*SGN(-DX%(N%))
  210 H%=Y%(N%)+DY%(N%):IF H%<K%ORH%>LY% DY%(N%)=(RND(17)+3)*SGN(-DY%(N%))
  220 X%(N%)+=DX%(N%):Y%(N%)+=DY%(N%)
  230 T=RADA%(1,N%):MOVEX%(N%)+K%*SINT,Y%(N%)+K%*COST
  240 T=RADA%(2,N%):MOVEX%(N%)+K%*SINT,Y%(N%)+K%*COST
  250 T=RADA%(3,N%):PLOT85,X%(N%)+K%*SINT,Y%(N%)+K%*COST
  260 NEXT
  270 UNTIL0
  280 
  290 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
