   10 REM > READER5
   20 
   30 MODE137:MODE9:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:P1%=0:P2%=1:DC%=0:S$="                    "
   60 VDU5,23,17,7,6,8;16|:COLOUR0,0,0,80
   70 DIMX(4),Y(4),DY(4),DX(4),F(4),XP%(16,1),YP%(16,1),F%(16)
   80 FORN%=0TO4
   90   X(N%)=RND(1215)+32:Y(N%)=RND(959)+32
  100   DY(N%)=(RND(16)+16)*SGN(RND(1)-.5)
  110   DX(N%)=(RND(16)+16)*SGN(RND(1)-.5)
  120   READ F(N%)
  130 NEXT
  140 X(0)=RND(448)+32:X(4)=RND(448)+800
  150 PROCBEZIER(X(),Y(),4,16,P1%):C%=0:P1%=1-P1%:P2%=1-P2%
  160 GX0=X(0):GY0=Y(0):GX4=X(4):GY4=Y(4)
  170 
  180 FIL$="TEST"
  190 CH%=OPENIN FIL$:GT%=0
  200 
  210 ONERRORGOTO770
  220 REPEAT
  230 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  240 C%=(C%+1)MOD5:C1%=5-C%
  250 FORN%=0TO15
  260   LX%=(C%*XP%(N%,P2%)+C1%*XP%(N%+1,P1%))/5-16
  270   LY%=(C%*YP%(N%,P2%)+C1%*YP%(N%+1,P1%))/5+32
  280   MOVE LX%,LY%:GCOLF%(N%):PRINTMID$(S$,N%+1,1);
  290 NEXT:WAIT
  300 GCOL1:CIRCLE FILL (C%*X(0)+C1%*GX0)/5,(C%*Y(0)+C1%*GY0)/5,32
  310 GCOL2:CIRCLE FILL (C%*X(4)+C1%*GX4)/5,(C%*Y(4)+C1%*GY4)/5,32
  320 IF C%=4 THEN
  330   GX0=X(0):GY0=Y(0):GX4=X(4):GY4=Y(4)
  340   FORN%=0TO4
  350   H%=Y(N%)+DY(N%):IF H%>988 OR H%<32 DY(N%)=-SGN(DY(N%))*(RND(32)+32)
  360   H%=X(N%)+DX(N%)
  370   CASE N% OF
  380   WHEN 0:
  390     IF H%>512 OR H%<32 DX(N%)=-SGN(DX(N%))*(RND(16)+16)
  400   WHEN 1,2,3:
  410     IF H%>1280 OR H%<0 DX(N%)=-SGN(DX(N%))*(RND(16)+16)
  420   WHEN 4:
  430     IF H%>1248 OR H%<768 DX(N%)=-SGN(DX(N%))*(RND(16)+16)
  440   ENDCASE
  450   X(N%)+=DX(N%):Y(N%)+=DY(N%)
  460   NEXT
  470   PROCBEZIER(X(),Y(),4,16,P1%):P1%=1-P1%:P2%=1-P2%
  480   IF NOT EOF#CH% THEN
  490     REPEAT
  500       GT%=T%:T%=BGET#CH%:IF T%<32 THEN T%=32
  510     UNTIL NOT (GT%=32 AND T%=32)
  520     K%=INSTR("[\]{|}",CHR$(T%))
  530     IFK%<>0 T%=ASCMID$("ÆØÅæøå",K%,1)
  540   ELSE
  550     T%=32:DC%+=1
  560   ENDIF
  570   S$=RIGHT$(S$+CHR$(T%),17)
  580   FORN%=1TO16:F%(N%-1)=F%(N%):NEXT:F%(16)=RND(7)
  590 ENDIF
  600 UNTIL DC%=18
  610 
  620 SYS 6,112,S1%:CLOSE#CH%:VDU4:ON:END
  630 
  640 DEFPROCBEZIER(X(),Y(),D%,P%,S%)
  650 LOCAL I%,J%,U1,U2,K,PX,PY
  660 FORJ%=0TOP%
  670   U1=J%/P%:U2=1-U1:PX=0:PY=0
  680   FORI%=0TOD%
  690     K=F(I%)*U1^I%*U2^(D%-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  700   NEXT
  710   XP%(J%,S%)=PX:YP%(J%,S%)=PY
  720 NEXT
  730 ENDPROC
  740 
  750 DATA 1,4,6,4,1
  760 
  770 SYS 6,112,S1%:CLOSE#CH%:VDU4
  780 REPORT:PRINT" at line "STR$ERL:ON
  790 END
