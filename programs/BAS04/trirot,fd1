   10 REM > TRIROT
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 MODE128:MODE0:OFF:ORIGIN 640,512
   50 DIMSI(6),CO(6)
   60 S1%=1:S2%=2:A%=0:B%=0
   70 
   80 ONERRORGOTO310
   90 REPEAT
  100 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  110 A%=(A%+2)MOD360
  120 FORN%=0TO2
  130 SI(N%+1)=SINRAD(A%+120*N%):CO(N%+1)=COSRAD(A%+120*N%)
  140 SI(N%+4)=SINRAD(-A%-120*N%):CO(N%+4)=COSRAD(-A%-120*N%)
  150 NEXT:PROCT(1,256,0,0,0)
  160 UNTIL0
  170 
  180 DEFPROCT(A%,Z%,X%,Y%,C%)
  190 LOCAL X1%,Y1%,X2%,Y2%,X3%,Y3%
  200 X1%=Z%*SI(A%)+X%:Y1%=Z%*CO(A%)+Y%
  210 X2%=Z%*SI(A%+1)+X%:Y2%=Z%*CO(A%+1)+Y%
  220 X3%=Z%*SI(A%+2)+X%:Y3%=Z%*CO(A%+2)+Y%
  230 MOVEX3%,Y3%:DRAWX1%,Y1%:DRAWX2%,Y2%:DRAWX3%,Y3%
  240 IF C%<3 THEN
  250 PROCT(5-A%,Z%/2,X1%,Y1%,C%+1)
  260 PROCT(5-A%,Z%/2,X2%,Y2%,C%+1)
  270 PROCT(5-A%,Z%/2,X3%,Y3%,C%+1)
  280 ENDIF
  290 ENDPROC
  300 
  310 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
