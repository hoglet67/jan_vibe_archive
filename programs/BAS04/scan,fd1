   10 REM > SCAN
   20 
   30 MODE12:COLOUR0,0,0,255:VDU19,0,24,0,0,255
   40 PROCSCAN
   50 END
   60 
   70 DEFPROCSCAN
   80 LOCAL NAME%,L%,N%,A$
   90 DIM NAME% &10
  100 *MOUNT
  110 SYS "OS_GBPB",5,,NAME%
  120 L%=?NAME%:FORN%=1TOL%:A$=A$+CHR$(NAME%?N%):NEXT
  130 PRINTA$:PRINT
  140 PROCSC("$",0)
  150 COLOUR 7:PRINT
  160 ENDPROC
  170 
  180 DEFPROCSC(PATH$,I%)
  190 LOCAL SEQ%,RC%,NAME%,TYPE%,L%,N%,A$
  200 SEQ%=0:RC%=0:DIM NAME% &10
  210 OSCLI"DIR "+PATH$
  220 COLOUR 6:PRINTTAB(I%);
  230 REPEAT
  240   SYS "OS_GBPB",8,,NAME%,1,SEQ% TO ,,,RC%,SEQ%
  250   IF RC%=0 THEN
  260     L%=?NAME%:A$=""
  270     FORN%=1TOL%:A$=A$+CHR$(NAME%?N%):NEXT
  280     SYS "OS_File",17,A$ TO TYPE%
  290     IF TYPE%=1 THEN
  300       IF LENA$+POS>=78 PRINT:PRINTTAB(I%);
  310       PRINTA$" ";
  320     ENDIF
  330   ENDIF
  340 UNTIL RC%=1
  350 SEQ%=0:RC%=0
  360 REPEAT
  370   SYS "OS_GBPB",8,,NAME%,1,SEQ% TO ,,,RC%,SEQ%
  380   IF RC%=0 THEN
  390     L%=?NAME%:A$=""
  400     FORN%=1TOL%:A$=A$+CHR$(NAME%?N%):NEXT
  410     SYS "OS_File",17,A$ TO TYPE%
  420     IF TYPE%=2 THEN
  430       COLOUR 3:PRINTTAB(I%)A$
  440       PROCSC(PATH$+"."+A$,I%+3)
  450     ENDIF
  460   ENDIF
  470 UNTIL RC%=1
  480 *UP
  490 ENDPROC
