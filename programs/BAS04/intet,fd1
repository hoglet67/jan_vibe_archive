   10 REM > INTET
   20 
   30 MODE12
   40 
   50 FX=5:FY=5:DX=100:DY=64
   60 DIM X(50),Y(50)
   70 FORN%=1TO15:COLOUR N%,16*N%,16*N%,16*N%:NEXT
   80 
   90 REM Antal,X1,Y1,X2,Y2,...Xantal,Yantal
  100 
  110 DATA 2,10,120,60,168
  120 DATA 2,35,10,35,143
  130 
  140 DATA 2,70,93,70,70
  150 DATA 2,70,70,102,70
  160 DATA 4,70,100,75,100,102,110,102,90
  170 DATA 2,102,90,102,70
  180 
  190 DATA 2,52,139,140,150
  200 DATA 2,130,145,130,25
  210 DATA 2,130,25,111,23
  220 
  230 DATA 2,172,148,153,20
  240 DATA 4,153,20,151,10,170,5,182,20
  250 DATA 2,153,133,190,120
  260 DATA 2,149,105,186,92
  270 
  280 DATA 0
  290 
  300 FORI%=0TO47:GCOL I%/3
  310   RESTORE
  320   REPEAT
  330     READ C%
  340     IF C%<>0 THEN
  350       C%-=1
  360       FORN%=0TOC%
  370         READ MX,MY:MX=FX*MX+DX-I%:MY=FY*MY+DY+I%
  380         X(N%)=MX:Y(N%)=MY
  390       NEXT
  400       PROCBEZDRAW(X(),Y(),C%,20)
  410       REM PROCBEZPLOT(X(),Y(),C%,20)
  420     ENDIF
  430   UNTIL C%=0
  440 NEXT
  450 END
  460 
  470 DEFPROCBEZDRAW(X(),Y(),D%,P%)
  480 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  490 IF D%=1 THEN LINE X(0),Y(0),X(1),Y(1):ENDPROC
  500 DIM F(D%)
  510 FOR I%=0TOD%:F(I%)=1
  520   FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  530   FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  540   FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  550 NEXT
  560 MOVE X(0),Y(0)
  570 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  580 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  590 NEXT:DRAW PX,PY:NEXT
  600 ENDPROC
  610 
  620 DEFPROCBEZPLOT(X(),Y(),D%,P%)
  630 LOCAL F(),I%,J%,U1,U2,K,PX,PY,@%
  640 @%=&00020200
  650 IF D%=1 THEN
  660   PRINT"PU "X(0)","Y(0)"; ";:IF POS > 60 PRINT
  670   PRINT"PD "X(1)","Y(1)"; ";:IF POS > 60 PRINT
  680   ENDPROC
  690 ENDIF
  700 DIM F(D%)
  710 FOR I%=0TOD%:F(I%)=1
  720   FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  730   FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  740   FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  750 NEXT
  760 PRINT"PU "X(0)","Y(0)"; ";:IF POS > 60 PRINT
  770 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  780 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%):PX+=X(I%)*K:PY+=Y(I%)*K
  790 NEXT
  800 PRINT"PD "PX","PY"; ";:IF POS > 60 PRINT
  810 NEXT
  820 ENDPROC
