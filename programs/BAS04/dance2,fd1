   10 REM > DANCE2
   20 
   30 MODE141:MODE13:OFF
   40 SYS"OS_UpdateMEMC",64,64
   50 S1%=1:S2%=2:P%=3:NP%=8:R%=24:R2%=R%/2
   60 DIM X(P%),Y(P%),DX(P%),DY(P%),XP%(NP%),YP%(NP%)
   70 
   80 *SNEW
   90 PROCBALL(R%,12,09,02,"1")
  100 PROCBALL(R%,12,09,05,"2")
  110 PROCBALL(R%,12,09,06,"3")
  120 PROCBALL(R%,12,09,08,"4")
  130 PROCBALL(R%,12,09,11,"5")
  140 PROCBALL(R%,12,09,22,"6")
  150 PROCBALL(R%,12,09,18,"7")
  160 PROCBALL(R%,12,09,25,"8")
  170 GCOL8,15
  180 
  190 
  200 ORIGIN 640-R%,512-R%
  210 FORN%=0TOP%
  220 X(N%)=RND(511-R%):Y(N%)=RND(511-R%)
  230 DX(N%)=(RND(5)+3)*SGN(RND(1)-.5)
  240 DY(N%)=(RND(5)+3)*SGN(RND(1)-.5)
  250 NEXT
  260 
  270 ONERRORGOTO1020
  280 REPEAT
  290 SYS"OS_Byte",112,S1%:SYS"OS_Byte",113,S2%:WAIT:CLS:SWAP S1%,S2%
  300 
  310 FORN%=0TOP%
  320 A%=X(N%)+DX(N%):IF ABS(A%)>511-R% DX(N%)=(RND(5)+3)*SGN(-DX(N%))
  330 A%=Y(N%)+DY(N%):IF ABS(A%)>511-R% DY(N%)=(RND(5)+3)*SGN(-DY(N%))
  340 X(N%)+=DX(N%):Y(N%)+=DY(N%):NEXT
  350 PROCBEZIER(X(),Y(),P%,NP%)
  360 *SCH. 1
  370 FORN%=0TONP%:PLOT&ED,XP%(N%),YP%(N%):NEXT
  380 *SCH. 2
  390 FORN%=0TONP%:PLOT&ED,XP%(N%),-YP%(N%):NEXT
  400 *SCH. 3
  410 FORN%=0TONP%:PLOT&ED,-XP%(N%),-YP%(N%):NEXT
  420 *SCH. 4
  430 FORN%=0TONP%:PLOT&ED,-XP%(N%),YP%(N%):NEXT
  440 *SCH. 5
  450 FORN%=0TONP%:PLOT&ED,YP%(N%),XP%(N%):NEXT
  460 *SCH. 6
  470 FORN%=0TONP%:PLOT&ED,YP%(N%),-XP%(N%):NEXT
  480 *SCH. 7
  490 FORN%=0TONP%:PLOT&ED,-YP%(N%),-XP%(N%):NEXT
  500 *SCH. 8
  510 FORN%=0TONP%:PLOT&ED,-YP%(N%),XP%(N%):NEXT
  520 
  530 UNTIL0
  540 
  550 DEFPROCBEZIER(X(),Y(),D%,P%)
  560 LOCAL F(),I%,J%,U1,U2,K,PX,PY
  570 DIM F(D%)
  580 FOR I%=0TOD%:F(I%)=1
  590 FOR J%=1 TO D%:F(I%)=F(I%)*J%:NEXT
  600 FOR J%=1 TO I%:F(I%)=F(I%)/J%:NEXT
  610 FOR J%=1 TO D%-I%:F(I%)=F(I%)/J%:NEXT
  620 NEXT
  630 MOVE X(0),Y(0)
  640 FORJ%=0TOP%:U1=J%/P%:U2=1-U1:PX=0:PY=0
  650 FORI%=0TOD%:K=F(I%)*U1^I%*U2^(D%-I%)
  660 PX+=X(I%)*K:PY+=Y(I%)*K
  670 NEXT:XP%(J%)=PX:YP%(J%)=PY:NEXT
  680 ENDPROC
  690 
  700 DEFPROCBALL(R%,HX%,HY%,C%,N$)
  710 LOCAL X%,Y%,N%,M%,CX%,CY%,I,S
  720 CLS:GCOL 63 TINT 255:CIRCLE FILL R%,R%,R%
  730 S=(SQR(HX%^2+HY%^2)+R%)/8:HX%+=R%:HY%+=R%
  740 FORY%=-R%TOR%STEP4:CY%=R%+Y%:X%=SQR(R%^2-Y%^2)
  750 FORCX%=0TO2*R%STEP4
  760 IF POINT(CX%,CY%)<>0 THEN
  770 I=SQR((HX%-CX%)^2+(HY%-CY%)^2)/S
  780 PROCF(C%,8-I):POINT CX%,CY%
  790 ENDIF
  800 NEXT,:GCOL63 TINT 255
  810 MOVE0,0:MOVE2*R%,2*R%:OSCLI"SGET "+N$
  820 SYS"OS_SpriteOp",29,0,N$
  830 FORY%=0TO2*R%STEP4
  840 FORX%=0TO2*R%STEP4:S%=1
  850 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  860 SYS"OS_SpriteOp",44,0,N$,X%/4,Y%/4,S%
  870 NEXT,
  880 ENDPROC
  890 
  900 DEFPROCF(F%,I%)
  910 IF I%>7 I%=7
  920 IF I%<0 I%=0
  930 LOCAL R%,G%,B%
  940 B%=F%DIV9:F%=F%-9*B%
  950 G%=F%DIV3:F%=F%-3*G%
  960 R%=F%
  970 F%=R%+4*G%+16*B%
  980 IF I%>=4 F%=F%+&15
  990 GCOL F% TINT (64*I%)MOD256
 1000 ENDPROC
 1010 
 1020 SYS"OS_Byte",112,S1%:REPORT:PRINT" at line "STR$ERL:ON
