   10 REM > BALLGEN
   20 
   30 MODE13:OFF
   40 R%=24:D%=2*R%:*SNEW
   50 FORN%=1TO26:PROCBALL(R%,R%*.2,R%*.5,N%,STR$(N%)):NEXT
   60 CLS:GCOL8,15:ORIGIN 612,484
   70 
   80 REPEAT
   90 S%=RND(200):A=0:B=PI*RND(1):C=0:D=B*RND(1)
  100 F%=RND(26):OSCLI"*SCH. "+STR$(F%):TIME=0
  110 REPEAT
  120 X%=250*SINA:Y%=250*COSA
  130 PLOT&ED,X%+S%*SINC,Y%+S%*COSC:A+=B:C+=D
  140 UNTILINKEY$(0)<>"" OR TIME>1000
  150 UNTIL0
  160 
  170 DEFPROCBALL(R%,HX%,HY%,C%,N$)
  180 LOCAL X%,Y%,N%,M%,CX%,CY%,I,S
  190 CLS:GCOL 63 TINT 255:CIRCLE FILL R%,R%,R%
  200 S=(SQR(HX%^2+HY%^2)+R%)/8:HX%+=R%:HY%+=R%
  210 FORY%=-R%TOR%STEP4:CY%=R%+Y%:X%=SQR(R%^2-Y%^2)
  220 FORCX%=0TO2*R%STEP4
  230 IF POINT(CX%,CY%)<>0 THEN
  240 I=SQR((HX%-CX%)^2+(HY%-CY%)^2)/S
  250 PROCF(C%,8-I):POINT CX%,CY%
  260 ENDIF
  270 NEXT,:GCOL63 TINT 255
  280 MOVE0,0:MOVE2*R%,2*R%:OSCLI"SGET "+N$
  290 SYS"OS_SpriteOp",29,0,N$
  300 FORY%=0TO2*R%STEP4
  310 FORX%=0TO2*R%STEP4:S%=1
  320 IF POINT(X%,Y%)=0 S%=0:POINT X%,Y%
  330 SYS"OS_SpriteOp",44,0,N$,X%/4,Y%/4,S%
  340 NEXT,
  350 ENDPROC
  360 
  370 DEFPROCF(F%,I%)
  380 IF I%>7 I%=7
  390 IF I%<0 I%=0
  400 LOCAL R%,G%,B%
  410 B%=F%DIV9:F%=F%-9*B%
  420 G%=F%DIV3:F%=F%-3*G%
  430 R%=F%
  440 F%=R%+4*G%+16*B%
  450 IF I%>=4 F%=F%+&15
  460 GCOL F% TINT (64*I%)MOD256
  470 ENDPROC
