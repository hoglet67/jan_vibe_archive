   10 REM > SHARTXT
   20 
   30 SYS"OS_UpdateMEMC",64,64
   40 MODE0:OFF
   50 VDU5,23,17,7,6,36;36|
   60 S1%=1:S2%=2:Z%=6:K%=100:LX%=1279-K%:LY%=1023-K%
   70 TX%=384:TY%=568:T$="         ":TC%=0
   80 FORN%=1TO8:SYS 6,112,S1%:SYS 6,113,S2%:CLS
   90 S1%=S1%MOD8+1:S2%=S2%MOD8+1:NEXT
  100 DIM X%(Z%),Y%(Z%),A%(3,Z%),DX%(Z%),DY%(Z%),DA%(3,Z%)
  110 FORN%=1TOZ%:X%(N%)=640:Y%(N%)=512
  120 FORM%=1TO3:A%(M%,N%)=RND(360):DA%(M%,N%)=(RND(4)+2)*SGN(RND(1)-.5):NEXT
  130 DX%(N%)=(RND(9)+7)*SGN(RND(1)-.5):DY%(N%)=(RND(9)+7)*SGN(RND(1)-.5)
  140 NEXT
  150 
  160 FIL$="TEST"
  170 CH%=OPENIN FIL$:GT%=0:DC%=0
  180 
  190 ONERRORGOTO520
  200 REPEAT
  210 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:S1%=S1%MOD8+1:S2%=S2%MOD8+1
  220 FORN%=1TOZ%
  230 FORM%=1TO3:A%(M%,N%)=(A%(M%,N%)+DA%(M%,N%))MOD360:NEXT
  240 H%=X%(N%)+DX%(N%):IF H%<K%ORH%>LX% DX%(N%)=(RND(9)+3)*SGN(-DX%(N%))
  250 H%=Y%(N%)+DY%(N%):IF H%<K%ORH%>LY% DY%(N%)=(RND(9)+3)*SGN(-DY%(N%))
  260 X%(N%)+=DX%(N%):Y%(N%)+=DY%(N%)
  270 T=RADA%(1,N%):X1%=X%(N%)+K%*SINT:Y1%=Y%(N%)+K%*COST
  280 T=RADA%(2,N%):X2%=X%(N%)+K%*SINT:Y2%=Y%(N%)+K%*COST
  290 T=RADA%(3,N%):X3%=X%(N%)+K%*SINT:Y3%=Y%(N%)+K%*COST
  300 GCOL0:MOVEX1%,Y1%:MOVEX2%,Y2%:PLOT85,X3%,Y3%
  310 GCOL1:DRAWX1%,Y1%:DRAWX2%,Y2%:DRAWX3%,Y3%
  320 NEXT
  330 VDU24,386;424;892;588;:CLG
  340 RECTANGLE 386,424,506,164
  350 TC%=(TC%+24)MOD72
  360 IF TC%=0 THEN
  370 IF NOT EOF#CH% THEN
  380 REPEAT
  390 GT%=T%:T%=BGET#CH%:IF T%<32 THEN T%=32
  400 UNTIL NOT (GT%=32 AND T%=32)
  410 KK%=INSTR("[\]{|}",CHR$(T%))
  420 IFKK%<>0 T%=ASCMID$("ÆØÅæøå",KK%,1)
  430 ELSE
  440 T%=32:DC%+=1
  450 ENDIF
  460 T$=RIGHT$(T$,8)+CHR$(T%)
  470 ENDIF
  480 MOVE TX%-TC%,TY%:PRINTT$
  490 VDU26
  500 UNTILDC%=15
  510 
  520 VDU4,26:SYS 6,112,S1%:CLOSE#CH%:ON
  530 IF DC%<>15 REPORT:PRINT" at line "STR$ERL
