   10 REM > KALEJ
   20 
   30 MODE137:MODE9:OFF:ORIGIN 640,512
   40 S1%=1:S2%=2:K%=7
   50 
   60 DIM X%(K%),Y%(K%),Z%(K%),DX%(K%),DY%(K%),R%(K%),A%(K%,3)
   70 DIM TX%(K%,3),TY%(K%,3)
   80 FORN%=1TOK%
   90 X%(N%)=256:Y%(N%)=256:Z%(N%)=RND(64)+32
  100 T=RAD(RND(360)):Z%=RND(13)+3
  110 DX%(N%)=Z%*SINT:DY%(N%)=Z%*COST
  120 R%(N%)=RND(8)*SGN(RND(1)-.5)
  130 FORM%=1TO3:A%(N%,M%)=RND(120)+(M%-1)*120:NEXT,
  140 COLOUR 15,128,128,128
  150 SI1=SINRAD120:CO1=COSRAD120
  160 SI2=SINRAD240:CO2=COSRAD240
  170 
  180 ONERRORGOTO580
  190 REPEAT
  200 SYS 6,112,S1%:SYS 6,113,S2%:WAIT:CLS:SWAP S1%,S2%
  210 
  220 FORN%=1TOK%
  230 X1%=X%(N%)+DX%(N%)
  240 IF X1%<0 OR X1%>400 DX%(N%)=-DX%(N%)
  250 Y1%=Y%(N%)+DY%(N%)
  260 IF Y1%<0 OR Y1%>400 DY%(N%)=-DY%(N%)
  270 X%(N%)+=DX%(N%):Y%(N%)+=DY%(N%)
  280 FORM%=1TO3:A%(N%,M%)=(A%(N%,M%)+R%(N%))MOD360:T=RAD(A%(N%,M%))
  290 TX%(N%,M%)=X%(N%)+Z%(N%)*SINT:TY%(N%,M%)=Y%(N%)+Z%(N%)*COST
  300 NEXT,
  310 
  320 T=RAD(30):X%=500*SINT:Y%=500*COST:GCOL15
  330 FORN%=90TO390STEP60:GX%=X%:GY%=Y%:T=RADN%:X%=500*SINT:Y%=500*COST
  340 MOVE0,0:MOVEGX%,GY%:PLOT85,X%,Y%:NEXT
  350 
  360 FORN%=1TOK%
  370 GCOL2,N%
  380 MOVE TX%(N%,1),TY%(N%,1):MOVE TX%(N%,2),TY%(N%,2)
  390 PLOT85,TX%(N%,3),TY%(N%,3)
  400 MOVE TX%(N%,1)*CO1+TY%(N%,1)*SI1,TY%(N%,1)*CO1-TX%(N%,1)*SI1
  410 MOVE TX%(N%,2)*CO1+TY%(N%,2)*SI1,TY%(N%,2)*CO1-TX%(N%,2)*SI1
  420 PLOT85,TX%(N%,3)*CO1+TY%(N%,3)*SI1,TY%(N%,3)*CO1-TX%(N%,3)*SI1
  430 MOVE TX%(N%,1)*CO2+TY%(N%,1)*SI2,TY%(N%,1)*CO2-TX%(N%,1)*SI2
  440 MOVE TX%(N%,2)*CO2+TY%(N%,2)*SI2,TY%(N%,2)*CO2-TX%(N%,2)*SI2
  450 PLOT85,TX%(N%,3)*CO2+TY%(N%,3)*SI2,TY%(N%,3)*CO2-TX%(N%,3)*SI2
  460 MOVE TX%(N%,1),-TY%(N%,1):MOVE TX%(N%,2),-TY%(N%,2)
  470 PLOT85,TX%(N%,3),-TY%(N%,3)
  480 MOVE TX%(N%,1)*CO1-TY%(N%,1)*SI1,-TY%(N%,1)*CO1-TX%(N%,1)*SI1
  490 MOVE TX%(N%,2)*CO1-TY%(N%,2)*SI1,-TY%(N%,2)*CO1-TX%(N%,2)*SI1
  500 PLOT85,TX%(N%,3)*CO1-TY%(N%,3)*SI1,-TY%(N%,3)*CO1-TX%(N%,3)*SI1
  510 MOVE TX%(N%,1)*CO2-TY%(N%,1)*SI2,-TY%(N%,1)*CO2-TX%(N%,1)*SI2
  520 MOVE TX%(N%,2)*CO2-TY%(N%,2)*SI2,-TY%(N%,2)*CO2-TX%(N%,2)*SI2
  530 PLOT85,TX%(N%,3)*CO2-TY%(N%,3)*SI2,-TY%(N%,3)*CO2-TX%(N%,3)*SI2
  540 NEXT
  550 
  560 UNTIL0
  570 
  580 SYS 6,112,S1%:REPORT:PRINT" at line "STR$ERL:ON
